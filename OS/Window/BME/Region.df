//
// Region manipulation functions.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<ll>/BME/Bme.h"

fn BmeBoxOverlapsBox { box1 box2 -- overlaps }
	0 overlaps!

	// rule out all of the conditions that tell us the boxes cannot overlap.

	if (box2@ BmeBox_X1I + gi
		box1@ BmeBox_X2I + gi >=)

		return
	end

	if (box1@ BmeBox_X1I + gi
		box2@ BmeBox_X2I + gi >=)

		return
	end

	if (box2@ BmeBox_Y1I + gi
		box1@ BmeBox_Y2I + gi >=)

		return
	end

	box1@ BmeBox_Y1I + gi
	box2@ BmeBox_Y2I + gi < overlaps!
end

fn BmeBoxContainsPoint { x y box -- contains }
	0 contains!

	if (box@ BmeBox_X1I + gi x@ >)
		return
	end

	if (x@ box@ BmeBox_X2I + gi >=)
		return
	end

	if (box@ BmeBox_Y1I + gi y@ >)
		return
	end

	y@ box@ BmeBox_Y2I + gi < contains!
end

fn BmeBoxContainsBox { containedbox box -- contains }
	0 contains!

	// rule out all of the conditions that tell us the box cannot be contained
	// by the other box.

	if (containedbox@ BmeBox_X1I + gi
		box@ BmeBox_X1I + gi <)

		return
	end

	if (containedbox@ BmeBox_Y1I + gi
		box@ BmeBox_Y1I + gi <)

		return
	end

	if (containedbox@ BmeBox_X2I + gi
		box@ BmeBox_X2I + gi >)

		return
	end

	containedbox@ BmeBox_Y2I + gi
	box@ BmeBox_Y2I + gi <= contains!
end