#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

fn Usage { -- }
	"usage: kill [-sig] pid
sig may either be a number or a unix-like symbolic name.\n" OSGetStdErr FPrintf
end

fn Main { ... -- ret }
	0 ret!

	if (argc@ 2 < argc@ 3 > ||)
		Usage
		-1 OSExit
	end

	auto signal
	OSSIGNAL_TERM signal!

	auto pid

	if (argc@ 3 ==)
		auto signame
		[1]argv@ signame!

		if (signame@ gb '-' ~=)
			Usage
			-1 OSExit
		end

		1 signame +=

		if (signame@ gb '0' >= signame@ gb '9' <= &&)
			// its a number
			signame@ atoi signal!
		end else
			// its a symbolic name
			signame@ OSSignalGetNumber ret! signal!

			if (ret@)
				ret@ OSStatusGetName "kill: %s\n" OSError
			end
		end

		[2]argv@ atoi pid!
	end else
		[1]argv@ atoi pid!
	end

	auto phandle
	ACCESS_EXEC // access
	pid@ // pid
	OSProcessOpenByPID ret! phandle!

	if (ret@)
		if (ret@ STATUS_INVALID_HANDLE ==)
			// nicer error message
			"kill: no such process\n" OSError
		end

		ret@ OSStatusGetName "kill: %s\n" OSError
	end

	signal@ // signal
	phandle@ // processhandle
	OSProcessSignal ret!

	phandle@ OSClose drop

	if (ret@)
		ret@ OSStatusGetName "kill: %s\n" OSError
	end
end