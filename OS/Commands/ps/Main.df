#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

table StatusNames
	"n/a"  // 0
	"INIT" // 1
	"RDY"  // 2
	"SUSP" // 3
	"CUR"  // 4
	"WAIT" // 5
	"UWIT" // 6
	"DEAD" // 7
	"8"
	"9"
	"10"
	"11"
	"12"
	"13"
	"14"
	"15"
endtable

fn Main { ... -- ret }
	0 ret!

	auto count
	OSProcessCountQuery ret! count!

	if (ret@)
		ret@ OSStatusGetName "ps: couldn't query process count: %s\n" OSError
	end

	auto querybuffer
	count@ OSProcessInformation_SIZEOF * OSHeapAllocate ret! querybuffer!

	if (ret@)
		ret@ OSStatusGetName "ps: couldn't allocate buffer: %s\n" OSError
	end

	querybuffer@ // buffer
	count@ // maxquery
	OSProcessQueryAll ret! count!

	if (ret@)
		querybuffer@ OSHeapFree

		ret@ OSStatusGetName "ps: couldn't query process list: %s\n" OSError
	end

	"CONSOLE"
	"PAGES"
	"PAGEFAULTS"
	"PRI"
	"STATE"
	"NAME"
	"PID"

	"%8s %16s %6s %4s %11s %8s %s\n" Printf

	auto record
	querybuffer@ record!

	while (count@)
		record@ OSProcessInformation_ConsoleName +
		record@ OSProcessInformation_WorkingSetSize + @
		record@ OSProcessInformation_PageFaultCount + @
		record@ OSProcessInformation_Priority + @
		[record@ OSProcessInformation_Status + @]StatusNames@
		record@ OSProcessInformation_Name +
		record@ OSProcessInformation_PID + @
		"%8d %16s %6s %02d   %11d %8d %s\n" Printf

		1 count -=
		OSProcessInformation_SIZEOF record +=
	end

	querybuffer@ OSHeapFree
end