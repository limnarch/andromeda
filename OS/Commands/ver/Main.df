#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

const SYSNAME 1
const NODENAME 2
const RELEASE 4
const VERSION 8
const MACHINE 16
const PROCESSOR 32

const ALLFLAGS (SYSNAME RELEASE | VERSION | MACHINE | PROCESSOR |)

buffer UNameBuf OSVersionQuery_SIZEOF

fn private Usage { -- }
	"usage: ver [-amnprs]\n" OSGetStdErr FPrintf
end

fn Main { ... -- ret }
	0 ret!

	auto flags
	0 flags!

	if (argc@ 2 >=)
		auto sw
		[1]argv@ sw!

		if (sw@ gb '-' ==)
			1 sw +=

			while (sw@ gb 0 ~=)
				if (sw@ gb 'a' ==)
					ALLFLAGS flags |=
				end elseif (sw@ gb 's' ==)
					SYSNAME flags |=
				end elseif (sw@ gb 'n' ==)
					NODENAME flags |=
				end elseif (sw@ gb 'r' ==)
					RELEASE flags |=
				end elseif (sw@ gb 'v' ==)
					VERSION flags |=
				end elseif (sw@ gb 'm' ==)
					MACHINE flags |=
				end elseif (sw@ gb 'p' ==)
					PROCESSOR flags |=
				end else
					Usage
					-1 OSExit
				end

				1 sw +=
			end
		end
	end

	UNameBuf OSVersion ret!

	if (ret@)
		ret@ OSStatusGetName "ver: %s\n" OSError
	end

	auto sp
	0 sp!

	if (flags@ ~~)
		ALLFLAGS flags!
	end

	if (flags@ SYSNAME &)
		if (sp@)
			' ' Putc
		end
		UNameBuf OSVersionQuery_OSName + "%s" Printf
		1 sp!
	end

	if (flags@ NODENAME &)
		if (sp@)
			' ' Putc
		end
		UNameBuf OSVersionQuery_NodeName + "%s" Printf
		1 sp!
	end

	if (flags@ RELEASE &)
		if (sp@)
			' ' Putc
		end
		UNameBuf OSVersionQuery_Release + "%s" Printf
		1 sp!
	end

	if (flags@ VERSION &)
		if (sp@)
			' ' Putc
		end
		UNameBuf OSVersionQuery_Version + "%s" Printf
		1 sp!
	end

	if (flags@ MACHINE &)
		if (sp@)
			' ' Putc
		end
		UNameBuf OSVersionQuery_Machine + "%s" Printf
		1 sp!
	end

	if (flags@ PROCESSOR &)
		if (sp@)
			' ' Putc
		end
		UNameBuf OSVersionQuery_Processor + "%s" Printf
		1 sp!
	end

	'\n' Putc

	0 ret!
end