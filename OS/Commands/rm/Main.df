#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

fn Main { ... -- ret }
	0 ret!

	auto i
	1 i!

	while (i@ argc@ <)
		[i@]argv@ Rm

		1 i +=
	end
end

fn Rm { path -- }
	auto dirname
	OSFILEPATHMAX alloc dirname!

	auto ok

	path@ // path
	dirname@ // dirname
	OSFILEPATHMAX // bufsize
	OSDirectoryName ok!

	if (ok@)
		ok@ OSStatusGetName path@ "rm: %s: %s\n" OSError
	end

	auto dirhandle

	0 // flags
	ACCESS_WRITE ACCESS_EXEC | // access
	dirname@ // dirname
	0 // ftype
	OSFileOpen ok! dirhandle!

	if (ok@)
		ok@ OSStatusGetName dirname@ "rm: %s: %s\n" OSError
	end

	auto name
	path@ OSLastComponent name!

	if (name@ "/" strcmp)
		"." name!
	end

	name@ // name
	dirhandle@ // filehandle
	OSDirectoryUnlink ok!

	if (ok@)
		ok@ OSStatusGetName path@ "rm: %s: %s\n" OSError
	end

	dirhandle@ OSClose drop
end