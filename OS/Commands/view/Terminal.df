#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"
#include "<ll>/OSDLL/OSConsoleControl.h"

var TermWidth 0
public TermWidth

var TermHeight 0
public TermHeight

fn TermInit { -- }
	auto ok
	OSGetStdOut OSConsoleQuerySize ok! TermHeight! TermWidth!

	if (ok@)
		ok@ OSStatusGetName "couldn't determine terminal size: %s\n" OSError
	end
end

fn TermSetMode { -- }
	OSCONSOLEMODE_RAW // arg2
	OSCONSOLEIOCONTROL_SETMODE // arg1
	OSGetStdOut // filehandle
	OSIOControl drop drop
end

fn TermResetMode { -- }
	0 // arg2
	OSCONSOLEIOCONTROL_RESETMODE // arg1
	OSGetStdOut // filehandle
	OSIOControl drop drop
end

fn TermSetScrollMargin { top bottom -- }
	bottom@ 1 + top@ 1 + "\[[%d;%dr" Printf
end

fn TermResetScrollMargin { -- }
	"\[[;r" Puts
end

fn TermScrollDown { -- }
	"\[[1T" Puts
end

fn TermScrollUp { -- }
	"\[[1S" Puts
end

fn TermSetCursorPosition { x y -- }
	x@ 1 + y@ 1 + "\[[%d;%dH" Printf
end

fn TermSetCursorHidden { hidden -- }
	if (hidden@)
		"\[[?25l" Puts
	end else
		"\[[?25h" Puts
	end
end

fn TermClearAll { -- }
	"\[[2J" Puts
end

fn TermClearLineAll { -- }
	"\[[2K" Puts
end

fn TermClearLine { -- }
	"\[[K" Puts
end

fn TermInvertColor { -- }
	"\[[7m" Puts
end

fn TermResetColor { -- }
	"\[[0m" Puts
end

fn TermFinishDrawing { -- }
	OSFlushLine

	0 // arg2
	OSCONSOLEIOCONTROL_EXPEDITE // arg1
	OSGetStdOut // filehandle
	OSIOControl drop drop
end