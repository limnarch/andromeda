#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "Edit.h"

// manages a cached screen line buffer

var ScreenLineTable 0
public ScreenLineTable

var ScreenHeight 0
public ScreenHeight

var ScreenCursorX 0
public ScreenCursorX

var ScreenCursorY 0
public ScreenCursorY

var ScreenCursorPosition 0 // position within the file
public ScreenCursorPosition

var ScreenLinePosition 0 // line start position
public ScreenLinePosition

var ScreenStartPosition 0
public ScreenStartPosition

fn ScreenInit { -- }
	TermHeight@ SCREENMARGIN - ScreenHeight!

	auto ok
	ScreenHeight@ ScreenLine_SIZEOF * OSHeapAllocate ok! ScreenLineTable!

	if (ok@)
		ok@ OSStatusGetName "edit: couldn't create screen line table: %s\n" OSError
	end

	ScreenLineTable@ // ptr
	ScreenHeight@ ScreenLine_SIZEOF * // size
	0 // word
	memset

	ScreenPopulate
end

fn ScreenNavigateX { x -- }
	auto oldslp
	ScreenLinePosition@ oldslp!

	x@ ScreenLinePosition +=

	if (ScreenLinePosition@ 0 s<)
		0 ScreenLinePosition!
	end else
		auto line
		ScreenLineTable@ ScreenCursorY@ ScreenLine_SIZEOF * + line!

		if (ScreenLinePosition@ line@ ScreenLine_Length + @ >=)
			line@ ScreenLine_Length + @ 1 - ScreenLinePosition!
		end

		ScreenLinePosition@ TermWidth@ % ScreenCursorX!
	end

	if (ScreenLinePosition@ TermWidth@ / oldslp@ TermWidth@ / ~=)
		ScreenCursorY@ ScreenCursorY@ DrawScreen
	end

	ScreenCursorX@
	ScreenCursorY@ SCREENROW +
	TermSetCursorPosition

	TermFinishDrawing
end

fn ScreenNavigateY { y -- }

end

fn ScreenPopulate { -- }
	auto rows
	ScreenHeight@ rows!

	auto line
	ScreenLineTable@ line!

	auto pos
	ScreenStartPosition@ pos!

	auto buffer
	EditorBuffer@ buffer!

	auto startofline
	auto runlength

	while (rows@)
		1 startofline!
		0 runlength!

		auto ok
		0 ok!

		while (1)
			auto char
			pos@ buffer@ BufferGetChar ok! char!

			if (ok@)
				break
			end

			1 runlength +=

			if (startofline@)
				pos@ line@ ScreenLine_Offset + !
				0 startofline!
			end

			1 pos +=

			if (char@ '\n' ==)
				break
			end
		end

		runlength@ line@ ScreenLine_Length + !

		if (ok@)
			break
		end

		1 rows -=
		ScreenLine_SIZEOF line +=
	end

	if (rows@)
		line@ // ptr
		rows@ ScreenLine_SIZEOF * // size
		0 // word
		memset
	end
end