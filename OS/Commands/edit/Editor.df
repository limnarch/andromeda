#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "Edit.h"

var EditorFileName 0
public EditorFileName

var EditorFileHandle OSNONE
public EditorFileHandle

var EditorFileAccess 0
public EditorFileAccess

var EditorBuffer 0
public EditorBuffer

var EditorRunning 0
public EditorRunning

fn Editor { -- }
	auto ok

	TermInit

	BufferCreate ok! EditorBuffer!

	if (ok@)
		ok@ OSStatusGetName "edit: couldn't create buffer: %s\n" OSError
	end

	if (EditorFileHandle@ OSNONE ~=)
		EditorFileHandle@ // filehandle
		EditorBuffer@ // buffer
		BufferPopulate ok!

		if (ok@)
			ok@ OSStatusGetName "edit: couldn't populate buffer: %s\n" OSError
		end
	end

	ScreenInit

	TermSetMode

	DrawAll

	1 EditorRunning!

	InputLoop

	TermResetMode

	"\[[0m\[[1;1H\[[2J" Printf
	OSFlushLine
end

fn InputLoop { -- }
	auto charbuf
	4 alloc charbuf!

	while (EditorRunning@)
		auto ok
		OSWAIT_TIMEOUTINFINITE // timeout
		0 // flags
		1 // length
		charbuf@ // buffer
		OSGetStdIn // filehandle
		OSFileRead ok! drop

		if (ok@)
			ok@ OSStatusGetName "edit: failed to read stdin: %s\n" OSError
		end

		charbuf@ gb InputCharacter
	end
end

const INPUTMODE_NORMAL 0
const INPUTMODE_ESC0   1
const INPUTMODE_ESC1   2

var InputMode INPUTMODE_NORMAL

fn InputCharacter { c -- }
	if (InputMode@ INPUTMODE_NORMAL ==)
		if (c@ 0x03 ==) // ^C
			0 EditorRunning!
		end elseif (c@ 0x1B ==) // ^[
			INPUTMODE_ESC0 InputMode!
		end
	end elseif (InputMode@ INPUTMODE_ESC0 ==)
		if (c@ '[' ==)
			INPUTMODE_ESC1 InputMode!
		end else
			INPUTMODE_NORMAL InputMode!
		end
	end elseif (InputMode@ INPUTMODE_ESC1 ==)
		INPUTMODE_NORMAL InputMode!
		c@ InputNavigationalKey
	end
end

fn InputNavigationalKey { c -- }
	if (c@ 'D' ==)
		-1 ScreenNavigateX
	end elseif (c@ 'C' ==)
		1 ScreenNavigateX
	end elseif (c@ 'B' ==)
		-1 ScreenNavigateY
	end elseif (c@ 'A' ==)
		1 ScreenNavigateY
	end
end