#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "Edit.h"

fn private Usage { -- }
	"usage: edit [file]\n" OSGetStdErr FPrintf
end

fn Main { ... -- ret }
	0 ret!

	if (argc@ 2 >)
		Usage
		-1 OSExit
	end

	if (argc@ 1 ==)
		"Unnamed" EditorFileName!
		ACCESS_READ ACCESS_WRITE | EditorFileAccess!
	end else
		[1]argv@ EditorFileName!

		OSFILETYPE_FILE // filetype
		0 // flags
		ACCESS_READ ACCESS_WRITE | // access
		[1]argv@ // path
		OSFileOpen ret! EditorFileHandle!

		if (ret@ STATUS_NOT_FOUND ==)
			OSNONE EditorFileHandle!
			ACCESS_READ ACCESS_WRITE | EditorFileAccess!
		end elseif (ret@)
			if (ret@ STATUS_PERMISSION_DENIED ~=)
				ret@ OSStatusGetName [1]argv@ "edit: %s: %s\n" OSError
			end

			// try to open it read-only

			OSFILETYPE_FILE // filetype
			0 // flags
			ACCESS_READ // access
			[1]argv@ // path
			OSFileOpen ret! EditorFileHandle!

			if (ret@)
				if (ret@ STATUS_NOT_FOUND ==)
					STATUS_PERMISSION_DENIED ret!
				end

				ret@ OSStatusGetName [1]argv@ "edit: %s: %s\n" OSError
			end

			ACCESS_READ EditorFileAccess!
		end else
			ACCESS_READ ACCESS_WRITE | EditorFileAccess!
		end
	end

	Editor
end