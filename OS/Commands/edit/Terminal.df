#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"
#include "<ll>/OSDLL/OSConsoleControl.h"

var TermWidth 0
public TermWidth

var TermHeight 0
public TermHeight

fn TermInit { -- }
	auto query
	OSConsoleQuery_SIZEOF alloc query!

	query@ // arg2
	OSCONSOLEIOCONTROL_QUERY // arg1
	OSGetStdOut // filehandle
	OSIOControl drop drop

	query@ OSConsoleQuery_Columns + @ TermWidth!
	query@ OSConsoleQuery_Rows + @ TermHeight!
end

fn TermSetMode { -- }
	OSCONSOLEMODE_RAW // arg2
	OSCONSOLEIOCONTROL_SETMODE // arg1
	OSGetStdOut // filehandle
	OSIOControl drop drop
end

fn TermResetMode { -- }
	0 // arg2
	OSCONSOLEIOCONTROL_RESETMODE // arg1
	OSGetStdOut // filehandle
	OSIOControl drop drop
end

fn TermSetCursorPosition { x y -- }
	x@ 1 + y@ 1 + "\[[%d;%dH" Printf
end

fn TermSetCursorHidden { hidden -- }
	if (hidden@)
		"\[[?25l" Puts
	end else
		"\[[?25h" Puts
	end
end

fn TermClearAll { -- }
	"\[[2J" Puts
end

fn TermClearLineAll { -- }
	"\[[2K" Puts
end

fn TermClearLine { -- }
	"\[[K" Puts
end

fn TermFinishDrawing { -- }
	OSFlushLine
end