#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

fn Main { ... -- ret }
	0 ret!

	if (argc@ 2 >=)
		auto i
		1 i!

		while (i@ argc@ <)
			argc@ 2 > // announce
			[i@]argv@ // path
			List ret!

			if (ret@)
				break
			end

			1 i +=
		end
	end else
		0 // announce
		"." // path
		List ret!
	end

	if (ret@)
		ret@ OSStatusGetName
		[i@]argv@
		"ls: %s: %s\n" OSError
	end
end

buffer DirectoryEntry OSDirectoryEntry_SIZEOF

fn List { announce path -- ok }
	auto handle

	0 // flags
	ACCESS_READ // access
	path@ // path
	OSFILETYPE_DIRECTORY // ftype
	OSFileOpen ok! handle!

	if (ok@)
		return
	end

	if (announce@)
		path@ "%s:\n" Printf
	end

	while (ok@ ~~)
		DirectoryEntry // dirent
		handle@ // filehandle
		OSDirectoryRead ok!

		if (ok@)
			handle@ OSClose drop

			if (ok@ STATUS_IO_END_OF_FILE ==)
				if (announce@)
					'\n' Putc
				end
			
				0 ok!
			end

			return
		end

		DirectoryEntry OSDirectoryEntry_Name + "%s\n" Printf
	end

	if (announce@)
		'\n' Putc
	end

	handle@ OSClose drop
end