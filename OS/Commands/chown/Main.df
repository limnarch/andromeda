#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

fn private Usage { -- }
	"usage: chown owner file\n" OSGetStdErr FPrintf
end

fn Main { ... -- ret }
	0 ret!

	if (argc@ 3 ~=)
		Usage
		-1 OSExit
	end

	auto uid

	if ([1]argv@ gb '0' >= [1]argv@ gb '9' <= &&)
		[1]argv@ atoi uid!
	end else
		auto userinfo
		OSUserInfoOpen ret! userinfo!

		if (ret@)
			ret@ OSStatusGetName "chown: couldn't open userinfo: %s\n" OSError
		end

		auto user
		[1]argv@ // username
		userinfo@ // userinfo
		OSUserInfoUserGetByName ret! user!

		if (ret@)
			ret@ OSStatusGetName [1]argv@ "chown: %s: %s\n" OSError
		end

		"uid" // property
		user@ // userptr
		OSUserInfoUserGetProperty ret! uid!

		if (ret@)
			ret@ OSStatusGetName [1]argv@ "chown: couldn't get uid for %s: %s\n" OSError
		end

		userinfo@ OSUserInfoClose

		uid@ atoi uid!
	end

	auto handle
	0 // flags
	0 // access
	[2]argv@ // path
	0 // ftype
	OSFileOpen ret! handle!

	if (ret@)
		ret@ OSStatusGetName [2]argv@ "chown: %s: %s\n" OSError
	end

	uid@ // uid
	-1 // gid
	-1 // permissions
	handle@ // handle
	OSSetSecurity ret!

	if (ret@)
		ret@ OSStatusGetName [2]argv@ "chown: %s: %s\n" OSError
	end

	handle@ OSClose drop
end