#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"
#include "<ll>/AMS/Ams.h"

fn private Usage { -- }
	"usage: chown owner file\n" OSGetStdErr FPrintf
end

fn Main { ... -- ret }
	0 ret!

	if (argc@ 3 ~=)
		Usage
		-1 OSExit
	end

	auto uid

	if ([1]argv@ gb '0' >= [1]argv@ gb '9' <= &&)
		[1]argv@ atoi uid!
	end else
		AmsInit ret!

		if (ret@)
			ret@ OSStatusGetName "chown: couldn't connect to AMS: %s\n" OSError
		end

		[1]argv@ // username
		AmsQueryUIDByName ret! uid!

		if (ret@)
			ret@ OSStatusGetName [1]argv@ "chown: %s: %s\n" OSError
		end
	end

	auto handle
	0 // flags
	0 // access
	[2]argv@ // path
	0 // ftype
	OSFileOpen ret! handle!

	if (ret@)
		ret@ OSStatusGetName [2]argv@ "chown: %s: %s\n" OSError
	end

	uid@ // uid
	-1 // gid
	-1 // permissions
	handle@ // handle
	OSSetSecurity ret!

	if (ret@)
		ret@ OSStatusGetName [2]argv@ "chown: %s: %s\n" OSError
	end

	handle@ OSClose drop
end