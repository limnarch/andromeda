#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

fn private Usage { -- }
	"usage: chgrp group file\n" OSGetStdErr FPrintf
end

fn Main { ... -- ret }
	0 ret!

	if (argc@ 3 ~=)
		Usage
		-1 OSExit
	end

	auto gid

	if ([1]argv@ gb '0' >= [1]argv@ gb '9' <= &&)
		[1]argv@ atoi gid!
	end else
		auto groupinfo
		OSGroupInfoOpen ret! groupinfo!

		if (ret@)
			ret@ OSStatusGetName "chgrp: couldn't open groupinfo: %s\n" OSError
		end

		auto group
		[1]argv@ // groupname
		groupinfo@ // groupinfo
		OSGroupInfoGroupGetByName ret! group!

		if (ret@)
			ret@ OSStatusGetName [1]argv@ "chgrp: %s: %s\n" OSError
		end

		"gid" // property
		group@ // groupptr
		OSGroupInfoGroupGetProperty ret! gid!

		if (ret@)
			ret@ OSStatusGetName [1]argv@ "chgrp: couldn't get gid for %s: %s\n" OSError
		end

		groupinfo@ OSGroupInfoClose

		gid@ atoi gid!
	end

	auto handle
	0 // flags
	0 // access
	[2]argv@ // path
	0 // ftype
	OSFileOpen ret! handle!

	if (ret@)
		ret@ OSStatusGetName [2]argv@ "chgrp: %s: %s\n" OSError
	end

	-1 // uid
	gid@ // gid
	-1 // permissions
	handle@ // handle
	OSSetSecurity ret!

	if (ret@)
		ret@ OSStatusGetName [2]argv@ "chgrp: %s: %s\n" OSError
	end

	handle@ OSClose drop
end