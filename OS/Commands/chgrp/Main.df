#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"
#include "<ll>/AMS/Ams.h"

fn private Usage { -- }
	"usage: chgrp group file\n" OSGetStdErr FPrintf
end

fn Main { ... -- ret }
	0 ret!

	if (argc@ 3 ~=)
		Usage
		-1 OSExit
	end

	auto gid

	if ([1]argv@ gb '0' >= [1]argv@ gb '9' <= &&)
		[1]argv@ atoi gid!
	end else
		AmsInit ret!

		if (ret@)
			ret@ OSStatusGetName "chgrp: couldn't connect to AMS: %s\n" OSError
		end

		[1]argv@ // groupname
		AmsQueryGIDByName ret! gid!

		if (ret@)
			ret@ OSStatusGetName [1]argv@ "chgrp: %s: %s\n" OSError
		end
	end

	auto handle
	OSFILETYPE_ANY // filetype
	0 // flags
	0 // access
	[2]argv@ // path
	OSFileOpen ret! handle!

	if (ret@)
		ret@ OSStatusGetName [2]argv@ "chgrp: %s: %s\n" OSError
	end

	-1 // uid
	gid@ // gid
	-1 // permissions
	handle@ // handle
	OSSetSecurity ret!

	if (ret@)
		ret@ OSStatusGetName [2]argv@ "chgrp: %s: %s\n" OSError
	end

	handle@ OSClose drop
end