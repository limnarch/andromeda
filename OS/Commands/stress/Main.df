#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "Stress.h"

fn private Usage { -- }
	"usage: stress testname ...\n" OSGetStdErr FPrintf
end

var ArgTable 0
public ArgTable

var ArgCount 0
public ArgCount

const TESTCOUNT 4

table Tests
	"forkbomb"
	pointerof ForkbombTest

	"swap"
	pointerof SwapTest

	"nullptr"
	pointerof NullptrTest

	"overflow"
	pointerof OverflowTest
endtable

fn Main { ... -- ret }
	if (argc@ 2 <)
		Usage
		-1 OSExit
	end

	argv ArgTable!
	argc@ ArgCount!

	auto testname
	[1]argv@ testname!

	auto test
	Tests test!

	auto count
	TESTCOUNT count!

	while (count@)
		if (test@@ testname@ strcmp)
			test@ 4 + @ StressTestF ret!

			return
		end

		8 test +=
		1 count -=
	end

	testname@ "stress: no such test %s\n" OSError
end

