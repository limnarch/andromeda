#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

buffer MountInformation OSMountInformation_SIZEOF

fn private Usage { -- }
	"usage: label device newlabel\n" OSGetStdErr FPrintf
end

fn Main { ... -- ret }
	0 ret!

	if (argc@ 3 <)
		Usage
		-1 OSExit
	end

	MountInformation OSMountInformationInitialize

	OSMOUNTSET_VOLUMELABEL MountInformation OSMountInformation_SetFlags + |=

	MountInformation OSMountInformation_VolumeLabel + // dest
	[2]argv@ // src
	strcpy

	auto devhandle
	OSFILETYPE_ANY // filetype
	0 // flags
	ACCESS_WRITE // access
	[1]argv@ // path
	OSFileOpen ret! devhandle!

	if (ret@)
		ret@ OSStatusGetName [1]argv@ "label: %s: %s\n" OSError
	end

	MountInformation // query
	devhandle@ // handle
	OSMountSet ret!

	devhandle@ OSClose drop

	if (ret@)
		ret@ OSStatusGetName [1]argv@ "label: %s: %s\n" OSError
	end
end