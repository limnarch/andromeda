#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

fn Main { ... -- ret }
	0 ret!

	auto catfd
	auto ok

	if (argc@ 2 >=)
		auto i
		1 i!

		while (i@ argc@ <)
			0 // flags
			ACCESS_READ // access
			[i@]argv@ // path
			0 // ftype
			OSFileOpen ok! catfd!

			if (ok@)
				ok@ OSStatusGetName [i@]argv@ "cat: couldn't open %s: %s\n" OSError
			end

			[i@]argv@ catfd@ Cat

			catfd@ OSClose drop

			1 i +=
		end
	end else
		"<stdin>" OSGetStdIn Cat 
	end
end

const CATUNIT 16384

buffer CatBuffer CATUNIT

fn Cat { name fd -- }
	auto bytes
	auto ok

	OSWAIT_TIMEOUTINFINITE // timeout
	0 // flags
	CATUNIT // length
	CatBuffer // buffer
	fd@ // filehandle
	OSFileRead ok! bytes!

	if (ok@)
		ok@ OSStatusGetName name@ "cat: couldn't read %s: %s\n" OSError
	end

	while (bytes@)
		auto byteswritten
		0 // flags
		bytes@ // length
		CatBuffer // buffer
		OSGetStdOut // filehandle
		OSFileWrite ok! byteswritten!

		if (ok@)
			ok@ OSStatusGetName "cat: couldn't write: %s\n" OSError
		end

		byteswritten@ bytes -=

		if (bytes@ ~~)
			OSWAIT_TIMEOUTINFINITE // timeout
			0 // flags
			CATUNIT // length
			CatBuffer // buffer
			fd@ // filehandle
			OSFileRead ok! bytes!

			if (ok@)
				ok@ OSStatusGetName name@ "cat: couldn't read %s: %s\n" OSError
			end
		end
	end
end