#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

fn Usage { -- }
	"usage: trunc [file] [size]\n" OSGetStdErr FPrintf
end

fn Main { ... -- ret }
	0 ret!

	if (argc@ 3 ~=)
		Usage
		-1 OSExit
	end

	auto handle
	auto ok

	OSFILETYPE_ANY_STREAM // filetype
	OSDEFAULTFILEPERMISSIONS // permissions
	0 // flags
	ACCESS_WRITE // access
	[1]argv@ // path
	OSFileCreate ret! handle!

	if (ret@)
		ret@ OSStatusGetName [1]argv@ "trunc: couldn't open %s: %s\n" OSError
	end

	auto oldsize
	[2]argv@ atoi // newsize
	0 // growing
	handle@ // handle
	OSFileTruncate ret! oldsize!

	if (ret@)
		ret@ OSStatusGetName [1]argv@ "trunc: couldn't truncate %s: %s\n" OSError
	end

	handle@ OSClose drop
end