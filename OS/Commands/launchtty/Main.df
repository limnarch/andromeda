#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<ll>/OSDLL/OSConsoleControl.h"

fn Main { ... -- ret }
	0 ret!

	if (argc@ 2 <)
		return
	end

	auto tty
	[1]argv@ tty!

	auto stdin

	0 // flags
	ACCESS_READ // access
	tty@ // path
	OSFILETYPE_CHARDEVICE // ftype
	OSFileOpen ret! stdin!

	if (ret@)
		return
	end

	stdin@ OSSetStdIn

	auto handle

	0 // flags
	ACCESS_WRITE // access
	tty@ // path
	OSFILETYPE_CHARDEVICE // ftype
	OSFileOpen ret! handle!

	if (ret@)
		return
	end

	handle@ OSSetStdOut

	0 // flags
	ACCESS_WRITE // access
	tty@ // path
	OSFILETYPE_CHARDEVICE // ftype
	OSFileOpen ret! handle!

	if (ret@)
		return
	end

	handle@ OSSetStdErr

	0 // arg2
	OSCONSOLEIOCONTROL_RESETMODE // arg1
	OSGetStdIn // filehandle
	OSIOControl ret! drop

	if (ret@)
		return
	end

	UID_SYSTEM // uid
	GID_SYSTEM // gid
	ACCESS_OWNER_READ ACCESS_OWNER_WRITE | // permissions
	handle@ // handle
	OSSetSecurity ret!

	if (ret@)
		return
	end

	OSSIGNALACTION_IGNORE // action
	OSSIGNAL_INT // signum
	OSSignalSetAction drop

	OSSIGNALACTION_IGNORE // action
	OSSIGNAL_QUIT // signum
	OSSignalSetAction drop

	stdin@ // filehandle
	OSCURRENTPROCESS // processhandle
	OSProcessSetConsoleGroup ret!

	if (ret@)
		return
	end

	auto phandle
	auto thandle

	"/mintia/login.exe" // path
	0 // creationflags
	0 // creationparams
	ACCESS_OWNER_ALL // permissions
	"login.exe" // name
	OSSpawn ret! phandle! thandle!

	if (ret@)
		return
	end

	thandle@ OSClose drop

	1 // alertable
	OSWAIT_TIMEOUTINFINITE // timeout
	phandle@ // objecthandle
	OSWaitForObject ret!

	phandle@ // processhandle
	OSProcessReadStatus drop ret!

	phandle@ OSClose drop

	return
end