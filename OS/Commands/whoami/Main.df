#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

buffer ProcessInformation OSProcessInformation_SIZEOF

fn Main { ... -- ret }
	auto userinfo
	OSUserInfoOpen ret! userinfo!

	if (ret@)
		ret@ OSStatusGetName "whoami: couldn't open userinfo: %s\n" OSError
	end

	OSCURRENTPROCESS // processhandle
	ProcessInformation // query
	OSProcessQuery ret!

	if (ret@)
		ret@ OSStatusGetName "whoami: couldn't query self: %s\n" OSError
	end

	auto user
	ProcessInformation OSProcessInformation_OwningUID + @ // uid
	userinfo@ // userinfo
	OSUserInfoUserGetByUID ret! user!

	if (ret@)
		ret@ OSStatusGetName "whoami: i don't know who i am: %s\n" OSError
	end

	user@ OSUserInfoUserGetName "%s\n" Printf
end