#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

externptr DiskInfo

extern PartitionDisk { update -- }

extern LoadVDB { -- }
extern FreeVDB { -- }
extern VDBValid { -- valid }
extern PTInfo { -- }
extern VDBLabel { -- label }
extern VDBWrite { new -- }
extern WriteBoot { -- }
extern ChangeLabel { -- }

fn PromptYN { ... fmt -- r }
	auto ch
	256 alloc ch!

	argv argc@ fmt@ VPrintf
	" [y/n]? " Printf

	ch@ 255 Gets

	ch@ gb r!

	if (r@ 'y' ==)
		1 r! return
	end

	0 r! return
end

fn PromptON { ... prompt -- num }
	'\n' Putc

	prompt@ Puts

	auto i
	1 i!
	while (i@ argc@ <=)
		[i@ 1 -]argv@ i@ "\t%d. %s\n" Printf
		i@ 1 + i!
	end

	'\n' Putc

	auto r
	12 alloc r!

	"option #: " Printf

	r@ 11 Gets

	r@ atoi num!
end

fn PromptAnyKey { -- }
	"\[[7mpress enter\[[0m" Printf

	auto ch
	256 alloc ch!

	ch@ 255 Gets
end

fn Prompt { -- }
	auto r
	0 r!

	auto dfo

	while (r@ 6 > r@ 0 == ||)
		"change partition label (preserve data)"
		"change the disklabel (preserve data)"
		"write new disklabel (erase data)"
		"partition the disk (erase data)"
		"view volume info"
		"exit"
		"which would you like to do?\n"
		PromptON r!
	end

	if (r@ 1 ==)
		0 OSExit
	end

	if (r@ 2 ==)
		'\n' Putc

		DiskInfo OSFileInformation_Size + @ 1024 / "size: %dkb\n" Printf

		LoadVDB

		if (VDBValid)
			VDBLabel "disk label: %s\n" Printf

			PTInfo
			FreeVDB
		end else
			"no valid disklabel\n" Printf
		end

		PromptAnyKey
	end elseif (r@ 4 ==)
		1 VDBWrite
	end elseif (r@ 3 ==)
		0 PartitionDisk
	end elseif (r@ 5 ==)
		2 VDBWrite
	end elseif (r@ 6 ==)
		ChangeLabel
	end
end