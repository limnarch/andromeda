#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

var DiskNode 0
public DiskNode

buffer DiskInfo OSFileInformation_SIZEOF
public DiskInfo

fn IDiskInit { path -- }
	auto ok
	OSFILEFLAG_NOCACHE // flags
	ACCESS_READ ACCESS_WRITE | // access
	path@ // path
	0 // ftype
	OSFileOpen ok! DiskNode!

	if (ok@)
		ok@ OSStatusGetName path@ "aptdisk: %s: %s\n" OSError
	end

	DiskNode@ // filehandle
	DiskInfo // query
	OSFileQuery ok!

	if (ok@)
		ok@ OSStatusGetName path@ "aptdisk: couldn't stat %s: %s\n" OSError
	end
end

fn IReadBlock { block buf -- }
	auto ok
	block@ 512 * // offset
	DiskNode@ // handle
	OSSEEK_SET // mode
	OSFileSeek ok! drop

	if (ok@)
		ok@ OSStatusGetName block@ "aptdisk: couldn't seek to block %d: %s\n" OSError
	end

	OSWAIT_TIMEOUTINFINITE // timeout
	0 // flags
	512 // length
	buf@ // buffer
	DiskNode@ // filehandle
	OSFileRead ok! drop

	if (ok@)
		ok@ OSStatusGetName block@ "aptdisk: couldn't read block %d: %s\n" OSError
	end
end

fn IWriteBlock { block buf -- }
	auto ok
	block@ 512 * // offset
	DiskNode@ // handle
	OSSEEK_SET // mode
	OSFileSeek ok! drop

	if (ok@)
		ok@ OSStatusGetName block@ "aptdisk: couldn't seek to block %d: %s\n" OSError
	end

	0 // flags
	512 // length
	buf@ // buffer
	DiskNode@ // filehandle
	OSFileWrite ok! drop

	if (ok@)
		ok@ OSStatusGetName block@ "aptdisk: couldn't write block %d: %s\n" OSError
	end
end