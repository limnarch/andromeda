//
// Implements querying functions for the AMS server.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/AmsAPI.h"

#include "Ams.h"

fn AmsQueryUIDByName { name -- uid ok }
	auto userptr
	name@ // username
	AmsDbUserGetByName ok! userptr!

	if (ok@)
		return
	end

	"uid" // property
	userptr@ // userptr
	AmsDbUserGetProperty ok! uid!

	if (ok@)
		return
	end

	uid@ atoi uid!
end

fn AmsQueryGIDByName { name -- gid ok }
	auto groupptr
	name@ // username
	AmsDbGroupGetByName ok! groupptr!

	if (ok@)
		return
	end

	"gid" // property
	groupptr@ // groupptr
	AmsDbGroupGetProperty ok! gid!

	if (ok@)
		return
	end

	gid@ atoi gid!
end

fn AmsQueryByUID { info uid -- ok }
	auto userptr
	uid@ // uid
	AmsDbUserGetByUID ok! userptr!

	if (ok@)
		return
	end

	auto prop
	"gid" // property
	userptr@ // userptr
	AmsDbUserGetProperty ok! prop!

	if (ok@ ~~)
		prop@ atoi info@ AmsUserInformation_GID + !
	end else
		-1 info@ AmsUserInformation_GID + !
	end

	"poolquota" // property
	userptr@ // userptr
	AmsDbUserGetProperty ok! prop!

	if (ok@ ~~)
		if (prop@ "default" strcmp)
			// 1/4 of physical commit limit with a minimum of 256KB

			AmsMemoryInformation OSMemoryInformation_PhysicalCommitLimit + @ OSPAGESHIFT << 2 >>
			256 1024 * max info@ AmsUserInformation_PoolQuota + !
		end else
			prop@ atoi info@ AmsUserInformation_PoolQuota + !
		end
	end else
		-1 info@ AmsUserInformation_PoolQuota + !
	end

	"pgpoolquota" // property
	userptr@ // userptr
	AmsDbUserGetProperty ok! prop!

	if (ok@ ~~)
		if (prop@ "default" strcmp)
			// a sixteenth the size of the swapfile with a minimum of 256KB

			AmsMemoryInformation OSMemoryInformation_TheoreticalCommitLimit + @ OSPAGESHIFT << 4 >>
			256 1024 * max info@ AmsUserInformation_PagedPoolQuota + !
		end else
			prop@ atoi info@ AmsUserInformation_PagedPoolQuota + !
		end
	end else
		-1 info@ AmsUserInformation_PagedPoolQuota + !
	end

	"vmquota" // property
	userptr@ // userptr
	AmsDbUserGetProperty ok! prop!

	if (ok@ ~~)
		if (prop@ "default" strcmp)
			// half the size of the swapfile with a minimum of 2MB

			AmsMemoryInformation OSMemoryInformation_TheoreticalCommitLimit + @ OSPAGESHIFT << 1 >>
			0x200000 max info@ AmsUserInformation_VirtualMemoryQuota + !
		end else
			prop@ atoi info@ AmsUserInformation_VirtualMemoryQuota + !
		end
	end else
		-1 info@ AmsUserInformation_VirtualMemoryQuota + !
	end

	"wsquota" // property
	userptr@ // userptr
	AmsDbUserGetProperty ok! prop!

	if (ok@ ~~)
		if (prop@ "default" strcmp)
			// 1/2 of nonpageable commit limit with a minimum of 160 pages

			AmsMemoryInformation OSMemoryInformation_PhysicalCommitLimit + @ 1 >>
			160 max info@ AmsUserInformation_WorkingSetQuota + !
		end else
			prop@ atoi info@ AmsUserInformation_WorkingSetQuota + !
		end
	end else
		-1 info@ AmsUserInformation_WorkingSetQuota + !
	end

	info@ AmsUserInformation_UserName + // dest
	userptr@ AmsDbUserGetName // src
	AMSNAMEMAX 1 - // max
	strncpy

	"home" // property
	userptr@ // userptr
	AmsDbUserGetProperty ok! prop!

	if (ok@ ~~)
		info@ AmsUserInformation_Home + // dest
		prop@ // src
		OSFILEPATHMAX 1 - // max
		strncpy
	end else
		0 info@ AmsUserInformation_Home + sb
	end

	"shell" // property
	userptr@ // userptr
	AmsDbUserGetProperty ok! prop!

	if (ok@ ~~)
		info@ AmsUserInformation_Shell + // dest
		prop@ // src
		OSFILEPATHMAX 1 - // max
		strncpy
	end else
		0 info@ AmsUserInformation_Shell + sb
	end

	0 ok!
end

fn AmsQueryByGID { info gid -- ok }
	auto groupptr
	gid@ // uid
	AmsDbGroupGetByGID ok! groupptr!

	if (ok@)
		return
	end

	info@ AmsGroupInformation_GroupName + // dest
	groupptr@ AmsDbGroupGetName // src
	AMSNAMEMAX 1 - // max
	strncpy

	0 ok!
end