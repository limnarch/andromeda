//
// Implements querying functions for the AMS server.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"
#include "<ll>/OSDLL/OSCrypt.h"

#include "<inc>/AmsAPI.h"

#include "Ams.h"

fn AmsQueryUIDByName { name -- uid ok }
	auto userptr
	name@ // username
	AmsDbUserGetByName ok! userptr!

	if (ok@)
		return
	end

	"uid" // property
	userptr@ // userptr
	AmsDbUserGetPropertyNumeric ok! uid!
end

fn AmsQueryGIDByName { name -- gid ok }
	auto groupptr
	name@ // username
	AmsDbGroupGetByName ok! groupptr!

	if (ok@)
		return
	end

	"gid" // property
	groupptr@ // groupptr
	AmsDbUserGetPropertyNumeric ok! gid!
end

fn AmsQueryByUserPtr { info userptr -- ok }
	auto prop
	"gid" // property
	userptr@ // userptr
	AmsDbUserGetPropertyNumeric ok! prop!

	if (ok@ ~~)
		prop@ info@ AmsUserInformation_GID + !
	end else
		-1 info@ AmsUserInformation_GID + !
	end

	"poolquota" // property
	userptr@ // userptr
	AmsDbUserGetPropertyNumeric ok! prop!

	if (ok@ ~~)
		if (prop@ -2 ==)
			// 1/4 of physical commit limit with a minimum of 256KB

			AmsMemoryInformation OSMemoryInformation_PhysicalCommitLimit + @ OSPAGESHIFT << 2 >>
			256 1024 * max info@ AmsUserInformation_PoolQuota + !
		end else
			prop@ info@ AmsUserInformation_PoolQuota + !
		end
	end else
		-1 info@ AmsUserInformation_PoolQuota + !
	end

	"pgpoolquota" // property
	userptr@ // userptr
	AmsDbUserGetPropertyNumeric ok! prop!

	if (ok@ ~~)
		if (prop@ -2 ==)
			// a sixteenth the size of the swapfile with a minimum of 256KB

			AmsMemoryInformation OSMemoryInformation_TheoreticalCommitLimit + @ OSPAGESHIFT << 4 >>
			256 1024 * max info@ AmsUserInformation_PagedPoolQuota + !
		end else
			prop@ info@ AmsUserInformation_PagedPoolQuota + !
		end
	end else
		-1 info@ AmsUserInformation_PagedPoolQuota + !
	end

	"vmquota" // property
	userptr@ // userptr
	AmsDbUserGetPropertyNumeric ok! prop!

	if (ok@ ~~)
		if (prop@ -2 ==)
			// half the size of the swapfile with a minimum of 2MB

			AmsMemoryInformation OSMemoryInformation_TheoreticalCommitLimit + @ OSPAGESHIFT << 1 >>
			0x200000 max info@ AmsUserInformation_VirtualMemoryQuota + !
		end else
			prop@ info@ AmsUserInformation_VirtualMemoryQuota + !
		end
	end else
		-1 info@ AmsUserInformation_VirtualMemoryQuota + !
	end

	"wsquota" // property
	userptr@ // userptr
	AmsDbUserGetProperty ok! prop!

	if (ok@ ~~)
		if (prop@ -2 ==)
			// 1/2 of nonpageable commit limit with a minimum of 160 pages

			AmsMemoryInformation OSMemoryInformation_PhysicalCommitLimit + @ 1 >>
			160 max info@ AmsUserInformation_WorkingSetQuota + !
		end else
			prop@ info@ AmsUserInformation_WorkingSetQuota + !
		end
	end else
		-1 info@ AmsUserInformation_WorkingSetQuota + !
	end

	info@ AmsUserInformation_UserName + // dest
	userptr@ AmsDbUserGetName // src
	AMSNAMEMAX 1 - // max
	strncpy

	"home" // property
	userptr@ // userptr
	AmsDbUserGetProperty ok! prop!

	if (ok@ ~~)
		info@ AmsUserInformation_Home + // dest
		prop@ // src
		OSFILEPATHMAX 1 - // max
		strncpy
	end else
		0 info@ AmsUserInformation_Home + sb
	end

	"shell" // property
	userptr@ // userptr
	AmsDbUserGetProperty ok! prop!

	if (ok@ ~~)
		info@ AmsUserInformation_Shell + // dest
		prop@ // src
		OSFILEPATHMAX 1 - // max
		strncpy
	end else
		0 info@ AmsUserInformation_Shell + sb
	end
end

fn AmsQueryByUID { info uid -- ok }
	auto userptr
	uid@ // uid
	AmsDbUserGetByUID ok! userptr!

	if (ok@)
		return
	end

	info@ // info
	userptr@ // userptr
	AmsQueryByUserPtr ok!
end

fn AmsQueryByGroupPtr { info groupptr -- ok }
	info@ AmsGroupInformation_GroupName + // dest
	groupptr@ AmsDbGroupGetName // src
	AMSNAMEMAX 1 - // max
	strncpy

	0 ok!
end

fn AmsQueryByGID { info gid -- ok }
	auto groupptr
	gid@ // uid
	AmsDbGroupGetByGID ok! groupptr!

	if (ok@)
		return
	end

	info@ // info
	groupptr@ // groupptr
	AmsQueryByGroupPtr ok!
end