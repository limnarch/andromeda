//
// Server-side implementation of AMS enumeration APIs.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"
#include "<ll>/OSDLL/OSCrypt.h"

#include "<inc>/AmsAPI.h"

#include "AmsServer.h"

fn AmsInternalEmumerateUsers { context max buf -- nextcontext count ok }
	auto iterator
	auto userptr

	if (context@ -1 ==)
		AmsDbUserGetIterator ok! iterator!
	end else
		context@ AmsDbUserGetByUID ok! iterator!
	end

	if (ok@)
		return
	end

	0 count!

	while (max@)
		iterator@ // iterator
		AmsDbUserNext ok! iterator! userptr!

		if (ok@)
			0 ok!
			-1 nextcontext!
			return
		end

		auto uid
		"uid" // property
		userptr@ // userptr
		AmsDbUserGetProperty ok! uid!

		if (ok@)
			continue
		end

		uid@ atoi uid!

		uid@ buf@ AmsUserEnumeration_UID + !

		buf@ AmsUserEnumeration_Info + // info
		userptr@ // userptr
		AmsQueryByUserPtr ok!

		if (ok@)
			return
		end

		1 count +=
		1 max -=
		AmsUserEnumeration_SIZEOF buf +=
	end

	iterator@ // iterator
	AmsDbUserNext ok! iterator! userptr!

	if (ok@)
		0 ok!
		-1 nextcontext!
	end else
		"uid" // property
		userptr@ // userptr
		AmsDbUserGetProperty ok! nextcontext!

		if (ok@)
			return
		end

		nextcontext@ atoi nextcontext!
	end
end

fn AmsInternalEmumerateGroups { context max buf -- nextcontext count ok }
	auto iterator
	auto groupptr

	if (context@ -1 ==)
		AmsDbGroupGetIterator ok! iterator!
	end else
		context@ AmsDbGroupGetByGID ok! iterator!
	end

	if (ok@)
		return
	end

	0 count!

	while (max@)
		iterator@ // iterator
		AmsDbGroupNext ok! iterator! groupptr!

		if (ok@)
			0 ok!
			-1 nextcontext!
			return
		end

		auto gid
		"gid" // property
		groupptr@ // groupptr
		AmsDbGroupGetProperty ok! gid!

		if (ok@)
			continue
		end

		gid@ atoi gid!

		gid@ buf@ AmsGroupEnumeration_GID + !

		buf@ AmsGroupEnumeration_Info + // info
		groupptr@ // groupptr
		AmsQueryByGroupPtr ok!

		if (ok@)
			return
		end

		1 count +=
		1 max -=
		AmsGroupEnumeration_SIZEOF buf +=
	end

	iterator@ // iterator
	AmsDbGroupNext ok! iterator! groupptr!

	if (ok@)
		0 ok!
		-1 nextcontext!
	end else
		"gid" // property
		groupptr@ // groupptr
		AmsDbGroupGetProperty ok! nextcontext!

		if (ok@)
			return
		end

		nextcontext@ atoi nextcontext!
	end
end