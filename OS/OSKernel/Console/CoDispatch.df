#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALArgs.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<inc>/Console.h"

#include "<ll>/OSDLL/OS.h"

table CoDispatch
	0                                    // open
	0                                    // close
	0                                    // iocontrol
	pointerof CoConsoleDispatchRead      // read
	pointerof CoConsoleDispatchWrite     // write
	0                                    // system control
	0                                    // parse
	0                                    // create
	0                                    // flush
	pointerof CoConsoleDeleteFileObject  // delete object
	0                                    // set information
	0                                    // get information
	0                                    // rename
	0                                    // readblock
	0                                    // writeblock
	0                                    // truncate
	0                                    // readdirectory
	0                                    // getpageaddr
	pointerof CoConsoleDeleteObject      // delete device object
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
endtable
public CoDispatch

table CoDriver
	IOVERSION_MAJOR                      // ioversion major
	IOVERSION_MINOR                      // ioversion minor

	"console"                            // name
	OSFILETYPE_CHARDEVICE                // type
	pointerof CoDispatch                 // dispatch table
	CoConsole_SIZEOF                     // extension size

	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
endtable
public CoDriver

fn (IODispatchDeleteObjectFunction) CoConsoleDeleteFileObject { object -- ok }
	0 ok!

	auto fcb
	object@ IOFile_FileControlBlock + @ fcb!

	auto deviceobject
	fcb@ IOFileControlBlock_DeviceObject + @ deviceobject!

	auto console
	deviceobject@ IODeviceGetExtension console!

	if (fcb@ IOFileControlBlock_FSContext + @)
		// host fcb! mark console detached
		1 console@ CoConsole_Detached + !

		// TODO send OSSIGNAL_HUP to attached processes
	end

	// forward to generic function
	object@ IODeviceDeleteFileObject ok!
end

fn (IODispatchReadFunction) CoConsoleDispatchRead { timeout flags kflags length offset buffer fcb lastmode -- bytesread ok }
	auto deviceobject
	fcb@ IOFileControlBlock_DeviceObject + @ deviceobject!

	auto console
	deviceobject@ IODeviceGetExtension console!

	if (fcb@ IOFileControlBlock_FSContext + @)
		// host fcb, reading from the OutputBuffer

		"CoConsoleRead: TODO\n" KeCrash
	end else
		// client fcb, reading from the CookedBuffer or RawBuffer

		timeout@ // timeout
		flags@ // flags
		length@ // length
		buffer@ // buffer
		lastmode@ // lastmode
		console@ // console
		CoConsoleRead ok! bytesread!
	end
end

fn (IODispatchWriteFunction) CoConsoleDispatchWrite { flags kflags length offset buffer fcb lastmode -- byteswritten ok }
	auto deviceobject
	fcb@ IOFileControlBlock_DeviceObject + @ deviceobject!

	auto console
	deviceobject@ IODeviceGetExtension console!

	if (fcb@ IOFileControlBlock_FSContext + @)
		// host fcb, writing to the RawBuffer

		"CoConsoleWrite: TODO\n" KeCrash
	end else
		// client fcb, writing to the OutputBuffer

		length@ // length
		buffer@ // buffer
		lastmode@ // lastmode
		console@ // console
		CoConsoleWrite ok! byteswritten!
	end
end

fn (IODispatchDeleteDeviceObjectFunction) CoConsoleDeleteObject { object -- }
	auto console
	object@ IODeviceGetExtension console!

	if (console@ CoConsole_Initialized + @ ~~)
		return
	end

	console@ CoConsole_CookedBuffer + @ ExRingBufferDelete
	console@ CoConsole_OutputBuffer + @ ExRingBufferDelete

	-1 // writeout
	console@ CoConsole_HostFCB + @ // fcb
	IOFileControlBlockDelete
end