#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALArgs.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALConsole.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<inc>/Video.h"

#include "<inc>/Console.h"

#include "<ll>/OSDLL/OS.h"

fn CoConsoleByFileHandle { permissions filehandle -- consoleobject ok }
	auto obj
	auto access

	IOFileTypeObject@ // type
	filehandle@ // handle
	ObObjectReferenceByHandle ok! obj! access!

	if (ok@)
		return
	end

	access@ permissions@ SeCheckAccess ok!

	if (ok@)
		obj@ ObObjectDereferenceByPointer drop

		return
	end

	auto fcb
	obj@ IOFile_FileControlBlock + @ fcb!

	if (fcb@ IOFileControlBlock_FileType + @ OSFILETYPE_CHARDEVICE ~=)
		obj@ ObObjectDereferenceByPointer drop
		STATUS_INVALID_OBJECT_TYPE ok!
		return
	end

	fcb@ IOFileControlBlock_DeviceObject + @ consoleobject!

	if (DEBUGCHECKS)
		if (consoleobject@ ~~)
			"CoConsoleByFileHandle: FCB had no deviceobject\n" KeCrash
		end
	end

	if (consoleobject@ IODevice_IsAConsole + @ ~~)
		obj@ ObObjectDereferenceByPointer drop
		STATUS_INVALID_OBJECT_TYPE ok!
		return
	end

	consoleobject@ ObObjectReferenceByPointer drop

	obj@ ObObjectDereferenceByPointer drop
end

fn OSSetSystemConsole { filehandle -- ok }
	SYSTEMPERMISSION_SETCONSOLE // permission
	KeProcessCurrent // process
	SeProcessCheckPermission ok!

	if (ok@)
		return
	end

	auto consoleobject
	ACCESS_READ ACCESS_WRITE | // permissions
	filehandle@ // filehandle
	CoConsoleByFileHandle ok! consoleobject!

	if (ok@)
		return
	end

	consoleobject@ ExSystemConsoleSet

	consoleobject@ ObObjectDereferenceByPointer drop
end