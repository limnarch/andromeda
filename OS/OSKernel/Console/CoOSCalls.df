//
// Implements the system calls for the console subsystem.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALArgs.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALConsole.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<inc>/Video.h"

#include "<inc>/Console.h"

#include "<ll>/OSDLL/OS.h"

fn CoConsoleByFileHandle { requiredaccess filehandle -- consoleobject ok }
	fnsection "PAGE$text"

	auto obj
	auto access

	IOFileTypeObject@ // type
	filehandle@ // handle
	ObObjectReferenceByHandle ok! obj! access!

	if (ok@)
		return
	end

	access@ requiredaccess@ SeCheckAccess ok!

	if (ok@)
		obj@ ObObjectDereferenceByPointer drop

		return
	end

	auto fcb
	obj@ IOFile_FileControlBlock + @ fcb!

	if (fcb@ IOFileControlBlockGetType OSFILETYPE_CHARDEVICE ~=)
		obj@ ObObjectDereferenceByPointer drop
		STATUS_NOT_A_CONSOLE ok!
		return
	end

	fcb@ IOFileControlBlockGetDeviceObject consoleobject!

	if (DEBUGCHECKS)
		if (consoleobject@ ~~)
			"CoConsoleByFileHandle: FCB had no deviceobject\n" KeCrash
		end
	end

	if (consoleobject@ IODevice_ConsoleHeader + @ ~~)
		obj@ ObObjectDereferenceByPointer drop
		STATUS_NOT_A_CONSOLE ok!
		return
	end

	consoleobject@ ObObjectReferenceByPointer drop

	obj@ ObObjectDereferenceByPointer drop
end

fn OSSetSystemConsole { filehandle -- ok }
	fnsection "PAGE$text"

	SYSTEMPERMISSION_SETCONSOLE // permission
	KeProcessCurrent // process
	SeProcessCheckPermission ok!

	if (ok@)
		return
	end

	auto consoleobject
	ACCESS_READ ACCESS_WRITE | // requiredaccess
	filehandle@ // filehandle
	CoConsoleByFileHandle ok! consoleobject!

	if (ok@)
		return
	end

	consoleobject@ ExSystemConsoleSet

	consoleobject@ ObObjectDereferenceByPointer drop
end

fn OSConsoleSignal { signal filehandle -- ok }
	fnsection "PAGE$text"

	auto consoleobject
	ACCESS_EXEC // requiredaccess
	filehandle@ // filehandle
	CoConsoleByFileHandle ok! consoleobject!

	if (ok@)
		return
	end

	signal@ // signal
	consoleobject@ IODevice_ConsoleHeader + @ // console
	CoConsoleSignal

	consoleobject@ ObObjectDereferenceByPointer drop
end