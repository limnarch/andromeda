//
// Implements the duplex pseudodevice driver.
// Acts like a unix pty or named pipe depending on flags.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALArgs.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<inc>/Console.h"

#include "<ll>/OSDLL/OS.h"

#include "<ll>/OSDLL/OSConsoleControl.h"

struct CoDuplexBuffer
	4 Buffer
	4 Size
	4 ReadIndex
	4 WriteIndex
	4 RemainingSpace

	4 UserBuffer
	4 UserBufferIndex
	4 UserBufferLength
endstruct

struct CoDuplex
	CoConsoleHeader_SIZEOF ConsoleHeader

	// the buffers are named for their perspective to the client in a PTY
	// set-up.

	CoDuplexBuffer_SIZEOF ReceiveBuffer  // read by client, written by server
	CoDuplexBuffer_SIZEOF TransmitBuffer // written by client, read by server

	KeMutex_SIZEOF Mutex // only used if paged buffers, otherwise IPL sync

	4 Paged
	4 QuotaBlock

	4 Flags
	4 InternalFlags

	4 Disconnected

	4 ServerFCB

	4 StoppedQueueListHead

	4 ServerReadListHead
	4 ServerReadListTail

	4 ServerWriteListHead
	4 ServerWriteListTail

	4 ClientReadListHead
	4 ClientReadListTail

	4 ClientWriteListHead
	4 ClientWriteListTail

	4 ServerBoost
	4 ClientBoost

	4 Context

	4 TXFunc
	4 FlushFunc
endstruct

table CoDuplexDriver
	IOVERSION_MAJOR                      // ioversion major
	IOVERSION_MINOR                      // ioversion minor

	"Duplex"                             // name
	pointerof CoDuplexDispatch           // dispatch table
	0                                    // flags

	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
endtable

table CoDuplexDispatch
	0                                    // open
	0                                    // close
	pointerof CoDuplexIOControl          // iocontrol
	0                                    // reserved
	0                                    // reserved
	0                                    // unlink
	0                                    // parse
	0                                    // delete
	0                                    // flush
	pointerof CoDuplexDeleteFileObject   // delete object
	0                                    // poke
	0                                    // setsecurity
	0                                    // rename
	0                                    // readblock
	0                                    // writeblock
	0                                    // truncate
	0                                    // readdirectory
	0                                    // getpageaddr
	pointerof CoDuplexDeleteObject       // delete device object
	0                                    // reclaim fcb
	pointerof CoDuplexCancel             // cancel operation
	pointerof CoDuplexRead               // read
	pointerof CoDuplexWrite              // write
endtable

const DUPLEXFLAG_PENDING_LF 0x80000000
const DUPLEXFLAG_STOPPED    0x40000000

fn CoDuplexBufferInitialize { size pri quotablock header -- ok }
	fnsection "PAGE$text"

	0 header@ CoDuplexBuffer_Buffer + !
	size@ header@ CoDuplexBuffer_Size + !
	0 header@ CoDuplexBuffer_ReadIndex + !
	0 header@ CoDuplexBuffer_WriteIndex + !
	size@ header@ CoDuplexBuffer_RemainingSpace + !

	0 header@ CoDuplexBuffer_UserBuffer + !
	0 header@ CoDuplexBuffer_UserBufferIndex + !
	0 header@ CoDuplexBuffer_UserBufferLength + !

	auto qcharge
	size@ MmChargeBytesGet qcharge!

	0 ok!

	if (quotablock@)
		// charge quota for buffer

		if (pri@ PAGED &)
			qcharge@ // charge
			quotablock@ // quotablock
			MmQuotaBlockChargePaged ok!
		end else
			qcharge@ // charge
			quotablock@ // quotablock
			MmQuotaBlockCharge ok!
		end

		if (ok@)
			return
		end
	end

	// allocate buffer

	auto buf
	size@ // size
	'DxBf' // tag
	pri@ CANBLOCK | // flags
	MmAllocWithTag ok! buf!

	if (ok@)
		if (quotablock@)
			if (pri@ PAGED &)
				qcharge@ // charge
				quotablock@ // quotablock
				MmQuotaBlockUnchargePaged
			end else
				qcharge@ // charge
				quotablock@ // quotablock
				MmQuotaBlockUncharge
			end
		end

		return
	end

	buf@ header@ CoDuplexBuffer_Buffer + !
end

fn CoDuplexBufferUninitialize { pri quotablock header -- }
	fnsection "PAGE$text"

	auto buf
	header@ CoDuplexBuffer_Buffer + @ buf!

	auto bufsize
	header@ CoDuplexBuffer_Size + @ bufsize!

	auto qcharge
	bufsize@ MmChargeBytesGet qcharge!

	if (quotablock@)
		if (pri@ PAGED &)
			qcharge@ // charge
			quotablock@ // quotablock
			MmQuotaBlockUnchargePaged
		end else
			qcharge@ // charge
			quotablock@ // quotablock
			MmQuotaBlockUncharge
		end
	end

	buf@ MmFree
end

fn CoDuplexLock { duplex alertable -- ipl ok }
	if (duplex@ CoDuplex_Paged + @ PAGED &)
		KERNELMODE // waitmode
		alertable@ // alertable
		OSWAIT_TIMEOUTINFINITE // timeout
		duplex@ CoDuplex_Mutex + // object
		KeThreadWaitForObject ok!
	end else
		IPLDPC KeIPLRaise ipl!
		0 ok!
	end
end

fn CoDuplexUnlock { ipl duplex -- }
	if (duplex@ CoDuplex_Paged + @ PAGED &)
		0 // abandon
		duplex@ CoDuplex_Mutex + // mutex
		KeMutexRelease drop
	end else
		ipl@ KeIPLLower
	end
end

fn CoDuplexObjectCreate { context txfunc flushfunc flags pri quotablock permanent name permissions -- duplexobject fileobject ok }
	fnsection "PAGE$text"

	if (flags@ OSDUPLEXFLAG_CONSOLE &)
		if (flags@ OSDUPLEXFLAG_SIMPLEX &)
			STATUS_INVALID_ARGUMENT ok!
			return
		end
	end

	if (flags@ OSDUPLEXFLAG_SIMPLEX &)
		if (name@)
			STATUS_INVALID_ARGUMENT ok!
			return
		end
	end

	CoDuplex_SIZEOF // extensionsize
	OSFILETYPE_CHARDEVICE // type
	name@ // name
	0 // sizeinbytes
	CoDuplexDriver // driver
	permissions@ // permissions
	permanent@ // permanent
	IODeviceCreateEx ok! duplexobject!

	if (ok@)
		return
	end

	auto duplex
	duplexobject@ IODevice_Extension + @ duplex!

	context@ duplex@ CoDuplex_Context + !

	txfunc@ duplex@ CoDuplex_TXFunc + !
	flushfunc@ duplex@ CoDuplex_FlushFunc + !

	0 duplex@ CoDuplex_InternalFlags + !

	flags@ duplex@ CoDuplex_Flags + !
	pri@ duplex@ CoDuplex_Paged + !
	0 duplex@ CoDuplex_Disconnected + !

	0 duplex@ CoDuplex_StoppedQueueListHead + !

	0 duplex@ CoDuplex_ServerReadListHead + !
	0 duplex@ CoDuplex_ServerReadListTail + !

	0 duplex@ CoDuplex_ServerWriteListHead + !
	0 duplex@ CoDuplex_ServerWriteListTail + !

	0 duplex@ CoDuplex_ClientReadListHead + !
	0 duplex@ CoDuplex_ClientReadListTail + !

	0 duplex@ CoDuplex_ClientWriteListHead + !
	0 duplex@ CoDuplex_ClientWriteListTail + !

	if (flags@ OSDUPLEXFLAG_CONSOLE &)
		duplex@ CoDuplex_ConsoleHeader + CoConsoleInitialize
		duplex@ CoDuplex_ConsoleHeader + duplexobject@ IODevice_ConsoleHeader + !

		IOBOOSTCONHOST duplex@ CoDuplex_ServerBoost + !
		IOBOOSTCONSOLE duplex@ CoDuplex_ClientBoost + !
	end else
		OSCONSOLEMODE_RAW OSCONSOLEMODE_NOECHO | duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + !

		if (pri@ PAGED &)
			IOBOOSTPIPE duplex@ CoDuplex_ClientBoost + !
		end else
			IOBOOSTSERIAL duplex@ CoDuplex_ClientBoost + !
		end

		IOBOOSTPIPE duplex@ CoDuplex_ServerBoost + !
	end

	128 // size
	pri@ // pri
	quotablock@ // quotablock
	duplex@ CoDuplex_ReceiveBuffer + // header
	CoDuplexBufferInitialize ok!

	if (ok@)
		duplexobject@ IODeviceDeallocateObject

		return
	end

	if (flags@ OSDUPLEXFLAG_SIMPLEX & ~~)
		512 // size
		pri@ // pri
		quotablock@ // quotablock
		duplex@ CoDuplex_TransmitBuffer + // header
		CoDuplexBufferInitialize ok!

		if (ok@)
			pri@ // pri
			quotablock@ // quotablock
			duplex@ CoDuplex_ReceiveBuffer + // header
			CoDuplexBufferUninitialize

			duplexobject@ IODeviceDeallocateObject

			return
		end
	end

	if (pri@ PAGED &)
		"CoDuplexMutex" // name
		KERNELMODE // mode
		duplex@ CoDuplex_Mutex + // mutex
		KeMutexInitialize
	end

	auto serverfcb

	duplexobject@ // devobj
	OSFILETYPE_CHARDEVICE // filetype
	0 // flags
	IOFileControlBlockCreate ok! serverfcb!

	if (ok@)
		pri@ // pri
		quotablock@ // quotablock
		duplex@ CoDuplex_ReceiveBuffer + // header
		CoDuplexBufferUninitialize

		if (flags@ OSDUPLEXFLAG_SIMPLEX & ~~)
			pri@ // pri
			quotablock@ // quotablock
			duplex@ CoDuplex_TransmitBuffer + // header
			CoDuplexBufferUninitialize
		end

		duplexobject@ IODeviceDeallocateObject

		return
	end

	1 serverfcb@ IOFileControlBlockSetContext

	serverfcb@ duplex@ CoDuplex_ServerFCB + !

	"DUPLEXSERVER" // openedpath
	0 // flags
	ACCESS_OWNER_ALL // permissions
	serverfcb@ // fcb
	IOFileCreateObject ok! fileobject!

	if (ok@)
		-1 // writeout
		serverfcb@ // fcb
		IOFileControlBlockDelete drop

		pri@ // pri
		quotablock@ // quotablock
		duplex@ CoDuplex_ReceiveBuffer + // header
		CoDuplexBufferUninitialize

		if (flags@ OSDUPLEXFLAG_SIMPLEX & ~~)
			pri@ // pri
			quotablock@ // quotablock
			duplex@ CoDuplex_TransmitBuffer + // header
			CoDuplexBufferUninitialize
		end

		duplexobject@ IODeviceDeallocateObject

		return
	end

	// bias refcounts for server fileobject
	duplexobject@ ObObjectReferenceByPointer drop

	if (quotablock@)
		quotablock@ MmQuotaBlockReference
	end

	quotablock@ duplex@ CoDuplex_QuotaBlock + !
end

fn CoDuplexCreate { flags name permissions -- duplexhandle filehandle ok }
	fnsection "PAGE$text"

	auto fileobject
	auto duplexobject

	0 // context
	0 // txfunc
	0 // flushfunc
	flags@ // flags
	PAGED // pri
	KeProcessCurrent PsProcess_PagedArea + @ PsProcessPaged_QuotaBlock + @ // quotablock
	0 // permanent
	name@ // name
	permissions@ // permissions
	CoDuplexObjectCreate ok! fileobject! duplexobject!

	if (ok@)
		return
	end

	1 // nocheck
	ACCESS_ALL // permissions
	fileobject@ // object
	ObObjectOpen ok! filehandle!

	if (ok@)
		fileobject@ ObObjectDereferenceByPointer drop
		duplexobject@ ObObjectDereferenceByPointer drop

		return
	end

	1 // nocheck
	ACCESS_ALL // permissions
	duplexobject@ // object
	ObObjectOpen ok! duplexhandle!

	if (ok@)
		filehandle@ ObObjectClose drop

		duplexobject@ ObObjectDereferenceByPointer drop

		return
	end
end

fn CoDuplexGetContext { duplex -- context }
	duplex@ CoDuplex_Context + @ context!
end

fn (IODispatchIOControlFunction) CoDuplexIOControl { arg2 arg1 access fcb lastmode -- ret ok }
	fnsection "PAGE$text"

	0 ok!
	0 ret!

	auto duplex
	fcb@ IOFileControlBlockGetDeviceObject IODevice_Extension + @ duplex!

	auto console
	fcb@ IOFileControlBlockGetDeviceObject IODevice_ConsoleHeader + @ console!

	if (console@ ~~)
		STATUS_NOT_SUPPORTED ok!
		return
	end

	if (arg1@ OSCONSOLEIOCONTROL_QUERY ==)
		auto query

		if (lastmode@ USERMODE ==)
			OSConsoleQuery_SIZEOF alloc query!

			query@ // ptr
			OSConsoleQuery_SIZEOF // size
			0 // word
			memset
		end else
			arg2@ query!
		end

		console@ CoConsoleHeader_Mode + @ query@ OSConsoleQuery_Mode + !

		if (lastmode@ USERMODE ==)
			arg2@ // dest
			query@ // src
			OSConsoleQuery_SIZEOF // size
			KeSafeCopyOut ok!
		end
	end elseif (arg1@ OSCONSOLEIOCONTROL_SETMODE ==)
		console@ CoConsoleHeader_Mode + @ ret!
		arg2@ console@ CoConsoleHeader_Mode + !
	end elseif (arg1@ OSCONSOLEIOCONTROL_RESETMODE ==)
		console@ CoConsoleHeader_Mode + @ ret!
		0 console@ CoConsoleHeader_Mode + !
	end elseif (arg1@ OSCONSOLEIOCONTROL_EXPEDITE ==)
		if (duplex@ CoVideoConsole@ ==)
			CoVideoConsoleExpediteRedraw
		end
	end else
		STATUS_NOT_SUPPORTED ok!
	end
end

const REQFLAG_WRITE     1
const REQFLAG_SERVER    2
const REQFLAG_RETURNANY 4

fn CoDuplexEnqueueRequest { lengthdone flags iopl -- }
	lengthdone@ iopl@ IOPacketLocation_Offset + !
	flags@ iopl@ IOPacketLocation_Flags + !

	auto duplex
	iopl@ IOPacketLocation_Context + @ duplex!

	auto iop
	iopl@ IOPacketFromLocation iop!

	iop@ IOPacketWasEnqueued

	0 iop@ IOPacketHeader_DeviceQueueNext + !

	auto header
	0 header!

	auto t

	if (flags@ REQFLAG_SERVER &)
		if (flags@ REQFLAG_WRITE &)
			if (DEBUGCHECKS)
				if (duplex@ CoDuplex_ClientReadListHead + @)
					"CoDuplexEnqueueRequest: server write & client read collide\n" KeCrash
				end
			end

			duplex@ CoDuplex_ServerWriteListTail + @ t!

			t@ iop@ IOPacketHeader_DeviceQueuePrev + !

			if (t@ ~~)
				iop@ duplex@ CoDuplex_ServerWriteListHead + !
				duplex@ CoDuplex_ReceiveBuffer + header!
			end else
				iop@ t@ IOPacketHeader_DeviceQueueNext + !
			end

			iop@ duplex@ CoDuplex_ServerWriteListTail + !
		end else
			if (DEBUGCHECKS)
				if (duplex@ CoDuplex_ClientWriteListHead + @)
					"CoDuplexEnqueueRequest: server read & client write collide\n" KeCrash
				end
			end

			duplex@ CoDuplex_ServerReadListTail + @ t!

			t@ iop@ IOPacketHeader_DeviceQueuePrev + !

			if (t@ ~~)
				iop@ duplex@ CoDuplex_ServerReadListHead + !
				duplex@ CoDuplex_TransmitBuffer + header!
			end else
				iop@ t@ IOPacketHeader_DeviceQueueNext + !
			end

			iop@ duplex@ CoDuplex_ServerReadListTail + !
		end
	end else
		if (flags@ REQFLAG_WRITE &)
			if (DEBUGCHECKS)
				if (duplex@ CoDuplex_ServerReadListHead + @)
					"CoDuplexEnqueueRequest: client write & server read collide\n" KeCrash
				end
			end

			duplex@ CoDuplex_ClientWriteListTail + @ t!

			t@ iop@ IOPacketHeader_DeviceQueuePrev + !

			if (t@ ~~)
				iop@ duplex@ CoDuplex_ClientWriteListHead + !
				duplex@ CoDuplex_TransmitBuffer + header!
			end else
				iop@ t@ IOPacketHeader_DeviceQueueNext + !
			end

			iop@ duplex@ CoDuplex_ClientWriteListTail + !
		end else
			if (DEBUGCHECKS)
				if (duplex@ CoDuplex_ServerWriteListHead + @)
					"CoDuplexEnqueueRequest: client read & server write collide\n" KeCrash
				end
			end

			duplex@ CoDuplex_ClientReadListTail + @ t!

			t@ iop@ IOPacketHeader_DeviceQueuePrev + !

			if (t@ ~~)
				iop@ duplex@ CoDuplex_ClientReadListHead + !
				duplex@ CoDuplex_ReceiveBuffer + header!
			end else
				iop@ t@ IOPacketHeader_DeviceQueueNext + !
			end

			iop@ duplex@ CoDuplex_ClientReadListTail + !
		end
	end

	if (header@)
		iopl@ IOPacketLocationMappedBuffer header@ CoDuplexBuffer_UserBuffer + !
		lengthdone@ header@ CoDuplexBuffer_UserBufferIndex + !
		iopl@ IOPacketLocation_Length + @ header@ CoDuplexBuffer_UserBufferLength + !
	end
end

const DONE_NORMAL 1
const DONE_CTRLD  2
const DONE_LF     4

fn CoDuplexCookedCopy { dest src sz -- bytecnt done }
	0 done!
	0 bytecnt!

	while (sz@)
		1 bytecnt +=

		if (src@ gb 4 ==) // ^D
			DONE_CTRLD done!
			return
		end

		src@ gb dest@ sb

		if (src@ gb '\n' ==)
			DONE_NORMAL done!
			return
		end

		1 dest +=
		1 src +=

		1 sz -=
	end
end

fn CoDuplexCookedCopyClient { dest src maxdest maxsrc duplex -- bufread bytecnt }
	0 bytecnt!
	0 bufread!

	while (1)
		if (bufread@ maxsrc@ >=)
			break
		end

		if (bytecnt@ maxdest@ >=)
			break
		end

		if (src@ gb '\n' ==)
			if (duplex@ CoDuplex_InternalFlags + @ DUPLEXFLAG_PENDING_LF &)
				DUPLEXFLAG_PENDING_LF ~ duplex@ CoDuplex_InternalFlags + &=

				'\n' dest@ sb
				1 bytecnt +=
				1 dest +=
			end else
				// needs to be turned into \r\n.

				if (maxdest@ bytecnt@ - 2 <)
					// not enough room in server buffer!
					// finish early so we don't lose this.

					'\r' dest@ sb

					DUPLEXFLAG_PENDING_LF duplex@ CoDuplex_InternalFlags + |=

					1 bytecnt +=

					return
				end

				'\r' dest@ sb
				'\n' dest@ 1 + sb

				2 dest +=
				2 bytecnt +=
			end
		end else
			DUPLEXFLAG_PENDING_LF ~ duplex@ CoDuplex_InternalFlags + &=

			src@ gb dest@ sb

			1 bytecnt +=
			1 dest +=
		end

		1 bufread +=
		1 src +=
	end
end

fn CoDuplexServerRead { iopl -- ok }
	auto flags
	iopl@ IOPacketLocation_Flags + @ flags!

	auto buf
	iopl@ IOPacketLocationMappedBuffer buf!

	auto duplex
	iopl@ IOPacketLocation_Context + @ duplex!

	auto iop
	iopl@ IOPacketFromLocation iop!

	// duplex is locked (which means IPL may be >=IPLDPC).
	// MDL is pinned.

	// server fcb, reading from the transmit buffer and/or client buffers.
	// this may pend.

	if (duplex@ CoDuplex_InternalFlags + @ DUPLEXFLAG_STOPPED &)
		if (iop@ IOPacketHeader_Timeout + @ ~~)
			0 iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

			0 ok!

			return
		end else
			// add to StoppedQueue

			duplex@ CoDuplex_StoppedQueueListHead + @ iop@ IOPacketHeader_DeviceQueueNext + !
			iop@ duplex@ CoDuplex_StoppedQueueListHead + !

			STATUS_OTHER_CONDITION ok!

			return
		end
	end

	if (flags@ OSACCESSFLAG_RETURNANY &)
		REQFLAG_RETURNANY flags!
	end else
		0 flags!
	end

	auto done
	0 done!

	0 ok!

	auto bytecnt
	0 bytecnt!

	if (duplex@ CoDuplex_ServerReadListHead + @)
		// if there's a pending request already, then that means the transmit
		// buffer and any client buffers have all been drained.

		if (iop@ IOPacketHeader_Timeout + @)
			STATUS_OTHER_CONDITION ok!
		end else
			0 iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

			0 ok!
		end
	end else
		// read as much as possible from the buffer. if this completes, cool.
		// if it does not complete, then we need to look for pending client
		// buffers. if that completes, cool. if not, we need to enqueue the
		// request.

		auto header
		duplex@ CoDuplex_TransmitBuffer + header!

		auto rp
		header@ CoDuplexBuffer_ReadIndex + @ rp!

		header@ CoDuplexBuffer_WriteIndex + @ rp@ - bytecnt! // bytes available

		if (iopl@ IOPacketLocation_Length + @ bytecnt@ <)
			iopl@ IOPacketLocation_Length + @ bytecnt!
		end

		if (bytecnt@)
			auto bufp
			header@ CoDuplexBuffer_Buffer + @ bufp!

			auto bufsize
			header@ CoDuplexBuffer_Size + @ bufsize!

			auto bufmask
			bufsize@ 1 - bufmask!

			if (rp@ bufmask@ ~ & rp@ bytecnt@ + 1 - bufmask@ ~ & ==)
				// doesn't cross the boundary, do a single copy

				buf@ // dest
				bufp@ rp@ bufmask@ & + // src
				bytecnt@ // sz
				memcpy
			end else
				// crosses the boundary, do two copies

				buf@ // dest
				bufp@ rp@ bufmask@ & + // src
				bufsize@ rp@ bufmask@ & - // sz
				memcpy

				buf@ bufsize@ rp@ bufmask@ & - + // dest
				bufp@ // src
				rp@ bytecnt@ + bufmask@ & // sz
				memcpy
			end

			rp@ bytecnt@ + header@ CoDuplexBuffer_ReadIndex + !
			bytecnt@ header@ CoDuplexBuffer_RemainingSpace + +=

			if (bytecnt@ iopl@ IOPacketLocation_Length + @ ==)
				DONE_NORMAL done |=
			end
		end

		// bytecnt now contains the amount of bytes that have been read.

		// try to complete as many client writes as possible by reading
		// from their buffer.

		while (done@ ~~)
			auto otheriop
			duplex@ CoDuplex_ClientWriteListHead + @ otheriop!

			if (otheriop@ ~~)
				break
			end

			auto otheriopl
			otheriop@ IOPacketCurrentLocation otheriopl!

			// calculate available bytes in buffer.

			header@ CoDuplexBuffer_UserBufferLength + @
			header@ CoDuplexBuffer_UserBufferIndex + @ - rp!

			if (iopl@ IOPacketLocation_Length + @ bytecnt@ - rp@ <)
				iopl@ IOPacketLocation_Length + @ bytecnt@ - rp!
			end

			auto bufread

			if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
				buf@ bytecnt@ + // dest
				header@ CoDuplexBuffer_UserBuffer + @
				header@ CoDuplexBuffer_UserBufferIndex + @ + // src
				rp@ // sz
				memcpy

				rp@ bufread!
			end else
				buf@ bytecnt@ + // dest
				header@ CoDuplexBuffer_UserBuffer + @
				header@ CoDuplexBuffer_UserBufferIndex + @ + // src
				iopl@ IOPacketLocation_Length + @ bytecnt@ - // maxdest
				header@ CoDuplexBuffer_UserBufferLength + @
				header@ CoDuplexBuffer_UserBufferIndex + @ - // maxsrc
				duplex@ // duplex
				CoDuplexCookedCopyClient rp! bufread!
			end

			bufread@ header@ CoDuplexBuffer_UserBufferIndex + +=

			rp@ bytecnt +=

			if (bytecnt@ iopl@ IOPacketLocation_Length + @ ==)
				DONE_NORMAL done |=
			end

			if (header@ CoDuplexBuffer_UserBufferIndex + @
				header@ CoDuplexBuffer_UserBufferLength + @ ==)

				// complete the client MDL.

				header@ CoDuplexBuffer_UserBufferIndex + @
				otheriop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

				// unlink the client request.

				auto niop
				otheriop@ IOPacketHeader_DeviceQueueNext + @ niop!

				0 // status
				0 // priboost
				otheriop@ // iop
				IOPacketCompleteLow

				niop@ otheriop!

				otheriop@ duplex@ CoDuplex_ClientWriteListHead + !

				if (otheriop@)
					otheriop@ IOPacketCurrentLocation otheriopl!

					0 otheriop@ IOPacketHeader_DeviceQueuePrev + !

					otheriopl@ IOPacketLocationMappedBuffer header@ CoDuplexBuffer_UserBuffer + !
					otheriopl@ IOPacketLocation_Offset + @ header@ CoDuplexBuffer_UserBufferIndex + !
					otheriopl@ IOPacketLocation_Length + @ header@ CoDuplexBuffer_UserBufferLength + !
				end else
					0 duplex@ CoDuplex_ClientWriteListTail + !
				end
			end
		end

		if (flags@ REQFLAG_RETURNANY &)
			if (bytecnt@)
				DONE_NORMAL done |=
				bytecnt@ iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
			end
		end

		if (done@ ~~)
			if (iop@ IOPacketHeader_Timeout + @)
				// switch to user buffer.

				STATUS_OTHER_CONDITION ok!
			end else
				bytecnt@ iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

				0 ok!
			end
		end
	end

	if (ok@ STATUS_OTHER_CONDITION ==)
		// enqueue the request at the tail of the appropriate queue.

		bytecnt@ // lengthdone
		REQFLAG_SERVER flags@ | // flags
		iopl@ // iopl
		CoDuplexEnqueueRequest
	end
end

fn CoDuplexClientRead { iopl -- ok }
	// duplex is locked (which means IPL may be >=IPLDPC).
	// MDL is pinned.

	// client fcb, reading from the receive buffer.
	// this may pend.

	auto flags
	iopl@ IOPacketLocation_Flags + @ flags!

	auto buf
	iopl@ IOPacketLocationMappedBuffer buf!

	auto duplex
	iopl@ IOPacketLocation_Context + @ duplex!

	auto iop
	iopl@ IOPacketFromLocation iop!

	if (flags@ OSACCESSFLAG_RETURNANY &)
		REQFLAG_RETURNANY flags!
	end else
		0 flags!
	end

	auto done
	0 done!

	0 ok!

	auto bytecnt
	0 bytecnt!

	if (duplex@ CoDuplex_ClientReadListHead + @)
		// if there's a pending request already, then that means the receive
		// buffer has been drained.

		if (iop@ IOPacketHeader_Timeout + @)
			STATUS_OTHER_CONDITION ok!
		end else
			0 iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

			0 ok!
		end
	end else
		// read as much as possible from the buffer. if this completes, cool.
		// if it does not complete, then we need to enqueue the request.

		auto header
		duplex@ CoDuplex_ReceiveBuffer + header!

		auto rp
		header@ CoDuplexBuffer_ReadIndex + @ rp!

		header@ CoDuplexBuffer_WriteIndex + @ rp@ - bytecnt! // bytes available

		if (iopl@ IOPacketLocation_Length + @ bytecnt@ <)
			iopl@ IOPacketLocation_Length + @ bytecnt!
		end

		if (bytecnt@)
			auto bufp
			header@ CoDuplexBuffer_Buffer + @ bufp!

			auto bufsize
			header@ CoDuplexBuffer_Size + @ bufsize!

			auto bufmask
			bufsize@ 1 - bufmask!

			if (rp@ bufmask@ ~ & rp@ bytecnt@ + 1 - bufmask@ ~ & ==)
				// doesn't cross the boundary, do a single copy

				if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
					buf@ // dest
					bufp@ rp@ bufmask@ & + // src
					bytecnt@ // sz
					memcpy
				end else
					buf@ // dest
					bufp@ rp@ bufmask@ & + // src
					bytecnt@ // sz
					CoDuplexCookedCopy done |= bytecnt!
				end
			end else
				// crosses the boundary, do two copies

				if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
					buf@ // dest
					bufp@ rp@ bufmask@ & + // src
					bufsize@ rp@ bufmask@ & - // sz
					memcpy

					buf@ bufsize@ rp@ bufmask@ & - + // dest
					bufp@ // src
					rp@ bytecnt@ + bufmask@ & // sz
					memcpy
				end else
					auto bc

					buf@ // dest
					bufp@ rp@ bufmask@ & + // src
					bufsize@ rp@ bufmask@ & - // sz
					CoDuplexCookedCopy done |= bc!

					if (done@ ~~)
						buf@ bufsize@ rp@ bufmask@ & - + // dest
						bufp@ // src
						rp@ bytecnt@ + bufmask@ & // sz
						CoDuplexCookedCopy done |= bc +=
					end

					bc@ bytecnt!
				end
			end

			if (done@ DONE_CTRLD &)
				if (bytecnt@ 1 >)
					rp@ bytecnt@ + 1 - header@ CoDuplexBuffer_ReadIndex + !
					bytecnt@ 1 - header@ CoDuplexBuffer_RemainingSpace + +=
				end else
					rp@ bytecnt@ + header@ CoDuplexBuffer_ReadIndex + !
					bytecnt@ header@ CoDuplexBuffer_RemainingSpace + +=
				end
			end else
				rp@ bytecnt@ + header@ CoDuplexBuffer_ReadIndex + !
				bytecnt@ header@ CoDuplexBuffer_RemainingSpace + +=
			end

			if (bytecnt@ iopl@ IOPacketLocation_Length + @ ==)
				DONE_NORMAL done |=
			end
		end

		// bytecnt now contains the amount of bytes that have been read.

		// try to complete as many server writes as possible by reading
		// from their buffer.

		while (done@ ~~)
			auto otheriop
			duplex@ CoDuplex_ServerWriteListHead + @ otheriop!

			if (otheriop@ ~~)
				break
			end

			auto otheriopl
			otheriop@ IOPacketCurrentLocation otheriopl!

			if (DEBUGCHECKS)
				if (duplex@ CoDuplex_Flags + @ OSDUPLEXFLAG_CONSOLE &)
					"CoDuplexClientRead: console read of enqueued server write\n" KeCrash
				end
			end

			// calculate available bytes in buffer.

			header@ CoDuplexBuffer_UserBufferLength + @
			header@ CoDuplexBuffer_UserBufferIndex + @ - rp!

			if (iopl@ IOPacketLocation_Length + @ bytecnt@ - rp@ <)
				iopl@ IOPacketLocation_Length + @ bytecnt@ - rp!
			end

			buf@ bytecnt@ + // dest
			header@ CoDuplexBuffer_UserBuffer + @
			header@ CoDuplexBuffer_UserBufferIndex + @ + // src
			rp@ // sz
			memcpy

			rp@ header@ CoDuplexBuffer_UserBufferIndex + +=

			rp@ bytecnt +=

			if (bytecnt@ iopl@ IOPacketLocation_Length + @ ==)
				DONE_NORMAL done |=
			end

			if (header@ CoDuplexBuffer_UserBufferIndex + @
				header@ CoDuplexBuffer_UserBufferLength + @ ==)

				// complete the server MDL.

				header@ CoDuplexBuffer_UserBufferIndex + @
				otheriop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

				// unlink the server request.

				auto niop
				otheriop@ IOPacketHeader_DeviceQueueNext + @ niop!

				0 // status
				duplex@ CoDuplex_ServerBoost + @ // priboost
				otheriop@ // iop
				IOPacketCompleteLow

				niop@ otheriop!

				otheriop@ duplex@ CoDuplex_ServerWriteListHead + !

				if (otheriop@)
					otheriop@ IOPacketCurrentLocation otheriopl!

					0 otheriop@ IOPacketHeader_DeviceQueuePrev + !

					otheriopl@ IOPacketLocationMappedBuffer header@ CoDuplexBuffer_UserBuffer + !
					otheriopl@ IOPacketLocation_Offset + @ header@ CoDuplexBuffer_UserBufferIndex + !
					otheriopl@ IOPacketLocation_Length + @ header@ CoDuplexBuffer_UserBufferLength + !
				end else
					0 duplex@ CoDuplex_ServerWriteListTail + !
				end
			end
		end

		if (flags@ REQFLAG_RETURNANY &)
			if (bytecnt@)
				DONE_NORMAL done |=
				bytecnt@ iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
			end
		end

		if (done@ ~~)
			if (iop@ IOPacketHeader_Timeout + @ duplex@ CoDuplex_Disconnected + @ ~~ &&)
				// switch to user buffer.

				STATUS_OTHER_CONDITION ok!
			end else
				bytecnt@ iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

				0 ok!
			end
		end else
			if (done@ DONE_CTRLD &)
				1 bytecnt -=
			end

			bytecnt@ iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
		end
	end

	if (ok@ STATUS_OTHER_CONDITION ==)
		// enqueue the request at the tail of the appropriate queue.

		bytecnt@ // lengthdone
		flags@ // flags
		iopl@ // iopl
		CoDuplexEnqueueRequest
	end
end

fn CoDuplexTXCharacter { c txfunc duplex -- done }
	auto ok

	0 done!

	if (duplex@ CoDuplex_InternalFlags + @ DUPLEXFLAG_STOPPED &)
		return
	end

	if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
		c@ // c
		duplex@ // duplex
		txfunc@ CoDuplexTXFunction ok!

		if (ok@ ~~)
			DONE_NORMAL done!
		end
	end elseif (c@ '\n' ==)
		if (duplex@ CoDuplex_InternalFlags + @ DUPLEXFLAG_PENDING_LF &)
			'\n' // c
			duplex@ // duplex
			txfunc@ CoDuplexTXFunction ok!

			if (ok@ ~~)
				DUPLEXFLAG_PENDING_LF ~ duplex@ CoDuplex_InternalFlags + &=
				DONE_NORMAL done!
			end
		end else
			'\r' // c
			duplex@ // duplex
			txfunc@ CoDuplexTXFunction ok!

			if (ok@ ~~)
				'\n' // c
				duplex@ // duplex
				txfunc@ CoDuplexTXFunction ok!

				if (ok@)
					DUPLEXFLAG_PENDING_LF duplex@ CoDuplex_InternalFlags + |=
				end else
					DONE_NORMAL done!
				end
			end
		end
	end else
		c@ // c
		duplex@ // duplex
		txfunc@ CoDuplexTXFunction ok!

		if (ok@ ~~)
			DONE_NORMAL done!
		end
	end
end

fn CoDuplexWriteCharacter { c duplex -- ok }
	auto ipl
	duplex@ // duplex
	1 // alertable
	CoDuplexLock ok! ipl!

	if (ok@)
		return
	end

	// client fcb, writing to the transmit buffer and/or server buffers.
	// this may pend.

	0 ok!

	// try to satisfy as many server reads as possible.
	// then resort to filling the transmit buffer.

	auto done
	0 done!

	if (duplex@ CoDuplex_TXFunc + @)
		c@ // c
		duplex@ CoDuplex_TXFunc + @ // txfunc
		duplex@ // duplex
		CoDuplexTXCharacter done |=
	end

	auto header
	duplex@ CoDuplex_TransmitBuffer + header!

	auto bufwritten
	1 bufwritten!

	auto rp

	while (done@ ~~)
		auto otheriop
		duplex@ CoDuplex_ServerReadListHead + @ otheriop!

		if (otheriop@ ~~)
			break
		end

		auto otheriopl
		otheriop@ IOPacketCurrentLocation otheriopl!

		if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
			c@
			header@ CoDuplexBuffer_UserBuffer + @
			header@ CoDuplexBuffer_UserBufferIndex + @ + sb

			DONE_NORMAL done!
		end elseif (c@ '\n' ==)
			if (duplex@ CoDuplex_InternalFlags + @ DUPLEXFLAG_PENDING_LF &)
				'\n'
				header@ CoDuplexBuffer_UserBuffer + @
				header@ CoDuplexBuffer_UserBufferIndex + @ + sb

				DUPLEXFLAG_PENDING_LF ~ duplex@ CoDuplex_InternalFlags + &=

				DONE_NORMAL done!
			end elseif (header@ CoDuplexBuffer_UserBufferLength + @
						header@ CoDuplexBuffer_UserBufferIndex + @ - 1 >)
				'\r'
				header@ CoDuplexBuffer_UserBuffer + @
				header@ CoDuplexBuffer_UserBufferIndex + @ + sb

				'\n'
				header@ CoDuplexBuffer_UserBuffer + @
				header@ CoDuplexBuffer_UserBufferIndex + @ + 1 + sb

				2 bufwritten!

				DONE_NORMAL done!
			end else
				'\r'
				header@ CoDuplexBuffer_UserBuffer + @
				header@ CoDuplexBuffer_UserBufferIndex + @ + sb

				DUPLEXFLAG_PENDING_LF duplex@ CoDuplex_InternalFlags + |=
			end
		end else
			c@
			header@ CoDuplexBuffer_UserBuffer + @
			header@ CoDuplexBuffer_UserBufferIndex + @ + sb

			DONE_NORMAL done!
		end

		bufwritten@ header@ CoDuplexBuffer_UserBufferIndex + +=

		if (header@ CoDuplexBuffer_UserBufferIndex + @
			header@ CoDuplexBuffer_UserBufferLength + @ ==
			otheriopl@ IOPacketLocation_Flags + @ REQFLAG_RETURNANY & ||)

			// complete the server MDL.

			header@ CoDuplexBuffer_UserBufferIndex + @
			otheriop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

			// unlink the server request.

			auto niop
			otheriop@ IOPacketHeader_DeviceQueueNext + @ niop!

			0 // status
			duplex@ CoDuplex_ServerBoost + @ // priboost
			otheriop@ // iop
			IOPacketCompleteLow

			niop@ otheriop!

			otheriop@ duplex@ CoDuplex_ServerReadListHead + !

			if (otheriop@)
				otheriop@ IOPacketCurrentLocation otheriopl!

				0 otheriop@ IOPacketHeader_DeviceQueuePrev + !

				otheriopl@ IOPacketLocationMappedBuffer header@ CoDuplexBuffer_UserBuffer + !
				otheriopl@ IOPacketLocation_Offset + @ header@ CoDuplexBuffer_UserBufferIndex + !
				otheriopl@ IOPacketLocation_Length + @ header@ CoDuplexBuffer_UserBufferLength + !
			end else
				0 duplex@ CoDuplex_ServerReadListTail + !
			end
		end
	end

	if (done@ ~~)
		// try to copy to the transmit buffer.

		auto bufp
		header@ CoDuplexBuffer_Buffer + @ bufp!

		header@ CoDuplexBuffer_RemainingSpace + @ rp!

		if (rp@)
			auto bufsize
			header@ CoDuplexBuffer_Size + @ bufsize!

			auto bufmask
			bufsize@ 1 - bufmask!

			auto index
			header@ CoDuplexBuffer_WriteIndex + @ index!

			if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
				c@
				bufp@ index@ bufmask@ & + sb

				DONE_NORMAL done!
			end elseif (c@ '\n' ==)
				if (duplex@ CoDuplex_InternalFlags + @ DUPLEXFLAG_PENDING_LF &)
					'\n'
					bufp@ index@ bufmask@ & + sb

					DUPLEXFLAG_PENDING_LF ~ duplex@ CoDuplex_InternalFlags + &=

					DONE_NORMAL done!
				end elseif (rp@ 1 >)
					'\r'
					bufp@ index@ bufmask@ & + sb

					'\n'
					bufp@ index@ 1 + bufmask@ & + sb

					2 bufwritten!

					DONE_NORMAL done!
				end
			end else
				c@
				bufp@ index@ bufmask@ & + sb

				DONE_NORMAL done!
			end

			index@ bufwritten@ + header@ CoDuplexBuffer_WriteIndex + !
			bufwritten@ header@ CoDuplexBuffer_RemainingSpace + -=
		end
	end

	ipl@ duplex@ CoDuplexUnlock
end

fn CoDuplexReadTransmitCharacter { duplex -- c ok }
	// duplex is locked (which means IPL may be >=IPLDPC).

	0 ok!

	if (duplex@ CoDuplex_InternalFlags + @ DUPLEXFLAG_STOPPED &)
		-1 ok!
		return
	end

	// read as much as possible from the buffer. if this completes, cool.
	// if it does not complete, then we need to look for pending client
	// buffers. if that completes, cool.

	auto header
	duplex@ CoDuplex_TransmitBuffer + header!

	auto rp
	header@ CoDuplexBuffer_ReadIndex + @ rp!

	if (header@ CoDuplexBuffer_WriteIndex + @ rp@ -)
		auto bufp
		header@ CoDuplexBuffer_Buffer + @ bufp!

		auto bufsize
		header@ CoDuplexBuffer_Size + @ bufsize!

		auto bufmask
		bufsize@ 1 - bufmask!

		bufp@ rp@ bufmask@ & + gb c!

		rp@ 1 + header@ CoDuplexBuffer_ReadIndex + !
		1 header@ CoDuplexBuffer_RemainingSpace + +=

		return
	end

	// try to complete as many client writes as possible by reading
	// from their buffer.

	auto iop
	duplex@ CoDuplex_ClientWriteListHead + @ iop!

	if (iop@)
		auto iopl
		iop@ IOPacketCurrentLocation iopl!

		if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
			header@ CoDuplexBuffer_UserBuffer + @
			header@ CoDuplexBuffer_UserBufferIndex + @ + gb c!

			1 header@ CoDuplexBuffer_UserBufferIndex + +=
		end else
			header@ CoDuplexBuffer_UserBuffer + @
			header@ CoDuplexBuffer_UserBufferIndex + @ + gb c!

			if (c@ '\n' ==)
				if (duplex@ CoDuplex_InternalFlags + @ DUPLEXFLAG_PENDING_LF &)
					'\n' c!
					1 header@ CoDuplexBuffer_UserBufferIndex + +=
					DUPLEXFLAG_PENDING_LF ~ duplex@ CoDuplex_InternalFlags + &=
				end else
					'\r' c!
					DUPLEXFLAG_PENDING_LF duplex@ CoDuplex_InternalFlags + |=
				end
			end else
				1 header@ CoDuplexBuffer_UserBufferIndex + +=
			end
		end

		if (header@ CoDuplexBuffer_UserBufferIndex + @
			header@ CoDuplexBuffer_UserBufferLength + @ ==)

			// complete the client MDL.

			header@ CoDuplexBuffer_UserBufferIndex + @
			iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

			// unlink the client request.

			auto niop
			iop@ IOPacketHeader_DeviceQueueNext + @ niop!

			0 // status
			0 // priboost
			iop@ // iop
			IOPacketCompleteLow

			niop@ iop!

			iop@ duplex@ CoDuplex_ClientWriteListHead + !

			if (iop@)
				iop@ IOPacketCurrentLocation iopl!

				0 iop@ IOPacketHeader_DeviceQueuePrev + !

				iopl@ IOPacketLocationMappedBuffer header@ CoDuplexBuffer_UserBuffer + !
				iopl@ IOPacketLocation_Offset + @ header@ CoDuplexBuffer_UserBufferIndex + !
				iopl@ IOPacketLocation_Length + @ header@ CoDuplexBuffer_UserBufferLength + !
			end else
				0 duplex@ CoDuplex_ClientWriteListTail + !
			end
		end

		return
	end

	-1 ok!
end

fn CoDuplexInputCharacter { c duplex -- ok }
	auto ipl
	duplex@ // duplex
	1 // alertable
	CoDuplexLock ok! ipl!

	if (ok@)
		return
	end

	auto iop
	auto niop
	auto iopl

	// duplex is locked (which means IPL may be >=IPLDPC).

	0 ok!

	if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW & ~~)
		if (c@ 3 ==) // ^C
			OSSIGNAL_INT // signal
			duplex@ CoDuplex_ConsoleHeader + // console
			CoConsoleSignal drop

			3 duplex@ CoDuplexEcho

			ipl@ duplex@ CoDuplexUnlock

			return
		end elseif (c@ 19 ==) // ^S
			DUPLEXFLAG_STOPPED duplex@ CoDuplex_InternalFlags + |=

			// satisfy any pending server read requests.

			duplex@ CoDuplex_ServerReadListHead + @ iop!

			while (iop@)
				if (iop@ duplex@ CoDuplex_ServerReadListHead + @ ==)
					duplex@ CoDuplex_TransmitBuffer + CoDuplexBuffer_UserBufferIndex + @
					iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
				end else
					0 iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
				end

				iop@ IOPacketHeader_DeviceQueueNext + @ niop!

				0 // status
				duplex@ CoDuplex_ServerBoost + @ // priboost
				iop@ // iop
				IOPacketCompleteLow

				niop@ iop!
			end

			0 duplex@ CoDuplex_ServerReadListHead + !
			0 duplex@ CoDuplex_ServerReadListTail + !

			ipl@ duplex@ CoDuplexUnlock

			return
		end
	end

	if (c@ 17 ==) // ^Q
		if (duplex@ CoDuplex_InternalFlags + @ DUPLEXFLAG_STOPPED &)
			DUPLEXFLAG_STOPPED ~ duplex@ CoDuplex_InternalFlags + &=

			if (duplex@ CoDuplex_FlushFunc + @)
				duplex@ // duplex
				duplex@ CoDuplex_FlushFunc + @ CoDuplexFlushFunction
			end

			// process the StoppedQueue of pending server requests.

			duplex@ CoDuplex_StoppedQueueListHead + @ iop!
			0 duplex@ CoDuplex_StoppedQueueListHead + !

			while (iop@)
				iop@ IOPacketHeader_DeviceQueueNext + @ niop!

				iop@ IOPacketCurrentLocation iopl!

				iopl@ CoDuplexServerRead ok!

				if (ok@ STATUS_OTHER_CONDITION ~=)
					0 // status
					duplex@ CoDuplex_ServerBoost + @ // priboost
					iop@ // iop
					IOPacketCompleteLow
				end

				niop@ iop!
			end

			0 ok!

			ipl@ duplex@ CoDuplexUnlock

			return
		end

		if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW & ~~)
			ipl@ duplex@ CoDuplexUnlock

			return
		end
	end

	// server fcb, writing to the receive buffer and/or client buffers.
	// if this is a console, this will not pend because the client can't be
	// expected to read characters promptly. otherwise it may pend.
	// also if it is a console, it will echo to the transmit buffer. if the
	// transmit buffer fills up, characters will be dropped.

	// try to satisfy as many client reads as possible.
	// then resort to filling the receive buffer. then, if we still have
	// characters, enqueue our own request.

	auto header
	duplex@ CoDuplex_ReceiveBuffer + header!

	auto done
	0 done!

	auto rp

	duplex@ CoDuplex_ClientReadListHead + @ iop!

	if (iop@)
		iop@ IOPacketCurrentLocation iopl!

		auto clientdone
		0 clientdone!

		if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
			c@
			header@ CoDuplexBuffer_UserBuffer + @
			header@ CoDuplexBuffer_UserBufferIndex + @ + sb

			1 header@ CoDuplexBuffer_UserBufferIndex + +=

			DONE_NORMAL done |=
		end elseif (c@ '\r' ==)
			'\n'
			header@ CoDuplexBuffer_UserBuffer + @
			header@ CoDuplexBuffer_UserBufferIndex + @ + sb

			1 header@ CoDuplexBuffer_UserBufferIndex + +=

			'\n' duplex@ CoDuplexEcho

			DONE_NORMAL clientdone |=
			DONE_NORMAL done |=
		end elseif (c@ '\b' ==)
			// backspace.

			if (header@ CoDuplexBuffer_UserBufferIndex + @)
				1 header@ CoDuplexBuffer_UserBufferIndex + -=

				header@ CoDuplexBuffer_UserBuffer + @
				header@ CoDuplexBuffer_UserBufferIndex + @ + gb // c
				duplex@ // duplex
				CoDuplexEchoRubout
			end

			DONE_NORMAL done |=
		end elseif (c@ 21 ==) // ^U
			// erase line.

			while (1)
				if (header@ CoDuplexBuffer_UserBufferIndex + @)
					1 header@ CoDuplexBuffer_UserBufferIndex + -=

					header@ CoDuplexBuffer_UserBuffer + @
					header@ CoDuplexBuffer_UserBufferIndex + @ + gb // c
					duplex@ // duplex
					CoDuplexEchoRubout
				end else
					break
				end
			end

			DONE_NORMAL done |=
		end elseif (c@ 4 ==) // ^D
			DONE_CTRLD clientdone |=
		end else
			c@
			header@ CoDuplexBuffer_UserBuffer + @
			header@ CoDuplexBuffer_UserBufferIndex + @ + sb

			1 header@ CoDuplexBuffer_UserBufferIndex + +=

			c@ duplex@ CoDuplexEcho

			DONE_NORMAL done |=
		end

		if (clientdone@ DONE_CTRLD &)
			if (header@ CoDuplexBuffer_UserBufferIndex + @ ~~)
				DONE_NORMAL done |=

				c@ duplex@ CoDuplexEcho
			end
		end

		if (header@ CoDuplexBuffer_UserBufferIndex + @
			header@ CoDuplexBuffer_UserBufferLength + @ ==)
			DONE_NORMAL clientdone |=
		end elseif (iopl@ IOPacketLocation_Flags + @ REQFLAG_RETURNANY &)
			DONE_NORMAL clientdone |=
		end

		if (clientdone@)
			// complete the client MDL.

			header@ CoDuplexBuffer_UserBufferIndex + @
			iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

			// unlink the client request.

			iop@ IOPacketHeader_DeviceQueueNext + @ niop!

			0 // status
			duplex@ CoDuplex_ClientBoost + @ // priboost
			iop@ // iop
			IOPacketCompleteLow

			niop@ iop!

			iop@ duplex@ CoDuplex_ClientReadListHead + !

			if (iop@)
				iop@ IOPacketCurrentLocation iopl!

				0 iop@ IOPacketHeader_DeviceQueuePrev + !

				iopl@ IOPacketLocationMappedBuffer header@ CoDuplexBuffer_UserBuffer + !
				iopl@ IOPacketLocation_Offset + @ header@ CoDuplexBuffer_UserBufferIndex + !
				iopl@ IOPacketLocation_Length + @ header@ CoDuplexBuffer_UserBufferLength + !
			end else
				0 duplex@ CoDuplex_ClientReadListTail + !
			end
		end
	end

	if (done@ ~~)
		// try to copy to the receive buffer.

		auto bufp
		header@ CoDuplexBuffer_Buffer + @ bufp!

		header@ CoDuplexBuffer_RemainingSpace + @ rp!

		auto allow
		0 allow!

		if (rp@)
			1 allow!
		end elseif (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW & ~~)
			if (c@ '\b' == c@ 21 == ||)
				1 allow!
			end
		end

		if (allow@)
			auto bufsize
			header@ CoDuplexBuffer_Size + @ bufsize!

			auto bufmask
			bufsize@ 1 - bufmask!

			auto index
			header@ CoDuplexBuffer_WriteIndex + @ index!

			if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
				c@
				bufp@ index@ bufmask@ & + sb

				index@ 1 + header@ CoDuplexBuffer_WriteIndex + !
				1 header@ CoDuplexBuffer_RemainingSpace + -=
			end elseif (c@ '\b' ==)
				if (header@ CoDuplexBuffer_RemainingSpace + @ bufsize@ <)
					if (bufp@ index@ 1 - bufmask@ & + gb '\n' ~=)
						index@ 1 - header@ CoDuplexBuffer_WriteIndex + !
						1 header@ CoDuplexBuffer_RemainingSpace + +=

						bufp@ index@ 1 - bufmask@ & + gb // c
						duplex@ // duplex
						CoDuplexEchoRubout
					end
				end
			end elseif (c@ 21 ==) // ^U
				while (header@ CoDuplexBuffer_RemainingSpace + @ bufsize@ <)
					if (bufp@ header@ CoDuplexBuffer_WriteIndex + @ 1 - bufmask@ & + gb '\n' ~=)
						1 header@ CoDuplexBuffer_WriteIndex + -=
						1 header@ CoDuplexBuffer_RemainingSpace + +=

						bufp@ header@ CoDuplexBuffer_WriteIndex + @ bufmask@ & + gb // c
						duplex@ // duplex
						CoDuplexEchoRubout
					end else
						break
					end
				end
			end elseif (c@ '\r' ==)
				'\n'
				bufp@ index@ bufmask@ & + sb

				index@ 1 + header@ CoDuplexBuffer_WriteIndex + !
				1 header@ CoDuplexBuffer_RemainingSpace + -=

				'\n' duplex@ CoDuplexEcho
			end else
				c@
				bufp@ index@ bufmask@ & + sb

				index@ 1 + header@ CoDuplexBuffer_WriteIndex + !
				1 header@ CoDuplexBuffer_RemainingSpace + -=

				c@ duplex@ CoDuplexEcho
			end
		end
	end

	ipl@ duplex@ CoDuplexUnlock
end

fn CoDuplexEcho { c duplex -- }
	if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_NOECHO &)
		return
	end

	1 duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Echoed + !

	if (c@ '\r' ==)
		'\r' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop
	end elseif (c@ '\n' ==)
		'\n' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop
	end elseif (c@ 0x20 <)
		'^' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop

		c@ 0x40 + // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop
	end elseif (c@ 0x7F <)
		c@ // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop
	end
end

fn CoDuplexEchoRubout { c duplex -- }
	if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_NOECHO &)
		return
	end

	1 duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Echoed + !

	if (c@ 0x20 <)
		'\b' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop

		' ' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop

		'\b' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop

		'\b' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop

		' ' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop

		'\b' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop
	end elseif (c@ 0x7F <)
		'\b' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop

		' ' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop

		'\b' // c
		duplex@ // duplex
		CoDuplexWriteCharacter drop
	end
end

fn CoDuplexServerWriteCooked { iopl -- ok }
	// duplex is locked (which means IPL may be >=IPLDPC).
	// MDL is pinned.

	auto len
	iopl@ IOPacketLocation_Length + @ len!

	auto duplex
	iopl@ IOPacketLocation_Context + @ duplex!

	auto buf
	iopl@ IOPacketLocationMappedBuffer buf!

	auto iop
	iopl@ IOPacketFromLocation iop!

	auto done
	0 done!

	while (len@)
		buf@ gb // c
		duplex@ // duplex
		CoDuplexInputCharacter ok!

		if (ok@)
			done@ iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

			return
		end

		1 done +=
		1 len -=
		1 buf +=
	end
end

fn CoDuplexServerWriteRaw { iopl -- ok }
	// duplex is locked (which means IPL may be >=IPLDPC).
	// MDL is pinned.

	// server fcb, writing to the receive buffer and/or client buffers.
	// if this is a console, this will not pend because the client can't be
	// expected to read characters promptly. otherwise it may pend.
	// also if it is a console, it will echo to the transmit buffer. if the
	// transmit buffer fills up, characters will be dropped.

	auto iop
	iopl@ IOPacketFromLocation iop!

	auto duplex
	iopl@ IOPacketLocation_Context + @ duplex!

	auto buf
	iopl@ IOPacketLocationMappedBuffer buf!

	0 ok!

	auto bytecnt
	0 bytecnt!

	auto done
	0 done!

	if (duplex@ CoDuplex_ServerWriteListHead + @)
		// there's already a pending request, so add ours to the end and return.

		0 // lengthdone
		REQFLAG_WRITE REQFLAG_SERVER | // flags
		iopl@ // iopl
		CoDuplexEnqueueRequest

		STATUS_OTHER_CONDITION ok!
	end else
		// try to satisfy as many client reads as possible.
		// then resort to filling the receive buffer. then, if we still have
		// characters, enqueue our own request.

		auto header
		duplex@ CoDuplex_ReceiveBuffer + header!

		auto rp

		while (done@ ~~)
			auto otheriop
			duplex@ CoDuplex_ClientReadListHead + @ otheriop!

			if (otheriop@ ~~)
				break
			end

			auto otheriopl
			otheriop@ IOPacketCurrentLocation otheriopl!

			// calculate available bytes in buffer.

			auto clientdone
			0 clientdone!

			header@ CoDuplexBuffer_UserBufferLength + @
			header@ CoDuplexBuffer_UserBufferIndex + @ - rp!

			if (iopl@ IOPacketLocation_Length + @ bytecnt@ - rp@ <)
				iopl@ IOPacketLocation_Length + @ bytecnt@ - rp!
			end

			header@ CoDuplexBuffer_UserBuffer + @
			header@ CoDuplexBuffer_UserBufferIndex + @ + // dest
			buf@ bytecnt@ + // src
			rp@ // sz
			memcpy

			rp@ header@ CoDuplexBuffer_UserBufferIndex + +=
			rp@ bytecnt +=

			if (bytecnt@ iopl@ IOPacketLocation_Length + @ ==)
				DONE_NORMAL done |=
			end

			if (header@ CoDuplexBuffer_UserBufferIndex + @
				header@ CoDuplexBuffer_UserBufferLength + @ ==)
				DONE_NORMAL clientdone |=
			end elseif (otheriopl@ IOPacketLocation_Flags + @ REQFLAG_RETURNANY &)
				DONE_NORMAL clientdone |=
			end

			if (clientdone@)
				// complete the client MDL.

				header@ CoDuplexBuffer_UserBufferIndex + @
				otheriop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

				// unlink the client request.

				auto niop
				otheriop@ IOPacketHeader_DeviceQueueNext + @ niop!

				0 // status
				duplex@ CoDuplex_ClientBoost + @ // priboost
				otheriop@ // iop
				IOPacketCompleteLow

				niop@ otheriop!

				otheriop@ duplex@ CoDuplex_ClientReadListHead + !

				if (otheriop@)
					otheriop@ IOPacketCurrentLocation otheriopl!

					0 otheriop@ IOPacketHeader_DeviceQueuePrev + !

					otheriopl@ IOPacketLocationMappedBuffer header@ CoDuplexBuffer_UserBuffer + !
					otheriopl@ IOPacketLocation_Offset + @ header@ CoDuplexBuffer_UserBufferIndex + !
					otheriopl@ IOPacketLocation_Length + @ header@ CoDuplexBuffer_UserBufferLength + !
				end else
					0 duplex@ CoDuplex_ClientReadListTail + !
				end
			end
		end

		if (done@ ~~)
			// try to copy to the receive buffer.

			auto bufp
			header@ CoDuplexBuffer_Buffer + @ bufp!

			header@ CoDuplexBuffer_RemainingSpace + @ rp!

			if (iopl@ IOPacketLocation_Length + @ bytecnt@ - rp@ <)
				iopl@ IOPacketLocation_Length + @ bytecnt@ - rp!
			end

			if (rp@)
				auto bufsize
				header@ CoDuplexBuffer_Size + @ bufsize!

				auto bufmask
				bufsize@ 1 - bufmask!

				auto index
				header@ CoDuplexBuffer_WriteIndex + @ index!

				auto endp
				index@ rp@ + endp!

				if (index@ bufmask@ ~ & endp@ 1 - bufmask@ ~ & ==)
					// doesn't cross the boundary, do a single copy

					bufp@ index@ bufmask@ & + // dest
					buf@ bytecnt@ + // src
					rp@ // sz
					memcpy
				end else
					// crosses the boundary, do two copies

					bufp@ index@ bufmask@ & + // dest
					buf@ bytecnt@ + // src
					bufsize@ index@ bufmask@ & - // sz
					memcpy

					bufp@ // dest
					buf@ bytecnt@ + bufsize@ index@ bufmask@ & - + // src
					endp@ bufmask@ & // sz
					memcpy
				end

				index@ rp@ + header@ CoDuplexBuffer_WriteIndex + !
				rp@ header@ CoDuplexBuffer_RemainingSpace + -=

				rp@ bytecnt +=

				if (bytecnt@ iopl@ IOPacketLocation_Length + @ ==)
					DONE_NORMAL done |=
				end
			end
		end

		if (done@ ~~)
			if (duplex@ CoDuplex_Flags + @ OSDUPLEXFLAG_CONSOLE & ~~)
				// enqueue the request.

				bytecnt@ // lengthdone
				REQFLAG_WRITE REQFLAG_SERVER | // flags
				iopl@ // iopl
				CoDuplexEnqueueRequest

				STATUS_OTHER_CONDITION ok!
			end else
				DONE_NORMAL done!
			end
		end

		if (done@)
			bytecnt@ iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
		end
	end
end

fn CoDuplexClientWrite { iopl -- ok }
	// duplex is locked (which means IPL may be >=IPLDPC).
	// MDL is pinned.

	// client fcb, writing to the transmit buffer and/or server buffers.
	// this may pend.

	auto iop
	iopl@ IOPacketFromLocation iop!

	auto duplex
	iopl@ IOPacketLocation_Context + @ duplex!

	auto buf
	iopl@ IOPacketLocationMappedBuffer buf!

	0 ok!

	auto bytecnt
	0 bytecnt!

	auto done
	0 done!

	if (duplex@ CoDuplex_ClientWriteListHead + @)
		// there's already a pending request, so add ours to the end and return.

		0 // lengthdone
		REQFLAG_WRITE // flags
		iopl@ // iopl
		CoDuplexEnqueueRequest

		STATUS_OTHER_CONDITION ok!
	end else
		auto txfunc
		duplex@ CoDuplex_TXFunc + @ txfunc!

		if (txfunc@)
			while (1)
				if (bytecnt@ iopl@ IOPacketLocation_Length + @ ==)
					DONE_NORMAL done |=
					break
				end

				buf@ bytecnt@ + gb // c
				txfunc@ // txfunc
				duplex@ // duplex
				CoDuplexTXCharacter ok!

				if (ok@ ~~)
					break
				end

				0 ok!

				1 bytecnt +=
			end
		end

		// try to satisfy as many server reads as possible.
		// then resort to filling the transmit buffer. then, if we still have
		// characters, enqueue our own request.

		auto header
		duplex@ CoDuplex_TransmitBuffer + header!

		auto rp

		while (done@ ~~)
			auto otheriop
			duplex@ CoDuplex_ServerReadListHead + @ otheriop!

			if (otheriop@ ~~)
				break
			end

			auto otheriopl
			otheriop@ IOPacketCurrentLocation otheriopl!

			// calculate available bytes in buffer.

			header@ CoDuplexBuffer_UserBufferLength + @
			header@ CoDuplexBuffer_UserBufferIndex + @ - rp!

			if (iopl@ IOPacketLocation_Length + @ bytecnt@ - rp@ <)
				iopl@ IOPacketLocation_Length + @ bytecnt@ - rp!
			end

			auto bufwritten

			if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
				header@ CoDuplexBuffer_UserBuffer + @
				header@ CoDuplexBuffer_UserBufferIndex + @ + // dest
				buf@ bytecnt@ + // src
				rp@ // sz
				memcpy

				rp@ bufwritten!
			end else
				header@ CoDuplexBuffer_UserBuffer + @
				header@ CoDuplexBuffer_UserBufferIndex + @ + // dest
				buf@ bytecnt@ + // src
				header@ CoDuplexBuffer_UserBufferLength + @
				header@ CoDuplexBuffer_UserBufferIndex + @ - // maxdest
				iopl@ IOPacketLocation_Length + @ bytecnt@ - // maxsrc
				duplex@ // duplex
				CoDuplexCookedCopyClient rp! bufwritten!
			end

			rp@ header@ CoDuplexBuffer_UserBufferIndex + +=

			bufwritten@ bytecnt +=

			if (bytecnt@ iopl@ IOPacketLocation_Length + @ ==)
				DONE_NORMAL done |=
			end

			if (header@ CoDuplexBuffer_UserBufferIndex + @
				header@ CoDuplexBuffer_UserBufferLength + @ ==
				otheriopl@ IOPacketLocation_Flags + @ REQFLAG_RETURNANY & ||)

				// complete the server MDL.

				header@ CoDuplexBuffer_UserBufferIndex + @
				otheriop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

				// unlink the server request.

				auto niop
				otheriop@ IOPacketHeader_DeviceQueueNext + @ niop!

				0 // status
				duplex@ CoDuplex_ServerBoost + @ // priboost
				otheriop@ // iop
				IOPacketCompleteLow

				niop@ otheriop!

				otheriop@ duplex@ CoDuplex_ServerReadListHead + !

				if (otheriop@)
					otheriop@ IOPacketCurrentLocation otheriopl!

					0 otheriop@ IOPacketHeader_DeviceQueuePrev + !

					otheriopl@ IOPacketLocationMappedBuffer header@ CoDuplexBuffer_UserBuffer + !
					otheriopl@ IOPacketLocation_Offset + @ header@ CoDuplexBuffer_UserBufferIndex + !
					otheriopl@ IOPacketLocation_Length + @ header@ CoDuplexBuffer_UserBufferLength + !
				end else
					0 duplex@ CoDuplex_ServerReadListTail + !
				end
			end
		end

		if (done@ ~~)
			// try to copy to the transmit buffer.

			auto bufp
			header@ CoDuplexBuffer_Buffer + @ bufp!

			header@ CoDuplexBuffer_RemainingSpace + @ rp!

			if (iopl@ IOPacketLocation_Length + @ bytecnt@ - rp@ <)
				iopl@ IOPacketLocation_Length + @ bytecnt@ - rp!
			end

			if (rp@)
				auto bufsize
				header@ CoDuplexBuffer_Size + @ bufsize!

				auto index
				header@ CoDuplexBuffer_WriteIndex + @ index!

				if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
					auto endp
					index@ rp@ + endp!

					if (index@ bufsize@ 1 - ~ & endp@ 1 - bufsize@ 1 - ~ & ==)
						// doesn't cross the boundary, do a single copy

						bufp@ index@ bufsize@ 1 - & + // dest
						buf@ bytecnt@ + // src
						rp@ // sz
						memcpy

						rp@ bufwritten!
					end else
						// crosses the boundary, do two copies

						bufp@ index@ bufsize@ 1 - & + // dest
						buf@ bytecnt@ + // src
						bufsize@ index@ bufsize@ 1 - & - // sz
						memcpy

						bufp@ // dest
						buf@ bytecnt@ + bufsize@ index@ bufsize@ 1 - & - + // src
						endp@ bufsize@ 1 - & // sz
						memcpy

						rp@ bufwritten!
					end
				end else
					// the geometry of what this does is 1-dimensional but
					// still too complicated for me to feel like writing a
					// comment explaining it.

					if (header@ CoDuplexBuffer_ReadIndex + @ bufsize@ 1 - & index@ bufsize@ 1 - & <)
						bufp@ index@ bufsize@ 1 - & + // dest
						buf@ bytecnt@ + // src
						bufsize@ index@ bufsize@ 1 - & - // maxdest
						iopl@ IOPacketLocation_Length + @ bytecnt@ - // maxsrc
						duplex@ // duplex
						CoDuplexCookedCopyClient rp! bufwritten!

						if (iopl@ IOPacketLocation_Length + @ bytecnt@ - bufwritten@ ~=)
							bufp@ // dest
							buf@ bytecnt@ + bufwritten@ + // src
							header@ CoDuplexBuffer_ReadIndex + @ bufsize@ 1 - & // maxdest
							iopl@ IOPacketLocation_Length + @ bytecnt@ - bufwritten@ - // maxsrc
							duplex@ // duplex
							CoDuplexCookedCopyClient rp += bufwritten +=
						end
					end else
						bufp@ index@ bufsize@ 1 - & + // dest
						buf@ bytecnt@ + // src
						header@ CoDuplexBuffer_ReadIndex + @ bufsize@ 1 - & index@ bufsize@ 1 - & - // maxdest
						iopl@ IOPacketLocation_Length + @ bytecnt@ - // maxsrc
						duplex@ // duplex
						CoDuplexCookedCopyClient rp! bufwritten!
					end
				end

				index@ rp@ + header@ CoDuplexBuffer_WriteIndex + !
				rp@ header@ CoDuplexBuffer_RemainingSpace + -=

				bufwritten@ bytecnt +=

				if (bytecnt@ iopl@ IOPacketLocation_Length + @ ==)
					DONE_NORMAL done |=
				end
			end
		end

		if (done@ ~~)
			// enqueue the request.

			bytecnt@ // lengthdone
			REQFLAG_WRITE // flags
			iopl@ // iopl
			CoDuplexEnqueueRequest

			STATUS_OTHER_CONDITION ok!
		end else
			bytecnt@ iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
		end
	end
end

fn (IODispatchEnqueueIOPFunction) CoDuplexRead { iopl -- done ok }
	0 ok!
	1 done!

	auto iop
	iopl@ IOPacketFromLocation iop!

	auto duplex
	iopl@ IOPacketLocation_FileControlBlock + @ IOFileControlBlockGetDeviceObject IODevice_Extension + @ duplex!

	duplex@ iopl@ IOPacketLocation_Context + !

	if (iopl@ IOPacketLocation_Length + @ ~~)
		0 // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end

	auto isserver
	iopl@ IOPacketLocation_FileControlBlock + @ IOFileControlBlockGetContext isserver!

	if (isserver@)
		if (duplex@ CoDuplex_Flags + @ OSDUPLEXFLAG_SIMPLEX &)
			STATUS_FORBIDDEN_OPERATION ok!

			ok@ // status
			0 // priboost
			iop@ // iop
			IOPacketCompleteLow

			return
		end
	end

	auto mdl
	iop@ IOPacketHeader_MDL + @ mdl!

	0 // lockforwrite
	mdl@ // mdl
	MmMDLPin ok!

	if (ok@)
		ok@ // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end

	mdl@ MmMDLMap ok!

	if (ok@)
		ok@ // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end

	auto ipl
	duplex@ // duplex
	1 // alertable
	CoDuplexLock ok! ipl!

	if (ok@)
		ok@ // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end

	if (isserver@)
		iopl@ CoDuplexServerRead ok!
	end else
		iopl@ CoDuplexClientRead ok!
	end

	ipl@ duplex@ CoDuplexUnlock

	if (ok@ STATUS_OTHER_CONDITION ~=)
		ok@ // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow
	end else
		// this means the request was enqueued successfully, so report success
		0 ok!
	end
end

fn (IODispatchEnqueueIOPFunction) CoDuplexWrite { iopl -- done ok }
	0 ok!
	1 done!

	auto iop
	iopl@ IOPacketFromLocation iop!

	auto duplex
	iopl@ IOPacketLocation_FileControlBlock + @ IOFileControlBlockGetDeviceObject IODevice_Extension + @ duplex!

	duplex@ iopl@ IOPacketLocation_Context + !

	if (iopl@ IOPacketLocation_Length + @ ~~)
		0 // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end

	auto isserver
	iopl@ IOPacketLocation_FileControlBlock + @ IOFileControlBlockGetContext isserver!

	if (isserver@ ~~)
		if (duplex@ CoDuplex_Flags + @ OSDUPLEXFLAG_SIMPLEX &)
			STATUS_FORBIDDEN_OPERATION ok!

			ok@ // status
			0 // priboost
			iop@ // iop
			IOPacketCompleteLow

			return
		end
	end

	auto mdl
	iop@ IOPacketHeader_MDL + @ mdl!

	0 // lockforwrite
	mdl@ // mdl
	MmMDLPin ok!

	if (ok@)
		ok@ // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end

	mdl@ MmMDLMap ok!

	if (ok@)
		ok@ // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end

	auto ipl
	duplex@ // duplex
	1 // alertable
	CoDuplexLock ok! ipl!

	if (ok@)
		ok@ // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end

	if (isserver@)
		if (duplex@ CoDuplex_Disconnected + @)
			ipl@ duplex@ CoDuplexUnlock

			0 // status
			0 // priboost
			iop@ // iop
			IOPacketCompleteLow

			0 ok!

			return
		end

		if (duplex@ CoDuplex_ConsoleHeader + CoConsoleHeader_Mode + @ OSCONSOLEMODE_RAW &)
			iopl@ CoDuplexServerWriteRaw ok!
		end else
			iopl@ CoDuplexServerWriteCooked ok!
		end
	end else
		if (duplex@ CoDuplex_Disconnected + @)
			ipl@ duplex@ CoDuplexUnlock

			STATUS_CONSOLE_HUNG_UP ok!

			ok@ // status
			0 // priboost
			iop@ // iop
			IOPacketCompleteLow

			return
		end

		iopl@ CoDuplexClientWrite ok!
	end

	ipl@ duplex@ CoDuplexUnlock

	if (ok@ STATUS_OTHER_CONDITION ~=)
		ok@ // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow
	end else
		// this means the request was enqueued successfully, so report success
		0 ok!
	end
end

fn (IODispatchCancelFunction) CoDuplexCancel { iop -- }
	auto iopl
	iop@ IOPacketCurrentLocation iopl!

	auto duplex
	iopl@ IOPacketLocation_Context + @ duplex!

	auto ipl
	duplex@ // duplex
	0 // alertable
	CoDuplexLock drop ipl!

	if (iop@ IOPacketHeader_IOPFlagsB + gb IOPFLAG_COMPLETE &)
		// already completed, nothing to do
		ipl@ duplex@ CoDuplexUnlock
		return
	end

	auto flags
	iopl@ IOPacketLocation_Flags + @ flags!

	auto header
	0 header!

	auto ls
	auto ns

	iop@ IOPacketHeader_DeviceQueuePrev + @ ls!
	iop@ IOPacketHeader_DeviceQueueNext + @ ns!

	if (flags@ REQFLAG_SERVER &)
		if (flags@ REQFLAG_WRITE &)
			if (ls@)
				ns@ ls@ IOPacketHeader_DeviceQueueNext + !
			end else
				ns@ duplex@ CoDuplex_ServerWriteListHead + !
				duplex@ CoDuplex_ReceiveBuffer + header!
			end

			if (ns@)
				ls@ ns@ IOPacketHeader_DeviceQueuePrev + !
			end else
				ls@ duplex@ CoDuplex_ServerWriteListTail + !
			end
		end else
			if (ls@)
				ns@ ls@ IOPacketHeader_DeviceQueueNext + !
			end else
				ns@ duplex@ CoDuplex_ServerReadListHead + !
				duplex@ CoDuplex_TransmitBuffer + header!
			end

			if (ns@)
				ls@ ns@ IOPacketHeader_DeviceQueuePrev + !
			end else
				ls@ duplex@ CoDuplex_ServerReadListTail + !
			end
		end
	end else
		if (flags@ REQFLAG_WRITE &)
			if (ls@)
				ns@ ls@ IOPacketHeader_DeviceQueueNext + !
			end else
				ns@ duplex@ CoDuplex_ClientWriteListHead + !
				duplex@ CoDuplex_TransmitBuffer + header!
			end

			if (ns@)
				ls@ ns@ IOPacketHeader_DeviceQueuePrev + !
			end else
				ls@ duplex@ CoDuplex_ClientWriteListTail + !
			end
		end else
			if (ls@)
				ns@ ls@ IOPacketHeader_DeviceQueueNext + !
			end else
				ns@ duplex@ CoDuplex_ClientReadListHead + !
				duplex@ CoDuplex_ReceiveBuffer + header!
			end

			if (ns@)
				ls@ ns@ IOPacketHeader_DeviceQueuePrev + !
			end else
				ls@ duplex@ CoDuplex_ClientReadListTail + !
			end
		end
	end

	if (header@ ns@ &&)
		auto nsiopl
		ns@ IOPacketCurrentLocation nsiopl!

		nsiopl@ IOPacketLocationMappedBuffer header@ CoDuplexBuffer_UserBuffer + !
		nsiopl@ IOPacketLocation_Offset + @ header@ CoDuplexBuffer_UserBufferIndex + !
		nsiopl@ IOPacketLocation_Length + @ header@ CoDuplexBuffer_UserBufferLength + !
	end

	ipl@ duplex@ CoDuplexUnlock

	STATUS_IO_CANCELLED // status
	0 // priboost
	iop@ // iop
	IOPacketCompleteLow
end

fn (IODispatchDeleteObjectFunction) CoDuplexDeleteFileObject { object -- ok }
	fnsection "PAGE$text"

	// the idea behind paging this routine being okay is that the cases where
	// the duplex is locked (thus, for nonpaged duplexes, raising IPL) won't
	// happen at all for nonpaged duplexes, since these are associated with
	// permanent devices such as serial ports.

	auto fcb
	object@ IOFile_FileControlBlock + @ fcb!

	auto deviceobject
	fcb@ IOFileControlBlockGetDeviceObject deviceobject!

	auto duplex
	deviceobject@ IODevice_Extension + @ duplex!

	auto ipl
	auto iop
	auto niop
	auto iopl

	if (fcb@ IOFileControlBlockGetContext)
		// server fcb! mark duplex headless and satisfy any pending requests.

		duplex@ // duplex
		0 // alertable
		CoDuplexLock drop ipl!

		1 duplex@ CoDuplex_Disconnected + !

		duplex@ CoDuplex_ClientWriteListHead + @ iop!

		while (iop@)
			iop@ IOPacketHeader_DeviceQueueNext + @ niop!

			STATUS_CONSOLE_HUNG_UP // status
			0 // priboost
			iop@ // iop
			IOPacketCompleteLow

			niop@ iop!
		end

		duplex@ CoDuplex_ClientReadListHead + @ iop!

		while (iop@)
			if (iop@ duplex@ CoDuplex_ClientReadListHead + @ ==)
				duplex@ CoDuplex_ReceiveBuffer + CoDuplexBuffer_UserBufferIndex + @
				iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
			end else
				0 iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
			end

			iop@ IOPacketHeader_DeviceQueueNext + @ niop!

			0 // status
			duplex@ CoDuplex_ClientBoost + @ // priboost
			iop@ // iop
			IOPacketCompleteLow

			niop@ iop!
		end

		0 duplex@ CoDuplex_ClientReadListHead + !
		0 duplex@ CoDuplex_ClientReadListTail + !

		0 duplex@ CoDuplex_ClientWriteListHead + !
		0 duplex@ CoDuplex_ClientWriteListTail + !

		ipl@ duplex@ CoDuplexUnlock

		if (deviceobject@ IODevice_ConsoleHeader + @)
			OSSIGNAL_HUP // signal
			deviceobject@ IODevice_ConsoleHeader + @ // console
			CoConsoleSignal drop
		end
	end elseif (deviceobject@ ObObjectReferences 2 <=)
		if (duplex@ CoDuplex_Flags + @ OSDUPLEXFLAG_SIMPLEX &)
			// last client is being closed and this is a pipe, satisfy server
			// write requests.

			duplex@ // duplex
			0 // alertable
			CoDuplexLock drop ipl!

			1 duplex@ CoDuplex_Disconnected + !

			duplex@ CoDuplex_ServerWriteListHead + @ iop!

			while (iop@)
				if (iop@ duplex@ CoDuplex_ServerWriteListHead + @ ==)
					duplex@ CoDuplex_ReceiveBuffer + CoDuplexBuffer_UserBufferIndex + @
					iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
				end else
					0 iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
				end

				iop@ IOPacketHeader_DeviceQueueNext + @ niop!

				0 // status
				duplex@ CoDuplex_ServerBoost + @ // priboost
				iop@ // mdl
				IOPacketCompleteLow

				niop@ iop!
			end

			0 duplex@ CoDuplex_ServerWriteListHead + !
			0 duplex@ CoDuplex_ServerWriteListTail + !

			ipl@ duplex@ CoDuplexUnlock
		end
	end

	// forward to generic function
	object@ IODeviceDeleteFileObject ok!
end

fn (IODispatchDeleteDeviceObjectFunction) CoDuplexDeleteObject { object -- }
	fnsection "PAGE$text"

	auto duplex
	object@ IODevice_Extension + @ duplex!

	-1 // writeout
	duplex@ CoDuplex_ServerFCB + @ // fcb
	IOFileControlBlockDelete drop

	duplex@ CoDuplex_Paged + @ // pri
	duplex@ CoDuplex_QuotaBlock + @ // quotablock
	duplex@ CoDuplex_ReceiveBuffer + // header
	CoDuplexBufferUninitialize

	if (duplex@ CoDuplex_Flags + @ OSDUPLEXFLAG_SIMPLEX & ~~)
		duplex@ CoDuplex_Paged + @ // pri
		duplex@ CoDuplex_QuotaBlock + @ // quotablock
		duplex@ CoDuplex_TransmitBuffer + // header
		CoDuplexBufferUninitialize
	end

	duplex@ CoDuplex_QuotaBlock + @ MmQuotaBlockDereference
end