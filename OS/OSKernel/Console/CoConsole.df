//
// Implements the console device object.
// Behaves similarly to a unix-like TTY.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALArgs.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALDebug.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<inc>/Console.h"

#include "<ll>/OSDLL/OS.h"

#include "<ll>/OSDLL/OSConsoleControl.h"

fn CoConsoleInitialize { header -- }
	fnsection "PAGE$text"

	0 header@ CoConsoleHeader_ProcessListHead + !
	0 header@ CoConsoleHeader_Mode + !

	0 header@ CoConsoleHeader_Echoed + !

	80 header@ CoConsoleHeader_WidthI + si
	25 header@ CoConsoleHeader_HeightI + si
end

fn CoConsoleSignal { signal console -- }
	auto process

	auto ipl
	IPLDPC KeIPLRaise ipl!

	console@ CoConsoleHeader_ProcessListHead + @ process!

	while (process@)
		signal@ // signal
		process@ // process
		KeProcessSignal drop

		process@ PsProcess_ConsoleListNext + @ process!
	end

	ipl@ KeIPLLower
end

fn CoConsoleRemoveProcess { process -- ok }
	auto ipl
	IPLDPC KeIPLRaise ipl!

	auto consoleobject
	process@ PsProcess_ConsoleObject + @ consoleobject!

	if (consoleobject@ ~~)
		ipl@ KeIPLLower
		STATUS_NO_CONSOLE ok!
		return
	end

	auto console
	consoleobject@ IODevice_ConsoleHeader + @ console!

	0 ok!

	auto ls
	process@ PsProcess_ConsoleListPrev + @ ls!

	auto ns
	process@ PsProcess_ConsoleListNext + @ ns!

	if (ls@)
		ns@ ls@ PsProcess_ConsoleListNext + !
	end else
		ns@ console@ CoConsoleHeader_ProcessListHead + !
	end

	if (ns@)
		ls@ ns@ PsProcess_ConsoleListPrev + !
	end

	0 process@ PsProcess_ConsoleObject + !

	ipl@ KeIPLLower

	consoleobject@ ObObjectDereferenceByPointer drop
end

fn CoConsoleInsertProcess { process consoleobject -- ok }
	auto ipl
	IPLDPC KeIPLRaise ipl!

	if (process@ PsProcess_ConsoleObject + @)
		ipl@ KeIPLLower
		STATUS_HAS_CONSOLE ok!
		return
	end

	consoleobject@ process@ PsProcess_ConsoleObject + !

	auto console
	consoleobject@ IODevice_ConsoleHeader + @ console!

	auto h
	console@ CoConsoleHeader_ProcessListHead + @ h!

	if (h@ ~~)
		0 process@ PsProcess_ConsoleListNext + !
		0 process@ PsProcess_ConsoleListPrev + !

		process@ console@ CoConsoleHeader_ProcessListHead + !
	end else
		0 process@ PsProcess_ConsoleListPrev + !

		h@ process@ PsProcess_ConsoleListNext + !
		process@ h@ PsProcess_ConsoleListPrev + !
		process@ console@ CoConsoleHeader_ProcessListHead + !
	end

	ipl@ KeIPLLower

	consoleobject@ ObObjectReferenceByPointer drop
end

table CoCtrl
	'@'
	'A' 'B' 'C'
	'D' 'E' 'F'
	'G' 'H' 'I'
	'J' 'K' 'L'
	'M' 'N' 'O'
	'P' 'Q' 'R'
	'S' 'T' 'U'
	'V' 'W' 'X'
	'Y' 'Z'
	'['
	'\\'
	']'
	'^'
	'_'
	' '
endtable
public CoCtrl