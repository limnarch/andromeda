//
// Implements IO transfer helper functions.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

// These should be used when the caller has little information on the
// parameters. These may be pretty terrible in some places where situation-
// specific information is handily available.

fn IORead { timeout flags kflags length offset buffer fcb lastmode -- bytesread ok }
	auto mdl
	MmMDLHeader_SIZEOF IOTRANSFERMAX PAGESHIFT >> 1 + 4 * + alloc mdl!

	auto readfunc
	fcb@ IOFileControlBlock_DispatchTable + @ IODispatchTable_Read + @ readfunc!

	0 bytesread!

	while (length@ IOTRANSFERMAX >=)
		lastmode@ // mode
		IOTRANSFERMAX // length
		buffer@ // vaddr
		mdl@ // mdl
		MmMDLInitialize

		auto chunkread
		timeout@ // timeout
		flags@ // flags
		kflags@ // kflags
		offset@ // offset
		mdl@ // mdl
		fcb@ // fcb
		readfunc@ IODispatchReadFunction ok! chunkread!

		if (ok@)
			0 length!
			break
		end

		chunkread@ bytesread +=

		if (chunkread@ IOTRANSFERMAX <)
			0 length!
			break
		end

		IOTRANSFERMAX buffer +=
		IOTRANSFERMAX length -=
	end

	if (length@)
		lastmode@ // mode
		length@ // length
		buffer@ // vaddr
		mdl@ // mdl
		MmMDLInitialize

		timeout@ // timeout
		flags@ // flags
		kflags@ // kflags
		offset@ // offset
		mdl@ // mdl
		fcb@ // fcb
		readfunc@ IODispatchReadFunction ok! bytesread +=
	end
end

fn IOWrite { flags kflags length offset buffer fcb lastmode -- byteswritten ok }
	auto mdl
	MmMDLHeader_SIZEOF IOTRANSFERMAX PAGESHIFT >> 1 + 4 * + alloc mdl!

	auto writefunc
	fcb@ IOFileControlBlock_DispatchTable + @ IODispatchTable_Write + @ writefunc!

	0 byteswritten!

	while (length@ IOTRANSFERMAX >=)
		lastmode@ // mode
		IOTRANSFERMAX // length
		buffer@ // vaddr
		mdl@ // mdl
		MmMDLInitialize

		auto chunkwritten
		flags@ // flags
		kflags@ // kflags
		offset@ // offset
		mdl@ // mdl
		fcb@ // fcb
		writefunc@ IODispatchWriteFunction ok! chunkwritten!

		if (ok@)
			0 length!
			break
		end

		chunkwritten@ byteswritten +=

		if (chunkwritten@ IOTRANSFERMAX <)
			0 length!
			break
		end

		IOTRANSFERMAX buffer +=
		IOTRANSFERMAX length -=
	end

	if (length@)
		lastmode@ // mode
		length@ // length
		buffer@ // vaddr
		mdl@ // mdl
		MmMDLInitialize

		flags@ // flags
		kflags@ // kflags
		offset@ // offset
		mdl@ // mdl
		fcb@ // fcb
		writefunc@ IODispatchWriteFunction ok! byteswritten +=
	end
end