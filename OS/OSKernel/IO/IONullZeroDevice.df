//
// Implements the null and zero device drivers.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALInterrupt.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/IO.h"

fn IONullInit { -- }
	auto ok
	"null" // name
	0 // sizeinbytes
	IODriverNull // driver
	ACCESS_OWNER_READ ACCESS_OWNER_WRITE | ACCESS_GROUP_READ | ACCESS_GROUP_WRITE | ACCESS_WORLD_READ | ACCESS_WORLD_WRITE | // permissions
	IODeviceCreate ok! drop

	if (ok@)
		ok@ "IONullInit: failed to create device object (%i)\n" KeCrash
	end

	"zero" // name
	0 // sizeinbytes
	IODriverZero // driver
	ACCESS_OWNER_READ ACCESS_OWNER_WRITE | ACCESS_GROUP_READ | ACCESS_GROUP_WRITE | ACCESS_WORLD_READ | ACCESS_WORLD_WRITE | // permissions
	IODeviceCreate ok! drop

	if (ok@)
		ok@ "IONullInit: failed to create device object (%i)\n" KeCrash
	end
end

// null driver

table IODriverNullDispatch
	0                                    // open
	0                                    // close
	0                                    // iocontrol
	pointerof IONullRead                 // read
	pointerof IONullWrite                // write
	0                                    // system control
	0                                    // parse
	0                                    // create
	0                                    // flush
	pointerof IODeviceDeleteFileObject   // delete object
	0                                    // set information
	0                                    // get information
	0                                    // rename
	0                                    // readblock
	0                                    // writeblock
	0                                    // truncate
	0                                    // readdirectory
	0                                    // getpageaddr
	0                                    // delete device object
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
endtable

table IODriverNull
	IOVERSION_MAJOR                      // ioversion major
	IOVERSION_MINOR                      // ioversion minor

	"null"                               // name
	OSFILETYPE_CHARDEVICE                // type
	pointerof IODriverNullDispatch       // dispatch table
	0                                    // extension size

	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
endtable

fn (IODispatchReadFunction) IONullRead { timeout flags kflags offset mdl fcb -- bytesread ok }
	0 bytesread!
	0 ok!
end

fn (IODispatchWriteFunction) IONullWrite { flags kflags offset mdl fcb -- byteswritten ok }
	mdl@ MmMDLHeader_Length + @ byteswritten!
	0 ok!
end

// zero driver

table IODriverZeroDispatch
	0                                    // open
	0                                    // close
	0                                    // iocontrol
	pointerof IOZeroRead                 // read
	pointerof IOZeroWrite                // write
	0                                    // system control
	0                                    // parse
	0                                    // create
	0                                    // flush
	pointerof IODeviceDeleteFileObject   // delete object
	0                                    // set information
	0                                    // get information
	0                                    // rename
	0                                    // readblock
	0                                    // writeblock
	0                                    // truncate
	0                                    // readdirectory
	0                                    // getpageaddr
	0                                    // delete device object
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
endtable

table IODriverZero
	IOVERSION_MAJOR                      // ioversion major
	IOVERSION_MINOR                      // ioversion minor

	"zero"                               // name
	OSFILETYPE_CHARDEVICE                // type
	pointerof IODriverZeroDispatch       // dispatch table
	0                                    // extension size

	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
endtable

fn (IODispatchReadFunction) IOZeroRead { timeout flags kflags offset mdl fcb -- bytesread ok }
	1 mdl@ MmMDLPin ok!

	if (ok@)
		return
	end

	mdl@ MmMDLMap ok!

	if (ok@)
		mdl@ MmMDLUnpin
		return
	end

	mdl@ MmMDLHeader_MappedAddress + @ // ptr
	mdl@ MmMDLHeader_Length + @ // size
	0 // word
	memset

	mdl@ MmMDLUnmap
	mdl@ MmMDLUnpin

	mdl@ MmMDLHeader_Length + @ bytesread!
	0 ok!
end

fn (IODispatchWriteFunction) IOZeroWrite { flags kflags offset mdl fcb -- byteswritten ok }
	mdl@ MmMDLHeader_Length + @ byteswritten!
	0 ok!
end