//
// Implements the file control block (FCB) management routines and cache.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALDriver.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

#include "IOInternal.h"

fn IOiFileControlBlockInitialize { dispatchtable devobj filetype flags fcb -- }
	fnsection "PAGE$text"

	devobj@ fcb@ IOFileControlBlock_DeviceObject + !
	filetype@ fcb@ IOFileControlBlock_FileType + !

	flags@ fcb@ IOFileControlBlock_Flags + !
	0 fcb@ IOFileControlBlock_CacheInfoBlock + !
	0 fcb@ IOFileControlBlock_References + !
	0 fcb@ IOFileControlBlock_SizeInBytes + !
	dispatchtable@ fcb@ IOFileControlBlock_DispatchTable + !
	0 fcb@ IOFileControlBlock_FSContext + !
	0 fcb@ IOFileControlBlock_Mount + !

	0 fcb@ IOFileControlBlock_AsyncIOCount + !

	1 // signaled
	OSEVENT_NOTIF // type
	"FCBAsyncEvent" // name
	fcb@ IOFileControlBlock_AsyncIOEvent + // event
	KeEventInitialize

	"FCBRwLock" // name
	fcb@ IOFileControlBlock_RwLock + // rwlock
	ExRwLockInitialize

	fcb@ IOFileControlBlock_AccessTime + // ptr
	KeTime_SIZEOF // size
	0 // word
	memset

	fcb@ IOFileControlBlock_ModifyTime + // ptr
	KeTime_SIZEOF // size
	0 // word
	memset

	fcb@ IOFileControlBlock_ChangeTime + // ptr
	KeTime_SIZEOF // size
	0 // word
	memset
end

fn IOFileControlBlockIncrementReclaimable { -- }
	1 IOFileControlBlockCount KeInterlockedIncrement drop

	if (IOFileControlBlockCount@ IOFileControlBlockMaximum@ >=)
		0 // priboost
		IOiFileControlBlockCacheTrimEvent // event
		KeEventSignal
	end
end

fn IOFileControlBlockDecrementReclaimable { -- }
	-1 IOFileControlBlockCount KeInterlockedIncrement drop
end

fn IOFileControlBlockThrottle { -- }
	fnsection "PAGE$text"

	if (IOFileControlBlockCount@ IOiFileControlBlockThrottleCount@ >=)
		0 // priboost
		IOiFileControlBlockCacheTrimEvent // event
		KeEventSignal

		100 // ms
		KERNELMODE // waitmode
		0 // alertable
		KeThreadSleep drop
	end
end

fn IOFileControlBlockCreate { dispatchtable devobj filetype flags -- fcb ok }
	fnsection "PAGE$text"

	IOFileControlBlock_SIZEOF // bytes
	'IOFC' // tag
	CANBLOCK // flags
	MmAllocWithTag ok! fcb!

	if (ok@)
		return
	end

	dispatchtable@ // dispatchtable
	devobj@ // devobj
	filetype@ // filetype
	flags@ // flags
	fcb@ // fcb
	IOiFileControlBlockInitialize
end

fn IOFileControlBlockFlush { fcb -- ok }
	fnsection "PAGE$text"

	auto cacheblock
	fcb@ IOFileControlBlock_CacheInfoBlock + @ cacheblock!

	0 ok!

	if (cacheblock@)
		cacheblock@ IOCacheInfoBlockFlush ok!
	end
end

fn IOiFileControlBlockReclaim { writeout fcb -- ok }
	fnsection "PAGE$text"

	auto cacheblock
	fcb@ IOFileControlBlock_CacheInfoBlock + @ cacheblock!

	if (cacheblock@)
		if (DEBUGCHECKS)
			if (writeout@ -1 ==)
				"IOiFileControlBlockReclaim: didn't expect a cacheblock\n" KeCrash
			end
		end

		writeout@ // writeout
		cacheblock@ // cacheblock
		IOiCacheInfoBlockDestroy ok!

		if (ok@)
			return
		end
	end

	fcb@ IOFileControlBlock_RwLock + ExRwLockUninitialize
end

fn IOFileControlBlockDelete { writeout fcb -- ok }
	fnsection "PAGE$text"

	// caller should be 100% sure this FCB won't be used again.
	// it also should have removed it from the reusable list by now.

	writeout@ // writeout
	fcb@ // fcb
	IOiFileControlBlockReclaim ok!

	if (ok@)
		return
	end

	fcb@ MmFree
end

fn IOFileControlBlockLock { fcb -- ok }
	fnsection "PAGE$text"

	1 // alertable
	0 // nowait
	fcb@ IOFileControlBlock_RwLock + // rwlock
	ExRwLockAcquireExclusive ok!
end

fn IOFileControlBlockDemoteToShared { fcb -- ok }
	fnsection "PAGE$text"

	fcb@ IOFileControlBlock_RwLock + // rwlock
	ExRwLockDemoteToShared ok!
end

fn IOFileControlBlockLockShared { fcb -- ok }
	fnsection "PAGE$text"

	1 // alertable
	0 // nowait
	0 // canstarve
	fcb@ IOFileControlBlock_RwLock + // rwlock
	ExRwLockAcquireShared ok!
end

fn IOFileControlBlockTryLock { fcb -- ok }
	0 // alertable
	1 // nowait
	fcb@ IOFileControlBlock_RwLock + // rwlock
	ExRwLockAcquireExclusive ok!

	if (ok@ z<)
		0 ok!
	end
end

fn IOFileControlBlockTryLockShared { fcb -- ok }
	0 // alertable
	1 // nowait
	0 // canstarve
	fcb@ IOFileControlBlock_RwLock + // rwlock
	ExRwLockAcquireShared ok!

	if (ok@ z<)
		0 ok!
	end
end

fn IOFileControlBlockUnlock { fcb -- }
	fcb@ IOFileControlBlock_RwLock + ExRwLockRelease
end

fn IOFileControlBlockDeleteLastReference { fcb -- }
	auto rs
	HALCPUInterruptDisable rs!
	IOFCBFLAG_DELETELASTREFERENCE fcb@ IOFileControlBlock_Flags + |=
	rs@ HALCPUInterruptRestore
end

fn IOFileControlBlockRescue { fcb -- }
	auto rs
	HALCPUInterruptDisable rs!
	IOFCBFLAG_DELETELASTREFERENCE ~ fcb@ IOFileControlBlock_Flags + &=
	rs@ HALCPUInterruptRestore
end

fn IOFileControlBlockReference { fcb -- oldcount }
	// should be called by filesystem driver in its parse method.
	// the circumstances in which this is called, and the interactions it has
	// with the filesystem driver's reclamation structures, should be very
	// well understood.

	auto rs
	HALCPUInterruptDisable rs!

	fcb@ IOFileControlBlock_References + @ oldcount!
	oldcount@ 1 + fcb@ IOFileControlBlock_References + !

	rs@ HALCPUInterruptRestore
end

fn IOFileControlBlockDereference { fcb -- oldcount }
	// should be called by filesystem driver in its delete method.

	while (1)
		auto rs
		HALCPUInterruptDisable rs!

		fcb@ IOFileControlBlock_References + @ oldcount!

		if (DEBUGCHECKS)
			if (oldcount@ 0 ==)
				"IOFileControlBlockDereference: refcount underflow\n" KeCrash
			end
		end

		if (oldcount@ 1 ==)
			if (fcb@ IOFileControlBlock_Flags + @ IOFCBFLAG_DELETELASTREFERENCE &)
				rs@ HALCPUInterruptRestore

				// this FCB was marked for deferred deletion, so instead of
				// reclaiming it or whatever else, we need to call the filesystem
				// driver so that it can delete the file on disk.
				//
				// assumes that the filesystem already guaranteed that the
				// refcount on this FCB can never be incremented again, by
				// removing its name from the directory tree or through some other
				// mechanism.

				KeThreadCurrent@ KeThreadIgnoreKill drop

				auto cacheblock
				fcb@ IOFileControlBlock_CacheInfoBlock + @ cacheblock!

				if (cacheblock@)
					0 // writeout
					cacheblock@ // cacheblock
					IOiCacheInfoBlockDestroy drop

					0 fcb@ IOFileControlBlock_CacheInfoBlock + !
				end

				fcb@ // fcb
				fcb@ IOFileControlBlock_DispatchTable + @ IODispatchTable_Delete + @
				IODispatchDeleteFunction

				KeThreadCurrent@ KeThreadAcceptKill drop

				0 // writeout
				fcb@ // fcb
				IOFileControlBlockDelete drop

				return
			end elseif (fcb@ IOFileControlBlock_DispatchTable + @ IODispatchTable_ZeroFCBReferences + @)
				rs@ HALCPUInterruptRestore

				// the filesystem driver wants to know when an FCB reaches zero
				// references, so instead of directly setting the refcount to
				// 0, leave it biased and call into this function to do some
				// work. if it asks us to try again, we loop back to the start
				// of this function. it will do this if it fails to set the
				// refcount to zero by itself for some reason, such as the
				// refcount being incremented inbetween now and when it locks
				// its own structures.

				auto tryagain

				KeThreadCurrent@ KeThreadIgnoreKill drop

				fcb@ // fcb
				fcb@ IOFileControlBlock_DispatchTable + @ IODispatchTable_ZeroFCBReferences + @
				IODispatchZeroFCBReferencesFunction tryagain!

				KeThreadCurrent@ KeThreadAcceptKill drop

				if (tryagain@)
					continue
				end

				return
			end else
				oldcount@ 1 - fcb@ IOFileControlBlock_References + !

				rs@ HALCPUInterruptRestore

				return
			end
		end else
			oldcount@ 1 - fcb@ IOFileControlBlock_References + !

			rs@ HALCPUInterruptRestore

			return
		end
	end
end

fn IOFileControlBlockCacheCheck { fcb -- cacheblock ok }
	fnsection "PAGE$text"

	fcb@ IOFileControlBlockLock ok!

	if (ok@)
		return
	end

	fcb@ IOFileControlBlock_CacheInfoBlock + @ cacheblock!

	if (cacheblock@ ~~)
		// still no cache block, plus we want there to be one.
		// therefore, initiate caching

		fcb@ // fcb
		IOiCacheInitialize ok! cacheblock!

		if (ok@)
			fcb@ IOFileControlBlockUnlock

			return
		end
	end

	fcb@ IOFileControlBlockUnlock
end

fn IOFileControlBlockTruncate { newsize growing keeplocked zero flags fcb -- oldsize ok }
	fnsection "PAGE$text"

	if (fcb@ IOFileControlBlock_FileType + @ OSFILETYPE_DIRECTORY ==)
		STATUS_IS_A_DIRECTORY ok!
		return
	end

	auto truncfunc
	fcb@ IOFileControlBlock_DispatchTable + @ IODispatchTable_Truncate + @ truncfunc!

	if (truncfunc@ ~~)
		STATUS_NOT_SUPPORTED ok!
		return
	end

	fcb@ IOFileControlBlockLock ok!

	if (ok@)
		return
	end

	if (fcb@ IOFileControlBlock_AsyncIOCount + @)
		// wait for async IO to complete. no more can be enqueued after this
		// value reaches zero because we are holding the rwlock exclusive.

		KERNELMODE // waitmode
		0 // alertable
		OSWAIT_TIMEOUTINFINITE // timeout
		fcb@ IOFileControlBlock_AsyncIOEvent + // object
		KeThreadWaitForObject drop
	end

	while (1)
		fcb@ IOFileControlBlock_SizeInBytes + @ oldsize!

		if (newsize@ oldsize@ <)
			// shrinking
			if (growing@)
				// don't shrink
				break
			end

			if (fcb@ IOFileControlBlock_CacheInfoBlock + @)
				newsize@ // newsize
				0 // writeout
				fcb@ IOFileControlBlock_CacheInfoBlock + @ // cacheblock
				IOCacheInfoBlockTruncate ok!

				if (ok@)
					fcb@ IOFileControlBlockUnlock
					return
				end
			end
		end

		newsize@ // newsize
		zero@ // zero
		flags@ // flags
		fcb@ // fcb
		truncfunc@ IODispatchTruncateFunction ok!

		if (ok@)
			fcb@ IOFileControlBlockUnlock
			return
		end

		if (DEBUGCHECKS)
			if (fcb@ IOFileControlBlock_SizeInBytes + @ newsize@ ~=)
				"IOFileControlBlockTruncate: fs driver reported success but didn't modify size\n" KeCrash
			end
		end

		break
	end

	if (keeplocked@)
		fcb@ IOFileControlBlockDemoteToShared ok!

		if (ok@)
			fcb@ IOFileControlBlockUnlock
			return
		end
	end else
		fcb@ IOFileControlBlockUnlock
	end
end

// millions of getters and setters so we don't break every driver whenever we
// change the struct

fn IOFileControlBlockGetReferences { fcb -- references }
	fcb@ IOFileControlBlock_References + @ references!
end

fn IOFileControlBlockGetContext { fcb -- context }
	fcb@ IOFileControlBlock_FSContext + @ context!
end

fn IOFileControlBlockSetContext { context fcb -- }
	context@ fcb@ IOFileControlBlock_FSContext + !
end

fn IOFileControlBlockGetMount { fcb -- mount }
	fcb@ IOFileControlBlock_DeviceObject + @ mount!
end

fn IOFileControlBlockGetDeviceObject { fcb -- devobj }
	fcb@ IOFileControlBlock_DeviceObject + @ devobj!
end

fn IOFileControlBlockGetType { fcb -- filetype }
	fcb@ IOFileControlBlock_FileType + @ filetype!
end

fn IOFileControlBlockGetSize { fcb -- size }
	fcb@ IOFileControlBlock_SizeInBytes + @ size!
end

fn IOFileControlBlockSetSize { size fcb -- }
	size@ fcb@ IOFileControlBlock_SizeInBytes + !
end

fn IOFileControlBlockGetCacheInfoBlock { fcb -- cacheblock }
	fcb@ IOFileControlBlock_CacheInfoBlock + @ cacheblock!
end

fn IOFileControlBlockSetAccessTime { time fcb -- }
	time@ KeTime_SecPart + @ fcb@ IOFileControlBlock_AccessTime + KeTime_SecPart + !
	time@ KeTime_MsPart + @ fcb@ IOFileControlBlock_AccessTime + KeTime_MsPart + !
end

fn IOFileControlBlockSetModifyTime { time fcb -- }
	time@ KeTime_SecPart + @ fcb@ IOFileControlBlock_ModifyTime + KeTime_SecPart + !
	time@ KeTime_MsPart + @ fcb@ IOFileControlBlock_ModifyTime + KeTime_MsPart + !
end

fn IOFileControlBlockSetChangeTime { time fcb -- }
	time@ KeTime_SecPart + @ fcb@ IOFileControlBlock_ChangeTime + KeTime_SecPart + !
	time@ KeTime_MsPart + @ fcb@ IOFileControlBlock_ChangeTime + KeTime_MsPart + !
end

fn IOFileControlBlockZeroReference { fcb -- }
	if (DEBUGCHECKS)
		if (fcb@ IOFileControlBlock_References + @ 1 ~=)
			"IOFileControlBlockZeroReference\n" KeCrash
		end
	end

	0 fcb@ IOFileControlBlock_References + !
end

fn IOFileControlBlockGetAccessTime { fcb -- time }
	fcb@ IOFileControlBlock_AccessTime + time!
end

fn IOFileControlBlockGetModifyTime { fcb -- time }
	fcb@ IOFileControlBlock_ModifyTime + time!
end

fn IOFileControlBlockGetChangeTime { fcb -- time }
	fcb@ IOFileControlBlock_ChangeTime + time!
end

fn IOFileControlBlockGetSizeof { -- sizeof }
	IOFileControlBlock_SIZEOF sizeof!
end

fn IOFileControlBlockIsDoomed { fcb -- doomed }
	fcb@ IOFileControlBlock_Flags + @ IOFCBFLAG_DELETELASTREFERENCE & doomed!
end