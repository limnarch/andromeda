//
// Implements MINTIA page cache management routines.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

fn IOCacheInfoBlockCreate { -- cacheblock ok }
	IOCacheInfoBlock_SIZEOF // bytes
	'Cach' // tag
	CANBLOCK POOLALLOC | // flags
	MmAllocWithTag ok! cacheblock!

	if (ok@)
		// allocation failed

		return
	end

	0 cacheblock@ IOCacheInfoBlock_MapCount + !
	0 cacheblock@ IOCacheInfoBlock_Flags + !

	"IOCacheInfoBlockRwLock" // name
	cacheblock@ IOCacheInfoBlock_RwLock + // rwlock
	ExRwLockInitialize

	auto buckets
	IOCACHEPAGEBUCKETS buckets!

	auto mutex
	cacheblock@ IOCacheInfoBlock_PageBucketMutexes + mutex!

	while (buckets@)
		"IOCacheInfoBucketMutex" // name
		KERNELMODE // mode
		mutex@ // mutex
		KeMutexInitialize

		KeMutex_SIZEOF mutex +=
		1 buckets -=
	end

	cacheblock@ IOCacheInfoBlock_PageBucketListHeads + // ptr
	IOCACHEPAGEBUCKETS 8 * // size
	0 // word
	memset
end

fn IOCacheInfoBlockLock { cacheblock -- ok }
	1 // alertable
	0 // nowait
	cacheblock@ IOCacheInfoBlock_RwLock + // rwlock
	ExRwLockAcquireExclusive ok!
end

fn IOCacheInfoBlockTryLock { cacheblock -- ok }
	0 // alertable
	1 // nowait
	cacheblock@ IOCacheInfoBlock_RwLock + // rwlock
	ExRwLockAcquireExclusive ok!

	if (ok@ 0 s<)
		0 ok!
	end
end

fn IOCacheInfoBlockLockShared { cacheblock -- ok }
	1 // alertable
	0 // nowait
	0 // canstarve
	cacheblock@ IOCacheInfoBlock_RwLock + // rwlock
	ExRwLockAcquireShared ok!
end

fn IOCacheInfoBlockTryLockShared { cacheblock -- ok }
	0 // alertable
	1 // nowait
	0 // canstarve
	cacheblock@ IOCacheInfoBlock_RwLock + // rwlock
	ExRwLockAcquireShared ok!

	if (ok@ 0 s<)
		0 ok!
	end
end

fn IOCacheInfoBlockUnlock { cacheblock -- }
	cacheblock@ IOCacheInfoBlock_RwLock + // rwlock
	ExRwLockRelease
end

fn IOCacheInfoBlockLockBucket { bucket cacheblock -- ok }
	if (DEBUGCHECKS)
		if (bucket@ IOCACHEPAGEBUCKETS >=)
			"IOCacheInfoBlockLockBucket: bucket >= IOCACHEPAGEBUCKETS\n" KeCrash
		end
	end

	KERNELMODE // waitmode
	1 // alertable
	OSWAIT_TIMEOUTINFINITE // timeout
	cacheblock@ IOCacheInfoBlock_PageBucketMutexes + bucket@ KeMutex_SIZEOF * + // object
	KeThreadWaitForObject ok!
end

fn IOCacheInfoBlockTryLockBucket { bucket cacheblock -- locked }
	if (DEBUGCHECKS)
		if (bucket@ IOCACHEPAGEBUCKETS >=)
			"IOCacheInfoBlockLockBucket: bucket >= IOCACHEPAGEBUCKETS\n" KeCrash
		end
	end

	KERNELMODE // waitmode
	0 // alertable
	0 // timeout
	cacheblock@ IOCacheInfoBlock_PageBucketMutexes + bucket@ KeMutex_SIZEOF * + // object
	KeThreadWaitForObject locked!
end

fn IOCacheInfoBlockUnlockBucket { bucket cacheblock -- }
	if (DEBUGCHECKS)
		if (bucket@ IOCACHEPAGEBUCKETS >=)
			"IOCacheInfoBlockUnlockBucket: bucket >= IOCACHEPAGEBUCKETS\n" KeCrash
		end
	end

	0 // abandon
	cacheblock@ IOCacheInfoBlock_PageBucketMutexes + bucket@ KeMutex_SIZEOF * + // mutex
	KeMutexRelease drop
end

fn IOCacheInfoBlockTryReference { cacheblock -- oldcount ok }
	0 ok!

	auto rs
	HALCPUInterruptDisable rs!

	if (cacheblock@ IOCacheInfoBlock_Flags + @ IOCACHEBLOCKFLAG_TRUNCATING &)
		rs@ HALCPUInterruptRestore

		STATUS_TRY_AGAIN_LATER ok!

		return
	end

	cacheblock@ IOCacheInfoBlock_MapCount + @ oldcount!

	oldcount@ 1 + cacheblock@ IOCacheInfoBlock_MapCount + !

	rs@ HALCPUInterruptRestore
end

fn IOCacheInfoBlockDereference { cacheblock -- oldcount }
	auto rs
	HALCPUInterruptDisable rs!

	cacheblock@ IOCacheInfoBlock_MapCount + @ oldcount!

	if (DEBUGCHECKS)
		if (oldcount@ 0 ==)
			"IOCacheInfoBlockDereference: underflow\n" KeCrash
		end
	end

	oldcount@ 1 - cacheblock@ IOCacheInfoBlock_MapCount + !

	rs@ HALCPUInterruptRestore
end

fn IOCacheInfoBlockFlush { cacheblock -- ok }
	// flush all of the cacheblock's dirty pages.

	cacheblock@ // cacheblock
	IOCacheInfoBlockLock ok!

	if (ok@)
		return
	end

	// iterate each bucket from 0 to IOCACHEPAGEBUCKETS-1.
	// for each bucket, iterate all pages. if a page if dirty, write it
	// out to disk.

	auto i
	0 i!

	while (i@ IOCACHEPAGEBUCKETS <)
		auto buckethead
		cacheblock@ IOCacheInfoBlock_PageBucketListHeads + i@ 3 << + buckethead!

		auto pfdbe

		auto ipl
		IPLDPC KeIPLRaise ipl!

		// get the tail so that we can go backwards thru the bucket.
		// reason for this is that LRU -> MRU is the most likely
		// order to go "in order" on the disk surface
		buckethead@ 4 + @ pfdbe!

		while (pfdbe@)
			if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_MODIFIED &)
				pfdbe@ MmEvictablePageReference drop

				ipl@ KeIPLLower

				0 // kflags
				pfdbe@ // pfdbe
				IOCachePageWrite drop

				IPLDPC KeIPLRaise ipl!

				pfdbe@ MmEvictablePageDereference drop
			end

			pfdbe@ IOPageFrameEntryCache_PrevCachePage + @ pfdbe!
		end

		ipl@ KeIPLLower

		1 i +=
	end

	cacheblock@ // cacheblock
	IOCacheInfoBlockUnlock
end

fn IOCacheInfoBlockTruncate { newsize writeout cacheblock -- ok }
	// attempt to evict all of the cacheblock's page frames beyond newsize.
	// also evicts the page that contains newsize, to prevent a weird
	// interaction with the dirty page writer.

	auto maxpage
	newsize@ PAGENUMBERMASK & maxpage!

	cacheblock@ // cacheblock
	IOCacheInfoBlockLock ok!

	if (ok@)
		return
	end

	auto rs
	HALCPUInterruptDisable rs!

	if (cacheblock@ IOCacheInfoBlock_MapCount + @)
		// this file is currently mapped. truncation isn't allowed.

		rs@ HALCPUInterruptRestore

		cacheblock@ IOCacheInfoBlockUnlock

		STATUS_TRY_AGAIN_LATER ok!

		return
	end

	IOCACHEBLOCKFLAG_TRUNCATING cacheblock@ IOCacheInfoBlock_Flags + |=

	rs@ HALCPUInterruptRestore

	// iterate each bucket from 0 to IOCACHEPAGEBUCKETS-1.
	// for each bucket, free all cached pages. if a page is dirty,
	// and caller wanted us to write out dirty pages (writeout==1),
	// write the page out first.

	auto i
	0 i!

	while (i@ IOCACHEPAGEBUCKETS <)
		auto buckethead
		cacheblock@ IOCacheInfoBlock_PageBucketListHeads + i@ 3 << + buckethead!

		auto pfdbe

		auto ipl
		IPLDPC KeIPLRaise ipl!

		// get the tail so that we can go backwards thru the bucket.
		// reason for this is that LRU -> MRU is the most likely
		// order to go "in order" on the disk surface
		buckethead@ 4 + @ pfdbe!

		while (pfdbe@)
			if (pfdbe@ IOPageFrameEntryCache_OffsetInFile + @ newsize@ >=)
				pfdbe@ MmEvictablePageReference drop

				if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_MODIFIED &)
					if (writeout@)
						// this is a dirty page, and caller wants us to
						// write them out to disk. the alternative is
						// writeout==0, which means the caller says its fine
						// to ignore and throw them out.

						ipl@ KeIPLLower

						0 // kflags
						pfdbe@ // pfdbe
						IOCachePageWrite drop

						IPLDPC KeIPLRaise ipl!
					end
				end

				auto npfdbe
				pfdbe@ IOPageFrameEntryCache_PrevCachePage + @ npfdbe!

				pfdbe@ // pfdbe
				buckethead@ // buckethead
				IOCachePageRemove

				pfdbe@ MmEvictablePageDelete

				pfdbe@ MmEvictablePageDereference drop

				npfdbe@ pfdbe!

				1 IOCachePagesUsed -=
			end elseif (pfdbe@ IOPageFrameEntryCache_OffsetInFile + @ maxpage@ ==)
				if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_VALID &)
					// zero out the end of the page

					pfdbe@ MmEvictablePageReference drop

					ipl@ KeIPLLower

					auto pageframe
					pfdbe@ MmPageFrameDatabase@ - MmPageFrameEntry_SIZEOF / PAGESHIFT << IDENTITYSPACE | pageframe!

					pageframe@ newsize@ PAGEOFFSETMASK & + // ptr
					PAGESIZE newsize@ PAGEOFFSETMASK & - // size
					0 // word
					memset

					pfdbe@ // pfdbe
					0 // process
					MmEvictablePageModify

					IPLDPC KeIPLRaise ipl!

					pfdbe@ MmEvictablePageDereference drop
				end

				pfdbe@ IOPageFrameEntryCache_PrevCachePage + @ pfdbe!
			end else
				pfdbe@ IOPageFrameEntryCache_PrevCachePage + @ pfdbe!
			end
		end

		ipl@ KeIPLLower

		1 i +=
	end

	HALCPUInterruptDisable rs!
	IOCACHEBLOCKFLAG_TRUNCATING ~ cacheblock@ IOCacheInfoBlock_Flags + &=
	rs@ HALCPUInterruptRestore

	cacheblock@ // cacheblock
	IOCacheInfoBlockUnlock
end

fn IOCacheInfoBlockDestroy { writeout cacheblock -- ok }
	// assumes caller made sure there's no way this cacheblock can be used by
	// anybody else.

	KeThreadCurrent@ KeThreadIgnoreKill drop

	0 // newsize
	writeout@ // writeout
	cacheblock@ // cacheblock
	IOCacheInfoBlockTruncate ok!

	if (ok@ ~~)
		cacheblock@ IOCacheInfoBlock_RwLock + ExRwLockUninitialize

		cacheblock@ MmFree
	end

	KeThreadCurrent@ KeThreadAcceptKill drop
end