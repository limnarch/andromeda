//
// Initialization of the MINTIA IO subsystem.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALArgs.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

#include "IOInternal.h"

rosection "INIT$text"

externptr HALLoaderInfo
externptr HALLoaderBootDeviceName

var IOFileControlBlockMaximum 0
public IOFileControlBlockMaximum

var IOFileControlBlockCount 0
public IOFileControlBlockCount

var IOFileControlBlockPreferred 0
public IOFileControlBlockPreferred

var IOiFileControlBlockThrottleCount 0
public IOiFileControlBlockThrottleCount

extern IOiNullInit { -- }

buffer IOBootDevicePath OSFILEPATHMAX
public IOBootDevicePath

var IODevicesDirectoryObject 0
public IODevicesDirectoryObject

var IODeviceTypeObject 0
public IODeviceTypeObject

var IOFileTypeObject 0
public IOFileTypeObject

var IOBootDevice 0
public IOBootDevice

var IOFileControlBlockCacheSize 0
public IOFileControlBlockCacheSize

buffer IOiWritebehindTimerDPC KeDPC_SIZEOF

buffer IOiWritebehindTimer KeTimer_SIZEOF
public IOiWritebehindTimer

buffer IOiWritebehindEvent KeEvent_SIZEOF
public IOiWritebehindEvent

buffer IOiFileControlBlockCacheTrimEvent KeEvent_SIZEOF
public IOiFileControlBlockCacheTrimEvent

buffer IOiDeviceDatabaseRwLock ExRwLock_SIZEOF
public IOiDeviceDatabaseRwLock

externptr IOiWritebehindTimerDPCFunction

fn IOInitPhase1 { -- }
	fnsection "INIT$text"

	// arbitrary heuristics about how many FCBs to cache follow

	// assume the average FS driver's context structure will be 128 bytes

	auto heapcharge
	IOFileControlBlock_SIZEOF MmChargeBytesGet
	IOiCacheInfoBlock_SIZEOF MmChargeBytesGet +
	IOAVERAGEFCBCONTEXTNP MmChargeBytesGet + heapcharge!

	MmPageFreeCount@ PAGESHIFT << heapcharge@ / 48 / IOFileControlBlockMaximum!
	IOFileControlBlockMaximum@ 500 min IOFileControlBlockMaximum!
	IOFileControlBlockMaximum@ 40 max IOFileControlBlockMaximum!

	IOFileControlBlockMaximum@ 30 - IOFileControlBlockPreferred!
	IOFileControlBlockMaximum@ 20 + IOiFileControlBlockThrottleCount!

	IOFileControlBlockMaximum@ heapcharge@ * IOFileControlBlockCacheSize!

	IOFileControlBlockCacheSize@
	IOFileControlBlockMaximum@
	"caching %d FCBs (~%d nonpaged bytes)\n" "IOInitPhase1" HALLog

	auto ok
	auto handle

	// create Devices directory object

	ACCESS_OWNER_ALL ACCESS_GROUP_ALL | ACCESS_WORLD_READ | ACCESS_WORLD_EXEC | // permissions
	"Devices" // name
	OBFLAGS_PERMANENT // flags
	ObDirectoryCreateObject ok! IODevicesDirectoryObject!

	if (ok@)
		ok@ "IOInitPhase1: couldn't create /Devices directory object (%i)\n" KeCrash
	end

	IODevicesDirectoryObject@ // object
	ObRootDirectoryObject@ // directory
	ObDirectoryInsertByPointers ok!

	if (ok@)
		ok@ "IOInitPhase1: couldn't insert /Devices directory object into root directory (%i)\n" KeCrash
	end

	// create Device type

	auto type

	auto typeinit
	ObTypeInitializer_SIZEOF alloc typeinit!

	// initialize ObTypeInitializer fields

	typeinit@ ObTypeInitializerInitialize

	pointerof IOParse typeinit@ ObTypeInitializer_ParseFunction + !

	// create Device type

	"Device" typeinit@ ObTypeInitializer_Name + !
	'Devi'   typeinit@ ObTypeInitializer_Tag + !
	0        typeinit@ ObTypeInitializer_BodySize + !
	pointerof IOiDeviceDeleteObject typeinit@ ObTypeInitializer_DeleteFunction + !
	typeinit@ ObTypeCreate ok! type!

	if (ok@)
		ok@ "IOInitPhase1: couldn't create Device type (%i)\n" KeCrash
	end

	type@ IODeviceTypeObject!

	// create File type

	"File" typeinit@ ObTypeInitializer_Name + !
	'File' typeinit@ ObTypeInitializer_Tag + !
	0 typeinit@ ObTypeInitializer_BodySize + !
	pointerof IOFileOpenObject typeinit@ ObTypeInitializer_OpenFunction + !
	pointerof IOFileCloseObject typeinit@ ObTypeInitializer_CloseFunction + !
	pointerof IOFileDeleteObject typeinit@ ObTypeInitializer_DeleteFunction + !
	pointerof IOFileSetSecurityObject typeinit@ ObTypeInitializer_SetSecurityFunction + !
	1 typeinit@ ObTypeInitializer_Paged + !
	typeinit@ ObTypeCreate ok! type!

	if (ok@)
		ok@ "IOInitPhase1: couldn't create File type (%i)\n" KeCrash
	end

	type@ IOFileTypeObject!

rosection "text"

	// initialize writebehind timer

	pointerof IOiWritebehindTimerDPCFunction // function
	IOiWritebehindTimerDPC // dpc
	KeDPCInitialize

	"IOiWritebehindTimer" // name
	IOiWritebehindTimerDPC // dpc
	IOiWritebehindTimer // timer
	KeTimerInitialize

	0 // signaled
	OSEVENT_SYNCH // type
	"IOiWritebehindEvent" // name
	IOiWritebehindEvent // event
	KeEventInitialize

	0 // signaled
	OSEVENT_SYNCH // type
	"IOiFileControlBlockCacheTrimEvent" // name
	IOiFileControlBlockCacheTrimEvent // event
	KeEventInitialize

	"IOiDeviceDatabaseRwLock" // name
	IOiDeviceDatabaseRwLock // rwlock
	ExRwLockInitialize

rosection "INIT$text"

	// initialize IOP zones

	IOiPacketZonesInit

	// initialize drivers

	STAGE_THREAD HALLoaderInfo@ HALDriverInit

	0 // context1
	0 // context2
	pointerof IOiFilesystemSyncWorker // startfunc
	0 // creationflags
	ACCESS_OWNER_ALL // permissions
	"FSSync" // name
	PsSystemProcess@ // processobject
	PsThreadCreateObject ok! drop

	if (ok@)
		ok@ "IOInitPhase1: couldn't create 'FSSync' thread (%i)\n" KeCrash
	end

	0 // context1
	0 // context2
	pointerof IOiWritebehindWorker // startfunc
	0 // creationflags
	ACCESS_OWNER_ALL // permissions
	"IOWritebehind" // name
	PsSystemProcess@ // processobject
	PsThreadCreateObject ok! drop

	if (ok@)
		ok@ "IOInitPhase1: couldn't create 'IOWritebehind' thread (%i)\n" KeCrash
	end

	IOiBootDeviceMount

	IOiPageFileInit

	IOiNullInit
end

fn IOiBootDeviceMount { -- }
	fnsection "INIT$text"

	auto bootdevname
	"ROOTDEV" HALArgsValue bootdevname!

	if (bootdevname@ ~~)
		HALLoaderBootDeviceName@ bootdevname!
	end

	auto bootdevpath
	IOBootDevicePath bootdevpath!

	':' bootdevpath@ sb
	bootdevpath@ 1 + bootdevname@ strcpy
	
	bootdevpath@ "boot device: %s\n" "IOiBootDeviceMount" HALLog

	auto bootdevhandle
	auto ok

	0 // flags
	ACCESS_EXEC // access
	-1 // initialobjecthandle
	bootdevpath@ // path
	ObObjectOpenByPath ok! bootdevhandle!

	if (ok@)
		ok@ "IOiBootDeviceMount: inaccessible boot device (%i)\n" KeCrash
	end

	OSMOUNT_READONLY // flags
	bootdevhandle@ // handle
	0 // fsname (any)
	IOFilesystemMount ok!

	if (ok@)
		ok@ "IOiBootDeviceMount: failed to mount boot device (%i)\n" KeCrash
	end

	auto bootfile
	IOFileTypeObject@ // type
	bootdevhandle@ // handle
	ObObjectReferenceByHandle ok! bootfile! drop

	if (ok@)
		ok@ "IOiBootDeviceMount: failed to ref boot device file (%i)\n" KeCrash
	end

	bootfile@ IOFile_FileControlBlock + @
	IOFileControlBlock_Paged + @ IOFileControlBlockPaged_DeviceObject + @ IOBootDevice!

	IOBootDevice@ IODevice_RelevantMount + @ IOMount_Filesystem + @ IOFilesystem_Driver + @ IODriver_Name + @
	"mounted read-only as: %s\n" "IOiBootDeviceMount" HALLog

	bootfile@ ObObjectDereferenceByPointer drop

	bootdevhandle@ ObObjectClose ok!

	if (ok@)
		ok@ "IOiBootDeviceMount: failed to close boot device (%i)\n" KeCrash
	end
end