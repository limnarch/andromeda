//
// Initialization of the MINTIA IO subsystem.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

externptr HALLoaderInfo
externptr HALLoaderBootDeviceName

externptr IOFileControlBlockMaximum

extern IOSwapFileInit { -- }

var IODevicesDirectoryObject 0
public IODevicesDirectoryObject

var IODeviceTypeObject 0
public IODeviceTypeObject

var IOFileTypeObject 0
public IOFileTypeObject

var IOBootDevice 0
public IOBootDevice

buffer IOMountListMutex KeMutex_SIZEOF
public IOMountListMutex

fn IOInitPhase1 { -- }
	// arbitrary heuristics about how many FCBs to cache follow

	// assume the average FS driver's context structure will be 128 bytes

	auto heapcharge
	IOFileControlBlock_SIZEOF MmChargeBytesGet
	128 MmChargeBytesGet + heapcharge!

	MmPageFreeCount@ PAGESHIFT << heapcharge@ / 128 / IOFileControlBlockMaximum!
	IOFileControlBlockMaximum@ 256 min IOFileControlBlockMaximum!
	IOFileControlBlockMaximum@ 16 max IOFileControlBlockMaximum!

	IOFileControlBlockMaximum@ heapcharge@ *
	IOFileControlBlockMaximum@
	"caching %d FCBs (~%d bytes)\n" "IOInitPhase1" HALLog

	auto ok
	auto handle

	// create Devices directory object

	ACCESS_OWNER_ALL ACCESS_GROUP_ALL | ACCESS_WORLD_READ | ACCESS_WORLD_EXEC | // permissions
	"Devices" // name
	OBFLAGS_PERMANENT // flags
	ObDirectoryCreateObject ok! IODevicesDirectoryObject!

	if (ok@)
		ok@ "IOInitPhase1: couldn't create /Devices directory object (%i)\n" KeCrash
	end

	IODevicesDirectoryObject@ ObHeader_SIZEOF - // object
	ObRootDirectoryObject@ // directory
	ObDirectoryInsertByPointers ok!

	if (ok@)
		ok@ "IOInitPhase1: couldn't insert /Devices directory object into root directory (%i)\n" KeCrash
	end

	// create Device type

	auto type

	auto typeinit
	ObTypeInitializer_SIZEOF alloc typeinit!

	// initialize ObTypeInitializer fields

	typeinit@ ObTypeInitializerInitialize

	pointerof IOParse typeinit@ ObTypeInitializer_ParseFunction + !

	// create Device type

	"Device" typeinit@ ObTypeInitializer_Name + !
	'Devi'   typeinit@ ObTypeInitializer_Tag + !
	0        typeinit@ ObTypeInitializer_BodySize + !
	pointerof IODeviceDeleteObject typeinit@ ObTypeInitializer_DeleteFunction + !
	typeinit@ ObTypeCreate ok! type!

	if (ok@)
		ok@ "IOInitPhase1: couldn't create Device type (%i)\n" KeCrash
	end

	type@ IODeviceTypeObject!

	// create File type

	"File" typeinit@ ObTypeInitializer_Name + !
	'File' typeinit@ ObTypeInitializer_Tag + !
	0 typeinit@ ObTypeInitializer_BodySize + !
	pointerof IOFileOpenObject typeinit@ ObTypeInitializer_OpenFunction + !
	pointerof IOFileCloseObject typeinit@ ObTypeInitializer_CloseFunction + !
	pointerof IOFileDeleteObject typeinit@ ObTypeInitializer_DeleteFunction + !
	pointerof IOFileSetSecurityObject typeinit@ ObTypeInitializer_SetSecurityFunction + !
	typeinit@ ObTypeCreate ok! type!

	if (ok@)
		ok@ "IOInitPhase1: couldn't create File type (%i)\n" KeCrash
	end

	type@ IOFileTypeObject!

	// initialize mount list mutex

	"IOMountListMutex" // name
	KERNELMODE // mode
	IOMountListMutex // mutex
	KeMutexInitialize

	// initialize drivers

	STAGE_THREAD HALLoaderInfo@ HALDriverInit

	0 // context1
	0 // context2
	pointerof IOFilesystemSyncWorker // startfunc
	0 // creationflags
	ACCESS_OWNER_ALL // permissions
	"FSSync" // name
	PsSystemProcess@ // processobject
	PsThreadCreateObject ok! drop

	if (ok@)
		ok@ "IOInitPhase1: couldn't create 'FSSync' thread (%i)\n" KeCrash
	end

	0 // context1
	0 // context2
	pointerof IOWritebehindWorker // startfunc
	0 // creationflags
	ACCESS_OWNER_ALL // permissions
	"IOWritebehindWorker" // name
	PsSystemProcess@ // processobject
	PsThreadCreateObject ok! drop

	if (ok@)
		ok@ "IOInitPhase1: couldn't create 'IOWritebehindWorker' thread (%i)\n" KeCrash
	end

	IOBootDeviceMount

	IOSwapFileInit
end

buffer IOBootDevicePath OSFILEPATHMAX
public IOBootDevicePath

fn IOBootDeviceMount { -- }
	auto bootdevpath
	IOBootDevicePath bootdevpath!

	':' bootdevpath@ sb
	bootdevpath@ 1 + HALLoaderBootDeviceName@ strcpy
	
	bootdevpath@ "boot device: %s\n" "IOBootDeviceMount" HALLog

	auto bootdevhandle
	auto ok

	0 // flags
	ACCESS_READ // access
	-1 // initialobjecthandle
	bootdevpath@ // path
	ObObjectOpenByPath ok! bootdevhandle!

	if (ok@)
		ok@ "IOBootDeviceMount: inaccessible boot device (%i)\n" KeCrash
	end

	auto mount

	OSMOUNT_READONLY // flags
	bootdevhandle@ // handle
	0 // fsname (any)
	IOFilesystemMount ok! mount!

	if (ok@)
		ok@ "IOBootDeviceMount: failed to mount boot device (%i)\n" KeCrash
	end

	auto bootfile

	IOFileTypeObject@ // type
	bootdevhandle@ // handle
	ObObjectReferenceByHandle ok! bootfile! drop

	if (ok@)
		ok@ "IOBootDeviceMount: failed to ref boot device file (%i)\n" KeCrash
	end

	bootfile@ IOFile_FileControlBlock + @ IOFileControlBlock_DeviceObject + @ IOBootDevice!

	bootfile@ ObObjectDereferenceByPointer drop

	bootdevhandle@ ObObjectClose ok!

	if (ok@)
		ok@ "IOBootDeviceMount: failed to close boot device (%i)\n" KeCrash
	end

	mount@ IOMount_Filesystem + @ IOFilesystem_Name + @ "mounted read-only as: %s\n" "IOBootDeviceMount" HALLog
end