//
// Implements the system calls for the object manager subsystem.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Object.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

fn OSObjectOpen { flags access initialobjecthandle path -- handle ok }
	auto kpath

	OSFILEPATHMAX // max
	path@ // string
	ExCallGetString ok! kpath!

	if (ok@)
		return
	end

	flags@ // flags
	access@ // access
	initialobjecthandle@ // initialobjecthandle
	kpath@ // path
	ObObjectOpenByPath ok! handle!

	kpath@ ExCallFreeString
end

fn OSQuery { handle query -- ok }
	auto kquery
	OSObjectQuery_SIZEOF alloc kquery!

	handle@ // objecthandle
	kquery@ // query
	ObObjectQuery ok!

	if (ok@)
		return
	end

	query@ // dest
	kquery@ // src
	OSObjectQuery_SIZEOF // size
	KeSafeCopyOut ok!
end

fn OSClose { handle -- ok }
	handle@ ObObjectClose ok!
end

fn OSWaitForMultipleObjects { alertable waittype timeout objectcount objecthandletable -- ok }
	auto ktable

	objectcount@ 4 * // bytes
	'ObWK' // tag
	MmAllocWithTag ok! ktable!

	if (ok@)
		return
	end

	ktable@ // dest
	objecthandletable@ // src
	objectcount@ 4 * // size
	KeSafeCopyIn ok!

	if (ok@)
		ktable@ MmFree

		return
	end

	alertable@ // alertable
	waittype@ // waittype
	timeout@ // timeout
	objectcount@ // objectcount
	ktable@ // objecthandletable
	ObWaitForMultipleObjects ok!

	ktable@ MmFree
end

fn OSWaitForObject { alertable timeout objecthandle -- ok }
	alertable@ timeout@ objecthandle@ ObWaitForObject ok!
end

fn OSHandleDuplicate { handle access srcprocesshandle destprocesshandle -- newhandle ok }
	handle@ access@ srcprocesshandle@ destprocesshandle@ ObHandleDuplicate ok! newhandle!
end

fn OSDirectoryCountQuery { directoryhandle -- count ok }
	directoryhandle@ ObDirectoryCountQuery ok! count!
end

fn OSDirectoryQueryAll { buffer maxquery directoryhandle -- count ok }
	// this is dumb because we're using the handle twice instead of just once
	// which is slower, but we must tolerate it because the alternative is
	// uglier and more effort and this won't get called with extreme frequency
	// since it is slow by nature anyway.

	auto maxcount
	directoryhandle@ ObDirectoryCountQuery ok! maxcount!

	if (ok@)
		return
	end

	maxcount@ maxquery@ min maxquery!

	auto kbuffer
	maxquery@ OSProcessInformation_SIZEOF * // bytes
	'ObQa' // tag
	MmAllocWithTag ok! kbuffer!

	if (ok@)
		return
	end

	kbuffer@ // buffer
	maxquery@ // maxquery
	directoryhandle@ // directoryhandle
	ObDirectoryQueryAll ok! count!

	if (ok@)
		kbuffer@ MmFree
		return
	end

	buffer@ // dest
	kbuffer@ // src
	maxquery@ OSObjectQuery_SIZEOF * // size
	KeSafeCopyOut ok!

	kbuffer@ MmFree
end