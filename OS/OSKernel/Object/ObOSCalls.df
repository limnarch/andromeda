#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Object.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

fn OSObjectOpen { flags access initialobjecthandle path -- handle ok }
	auto kpath

	OSFILEPATHMAX // max
	path@ // string
	ExCallGetString ok! kpath!

	if (ok@)
		return
	end

	flags@ // flags
	access@ // access
	initialobjecthandle@ // initialobjecthandle
	kpath@ // path
	ObObjectOpenByPath ok! handle!

	kpath@ ExCallFreeString
end

fn OSQuery { handle query -- ok }
	auto kquery
	OSObjectQuery_SIZEOF alloc kquery!

	handle@ // objecthandle
	kquery@ // query
	ObObjectQuery ok!

	if (ok@)
		return
	end

	query@ // dest
	kquery@ // src
	OSObjectQuery_SIZEOF // size
	KeSafeCopyOut ok!
end

fn OSClose { handle -- ok }
	handle@ ObObjectClose ok!
end

fn OSWaitForMultipleObjects { waittype timeout objectcount objecthandletable -- ok }
	auto ktable

	objectcount@ 4 * // bytes
	'ObWK' // tag
	MmAllocWithTag ok! ktable!

	if (ok@)
		return
	end

	ktable@ // dest
	objecthandletable@ // src
	objectcount@ 4 * // size
	KeSafeCopyIn ok!

	if (ok@)
		ktable@ MmFree

		return
	end

	1 // alertable
	waittype@ // waittype
	timeout@ // timeout
	objectcount@ // objectcount
	ktable@ // objecthandletable
	ObWaitForMultipleObjects ok!

	ktable@ MmFree
end

fn OSWaitForObject { timeout objecthandle -- ok }
	1 timeout@ objecthandle@ ObWaitForObject ok!
end