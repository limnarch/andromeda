//
// Implements page directory management for the limn2600 architecture.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALDebug.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<ll>/OSDLL/OSStatus.h"

fn MmPageDirectoryDestroy { pagemap -- }
	auto count
	PAGESIZE 3 >> count!

	auto ptr
	pagemap@ IDENTITYSPACE | ptr!

	if (DEBUGCHECKS)
		while (count@)
			auto pde
			ptr@@ pde!

			if (pde@)
				"MmPageDirectoryDestroy: broken\n" KeCrash
			end

			1 count -=
			4 ptr +=
		end
	end

	pagemap@ PAGESHIFT >> MmPageFree
end

fn MmPageDirectoryAlloc { -- pdir ok }
	ZEROMUST CANBLOCK | // priority
	MmPageAlloc ok! pdir! drop

	if (ok@)
		return
	end

	PAGESHIFT pdir <<=

	pdir@ IDENTITYSPACE | 2048 + // dest
	HALPlatformKernelPageDirectory@ IDENTITYSPACE | 2048 + // src
	2048 // size
	memcpy
end