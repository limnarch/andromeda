//
// Implements virtual memory mapping of section and file objects.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/Object.h"

#include "<inc>/IO.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

fn MmFilePrepareForMap { prot endofview offset fileobject -- ok }
	auto fcb
	fileobject@ IOFile_FileControlBlock + @ fcb!

	if (endofview@ fcb@ IOFileControlBlock_SizeInBytes + @ >)
		STATUS_END_OF_FILE ok!

		return
	end

	if (fcb@ IOFileControlBlock_FileType + @ OSFILETYPE_CHARDEVICE ==)
		if (prot@ PAGEACCESS_WRITECOPY &)
			// can't COW character devices, that's a weird thing to do.
			// also it breaks everything which is secretly the main reason

			STATUS_INVALID_ARGUMENT ok!

			return
		end

		// character devices need a GetPageAddress function to be mappable

		if (fcb@ IOFileControlBlock_DispatchTable + @ IODispatchTable_GetPageAddress + @ ~~)
			STATUS_NOT_SUPPORTED ok!

			return
		end
	end elseif (fcb@ IOFileControlBlock_FileType + @ OSFILETYPE_DIRECTORY ==)
		STATUS_IS_A_DIRECTORY ok!

		return
	end else
		auto cacheblock
		fcb@ IOFileControlBlock_CacheInfoBlock + @ cacheblock!

		if (cacheblock@ ~~)
			// initialize caching on mapped FCB

			1 // wantcaching
			fcb@ // fcb
			IOFileControlBlockCacheCheck ok! cacheblock!

			if (ok@)
				return
			end
		end

		fcb@ IOFileControlBlockLockShared ok!

		if (ok@)
			return
		end

		cacheblock@ // cacheblock
		IOCacheInfoBlockTryReference ok! drop

		if (ok@)
			fcb@ IOFileControlBlockUnlock

			return
		end

		if (endofview@ fcb@ IOFileControlBlock_SizeInBytes + @ >)
			// could have been truncated by now, check again

			cacheblock@ // cacheblock
			IOCacheInfoBlockDereference drop

			fcb@ IOFileControlBlockUnlock

			STATUS_END_OF_FILE ok!

			return
		end

		fcb@ IOFileControlBlockUnlock
	end
end

fn MmFileUnprepareForMap { fileobject -- }
	auto fcb
	fileobject@ IOFile_FileControlBlock + @ fcb!

	if (fcb@ IOFileControlBlock_FileType + @ OSFILETYPE_CHARDEVICE ~=)
		auto cacheblock
		fcb@ IOFileControlBlock_CacheInfoBlock + @ cacheblock!

		if (DEBUGCHECKS)
			if (cacheblock@ ~~)
				"MmFileUnprepareForMap: no cacheblock\n" KeCrash
			end
		end

		cacheblock@ IOCacheInfoBlockDereference drop
	end
end

fn MmMapViewProcessObject { length startva sectionoffset mappedobject processobject pageprotection flags -- realva ok }
	if (sectionoffset@ PAGEOFFSETMASK &)
		// requested section offset isn't page-aligned
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	if (startva@ PAGEOFFSETMASK &)
		// requested startva isn't page-aligned
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	auto endofview
	length@ sectionoffset@ + endofview!

	if (endofview@ sectionoffset@ <)
		// length+sectionoffset overflows
		STATUS_OVERFLOW ok!

		return
	end

	if (length@ 0 ==)
		// length is given as 0 which makes this a no-op
		0 ok!

		return
	end

	length@ PAGEOFFSETMASK + PAGENUMBERMASK & length!

	if (length@ 0 ==)
		// length overflowed
		STATUS_OVERFLOW ok!

		return
	end

	if (startva@ length@ + startva@ <)
		// startva+length overflows
		STATUS_OVERFLOW ok!

		return
	end

	if (startva@ length@ + MMHIGHESTUSERADDRESS >)
		// requested region goes outside userspace

		if (flags@ OSMAPFLAG_FIXEDADDRESS &)
			STATUS_UNAVAILABLE_ADDRESS ok!

			return
		end

		0 startva!
	end

	auto perm
	pageprotection@ PAGEACCESS_ALL & perm!

	if (mappedobject@ ObHeader_SIZEOF - ObHeader_TypeObject + @ MmSectionObjectType@ ==)
		if (mappedobject@ MmSection_PageProtection + @ perm@ & perm@ ~=)
			// section doesn't have required permissions
			STATUS_PERMISSION_DENIED ok!

			return
		end

		if (endofview@ mappedobject@ MmSection_AnonSize + @ >)
			STATUS_INVALID_ARGUMENT ok!

			return
		end
	end

	// XXX in principle we only need to do this if the target process is
	// different from the calling process, but its easier to just always lock
	// the createdelete rwlock.

	0 // needsthread
	processobject@ // processobject
	PsProcessCreateDeleteAcquireCheck ok!

	if (ok@)
		return
	end

	if (mappedobject@ ObHeader_SIZEOF - ObHeader_TypeObject + @ IOFileTypeObject@ ==)
		// if the section has a file object, initialize caching on its FCB and
		// reference the cacheblock so that it can't be truncated.

		pageprotection@ // prot
		endofview@ // endofview
		sectionoffset@ // offset
		mappedobject@ // fileobject
		MmFilePrepareForMap ok!

		if (ok@)
			processobject@ PsProcessCreateDeleteUnlock

			return
		end
	end

	// from here on we need to call MmFileUnprepareForMap when we return with
	// an error...

	length@ // viewsize
	startva@ // startva
	flags@ OSMAPFLAG_FIXEDADDRESS & // fixedva
	pageprotection@ // pageprotection
	sectionoffset@ // sectionoffset
	mappedobject@ // mappedobject
	processobject@ // processobject
	MmVADFindAndMapFreeRegion ok! realva!

	if (ok@)
		if (mappedobject@ ObHeader_SIZEOF - ObHeader_TypeObject + @ IOFileTypeObject@ ==)
			mappedobject@ // fileobject
			MmFileUnprepareForMap
		end

		processobject@ PsProcessCreateDeleteUnlock

		return
	end

	processobject@ PsProcessCreateDeleteUnlock
end

fn MmMapView { length startva sectionoffset mappedhandle processhandle pageprotection flags -- realva ok }
	auto access
	auto mappedobject

	0 // type
	mappedhandle@ // handle
	ObObjectReferenceByHandle ok! mappedobject! access!

	if (ok@)
		return
	end

	if (mappedobject@ ObHeader_SIZEOF - ObHeader_TypeObject + @ IOFileTypeObject@ ~=)
		if (mappedobject@ ObHeader_SIZEOF - ObHeader_TypeObject + @ MmSectionObjectType@ ~=)
			STATUS_INVALID_OBJECT_TYPE ok!

			mappedobject@ ObObjectDereferenceByPointer drop

			return
		end
	end

	// XXX assumes access bits and page protection bits lines up.
	// but they do now and always should.
	access@ pageprotection@ PAGEACCESS_ALL & SeCheckAccess ok!

	if (ok@)
		mappedobject@ ObObjectDereferenceByPointer drop

		return
	end

	auto processobject
	PsProcessObjectType@ // type
	processhandle@ // handle
	ObObjectReferenceByHandle ok! processobject! access!

	if (ok@)
		mappedobject@ ObObjectDereferenceByPointer drop

		return
	end

	access@ ACCESS_WRITE SeCheckAccess ok!

	if (ok@)
		processobject@ ObObjectDereferenceByPointer drop
		mappedobject@ ObObjectDereferenceByPointer drop

		return
	end

	length@ // length
	startva@ // startva
	sectionoffset@ // sectionoffset
	mappedobject@ // mappedobject
	processobject@ // processobject
	pageprotection@ // pageprotection
	flags@ // flags
	MmMapViewProcessObject ok! realva!

	processobject@ ObObjectDereferenceByPointer drop
	mappedobject@ ObObjectDereferenceByPointer drop
end

fn MmUnmapViewProcess { length vaddr processobject -- ok }
	length@ PAGEOFFSETMASK + PAGENUMBERMASK & length!

	if (length@ 0 ==)
		// length overflowed
		STATUS_OVERFLOW ok!

		return
	end

	if (vaddr@ length@ + vaddr@ <)
		// vaddr+length overflows
		STATUS_OVERFLOW ok!

		return
	end

	if (vaddr@ length@ + MMHIGHESTUSERADDRESS >)
		// requested region goes outside userspace

		STATUS_FORBIDDEN_OPERATION ok!

		return
	end

	KeThreadCurrent@ KeThreadIgnoreKill drop

	processobject@ MmVADListLock ok!

	if (DEBUGCHECKS)
		if (ok@)
			"MmUnmapViewProcess: failed to lock\n" KeCrash
		end
	end

	auto vad

	vaddr@ // vaddr
	length@ // length
	1 // mustbestart
	processobject@
	MmVADFind ok! vad!

	if (ok@)
		processobject@ MmVADListUnlock

		KeThreadCurrent@ KeThreadAcceptKill drop

		return
	end

	vad@ // vad
	processobject@ // processobject
	MmVADUnmap

	processobject@ MmVADListUnlock

	KeThreadCurrent@ KeThreadAcceptKill drop
end

fn MmUnmapView { length vaddr processhandle -- ok }
	auto processobject
	auto access

	PsProcessObjectType@ // type
	processhandle@ // handle
	ObObjectReferenceByHandle ok! processobject! access!

	if (ok@)
		return
	end

	access@ ACCESS_WRITE SeCheckAccess ok!

	if (ok@)
		processobject@ ObObjectDereferenceByPointer drop

		return
	end

	length@ // length
	vaddr@ // vaddr
	processobject@ // processobject
	MmUnmapViewProcess ok!

	processobject@ ObObjectDereferenceByPointer drop
end

fn MmRemapViewProcess { pageprotection length vaddr processobject -- ok }
	length@ PAGEOFFSETMASK + PAGENUMBERMASK & length!

	if (length@ 0 ==)
		// length overflowed
		STATUS_OVERFLOW ok!

		return
	end

	if (vaddr@ length@ + vaddr@ <)
		// vaddr+length overflows
		STATUS_OVERFLOW ok!

		return
	end

	if (vaddr@ length@ + MMHIGHESTUSERADDRESS >)
		// requested region goes outside userspace
		STATUS_FORBIDDEN_OPERATION ok!

		return
	end

	processobject@ MmVADListLock ok!

	if (ok@)
		return
	end

	auto vad

	vaddr@ // vaddr
	length@ // length
	1 // mustbestart
	processobject@
	MmVADFind ok! vad!

	if (ok@)
		processobject@ MmVADListUnlock

		return
	end

	if (vad@ MmVAD_Flags + @ MMVADFLAG_NOCOMMIT &)
		// we'd need to traverse the page tables to update the PTEs; not
		// implemented for now.

		processobject@ MmVADListUnlock
		STATUS_NOT_IMPLEMENTED ok!
		return
	end

	auto oldprot
	vad@ MmVAD_PageProtection + @ oldprot!

	if (oldprot@ pageprotection@ & pageprotection@ ~=)
		processobject@ MmVADListUnlock

		// not a subset
		STATUS_PERMISSION_DENIED ok!

		return
	end

	if (oldprot@ PAGEACCESS_WRITECOPY &)
		if (pageprotection@ PAGEACCESS_WRITECOPY & ~~)
			// no longer mapped COW, uncharge all VM quota except for what was
			// actually COW'd.

			vad@ MmVAD_ChargeVMQuota + @ vad@ MmVAD_COWCount + @ PAGESHIFT << - // charge
			vad@ MmVAD_QuotaBlock + @ // quotablock
			MmQuotaBlockUnchargeVM

			vad@ MmVAD_COWCount + @ PAGESHIFT << vad@ MmVAD_ChargeVMQuota + !
		end
	end

	pageprotection@ vad@ MmVAD_PageProtection + !

	auto flags
	PTE_V flags!

	if (pageprotection@ PAGEACCESS_WRITECOPY PAGEACCESS_WRITE | &)
		PTE_W flags |=
	end

	flags@ // flags
	vad@ MmVAD_StartVA + @ // startva
	vad@ MmVAD_EndVA + @ // endva
	processobject@ // process
	MmWorkingSetRemap

	processobject@ MmVADListUnlock
end

fn MmRemapView { pageprotection length vaddr processhandle -- ok }
	auto processobject
	auto access

	PsProcessObjectType@ // type
	processhandle@ // handle
	ObObjectReferenceByHandle ok! processobject! access!

	if (ok@)
		return
	end

	access@ ACCESS_WRITE SeCheckAccess ok!

	if (ok@)
		processobject@ ObObjectDereferenceByPointer drop

		return
	end

	pageprotection@ // pageprotection
	length@ // length
	vaddr@ // vaddr
	processobject@ // processobject
	MmRemapViewProcess ok!

	processobject@ ObObjectDereferenceByPointer drop
end