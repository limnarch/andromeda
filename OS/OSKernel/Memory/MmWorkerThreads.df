#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALDebug.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

// fraction over 128 of the number of evicted pages that should be written to
// swap.
var MmSwappiness 0
public MmSwappiness

fn MmEvictionWorker { context1 context2 -- }
	PRIORITY_LOWREALTIME 1 + // priority
	KeThreadCurrent@ // thread
	KeThreadPrioritySet

	IPLLOW KeIPLLower // kernel threads start in IPLDPC

	// the eviction/swap thread

	auto sleptonce
	0 sleptonce!

	auto failcount
	0 failcount!

	auto ipl

	while (1)
		auto ok
		auto reason
		auto evictcount

		KERNELMODE // waitmode
		0 // alertable
		OSWAIT_TIMEOUTINFINITE // timeout
		MmEventLowMemory // object
		KeThreadWaitForObject reason!

		if (DEBUGCHECKS)
			if (reason@)
				reason@ "MmEvictionWorker: failed to wait on low memory event (%i)\n" KeCrash
			end
		end

		IPLDPC KeIPLRaise ipl!

		if (MmPageFreeCount@ MmPageFreeCountSufficient@ >=)
			MmEventLowMemory // event
			KeEventReset drop

			ipl@ KeIPLLower

			0 sleptonce!
			0 failcount!

			continue
		end else
			MmPageFreeCountSufficient@ MmPageFreeCount@ - evictcount!
		end

		ipl@ KeIPLLower

		if (failcount@ 4 >=)
			// we've been here a lot of times...
			// giving up the processor for 50ms might untangle things and help
			// us evict more pages...

			50 // interval
			KERNELMODE // waitmode
			0 // alertable
			KeThreadSleep ok!

			if (DEBUGCHECKS)
				if (ok@ STATUS_WAIT_TIMEOUT ~=)
					ok@ "MmEvictionWorker: wait failed (%i)\n" KeCrash
				end
			end

			0 failcount!

			continue
		end

		auto slowcount

		// in normal person speak this is
		// swapcount = evictcount * (MmSwappiness/128)
		evictcount@ 7 << MmSwappiness@ * 14 >> slowcount!

		auto fastcount
		evictcount@ slowcount@ - fastcount!

		if (sleptonce@ ~~)
			if (IOCachePagesModifiedCount@ IOCachePagesModifiedMaximum@ 3 / >=)
				// there's IOCachePagesModifiedMaximum/3 or more modified page frames.
				// go to sleep for 50ms with the hope that the modified page writer will have freed some up by
				// the time we wake up.

				// we may do this only once per eviction period.
				1 sleptonce!

				50 // interval
				KERNELMODE // waitmode
				0 // alertable
				KeThreadSleep ok!

				if (DEBUGCHECKS)
					if (ok@ STATUS_WAIT_TIMEOUT ~=)
						ok@ "MmEvictionWorker: wait failed (%i)\n" KeCrash
					end
				end

				continue
			end
		end

		// ensure we can progress by putting a minimum on these
		if (MmSwappiness@)
			slowcount@ 20 max slowcount!
		end
		fastcount@ 32 max fastcount!

		// attempt to collect enough pages in both lists to satisfy these
		// targets

		MmPageFreeCount@ MMMUSTSUCCEEDPAGES 2 * < // dire
		fastcount@ slowcount@ + // pagecount
		MmPopulateLists

		auto succeedcount

		auto done

		fastcount@ // trycount
		1 // fast
		MmEvictableFastPageListHead // listhead
		MmEvictFromList ok! done! succeedcount!

		if (ok@)
			ok@ "MmEvictionWorker: fast page evict (%i)\n" KeCrash
		end

		if (succeedcount@)
			0 failcount!
		end

		if (done@)
			// free page count became sufficient while evicting
			continue
		end

		slowcount@ // trycount
		0 // fast
		MmEvictablePageListHead // listhead
		MmEvictFromList ok! done! succeedcount!

		if (ok@)
			ok@ "MmEvictionWorker: slow page evict (%i)\n" KeCrash
		end

		if (succeedcount@)
			0 failcount!
		end

		if (done@)
			continue
		end

		1 failcount +=
	end
end

fn MmPopulateLists { dire pagecount -- }
	auto ok

	auto ipl
	IPLDPC KeIPLRaise ipl!

	// check if we need to populate the lists at all judging by their current
	// sizes and the given pagecount.

	auto fcmp
	if (IOCachePagesLowWatermark@ MmEvictableFastPageCount@ >=)
		0 fcmp!
	end else
		MmEvictableFastPageCount@ IOCachePagesLowWatermark@ - fcmp!
	end

	if (fcmp@ MmEvictableSlowPageCount@ + pagecount@ >=)
		ipl@ KeIPLLower

		return
	end

	fcmp@ MmEvictableSlowPageCount@ + pagecount -=

	ipl@ KeIPLLower

	PsProcessListLock ok!

	if (DEBUGCHECKS)
		if (ok@)
			ok@ "MmPopulateLists: failed to lock process list\n" KeCrash
		end
	end

	auto process
	PsProcessListHead@ process!

	auto head
	process@ head!

	auto nprocess

	while (process@)
		process@ PsProcess_GlobalListNext + @ nprocess!

		if (process@ PsProcess_WorkingSetSize + @)
			process@ MmWorkingSetLockTimeout ok!

			if (ok@ ~~)
				dire@ // dire
				pagecount@ // pages
				process@ // process
				MmWorkingSetTrim pagecount -=

				process@ MmWorkingSetUnlock

				if (pagecount@ ~~)
					break
				end

				if (process@ PsProcessListTail@ ~=)
					process@ PsProcessRemove
					process@ PsProcessInsert
				end
			end
		end

		if (nprocess@ head@ ==)
			break
		end

		nprocess@ process!
	end

	PsProcessListUnlock
end

fn MmZeroPageWorker { -- }
	// the fate of the idle thread is to endlessly spin and check for page
	// frames in the free list. if it finds any, it zeroes them out and places
	// them on the zero list. if it doesn't, it halts the processor until next
	// interrupt.

	while (1)
		auto pfdbe
		MmPageFreeListHead@ pfdbe!

		if (pfdbe@)
			// there's a page, zero it

			auto rs
			HALCPUInterruptDisable rs!

			MmPageFreeListHead@ pfdbe!

			// make sure the free list didnt become empty in that brief timespan

			if (pfdbe@ ~~)
				rs@ HALCPUInterruptRestore

				continue
			end

			// yoink off the list

			auto pfn
			pfdbe@ MmPageFrameEntry_PFN + @ pfn!
			pfdbe@ MmPageFrameEntryFree_Next + @ MmPageFreeListHead!
			0 pfdbe@ MmPageFrameEntryFree_Next + !

			// account for the absence of this page frame from either list while we're zeroing it

			1 MmPageFreeCount -=

			rs@ HALCPUInterruptRestore

			// zero out this page frame

			pfn@ PAGESHIFT << IDENTITYSPACE | // ptr
			PAGESIZE // size
			0 // word
			memset

			HALCPUInterruptDisable rs!

			// place on zero list

			MmPageZeroListHead@ pfdbe@ MmPageFrameEntryFree_Next + !
			pfdbe@ MmPageZeroListHead!

			1 MmPageFreeCount +=

			rs@ HALCPUInterruptRestore
		end else
			// there are no free list pages for now, halt

			HALCPUHalt
		end
	end
end