//
// Implements the system page fault handler.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALInterrupt.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/Object.h"

#include "<inc>/IO.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

var MmPageFaultCount 0
public MmPageFaultCount

const REFAULT -2
const ERROR   -1

fn MmPageFault { writing badaddr trapframe -- handled }
	HALCPUInterruptEnable

	if (KeIPLCurrentGet IPLDPC >=)
		0 handled!
		return
	end

	auto signal
	0 signal!

	if (badaddr@ PAGESIZE <)
		OSSIGNAL_SEGV signal!
	end else
		if (trapframe@ HALCPUIsUserTrapFrame)
			if (badaddr@ MMHIGHESTUSERADDRESS >)
				OSSIGNAL_SEGV signal!
			end
		end elseif (badaddr@ MMLOWESTSYSTEMADDRESS >=)
			// kernel mode fault on kernel mode address... bad
			ERROR signal!
		end
	end

	if (signal@ ~~)
		// too complex to deal with being murdered here
		KeThreadCurrent@ KeThreadIgnoreKill drop
		KeProcessCurrent MmVADListLock drop

		REFAULT signal!

		while (signal@ REFAULT ==)
			writing@ // writing
			badaddr@ // vaddr
			MmSatisfyFault signal!
		end

		KeProcessCurrent MmVADListUnlock
		KeThreadCurrent@ KeThreadAcceptKill drop
	end

	if (signal@ ERROR ==)
		// system error happened
		0 handled!
	end elseif (signal@)
		// generate signal or cause return from safecopy.
		1 handled!

		if (trapframe@ HALCPUIsUserTrapFrame)
			// user mode

			if (signal@ OSSIGNAL_KILL ==)
				OSSIGNAL_KILL // signal
				KeProcessCurrent // process
				KeProcessSignal drop
			end else
				signal@ // signal
				KeThreadCurrent@ // thread
				KeThreadException drop
			end
		end else
			// kernel mode

			auto abort
			KeThreadCurrent@ KeThread_SafeAccessAbort + @ abort!

			if (abort@ ~~)
				// happened at an unexpected time!
				0 handled!
			end else
				// thought this might happen.
				abort@ trapframe@ HALCPUTrapFrameAbort
			end
		end
	end else
		auto rs
		HALCPUInterruptDisable rs!
		1 MmPageFaultCount +=
		rs@ HALCPUInterruptRestore

		// handled successfully
		1 handled!
	end

	HALCPUInterruptDisable drop
end

fn MmThrottle { -- }
	auto process
	KeProcessCurrent process!

	if (IOSwapFileCount@ ~~)
		return
	end

	if (process@ PsProcess_ModifiedPages + @ 20 >)
		while (MmModifiedPageCount@ MmModifiedPageMaximum@ >=)
			// throttle the thread

			75 // ms
			KERNELMODE // waitmode
			0 // alertable
			KeThreadSleep drop
		end

		0 process@ PsProcess_ModifiedPages + !
	end
end

fn MmSatisfyFault { writing vaddr -- signal }
	0 signal!

	auto process
	KeProcessCurrent process!

	auto ok
	auto pteaddr
	auto phyaddr
	auto flags
	auto pfdbe
	auto wsleptr
	auto wsli
	auto ptpfdbe

	MmThrottle

	0 // pri
	vaddr@ // vaddr
	process@ KeProcess_PageDirectory + @ // pagemap
	MmPTEReference ok! pteaddr! ptpfdbe!

	if (ok@)
		if (ok@ -1 ==)
			// no page table
			0 ptpfdbe!
		end else
			// something else happened
			OSSIGNAL_KILL signal!
			return
		end
	end

	auto ipl
	IPLDPC KeIPLRaise ipl!

	if (ok@ ~~)
		pteaddr@ // pteaddr
		MmPTEInterpret ok! flags! phyaddr!

		if (ok@ ~~)
			ipl@ KeIPLLower

			// valid

			if (flags@ PTE_W &)
				// no work to do
				ptpfdbe@ process@ MmPTEDereference
				return
			end elseif (writing@)
				pteaddr@ // pteaddr
				vaddr@ // vaddr
				process@ // process
				MmWriteFault signal!

				ptpfdbe@ process@ MmPTEDereference
				return
			end else
				// no work to do
				ptpfdbe@ process@ MmPTEDereference
				return
			end
		end else
			// hold PTE contents in flags temporarily
			pteaddr@@ flags!

			if (flags@ PTE_TRANSITION PTE_INSWAP | &)
				// private page. we need to do work here

				flags@ PAGENUMBERMASK & phyaddr!

				if (flags@ PTE_TRANSITION &)
					// reference it now so we can drop IPL

					auto waited
					CANBLOCK MmPageWait ok! waited!

					if (ok@)
						ipl@ KeIPLLower
						ptpfdbe@ process@ MmPTEDereference
						OSSIGNAL_KILL signal!
						return
					end

					if (waited@)
						ipl@ KeIPLLower
						ptpfdbe@ process@ MmPTEDereference
						REFAULT signal!
						return
					end

					phyaddr@ PAGESHIFT >> MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!

					if (DEBUGCHECKS)
						if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_PRIVATE & ~~)
							flags@ vaddr@ "MmSatisfyFault: %x: transition PTE (%x) wasn't COW \n" KeCrash
						end
					end

					pfdbe@ MmEvictablePageReference drop
				end

				ipl@ KeIPLLower

				auto vad
				vaddr@ // vaddr
				process@ // process
				MmFaultParameters ok! vad! drop drop

				if (DEBUGCHECKS)
					if (ok@)
						ok@ "MmSatisfyFault: couldn't find VAD for private page (%i)\n" KeCrash
					end
				end

				process@ MmWorkingSetLock drop // will NEVER fail

				process@ // process
				MmWorkingSetEntryReserve ok! wsleptr! wsli!

				if (ok@)
					if (flags@ PTE_TRANSITION &)
						pfdbe@ MmEvictablePageDereference drop
					end

					process@ MmWorkingSetUnlock
					ptpfdbe@ process@ MmPTEDereference

					OSSIGNAL_KILL signal!

					return
				end

				if (flags@ PTE_INSWAP &)
					// swap it in. unlock and relock the working set to avoid
					// deadlock.

					process@ MmWorkingSetUnlock

					IPLDPC KeIPLRaise ipl!

					CANBLOCK MmPageWait ok! drop

					if (ok@)
						ipl@ KeIPLLower

						process@ MmWorkingSetLock drop

						wsli@ // wsli
						wsleptr@ // wsleptr
						process@ // process
						MmWorkingSetEntryFree

						process@ MmWorkingSetUnlock
						ptpfdbe@ process@ MmPTEDereference

						OSSIGNAL_KILL signal!

						return
					end

					0 // pri
					pteaddr@ // pte
					phyaddr@ PAGESHIFT >> // pno
					MmAnonymousSwapIn ok! phyaddr! pfdbe!

					process@ MmWorkingSetLock drop

					if (ok@)
						wsli@ // wsli
						wsleptr@ // wsleptr
						process@ // process
						MmWorkingSetEntryFree

						process@ MmWorkingSetUnlock
						ptpfdbe@ process@ MmPTEDereference

						OSSIGNAL_KILL signal!

						return
					end

					pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_PRIVATE |
					pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + sb

					process@ pfdbe@ MmPageFrameEntryAnonymous_Process + !
					vaddr@ pfdbe@ MmPageFrameEntryAnonymous_VirtualAddress + !

					PAGESHIFT phyaddr <<=

					ptpfdbe@ MmEvictablePageReference drop
				end

				PTE_V flags!

				if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_MODIFIED &)
					if (vad@ MmVAD_PageProtection + @ PAGEACCESS_WRITECOPY PAGEACCESS_WRITE | &)
						PTE_W flags |=
					end
				end

				phyaddr@ // phyaddr
				flags@ // flags
				vaddr@ // vaddr
				pteaddr@ // pteaddr
				process@ KeProcess_ASID + @ // asid
				MmPTEUpdate drop drop drop

				vaddr@ PAGENUMBERMASK & // vaddr
				wsleptr@ // wsleptr
				process@ // process
				MmWorkingSetEntryInsert

				process@ MmWorkingSetUnlock

				ptpfdbe@ process@ MmPTEDereference

				return
			end elseif (flags@ ~~)
				ipl@ KeIPLLower
			end else
				flags@ "MmSatisfyFault: odd PTE %08x\n" KeCrash
			end
		end
	end else
		ipl@ KeIPLLower
	end

	vaddr@ // vaddr
	process@ // process
	MmNormalFault signal!

	if (ptpfdbe@)
		ptpfdbe@ process@ MmPTEDereference
	end

	if (signal@ ~~)
		if (writing@)
			REFAULT signal!
		end
	end
end

fn MmFaultParameters { vaddr process -- offset object vad ok }
	vaddr@ // vaddr
	0 // length
	0 // mustbestart
	process@ // processobject
	MmVADFind ok! vad!

	if (ok@)
		return
	end

	if (vaddr@ vad@ MmVAD_FurthestExtentMapped + @ >)
		vaddr@ PAGENUMBERMASK & vad@ MmVAD_FurthestExtentMapped + !
	end

	vad@ MmVAD_MappedObject + @ object!
	vaddr@ vad@ MmVAD_StartVA + @ - vad@ MmVAD_OffsetInSection + @ + offset!
end

fn MmNormalFault { vaddr process -- signal }
	0 signal!

	auto offset
	auto object
	auto vad
	auto ok
	auto flags
	auto phyaddr
	auto pfdbe
	auto pteaddr
	auto wsli
	auto wsleptr

	vaddr@ // vaddr
	process@ // process
	MmFaultParameters ok! vad! object! offset!

	if (ok@)
		OSSIGNAL_SEGV signal!
		return
	end

	if (vad@ MmVAD_PageProtection + @ ~~)
		// guard region
		OSSIGNAL_SEGV signal!
		return
	end

	if (vad@ MmVAD_Flags + @ MMVADFLAG_FILE &)
		if (object@ IOFile_FileControlBlock + @ IOFileControlBlock_FileType + @ OSFILETYPE_CHARDEVICE ==)
			vaddr@ // vaddr
			process@ // process
			object@ // fileobject
			offset@ // offset
			vad@ // vad
			MmSatisfyCharFault ok!

			if (ok@)
				OSSIGNAL_KILL signal!
			end

			return
		end
	end

	vaddr@ // vaddr
	process@ // process
	MmPTECreate ok! pteaddr!

	if (ok@)
		OSSIGNAL_KILL signal!
		return
	end

	process@ MmWorkingSetLock drop // will NEVER fail

	process@ // process
	MmWorkingSetEntryReserve ok! wsleptr! wsli!

	process@ MmWorkingSetUnlock

	if (ok@)
		1 // deref
		pteaddr@ // pteaddr
		vaddr@ // vaddr
		process@ // process
		MmPTEDelete drop

		OSSIGNAL_KILL signal!
		return
	end

	if (vad@ MmVAD_Flags + @ MMVADFLAG_FILE &)
		object@ // fileobject
		offset@ // offset
		vad@ // vad
		MmSatisfyFileFault ok! phyaddr! pfdbe! flags!
	end else
		vaddr@ // vaddr
		pteaddr@ // pteaddr
		object@ // section
		offset@ // offset
		vad@ // vad
		MmSatisfyAnonFault ok! phyaddr! pfdbe! flags!
	end

	process@ MmWorkingSetLock drop

	if (ok@)
		wsli@ // wsli
		wsleptr@ // wsleptr
		process@ // process
		MmWorkingSetEntryFree

		process@ MmWorkingSetUnlock

		1 // deref
		pteaddr@ // pteaddr
		vaddr@ // vaddr
		process@ // process
		MmPTEDelete drop

		OSSIGNAL_KILL signal!
		return
	end

	1 process@ PsProcess_ThrottleFaultCount + +=

	phyaddr@ // phyaddr
	flags@ // flags
	vaddr@ // vaddr
	pteaddr@ // pteaddr
	process@ KeProcess_ASID + @ // asid
	MmPTEUpdate drop drop drop

	vaddr@ PAGENUMBERMASK & // vaddr
	wsleptr@ // wsleptr
	process@ // process
	MmWorkingSetEntryInsert

	process@ MmWorkingSetUnlock
end

fn MmSatisfyCharFault { vaddr process fileobject offset vad -- ok }
	auto fcb
	fileobject@ IOFile_FileControlBlock + @ fcb!

	auto getpagefunc
	fcb@ IOFileControlBlock_DispatchTable + @ IODispatchTable_GetPageAddress + @ getpagefunc!

	if (DEBUGCHECKS)
		if (getpagefunc@ ~~)
			"MmSatisfyCharFault: no GetPageAddress\n" KeCrash
		end
	end

	auto phyaddr
	offset@ // offset
	fcb@ // fcb
	getpagefunc@ IODispatchGetPageAddressFunction ok! phyaddr!

	if (DEBUGCHECKS)
		if (ok@)
			ok@ "MmSatisfyCharFault: failed to get page address (%i)\n" KeCrash
		end
	end

	auto flags
	PTE_V flags!

	if (vad@ MmVAD_PageProtection + @ PAGEACCESS_WRITE &)
		PTE_W flags |=
	end

	if (vad@ MmVAD_PageProtection + @ PAGEACCESS_NONCACHED &)
		PTE_NC flags |=
	end

	auto pteaddr
	vaddr@ // vaddr
	process@ // process
	MmPTECreate ok! pteaddr!

	if (ok@)
		return
	end

	phyaddr@ // phyaddr
	flags@ // flags
	vaddr@ // vaddr
	pteaddr@ // pteaddr
	process@ KeProcess_ASID + @ // asid
	MmPTEUpdate drop drop drop
end

fn MmSatisfyFileFault { fileobject offset vad -- flags pfdbe phyaddr ok }
	auto fcb
	fileobject@ IOFile_FileControlBlock + @ fcb!

	0 // flags
	0 // kflags
	offset@ PAGENUMBERMASK & // offset
	fcb@ // fcb
	IOCachePageRead ok! pfdbe! phyaddr!

	if (ok@)
		return
	end

	IDENTITYSPACEMASK phyaddr &=

	PTE_V flags!
end

fn MmSatisfyAnonFault { vaddr pteaddr section offset vad -- flags pfdbe phyaddr ok }
	if (section@)
		0 // pri
		offset@ // sectionoffset
		section@ // sectionobject
		MmSectionPageGet ok! pfdbe! phyaddr!

		if (ok@)
			return
		end
	end else
		// private page

		pteaddr@ // pte
		ZEROMUST // pri
		MmAnonymousPageAlloc ok! phyaddr! pfdbe!

		if (ok@)
			return
		end

		pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_PRIVATE |
		pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + sb

		KeProcessCurrent pfdbe@ MmPageFrameEntryAnonymous_Process + !
		vaddr@ pfdbe@ MmPageFrameEntryAnonymous_VirtualAddress + !

		PAGESHIFT phyaddr <<=
	end

	PTE_V flags!

	if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_MODIFIED &)
		if (vad@ MmVAD_PageProtection + @ PAGEACCESS_WRITE &)
			PTE_W flags |=
		end
	end
end

fn MmWriteFault { pteaddr vaddr process -- signal }
	// COW or dirty fault. lets find out which one

	0 signal!

	auto phyaddr

	auto cowpfdbe
	auto cowaddr
	0 cowaddr!

	auto vad
	0 vad!

	while (1)
		process@ MmWorkingSetLock drop

		// re-check the PTE with the working set mutex held, since it may have
		// been trimmed since we decided it was valid. we know that it's the same
		// PTE if it's still valid, and hasn't been switched out with something
		// else, because the VAD list mutex is held during all such events.
		// the loss of this convenient assumption is one reason that it sounds
		// hard to allow multiple threads to take a page fault per process without
		// waiting for eachother.

		pteaddr@ // pteaddr
		MmPTEInterpret ok! drop phyaddr!

		if (ok@)
			// invalid
			REFAULT signal!
			break
		end

		auto section
		auto ok
		auto pfdbe
		auto wsli
		auto wsleptr

		if (vad@ ~~)
			// only look up the VAD if we didn't already do it last time.

			vaddr@ // vaddr
			process@ // process
			MmFaultParameters ok! vad! section! drop

			if (DEBUGCHECKS)
				if (ok@)
					ok@ "MmWriteFault: failed to look up VAD (%i)\n" KeCrash
				end

				if (vad@ MmVAD_PageProtection + @ ~~)
					"MmWriteFault: VAD was guard region\n" KeCrash
				end
			end
		end

		auto ipl

		phyaddr@ PAGESHIFT >> MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!

		if (vad@ MmVAD_PageProtection + @ PAGEACCESS_WRITECOPY &)
			if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_PRIVATE &)
				pfdbe@ // pfdbe
				process@ // process
				MmEvictablePageModify

				phyaddr@ // phyaddr
				PTE_V PTE_W | // flags
				vaddr@ // vaddr
				pteaddr@ // pteaddr
				process@ KeProcess_ASID + @ // asid
				MmPTEUpdate drop drop drop

				break
			end

			// COW, allocate a page frame and copy the old page frame into it.

			if (cowaddr@ ~~)
				process@ MmWorkingSetUnlock

				pteaddr@ // pte
				FREEFIRST // pri
				MmAnonymousPageAlloc ok! cowaddr! cowpfdbe!

				if (ok@)
					OSSIGNAL_KILL signal!
					return
				end

				// starts at demand-zero, which isn't true here
				MMSWAPPAGE_NONE cowpfdbe@ MmPageFrameEntryAnonymous_SwapPTE + !

				cowpfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_MODIFIED |
				cowpfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + sb

				// we unlocked the working set mutex so we have to recheck
				// everything...

				continue
			end

			1 vad@ MmVAD_COWCount + +=

			cowpfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_PRIVATE |
			cowpfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + sb

			process@ cowpfdbe@ MmPageFrameEntryAnonymous_Process + !
			vaddr@ cowpfdbe@ MmPageFrameEntryAnonymous_VirtualAddress + !

			PAGESHIFT cowaddr <<=

			cowaddr@ IDENTITYSPACE | // dest
			phyaddr@ IDENTITYSPACE | // src
			PAGESIZE // size
			memcpy

			cowaddr@ // phyaddr
			PTE_V PTE_W | // flags
			vaddr@ // vaddr
			pteaddr@ // pteaddr
			process@ KeProcess_ASID + @ // asid
			MmPTEUpdate drop drop drop

			// we COW'd over top of the old page frame and no longer have it
			// mapped, so dereference it
			pfdbe@ MmEvictablePageDereference drop

			process@ MmWorkingSetUnlock

			return
		end elseif (vad@ MmVAD_PageProtection + @ PAGEACCESS_WRITE & ~~)
			// privilege violation
			OSSIGNAL_SEGV signal!
			break
		end else
			pfdbe@ // pfdbe
			process@ // process
			MmEvictablePageModify

			phyaddr@ // phyaddr
			PTE_V PTE_W | // flags
			vaddr@ // vaddr
			pteaddr@ // pteaddr
			process@ KeProcess_ASID + @ // asid
			MmPTEUpdate drop drop drop

			break
		end

		"MmWriteFault: unreachable\n" KeCrash
	end

	process@ MmWorkingSetUnlock

	if (cowaddr@)
		IPLDPC KeIPLRaise ipl!

		cowpfdbe@ MmEvictablePageDereference drop

		ipl@ // ipl
		cowpfdbe@ // pfdbe
		MmAnonymousPageDelete
	end
end