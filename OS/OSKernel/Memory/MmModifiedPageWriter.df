//
// Implements the modified page writer thread.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALDebug.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<inc>/Transfer.h"

#include "<ll>/OSDLL/OS.h"

const MPWTRANSFERCOUNT 4

struct IOTransferTable
	KeEvent_SIZEOF Event
	4 FreeListHead

	(IOTransfer_SIZEOF MPWTRANSFERCOUNT *) Transfers
endstruct

buffer MmMPWFileTransfers IOTransferTable_SIZEOF
buffer MmMPWAnonTransfers IOTransferTable_SIZEOF

fn IOTransferTableInitialize { transfertable -- }
	0 // signaled
	OSEVENT_SYNCH // type
	"IOTransferAvailableEvent" // name
	transfertable@ IOTransferTable_Event + // event
	KeEventInitialize

	auto i
	0 i!

	auto transfer
	transfertable@ IOTransferTable_Transfers + transfer!

	while (i@ MPWTRANSFERCOUNT <)
		transfertable@ transfer@ IOTransfer_TransferTable + !

		transfertable@ IOTransferTable_FreeListHead + @ transfer@ IOTransfer_Next + !
		transfer@ transfertable@ IOTransferTable_FreeListHead + !

		IOTransfer_SIZEOF transfer +=
		1 i +=
	end
end

fn IOTransferComplete { transfer -- }
	// complete a transfer by putting it on the free list and signaling its
	// event.

	auto ttable
	transfer@ IOTransfer_TransferTable + @ ttable!

	ttable@ IOTransferTable_FreeListHead + @ transfer@ IOTransfer_Next + !
	transfer@ ttable@ IOTransferTable_FreeListHead + !

	0 // priboost
	ttable@ IOTransferTable_Event + // event
	KeEventSignal
end

fn MmMPWWaitForTransfer { transfertable -- }
	// try to find an available transfer to use.
	// if none are available, wait for one to complete and use that.

	auto transfer
	transfertable@ IOTransferTable_FreeListHead + @ transfer!

	while (transfer@ ~~)
		KERNELMODE // waitmode
		0 // alertable
		OSWAIT_TIMEOUTINFINITE // timeout
		transfertable@ IOTransferTable_Event + // object
		KeThreadWaitForObject drop

		transfertable@ IOTransferTable_FreeListHead + @ transfer!
	end
end

fn MmModifiedPageWriter { context1 context2 -- }
	PRIORITY_LOWREALTIME 1 + // priority
	KeThreadCurrent@ // thread
	KeThreadPrioritySet

	IPLLOW KeIPLLower // kernel threads start in IPLDPC

	KeThreadCurrent@ KeThreadIgnoreKill drop

	auto anontransfers
	MmMPWAnonTransfers anontransfers!

	auto filetransfers
	MmMPWFileTransfers filetransfers!

	anontransfers@ // transfertable
	IOTransferTableInitialize

	filetransfers@ // transfertable
	IOTransferTableInitialize

	auto mdlpos
	auto pagecount

	auto transfer
	0 transfer!

	auto ftransfer
	0 ftransfer!

	KERNELMODE // waitmode
	0 // alertable
	OSWAIT_TIMEOUTINFINITE // timeout
	IOSwapFileCreationEvent
	KeThreadWaitForObject drop

	// the page writer thread. writes out modified pages and slaps them on the
	// evictable list. does this when memory is low or there are too many
	// modified pages.

	while (1)
		auto ok
		auto waitok

		KERNELMODE // waitmode
		0 // alertable
		OSWAIT_TIMEOUTINFINITE // timeout
		MmModifiedPageEvent // object
		KeThreadWaitForObject waitok!

		if (DEBUGCHECKS)
			if (waitok@ z<)
				if (waitok@ STATUS_WAIT_TIMEOUT ~=)
					waitok@ "MmModifiedPageWriter: failed to wait on low memory event (%i)\n" KeCrash
				end
			end
		end

		0 pagecount!

		auto modcount
		MmModifiedPageCount@ modcount!

		while (1)
			if (modcount@ ~~)
				break
			end

			auto ipl
			IPLDPC KeIPLRaise ipl!

			auto pfdbe
			MmModifiedPageListHead@ pfdbe!

			if (pfdbe@)
				if (pfdbe@ MmPageFrameEntryEvictable_EvictionTypeB + gb MMEVICTTYPE_ANON ==)
					if (transfer@ ~~)
						anontransfers@ IOTransferTable_FreeListHead + @ transfer!

						if (transfer@ ~~)
							ipl@ KeIPLLower

							anontransfers@ // transfertable
							MmMPWWaitForTransfer

							continue
						end

						transfer@ IOTransfer_Next + @ anontransfers@ IOTransferTable_FreeListHead + !
						transfer@ MmMDLHeader_SIZEOF + mdlpos!
					end
				end else
					if (ftransfer@ ~~)
						filetransfers@ IOTransferTable_FreeListHead + @ ftransfer!

						if (ftransfer@ ~~)
							ipl@ KeIPLLower

							filetransfers@ // transfertable
							MmMPWWaitForTransfer

							continue
						end

						ftransfer@ IOTransfer_Next + @ filetransfers@ IOTransferTable_FreeListHead + !
					end
				end

				pfdbe@ MmEvictablePageReference drop

				if (pfdbe@ MmPageFrameEntryEvictable_EvictionTypeB + gb MMEVICTTYPE_ANON ==)
					if (DEBUGCHECKS)
						if (pfdbe@ MmPageFrameEntryAnonymous_SwapPTE + @ PTE_INSWAP &)
							"MmModifiedPageWriter: swap page exists\n" KeCrash
						end
					end

					pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_MODIFIED ~ &
					pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + sb

					ipl@ KeIPLLower

					pfdbe@ mdlpos@!
					4 mdlpos +=
					-1 mdlpos@!

					1 pagecount +=

					if (pagecount@ IOTRANSFERMAX PAGESHIFT >> ==)
						// maximum transfer has been clustered, do it

						pagecount@ // pagecount
						transfer@ // mdl
						IOSwapFileWriteMDL drop

						0 pagecount!
						0 transfer!
					end

					IPLDPC KeIPLRaise ipl!
				end else
					ftransfer@ // transfer
					IOKFLAG_SWAPOUT // kflags
					pfdbe@ // pfdbe
					IOCachePageWrite drop

					0 ftransfer!

					pfdbe@ MmEvictablePageDereference drop
				end

				if (modcount@)
					1 modcount -=
				end
			end else
				0 modcount!
			end

			ipl@ KeIPLLower
		end

		if (pagecount@)
			pagecount@ // pagecount
			transfer@ // mdl
			IOSwapFileWriteMDL drop

			0 pagecount!
			0 transfer!
		end
	end
end