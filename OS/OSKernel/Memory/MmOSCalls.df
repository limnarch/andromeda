//
// Implements the system calls for the memory manager subsystem.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/Object.h"

#include "<inc>/IO.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

fn OSSectionCreate { pageprotection anonsize filehandle permissions name -- sectionhandle ok }
	auto kname
	0 kname!

	if (name@)
		OBNAMEMAX // max
		name@ // string
		ExCallGetString ok! kname!

		if (ok@)
			return
		end
	end

	pageprotection@ // pageprotection
	anonsize@ // anonsize
	filehandle@ // filehandle
	permissions@ // permissions
	kname@ // name
	MmSectionObjectCreate ok! sectionhandle!

	if (name@)
		kname@ ExCallFreeString
	end
end

fn OSSectionMapView { length startva sectionoffset sectionhandle processhandle pageprotection flags -- realva ok }
	length@ startva@ sectionoffset@ sectionhandle@ processhandle@ pageprotection@ flags@ MmSectionMapView ok! realva!
end

fn OSUnmapView { length vaddr processhandle -- ok }
	length@ vaddr@ processhandle@ MmUnmapView ok!
end

fn OSRemapView { pageprotection length vaddr processhandle -- ok }
	pageprotection@ length@ vaddr@ processhandle@ MmRemapView ok!
end

fn OSMemoryQuery { query -- ok }
	auto kquery
	OSMemoryInformation_SIZEOF alloc kquery!

	kquery@ MmQuery ok!

	if (ok@)
		return
	end

	query@ // dest
	kquery@ // src
	OSMemoryInformation_SIZEOF // size
	KeSafeCopyOut ok!
end

fn OSWorkingSetPurge { -- ok }
	auto process
	KeProcessCurrent process!

	process@ MmWorkingSetLock ok!

	if (ok@)
		return
	end

	process@ MmWorkingSetPurge

	process@ MmWorkingSetUnlock
end