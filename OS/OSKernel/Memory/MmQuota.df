//
// Implements job quota management.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/IO.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

#include "MmInternal.h"

var MiQuotaBlockListHead 0

fn MmQuotaBlockGetByUID { uid forkquotablock -- quotablock ok }
	KERNELMODE // waitmode
	1 // alertable
	OSWAIT_TIMEOUTINFINITE // timeout
	MmQuotaListMutex // object
	KeThreadWaitForObject ok!

	if (ok@)
		return
	end

	auto ipl
	IPLDPC KeIPLRaise ipl!

	MiQuotaBlockListHead@ quotablock!

	while (quotablock@)
		if (quotablock@ MiQuotaBlock_UID + @ uid@ ==)
			quotablock@ MmQuotaBlockReference

			ipl@ KeIPLLower

			0 MmQuotaListMutex KeMutexRelease drop

			return
		end

		quotablock@ MiQuotaBlock_Next + @ quotablock!
	end

	ipl@ KeIPLLower

	// not found, fork the forkquotablock to create one for this UID

	if (forkquotablock@ ~~)
		0 MmQuotaListMutex KeMutexRelease drop
		-1 ok!
		return
	end

	uid@ // uid
	forkquotablock@ // quotablock
	MmQuotaBlockFork ok! quotablock!

	0 MmQuotaListMutex KeMutexRelease drop
end

fn MmQuotaBlockCreate { uid wsmax pghpmax vmmax heapmax -- quotablock ok }
	fnsection "PAGE$text"

	MiQuotaBlock_SIZEOF // bytes
	'Quot' // tag
	CANBLOCK // flags
	MmAllocWithTag ok! quotablock!

	if (ok@)
		return
	end

	uid@ wsmax@ pghpmax@ vmmax@ heapmax@ quotablock@ MmQuotaBlockInitialize ok!
end

fn MmQuotaBlockFork { uid quotablock -- newquotablock ok }
	fnsection "PAGE$text"

	MiQuotaBlock_SIZEOF // bytes
	'Quot' // tag
	CANBLOCK // flags
	MmAllocWithTag ok! newquotablock!

	if (ok@)
		return
	end

	uid@ // uid
	quotablock@ MiQuotaBlock_WorkingSetMax + @ // wsmax
	quotablock@ MiQuotaBlock_PagedPoolMax + @ // pghpmax
	quotablock@ MiQuotaBlock_VMMax + @ // vmmax
	quotablock@ MiQuotaBlock_PoolMax + @ // heapmax
	newquotablock@ // quotablock
	MmQuotaBlockInitialize ok!
end

fn MmQuotaBlockInitialize { uid wsmax pghpmax vmmax heapmax quotablock -- ok }
	0 ok!

	1 quotablock@ MiQuotaBlock_References + !
	uid@ quotablock@ MiQuotaBlock_UID + !

	0 quotablock@ MiQuotaBlock_PoolUsed + !
	heapmax@ quotablock@ MiQuotaBlock_PoolMax + !

	0 quotablock@ MiQuotaBlock_VMUsed + !
	vmmax@ quotablock@ MiQuotaBlock_VMMax + !

	0 quotablock@ MiQuotaBlock_PagedPoolUsed + !
	pghpmax@ quotablock@ MiQuotaBlock_PagedPoolMax + !

	0 quotablock@ MiQuotaBlock_WorkingSetUsed + !
	wsmax@ quotablock@ MiQuotaBlock_WorkingSetMax + !

	0 quotablock@ MiQuotaBlock_Prev + !

	auto rs
	HALCPUInterruptDisable rs!

	auto h
	MiQuotaBlockListHead@ h!

	h@ quotablock@ MiQuotaBlock_Next + !

	if (h@)
		quotablock@ h@ MiQuotaBlock_Prev + !
	end

	quotablock@ MiQuotaBlockListHead!

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockReference { quotablock -- }
	auto rs
	HALCPUInterruptDisable rs!

	if (DEBUGCHECKS)
		if (quotablock@ MiQuotaBlock_References + @ 0xFFFFFFFF ==)
			"MmQuotaBlockReference: overflow!\n" KeCrash
		end
	end

	1 quotablock@ MiQuotaBlock_References + +=

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockDereference { quotablock -- }
	auto rs
	HALCPUInterruptDisable rs!

	auto oldcount
	quotablock@ MiQuotaBlock_References + @ oldcount!

	if (DEBUGCHECKS)
		if (oldcount@ 0 ==)
			"MmQuotaBlockDereference: references<0\n" KeCrash
		end
	end

	oldcount@ 1 - quotablock@ MiQuotaBlock_References + !

	if (oldcount@ 1 ==)
		// got dereferenced to 0, delete

		auto ls
		auto ns

		quotablock@ MiQuotaBlock_Prev + @ ls!
		quotablock@ MiQuotaBlock_Next + @ ns!

		if (ls@)
			ns@ ls@ MiQuotaBlock_Next + !
		end else
			ns@ MiQuotaBlockListHead!
		end

		if (ns@)
			ls@ ns@ MiQuotaBlock_Prev + !
		end

		rs@ HALCPUInterruptRestore

		quotablock@ MmFree
	end else
		rs@ HALCPUInterruptRestore
	end
end

fn MmQuotaChargeBuffer { buffersize -- ok }
	// helper function for charging heap quota for query syscalls

	buffersize@ MmChargeBytesGet // charge
	KeProcessCurrent PsProcess_PagedArea + @ PsProcessPaged_QuotaBlock + @ // quotablock
	MmQuotaBlockCharge ok!
end

fn MmQuotaUnchargeBuffer { buffersize -- }
	buffersize@ MmChargeBytesGet // charge
	KeProcessCurrent PsProcess_PagedArea + @ PsProcessPaged_QuotaBlock + @ // quotablock
	MmQuotaBlockUncharge
end

fn MmQuotaBlockCharge { charge quotablock -- ok }
	if (charge@ z<)
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	0 ok!

	auto rs
	HALCPUInterruptDisable rs!

	auto heapmax
	quotablock@ MiQuotaBlock_PoolMax + @ heapmax!

	auto oldcharge
	quotablock@ MiQuotaBlock_PoolUsed + @ oldcharge!

	if (oldcharge@ charge@ + heapmax@ >)
		rs@ HALCPUInterruptRestore

		STATUS_QUOTA_EXCEEDED ok!

		return
	end

	oldcharge@ charge@ + quotablock@ MiQuotaBlock_PoolUsed + !

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockUncharge { charge quotablock -- }
	auto rs
	HALCPUInterruptDisable rs!

	auto oldcharge
	quotablock@ MiQuotaBlock_PoolUsed + @ oldcharge!

	if (DEBUGCHECKS)
		if (charge@ oldcharge@ >)
			charge@
			oldcharge@
			"MmQuotaBlockUncharge: attempt to uncharge more than was charged (oldcharge=%d charge=%d)\n" KeCrash
		end
	end

	oldcharge@ charge@ - quotablock@ MiQuotaBlock_PoolUsed + !

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockChargeWS { charge quotablock -- ok }
	if (charge@ z<)
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	0 ok!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	auto commitlimit
	MmPhysicalCommitLimit@ commitlimit!

	auto commitused
	MmPhysicalCommitUsage@ commitused!

	if (commitused@ charge@ + commitlimit@ >)
		ipl@ KeIPLLower

		STATUS_PHYSICAL_COMMIT_EXCEEDED ok!

		return
	end

	auto wsmax
	quotablock@ MiQuotaBlock_WorkingSetMax + @ wsmax!

	auto oldcharge
	quotablock@ MiQuotaBlock_WorkingSetUsed + @ oldcharge!

	if (oldcharge@ charge@ + wsmax@ >)
		ipl@ KeIPLLower

		STATUS_WS_QUOTA_EXCEEDED ok!

		return
	end

	oldcharge@ charge@ + quotablock@ MiQuotaBlock_WorkingSetUsed + !
	commitused@ charge@ + MmPhysicalCommitUsage!

	ipl@ KeIPLLower
end

fn MmQuotaBlockUnchargeWS { charge quotablock -- }
	auto rs
	HALCPUInterruptDisable rs!

	auto oldcharge
	quotablock@ MiQuotaBlock_WorkingSetUsed + @ oldcharge!

	auto commitused
	MmPhysicalCommitUsage@ commitused!

	if (DEBUGCHECKS)
		if (charge@ oldcharge@ >)
			charge@
			oldcharge@
			"MmQuotaBlockUnchargeWS: attempt to uncharge more than was charged (oldcharge=%d charge=%d)\n" KeCrash
		end

		if (charge@ commitused@ >)
			commitused@
			charge@
			"MmQuotaBlockUnchargeWS: attempt to uncharge more commit than was charged %d > %d\n" KeCrash
		end
	end

	oldcharge@ charge@ - quotablock@ MiQuotaBlock_WorkingSetUsed + !
	commitused@ charge@ - MmPhysicalCommitUsage!

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockChargeVMNoExtend { charge quotablock -- ok }
	if (DEBUGCHECKS)
		if (charge@ PAGEOFFSETMASK &)
			"MmQuotaBlockChargeVM: charge isn't aligned to page size\n" KeCrash
		end
	end

	if (charge@ z<)
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	PAGESHIFT charge >>=

	0 ok!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	auto vmmax
	quotablock@ MiQuotaBlock_VMMax + @ vmmax!

	auto oldcharge
	quotablock@ MiQuotaBlock_VMUsed + @ oldcharge!

	if (oldcharge@ charge@ PAGESHIFT << + vmmax@ >)
		ipl@ KeIPLLower

		STATUS_VM_QUOTA_EXCEEDED ok!

		return
	end

	oldcharge@ charge@ PAGESHIFT << + quotablock@ MiQuotaBlock_VMUsed + !

	auto commitlimit
	MmCommitLimit@ commitlimit!

	auto commitused
	MmCommitUsage@ commitused!

	if (commitused@ charge@ + commitlimit@ >)
		// uncharge quota
		charge@ PAGESHIFT << quotablock@ MiQuotaBlock_VMUsed + -=

		ipl@ KeIPLLower

		STATUS_VM_QUOTA_EXCEEDED ok!

		return
	end

	commitused@ charge@ + MmCommitUsage!

	ipl@ KeIPLLower
end

fn MmQuotaBlockChargeVM { charge quotablock -- ok }
	if (DEBUGCHECKS)
		if (charge@ PAGEOFFSETMASK &)
			"MmQuotaBlockChargeVM: charge isn't aligned to page size\n" KeCrash
		end
	end

	if (charge@ z<)
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	PAGESHIFT charge >>=

	0 ok!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	auto vmmax
	quotablock@ MiQuotaBlock_VMMax + @ vmmax!

	auto oldcharge
	quotablock@ MiQuotaBlock_VMUsed + @ oldcharge!

	if (oldcharge@ charge@ PAGESHIFT << + vmmax@ >)
		ipl@ KeIPLLower

		STATUS_VM_QUOTA_EXCEEDED ok!

		return
	end

	oldcharge@ charge@ PAGESHIFT << + quotablock@ MiQuotaBlock_VMUsed + !

	auto commitlimit
	MmCommitLimit@ commitlimit!

	auto commitused
	MmCommitUsage@ commitused!

	while (commitused@ charge@ + commitlimit@ >)
		charge@ IOPageFileRequestExpansion ok!

		if (ok@)
			// uncharge quota
			charge@ PAGESHIFT << quotablock@ MiQuotaBlock_VMUsed + -=

			ipl@ KeIPLLower

			return
		end

		MmCommitLimit@ commitlimit!
		MmCommitUsage@ commitused!
	end

	commitused@ charge@ + MmCommitUsage!

	ipl@ KeIPLLower
end

fn MmQuotaBlockUnchargeVM { charge quotablock -- }
	if (DEBUGCHECKS)
		if (charge@ PAGEOFFSETMASK &)
			"MmQuotaBlockUnchargeVM: charge isn't aligned to page size\n" KeCrash
		end
	end

	auto rs
	HALCPUInterruptDisable rs!

	auto oldcharge
	quotablock@ MiQuotaBlock_VMUsed + @ oldcharge!

	auto commitused
	MmCommitUsage@ commitused!

	if (DEBUGCHECKS)
		if (charge@ oldcharge@ >)
			"MmQuotaBlockUnchargeVM: attempt to uncharge more than was charged\n" KeCrash
		end

		if (charge@ PAGESHIFT >> commitused@ >)
			commitused@
			charge@ PAGESHIFT >>
			"MmQuotaBlockUnchargeVM: attempt to uncharge more commit than was charged %d > %d\n" KeCrash
		end
	end

	oldcharge@ charge@ - quotablock@ MiQuotaBlock_VMUsed + !
	commitused@ charge@ PAGESHIFT >> - MmCommitUsage!

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockChargePaged { charge quotablock -- ok }
	if (charge@ z<)
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	0 ok!

	auto rs
	HALCPUInterruptDisable rs!

	auto phmax
	quotablock@ MiQuotaBlock_PagedPoolMax + @ phmax!

	auto oldcharge
	quotablock@ MiQuotaBlock_PagedPoolUsed + @ oldcharge!

	if (oldcharge@ charge@ + phmax@ >)
		rs@ HALCPUInterruptRestore

		STATUS_PAGED_QUOTA_EXCEEDED ok!

		return
	end

	oldcharge@ charge@ + quotablock@ MiQuotaBlock_PagedPoolUsed + !

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockUnchargePaged { charge quotablock -- }
	auto rs
	HALCPUInterruptDisable rs!

	auto oldcharge
	quotablock@ MiQuotaBlock_PagedPoolUsed + @ oldcharge!

	if (DEBUGCHECKS)
		if (charge@ oldcharge@ >)
			"MmQuotaBlockUnchargePaged: attempt to uncharge more than was charged\n" KeCrash
		end
	end

	oldcharge@ charge@ - quotablock@ MiQuotaBlock_PagedPoolUsed + !

	rs@ HALCPUInterruptRestore
end

fn MmSetQuota { quota quotablock -- }
	fnsection "PAGE$text"

	// set new limits on the quotablock.

	quota@ OSQuota_Pool + @ quotablock@ MiQuotaBlock_PoolMax + !
	quota@ OSQuota_VirtualMemory + @ quotablock@ MiQuotaBlock_VMMax + !
	quota@ OSQuota_PagedPool + @ quotablock@ MiQuotaBlock_PagedPoolMax + !
	quota@ OSQuota_WorkingSet + @ quotablock@ MiQuotaBlock_WorkingSetMax + !
end

fn MmQuotaQuery { query quotablock -- }
	fnsection "PAGE$text"

	query@ // ptr
	OSQuotaInformation_SIZEOF // size
	0 // word
	memset

	quotablock@ MiQuotaBlock_PoolUsed + @ query@ OSQuotaInformation_PoolUsed + !
	quotablock@ MiQuotaBlock_PoolMax + @ query@ OSQuotaInformation_PoolMaximum + !

	quotablock@ MiQuotaBlock_VMUsed + @ query@ OSQuotaInformation_VirtualMemoryUsed + !
	quotablock@ MiQuotaBlock_VMMax + @ query@ OSQuotaInformation_VirtualMemoryMaximum + !

	quotablock@ MiQuotaBlock_PagedPoolUsed + @ query@ OSQuotaInformation_PagedPoolUsed + !
	quotablock@ MiQuotaBlock_PagedPoolMax + @ query@ OSQuotaInformation_PagedPoolMaximum + !

	quotablock@ MiQuotaBlock_WorkingSetUsed + @ query@ OSQuotaInformation_WorkingSetUsed + !
	quotablock@ MiQuotaBlock_WorkingSetMax + @ query@ OSQuotaInformation_WorkingSetMaximum + !
end

#ifdef DEBUGCHECKS

fn MmQuotaBlockPrint { quotablock -- }
	quotablock@ MiQuotaBlock_PoolMax + @
	quotablock@ MiQuotaBlock_PoolUsed + @       " POOL = %d/%i bytes\n" Printf

	quotablock@ MiQuotaBlock_PagedPoolMax + @
	quotablock@ MiQuotaBlock_PagedPoolUsed + @  " PGPL = %d/%i pages\n" Printf

	quotablock@ MiQuotaBlock_VMMax + @
	quotablock@ MiQuotaBlock_VMUsed + @         " VM   = %d/%i bytes\n" Printf

	quotablock@ MiQuotaBlock_References + @     " REFS = %d\n" Printf

	quotablock@ MiQuotaBlock_WorkingSetMax + @
	quotablock@ MiQuotaBlock_WorkingSetUsed + @ " WS   = %d/%i pages\n" Printf
end

#endif