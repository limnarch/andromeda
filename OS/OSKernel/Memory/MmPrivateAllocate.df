//
// Implements allocation of private virtual memory for userspace.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/Object.h"

#include "<inc>/IO.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

#include "MmInternal.h"

fn MmAllocateProcessObject { length startva processobject pageprotection flags -- realva ok }
	fnsection "PAGE$text"

	if (pageprotection@ PAGEACCESS_WRITECOPY &)
		// COW makes no sense for private memory.
		STATUS_INVALID_ARGUMENT ok!

		return
	end elseif (pageprotection@)
		if (pageprotection@ PAGEACCESS_WRITE PAGEACCESS_READ | & PAGEACCESS_WRITE PAGEACCESS_READ | ~=)
			// read-only or write-only memory makes no sense for private memory.
			STATUS_INVALID_ARGUMENT ok!

			return
		end
	end

	if (processobject@ PsSystemProcess@ ==)
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	if (startva@ PAGEOFFSETMASK &)
		// requested startva isn't page-aligned
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	if (length@ 0 ==)
		// length is given as 0 which makes this a no-op
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	length@ PAGEOFFSETMASK + PAGENUMBERMASK & length!

	if (length@ 0 ==)
		// length overflowed
		STATUS_OVERFLOW ok!

		return
	end

	if (startva@ length@ + startva@ <)
		// startva+length overflows
		STATUS_OVERFLOW ok!

		return
	end

	if (startva@ length@ + MMHIGHESTUSERADDRESS >)
		// requested region goes outside userspace

		if (flags@ OSMAPFLAG_FIXEDADDRESS &)
			STATUS_UNAVAILABLE_ADDRESS ok!

			return
		end

		0 startva!
	end

	auto locked
	processobject@ KeThreadCurrent@ KeThread_Process + @ ~= locked!

	if (locked@)
		0 // needsthread
		processobject@ // processobject
		PsProcessCreateDeleteAcquireCheck ok!

		if (ok@)
			return
		end
	end

	length@ // viewsize
	startva@ // startva
	flags@ OSMAPFLAG_FIXEDADDRESS & // fixedva
	pageprotection@ // pageprotection
	0 // sectionoffset
	0 // mappedobject
	processobject@ // processobject
	MiVADFindAndMapFreeRegion ok! realva!

	if (locked@)
		processobject@ PsProcessCreateDeleteUnlock
	end
end

fn MmAllocate { length startva processhandle pageprotection flags -- realva ok }
	fnsection "PAGE$text"

	auto access
	auto processobject

	PsProcessObjectType@ // type
	processhandle@ // handle
	ObObjectReferenceByHandle ok! processobject! access!

	if (ok@)
		return
	end

	access@ ACCESS_WRITE SeCheckAccess ok!

	if (ok@)
		processobject@ ObObjectDereferenceByPointer drop

		return
	end

	length@ // length
	startva@ // startva
	processobject@ // processobject
	pageprotection@ // pageprotection
	flags@ // flags
	MmAllocateProcessObject ok! realva!

	processobject@ ObObjectDereferenceByPointer drop
end