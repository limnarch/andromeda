//
// Implements the balance set manager thread.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALRTC.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

buffer MmSwapEvent KeEvent_SIZEOF
public MmSwapEvent

buffer MmBalanceSetManagerTimer KeTimer_SIZEOF

const MMBALANCESETINTERVAL 1000

const MMSTACKSCANPERIOD 5
const MMSTACKAGE        10

var MmSwapOutStacksPlease 0

externptr MmOkToUnwire

fn MmBalanceSetManager { -- }
	auto ipl
	IPLDPC KeIPLRaise ipl!
	PRIORITY_LOWREALTIME 1 + // priority
	KeThreadCurrent@ // thread
	KeThreadPrioritySet
	ipl@ KeIPLLower

	KeThreadCurrent@ KeThreadIgnoreKill drop

	// create the swap thread in the context of the balance set manager cuz
	// I like to be confusing.

	1 MmOkToUnwire!

	0 // signaled
	OSEVENT_SYNCH // type
	"MmSwapEvent" // name
	MmSwapEvent // event
	KeEventInitialize

	0 // context1
	0 // context2
	pointerof MmSwapWorker // startfunc
	0 // creationflags
	ACCESS_OWNER_ALL // permissions
	"SwapWorker" // name
	PsSystemProcess@ // processobject
	PsThreadCreateObject ok! drop

	if (ok@)
		ok@ "MmBalanceSetManager: couldn't create 'SwapWorker' thread (%i)\n" KeCrash
	end

	// responsible for:
	// - kernel thread stack swapping
	// - process swapping (page table swapping & working set purging)
	// - random thread priority boosting
	// - working set trimming

	// this little idiot is the only reason we will fit snugly in 4MB RAM.

	"MmBalanceSetManagerTimer" // name
	0 // function
	MmBalanceSetManagerTimer // timer
	KeTimerInitialize

	auto objecttable
	8 alloc objecttable!

	MmBalanceSetManagerTimer objecttable@!
	MmEventLowMemory objecttable@ 4 + !

	auto stackperiod
	MMSTACKSCANPERIOD stackperiod!

	0 // context1
	0 // context2
	MMBALANCESETINTERVAL // interval
	MmBalanceSetManagerTimer // timer
	KeTimerEnqueue

	while (1)
		auto ok
		auto reason

		KERNELMODE // waitmode
		0 // alertable
		OSWAITANY // waittype
		OSWAIT_TIMEOUTINFINITE // timeout
		2 // objectcount
		objecttable@ // objecttable
		KeThreadWaitForMultipleObjects reason!

		if (DEBUGCHECKS)
			if (reason@ z<)
				if (reason@ STATUS_WAIT_TIMEOUT ~=)
					reason@ "MmBalanceSetManager: failed to wait on low memory event (%i)\n" KeCrash
				end
			end
		end

		auto needed
		auto dire

		if (reason@ 0 ==)
			// timer expired

			if (ExShuttingDown@ ~~)
				if (PsSystemInitProcess@ KeProcess_Terminated + @)
					PsSystemInitProcess@ KeProcess_KilledStatus + @
					"SystemInit died (%i)\n" KeCrash
				end
			end

			MmManageWorkingSets drop

			1 stackperiod -=

			if (stackperiod@ ~~)
				auto rs
				HALCPUInterruptDisable rs!

				if (MmSwapOutStacksPlease@ ~~)
					1 MmSwapOutStacksPlease!
					rs@ HALCPUInterruptRestore

					0 // priboost
					MmSwapEvent // event
					KeEventSignal
				end else
					rs@ HALCPUInterruptRestore
				end

				MMSTACKSCANPERIOD stackperiod!
			end

			0 // context1
			0 // context2
			MMBALANCESETINTERVAL // interval
			MmBalanceSetManagerTimer // timer
			KeTimerEnqueue
		end elseif (reason@ 1 ==)
			// low memory event

			if (MmManageWorkingSets ~~)
				0 // priboost
				MmModifiedPageEvent // event
				KeEventSignal
			end

			// wait 80ms before trying again

			80 // interval
			KERNELMODE // waitmode
			0 // alertable
			KeThreadSleep drop
		end
	end
end

var MmProcessSwapOutListHead 0

fn MmSwapWorker { context1 context2 -- }
	PRIORITY_HIGHREALTIME 1 - // priority
	KeThreadCurrent@ // thread
	KeThreadPrioritySet

	IPLLOW KeIPLLower // kernel threads start in IPLDPC

	KeThreadCurrent@ KeThreadIgnoreKill drop

	// not to be confused with the thread that writes pages to swap, which is
	// the modified page writer. this evicts entire processes from memory for
	// being inactive too long.

	while (1)
		auto ok
		auto waitok

		KERNELMODE // waitmode
		0 // alertable
		OSWAIT_TIMEOUTINFINITE // timeout
		MmSwapEvent // object
		KeThreadWaitForObject waitok!

		if (DEBUGCHECKS)
			if (waitok@ z<)
				waitok@ "MmSwapWorker: failed to wait on swap event (%i)\n" KeCrash
			end
		end

		while (1)
			auto ipl
			IPLDPC KeIPLRaise ipl!

			if (MmSwapOutStacksPlease@)
				0 MmSwapOutStacksPlease!
				MmSwapOutStacks

				ipl@ KeIPLLower

				continue
			end elseif (KeThreadSwapInListHead@)
				MmSwapInStacks

				ipl@ KeIPLLower

				continue
			end elseif (MmProcessSwapOutListHead@)
				MmSwapOutProcesses

				ipl@ KeIPLLower

				continue
			end

			ipl@ KeIPLLower

			break
		end
	end
end

fn MmSwapOutStacks { -- }
	// scans the list of blocked threads and makes the kernel stacks of those
	// that have been blocked longer than MMSTACKAGE seconds non-resident.

	// called at IPLDPC.

	auto uptime
	KeTime_SIZEOF alloc uptime!

	uptime@ HALUptimeQuery

	auto uptimesec
	uptime@ KeTime_SecPart + @ uptimesec!

	auto thread
	KeThreadWaitListHead@ thread!

	while (thread@)
		if (thread@ KeThread_KernelStackResident + @)
			if (uptimesec@ thread@ KeThread_WaitOrReadyTimeSec + @ - MMSTACKAGE >=)
				if (thread@ KeThread_WaitMode + @ USERMODE ==)
					// yeet it.
					thread@ MmKernelStackSwapOut

					auto process
					thread@ KeThread_Process + @ process!

					1 process@ KeProcess_ResidentStackCount + -=

					if (DEBUGCHECKS)
						if (process@ KeProcess_ResidentStackCount + @ z<)
							"MmSwapOutStacks: underflow\n" KeCrash
						end
					end

					if (process@ KeProcess_ResidentStackCount + @ ~~)
						0 process@ PsProcess_SwappedOut + !

						auto h
						MmProcessSwapOutListHead@ h!

						h@ process@ PsProcess_SwapOutNext + !
						0 process@ PsProcess_SwapOutPrev + !

						if (h@)
							process@ h@ PsProcess_SwapOutPrev + !
						end

						process@ MmProcessSwapOutListHead!
					end
				end
			end
		end

		thread@ KeThread_QueueNext + @ thread!
	end
end

fn MmSwapInStacks { -- }
	// called at IPLDPC

	while (1)
		auto thread
		KeThreadSwapInListHead@ thread!

		if (thread@ ~~)
			break
		end

		thread@ KeThread_SwapInNext + @ KeThreadSwapInListHead!

		auto process
		thread@ KeThread_Process + @ process!

		1 process@ KeProcess_ResidentStackCount + +=

		if (process@ KeProcess_ResidentStackCount + @ 1 ==)
			if (process@ PsProcess_SwappedOut + @ ~~)
				// process had no resident stacks but wasn't swapped out which
				// means it is on the swap-out queue. we need to remove it or
				// terrible things will happen.

				auto ls
				process@ PsProcess_SwapOutPrev + @ ls!

				auto ns
				process@ PsProcess_SwapOutNext + @ ns!

				if (ls@)
					ns@ ls@ PsProcess_SwapOutNext + !
				end else
					ns@ MmProcessSwapOutListHead!
				end

				if (ns@)
					ls@ ns@ PsProcess_SwapOutPrev + !
				end
			end else
				process@ MmProcessSwapIn
			end
		end

		IPLLOW KeIPLLower

		thread@ MmKernelStackSwapIn
		QUEUEBACK thread@ KeThreadReady

		IPLDPC KeIPLRaise drop
	end
end

fn MmSwapOutProcesses { -- }
	// called at IPLDPC

	while (1)
		auto process
		MmProcessSwapOutListHead@ process!

		if (process@ ~~)
			break
		end

		process@ PsProcess_SwapOutNext + @ MmProcessSwapOutListHead!

		if (MmProcessSwapOutListHead@)
			0 MmProcessSwapOutListHead@ PsProcess_SwapOutPrev + !
		end

		1 process@ PsProcess_SwappedOut + !

		IPLLOW KeIPLLower

		process@ MmProcessSwapOut

		IPLDPC KeIPLRaise drop
	end
end