//
// Implements page frame allocation.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALResource.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Process.h"

#include "<inc>/Object.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

fn MmKflagToPriority { kflags -- pri }
	0 pri!

	if (kflags@ IOKFLAG_SWAPOUT &)
		MUSTSUCCEED pri!
	end elseif (kflags@ IOKFLAG_URGENT &)
		MUSTSUCCEEDL2 pri!
	end

	if (kflags@ IOKFLAG_SWAPIN &)
		TRYFOREVER pri |=
	end
end

fn MmPageGet { priority -- pfdbe pfn ok }
	auto ipl
	IPLDPC KeIPLRaise ipl!

	if (priority@ 0xFF & FREEFIRST ==)
		// attempt to get a page from the free list first.
		// if its empty, try to get one from the zero list.

		MmPageFreeListHead@ pfdbe!

		if (pfdbe@)
			pfdbe@ MmPageFrameDatabase@ - MmPageFrameEntry_SIZEOF / pfn!
			pfdbe@ MmPageFrameEntryFree_Next + @ MmPageFreeListHead!
			0 pfdbe@ MmPageFrameEntryFree_Next + !

			1 MmPageFreeCount -=

			ipl@ KeIPLLower

			0 ok!

			return
		end else
			// resort to a zero page

			MmPageZeroListHead@ pfdbe!

			if (pfdbe@)
				pfdbe@ MmPageFrameDatabase@ - MmPageFrameEntry_SIZEOF / pfn!
				pfdbe@ MmPageFrameEntryFree_Next + @ MmPageZeroListHead!
				0 pfdbe@ MmPageFrameEntryFree_Next + !

				1 MmPageFreeCount -=

				ipl@ KeIPLLower

				0 ok!

				return
			end
		end
	end elseif (priority@ 0xFF & ZEROMUST ==)
		// attempt to get one from the zero list first.
		// if its empty, get one from the free list, and zero it out.

		MmPageZeroListHead@ pfdbe!

		if (pfdbe@)
			pfdbe@ MmPageFrameDatabase@ - MmPageFrameEntry_SIZEOF / pfn!
			pfdbe@ MmPageFrameEntryFree_Next + @ MmPageZeroListHead!
			0 pfdbe@ MmPageFrameEntryFree_Next + !

			1 MmPageFreeCount -=

			ipl@ KeIPLLower

			0 ok!

			return
		end else
			// resort to a free page

			MmPageFreeListHead@ pfdbe!

			if (pfdbe@)
				pfdbe@ MmPageFrameDatabase@ - MmPageFrameEntry_SIZEOF / pfn!
				pfdbe@ MmPageFrameEntryFree_Next + @ MmPageFreeListHead!
				0 pfdbe@ MmPageFrameEntryFree_Next + !

				1 MmPageFreeCount -=

				ipl@ KeIPLLower

				0 ok!

				// zero the free page out

				pfn@ PAGESHIFT << IDENTITYSPACE |
				PAGESIZE
				0
				memset

				return
			end
		end
	end elseif (DEBUGCHECKS)
		priority@ "MmPageGet: unknown priority %d\n" KeCrash
	end

	// none in free or zero list, get one from standby list

	MmEvictSinglePage ok! pfdbe!

	ipl@ KeIPLLower

	if (ok@ ~~)
		pfdbe@ MmPageFrameDatabase@ - MmPageFrameEntry_SIZEOF / pfn!

		if (priority@ 0xFF & ZEROMUST ==)
			pfn@ PAGESHIFT << IDENTITYSPACE |
			PAGESIZE
			0
			memset
		end

		return
	end
end

fn MmPageAlloc { priority -- pfdbe pfn ok }
	0 ok!

	if (DEBUGCHECKS)
		if (MmInited@ ~~)
			"MmPageAlloc: used before MmInit called\n" KeCrash
		end
	end

	auto ipl
	IPLDPC KeIPLRaise ipl!

	0 priority@ MmPageWait ok! drop

	if (ok@)
		ipl@ KeIPLLower
		return
	end

	priority@ MmPageGet ok! pfn! pfdbe!

	ipl@ KeIPLLower
end

fn MmPageWait { process priority -- waited ok }
	// wait until the appropriate available page threshold is reached.

	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"MmPageWait: ipl != IPLDPC\n" KeCrash
		end

		if (process@)
			if (KeThreadCurrent@ KeThread_IgnoreKill + @ ~~)
				"MmPageWait: kill not ignored\n" KeCrash
			end
		end
	end

	0 waited!

	if (MmPageFreeCount@ MmEvictablePageCount@ + MMNORMALTHRESH >=)
		0 ok!
		return
	end

	auto wprocess
	KeThreadCurrent@ KeThread_Process + @ wprocess!

	auto blockon
	auto alertable
	auto tries

	if (priority@ MUSTSUCCEED &)
		// CANBLOCK page allocations have different wait rules if MUSTSUCCEED
		// is also specified:

		MMMUSTSUCCEEDTHRESH blockon!
		0 alertable!
		-1 tries!
	end elseif (priority@ MUSTSUCCEEDL2 &)
		MMMUSTSUCCEEDL2THRESH blockon!
		0 alertable!
		-1 tries!
	end elseif (priority@ POOLALLOC &)
		MMPOOLTHRESH blockon!
		0 alertable!
		-1 tries!
	end else
		// normal rules:
		MMNORMALTHRESH blockon! // don't dip into MUSTSUCCEED page pool
	end

	if (priority@ TRYFOREVER &)
		-1 tries!
	end

	if (wprocess@ PsSystemInitProcess@ ==)
		-1 tries!
		0 alertable!
	end elseif (wprocess@ PsSystemProcess@ ==)
		-1 tries!
		0 alertable!

		if (KeThreadCurrent@ MmModifiedPageWriterThread@ ==)
			MMMUSTSUCCEEDTHRESH blockon!
		end
	end else
		200 tries! // try 200 times to get a page (about 20 seconds)
		1 alertable!
	end

	if (priority@ CANBLOCK & ~~)
		// check this now instead of doing it every iteration in the while
		// loop

		if (MmPageFreeCount@ MmEvictablePageCount@ + blockon@ <)
			STATUS_NO_MEMORY ok!
			return
		end
	end

	while (MmPageFreeCount@ MmEvictablePageCount@ + blockon@ <)
		0 // priboost
		MmEventLowMemory // event
		KeEventSignal

		IPLAPC KeIPLLower

		if (process@)
			process@ MmVADListUnlock
		end

		1 waited!

		KERNELMODE // waitmode
		alertable@ // alertable
		100 // timeout
		MmEventPageAvailable // object
		KeThreadWaitForObject ok!

		if (process@)
			process@ MmVADListLock drop
		end

		IPLDPC KeIPLRaise drop

		if (ok@ STATUS_WAIT_TIMEOUT ==)
			if (tries@ -1 ~=)
				1 tries -=

				if (tries@ ~~)
					STATUS_NO_MEMORY ok!
					return
				end
			end
		end elseif (ok@)
			return
		end
	end

	0 ok!
end

fn MmPageFreeByEntry { pfdbe -- }
	// insert a page frame into the free list

	auto rs
	HALCPUInterruptDisable rs!

	MmPageFreeListHead@ pfdbe@ MmPageFrameEntryFree_Next + !
	pfdbe@ MmPageFreeListHead!

	1 MmPageFreeCount +=

	if (MmEventPageAvailable KeEvent_DispatchHeader + KeDispatchHeader_WaitBlockListHead + @)
		// there's a waiter for a free page, wake him up

		// restore interrupts and go to IPLDPC since this is a much lengthier
		// operation...

		auto ipl
		IPLDPC KeIPLRaise ipl!

		rs@ HALCPUInterruptRestore

		0 // priboost
		MmEventPageAvailable // event
		KeEventSignal

		ipl@ KeIPLLower
	end else
		rs@ HALCPUInterruptRestore
	end
end

fn MmPageFree { pfn -- }
	auto pfdbe
	pfn@ MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!

	pfdbe@ MmPageFreeByEntry
end