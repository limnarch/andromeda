//
// Implements kernel stack management.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALDebug.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/IO.h"

#include "<inc>/Object.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OSStatus.h"

fn MmKernelStackAlloc { -- pooladdr kstack ok }
	fnsection "PAGE$text"

	// reserve enough pages in POOLSPACE for the size of our kernel stack and
	// for a guard page and for 2 reserved pool pages.

	auto offset
	KETHREADSTACKSIZE PAGESHIFT >> 1 + 2 + // pagesneeded
	MmPoolSpaceReserve ok! offset!

	if (ok@)
		return
	end

	auto vaddr
	POOLSPACE offset@ PAGESHIFT << + PAGESIZE + vaddr!

	vaddr@ KETHREADSTACKSIZE + pooladdr!

	// allocate all the page frames needed for our kernel stack.

	auto stackpagestab
	KETHREADSTACKSIZE PAGESHIFT >> 2 << alloc stackpagestab!

	auto i
	0 i!

	auto ptr
	stackpagestab@ ptr!

	auto pteaddr

	while (i@ KETHREADSTACKSIZE PAGESHIFT >> <)
		auto pfdbe
		auto phyaddr

		vaddr@ // vaddr
		MmVirtualtoPTEAddress pteaddr!

		pteaddr@ // pte
		FREEFIRST CANBLOCK | // pri
		MmAnonymousPageAlloc ok! phyaddr! pfdbe!

		if (ok@)
			// failed, free the ones we just allocated

			while (i@)
				4 stackpagestab -=
				1 i -=

				stackpagestab@@ MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!

				auto ipl
				IPLDPC KeIPLRaise ipl!

				pfdbe@ MmEvictablePageDereference drop

				ipl@ // ipl
				pfdbe@ // pfdbe
				MmAnonymousPageDelete
			end

			KETHREADSTACKSIZE PAGESHIFT >> 1 + // pages
			offset@ // offset
			MmPoolSpaceRelease

			return
		end

		pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_MODIFIED |
		pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + sb

		SWAPPTE_DEMANDZERO PTE_TLBHACK | PTE_KERNELSTACK | pfdbe@ MmPageFrameEntryAnonymous_SwapPTE + !

		phyaddr@ ptr@!

		PAGESIZE vaddr +=
		4 ptr +=
		1 i +=
	end

	// map the guard page at the bottom
	POOLSPACE offset@ PAGESHIFT << + // vaddr
	MmVirtualtoPTEAddress pteaddr!

	// set special guard page magic pte
	MMGUARDPTE pteaddr@!

	// as a separate step, map in the page frames for our kernel stack.

	POOLSPACE offset@ PAGESHIFT << + PAGESIZE + vaddr!

	vaddr@ kstack!

	stackpagestab@ ptr!

	0 i!

	while (i@ KETHREADSTACKSIZE PAGESHIFT >> <)
		ptr@@ PAGESHIFT << // phyaddr
		PTE_V PTE_W | PTE_K | PTE_G | // flags
		vaddr@ // vaddr
		MmPTEUpdateByVirtual drop drop

		PAGESIZE vaddr +=
		4 ptr +=
		1 i +=
	end
end

fn MmKernelStackFree { kstack -- }
	fnsection "PAGE$text"

	PAGESIZE kstack -=

	// unmap guard page

	auto pteaddr

	kstack@ // vaddr
	MmVirtualtoPTEAddress pteaddr!

	if (DEBUGCHECKS)
		if (pteaddr@@ MMGUARDPTE ~=)
			"MmKernelStackFree: not a kernel stack\n" KeCrash
		end
	end

	// clear special guard page magic pte
	PTE_KERNEL_ZERO pteaddr@!

	auto offset
	kstack@ POOLSPACE - PAGESHIFT >> offset!

	// unmap and free all the kernel stack page frames.

	PAGESIZE kstack +=

	auto i
	0 i!

	while (i@ KETHREADSTACKSIZE PAGESHIFT >> <)
		auto phyaddr
		auto flags

		0 // phyaddr
		0 // flags
		kstack@ // vaddr
		MmPTEUpdateByVirtual flags! phyaddr!

		if (DEBUGCHECKS)
			if (flags@ PTE_V & ~~)
				"MmKernelStackFree: invalid stack page\n" KeCrash
			end
		end

		auto pfdbe
		phyaddr@ PAGESHIFT >> MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!
		pfdbe@ MmEvictablePageDelete

		-1 MmAnonymousPageCount KeInterlockedIncrement drop

		if (DEBUGCHECKS)
			if (pfdbe@ MmPageFrameEntryAnonymous_SwapPTE + @ PTE_INSWAP &)
				"MmKernelStackFree: had a swap page\n" KeCrash
			end
		end

		pfdbe@ MmEvictablePageDereference drop

		PAGESIZE kstack +=
		1 i +=
	end

	// release the POOLSPACE.

	KETHREADSTACKSIZE PAGESHIFT >> 1 + 2 + // pages
	offset@ // offset
	MmPoolSpaceRelease
end

fn MmKernelStackSwapOut { thread -- }
	// dereference the thread's kernel stack pages.

	// called at IPLDPC.

	auto vaddr
	thread@ KeThread_KernelStackTop + @ KETHREADSTACKSIZE - vaddr!

	auto kdir
	HALPlatformKernelPageDirectory@ kdir!

	auto i
	0 i!

	while (i@ KETHREADSTACKSIZE PAGESHIFT >> <)
		auto ok
		auto pteaddr

		vaddr@ // vaddr
		MmVirtualtoPTEAddress pteaddr!

		auto phyaddr
		auto pfdbe
		auto flags

		pteaddr@ // pteaddr
		MmPTEInterpret ok! flags! phyaddr!

		if (DEBUGCHECKS)
			if (ok@)
				pteaddr@@ "MmKernelStackSwapOut: invalid PTE %x\n" KeCrash
			end
		end

		phyaddr@ PAGESHIFT >> MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!

		// unmap

		phyaddr@ PTE_TRANSITION | PTE_KERNELSTACK | // pte
		pteaddr@ // pteaddr
		MmPTESet

		pfdbe@ MmEvictablePageDereference drop

		PAGESIZE vaddr +=
		1 i +=
	end
end

fn MmKernelStackSwapIn { thread -- }
	// bring in the thread's kernel stack pages.
	// read from swap if necessary.

	// called at IPLLOW.

	auto vaddr
	thread@ KeThread_KernelStackTop + @ KETHREADSTACKSIZE - vaddr!

	auto i
	0 i!

	while (i@ KETHREADSTACKSIZE PAGESHIFT >> <)
		auto ok
		auto pteaddr

		vaddr@ // vaddr
		MmVirtualtoPTEAddress pteaddr!

		auto ipl
		IPLDPC KeIPLRaise ipl!

		auto phyaddr
		auto pfdbe
		auto flags

		if (DEBUGCHECKS)
			pteaddr@ // pteaddr
			MmPTEInterpret ok! flags! phyaddr!

			if (ok@ ~~)
				"MmKernelStackSwapIn: valid PTE\n" KeCrash
			end

			if (pteaddr@@ PTE_KERNELSTACK & ~~)
				pteaddr@@ "MmKernelStackSwapIn: lost the KERNELSTACK debug bit: %x\n" KeCrash
			end
		end

		0 // process
		TRYFOREVER CANBLOCK | // pri
		MmPageWait ok! drop

		if (DEBUGCHECKS)
			if (ok@)
				ok@ "MmKernelStackSwapIn: failed to wait for page (%i)\n" KeCrash
			end
		end

		-1 // dzpte
		0 // evictflag
		0 // refpfdbe
		0 // process
		TRYFOREVER // pri
		vaddr@ // vaddr
		pteaddr@ // pteaddr
		0 // localpteaddr
		MmAnonymousPageReferenceByPTE ok! phyaddr! pfdbe!

		ipl@ KeIPLLower

		if (DEBUGCHECKS)
			if (ok@)
				ok@ "MmKernelStackSwapIn: failed to reference kernel stack (%i)\n" KeCrash
			end
		end

		if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlagsB + gb MMEVICTFLAG_MODIFIED & ~~)
			pfdbe@ // pfdbe
			0 // process
			MmEvictablePageModify
		end

		// make sure its writable

		phyaddr@ // phyaddr
		PTE_V PTE_W | PTE_K | PTE_G | // flags
		pteaddr@ // pteaddr
		MmPTEUpdate drop drop

		PAGESIZE vaddr +=
		1 i +=
	end
end