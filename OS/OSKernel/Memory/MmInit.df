//
// Initialization of the MINTIA memory manager.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALResource.h"

#include "../../OSLoader/OSLoaderGlobal.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Process.h"

#include "<inc>/Object.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

externptr HALLoaderPFDB
externptr HALLoaderTotalRAM

externptr HALResourceKeepHeaders
externptr HALLoaderInfo

externptr ObFullyNonpagedObjects

var MmPageFrameDatabase 0
public MmPageFrameDatabase

var MmPageFreeListHead 0
public MmPageFreeListHead

var MmPageZeroListHead 0
public MmPageZeroListHead

var MmPageTotal 0
public MmPageTotal

var MmPageTotalTotal 0
public MmPageTotalTotal

var MmPageFreeCount 0
public MmPageFreeCount

var MmConstantZeroPage 0
public MmConstantZeroPage

var MmInited 0
public MmInited

buffer MmQuotaSystem MmQuotaBlock_SIZEOF
public MmQuotaSystem

buffer MmEventLowMemory KeEvent_SIZEOF
public MmEventLowMemory

buffer MmModifiedPageEvent KeEvent_SIZEOF
public MmModifiedPageEvent

var MmPageFreeCountLow 0
public MmPageFreeCountLow

var MmPageFreeCountSufficient 0
public MmPageFreeCountSufficient

var MmModifiedPageMaximum 0
public MmModifiedPageMaximum

var MmTheoreticalCommitLimit 0
public MmTheoreticalCommitLimit

var MmCommitLimit 0
public MmCommitLimit

var MmCommitUsage 0
public MmCommitUsage

var MmNonpageableCommitLimit 0
public MmNonpageableCommitLimit

var MmNonpageableCommitUsage 0
public MmNonpageableCommitUsage

buffer MmEventPageAvailable KeEvent_SIZEOF
public MmEventPageAvailable

var MmModifiedPageWriterThread 0
public MmModifiedPageWriterThread

extern MmPlatformInit { -- }

fn MmInitPhase0 { -- }
	HALLoaderPFDB@ MmPageFrameDatabase!

	HALLoaderTotalRAM@ PAGESHIFT >> MmPageTotalTotal!

	// initialize the page DB by walking the memory descriptor list given to
	// us by OSLoader and adding the pages of all the regions marked free to
	// the free list.

	auto desc
	HALLoaderInfo@ LdrInfo_MemoryDescriptorListHead + @ desc!

	while (desc@)
		if (desc@ LdrMemoryDescriptor_Type + @ OSLOADERMEM_FREE ==)
			auto pages
			desc@ LdrMemoryDescriptor_Pages + @ pages!

			auto pfdbe
			desc@ LdrMemoryDescriptor_StartPFN + @ MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!

			pages@ MmPageFreeCount +=
			pages@ MmPageTotal +=

			while (pages@)
				MmPageFreeListHead@ pfdbe@ MmPageFrameEntryFree_Next + !
				pfdbe@ MmPageFreeListHead!

				MmPageFrameEntry_SIZEOF pfdbe +=
				1 pages -=
			end
		end

		desc@ LdrMemoryDescriptor_Next + @ desc!
	end

	MmPlatformInit

	1 MmInited!

	auto idleproc
	KeProcessCurrent idleproc!

	auto ok
	MMQUOTAINFINITE // pghpmax
	MMQUOTAINFINITE // vmmax
	MMQUOTAINFINITE // heapmax
	MmQuotaSystem // quota
	MmQuotaBlockInitialize ok!

	if (ok@)
		"MmInitPhase0: couldn't initialize system quota block\n" KeCrash
	end

	// bias the reference by one to make sure the system never attempts to
	// free this
	MmQuotaSystem MmQuotaBlockReference

	MmQuotaSystem idleproc@ PsProcess_PagedArea + @ PsProcessPaged_QuotaBlock + !

	0 // signaled
	OSEVENT_NOTIF // type
	"MmEventLowMemory" // name
	MmEventLowMemory // event
	KeEventInitialize

	0 // signaled
	OSEVENT_SYNCH // type
	"MmEventPageAvailable" // name
	MmEventPageAvailable // event
	KeEventInitialize

	0 // signaled
	OSEVENT_SYNCH // type
	"MmModifiedPageEvent" // name
	MmModifiedPageEvent // event
	KeEventInitialize

	0 // signaled
	OSEVENT_SYNCH // type
	"IOSwapFileExtendEvent" // name
	IOSwapFileExtendEvent // event
	KeEventInitialize

	MmPoolInit

	-1 MmNonpageableCommitLimit!

	MmReclaimMemory

	// allocate one constantly-zeroed page which will be used by a lot of
	// stuff.

	ZEROMUST MUSTSUCCEED | // priority
	MmPageGet ok! drop pfdbe!

	if (ok@)
		"MmInitPhase0: couldn't allocate the constant zero page\n" KeCrash
	end

	pfdbe@ MmConstantZeroPage!

	// initialize the commit limit to the free page count minus 10 pages.
	// will get set to the size of the swapfile later.
	MmPageFreeCount@ 10 - MmCommitLimit!
	MmCommitLimit@ MmTheoreticalCommitLimit!
end

var MmSectionObjectType 0
public MmSectionObjectType

fn MmInitPhase1 { -- }
	auto typeinit
	ObTypeInitializer_SIZEOF alloc typeinit!

	// initialize ObTypeInitializer fields

	typeinit@ ObTypeInitializerInitialize

	// create Section type

	auto type

	"Section" typeinit@ ObTypeInitializer_Name + !
	'Sect' typeinit@ ObTypeInitializer_Tag + !
	0 typeinit@ ObTypeInitializer_BodySize + !
	pointerof MmSectionObjectDelete typeinit@ ObTypeInitializer_DeleteFunction + !
	1 typeinit@ ObTypeInitializer_Paged + !
	typeinit@ ObTypeCreate ok! type!

	if (ok@)
		ok@ "MmInitPhase1: couldn't create Section type (%i)\n" KeCrash
	end

	type@ MmSectionObjectType!

	// initialize eviction/swap

	auto membytes
	MmPageTotalTotal@ PAGESHIFT << membytes!

	if (membytes@ 2 1024 * 1024 * <=)
		// tiny system, 2MB

		25 MmPageFreeCountLow!
		60 MmPageFreeCountSufficient!
		50 MmModifiedPageMaximum!
		40 MmPinnedPagesLimit!
	end elseif (membytes@ 4 1024 * 1024 * <=)
		// small system, 4MB

		60 MmPageFreeCountLow!
		150 MmPageFreeCountSufficient!
		100 MmModifiedPageMaximum!
		80 MmPinnedPagesLimit!
	end elseif (membytes@ 8 1024 * 1024 * <=)
		// medium system, 8MB

		60 MmPageFreeCountLow!
		200 MmPageFreeCountSufficient!
		150 MmModifiedPageMaximum!
		120 MmPinnedPagesLimit!
	end elseif (membytes@ 32 1024 * 1024 * <)
		// large system, 12-32MB

		60 MmPageFreeCountLow!
		250 MmPageFreeCountSufficient!
		200 MmModifiedPageMaximum!
		300 MmPinnedPagesLimit!
	end else
		// massive system, >=32MB

		70 MmPageFreeCountLow!
		300 MmPageFreeCountSufficient!
		300 MmModifiedPageMaximum!
		500 MmPinnedPagesLimit!
	end

	MmPageFreeCountSufficient@
	MmPageFreeCountLow@
	"free page targets: %d pages -> %d pages\n" "MmInitPhase1" HALLog

	MmModifiedPageMaximum@ PAGESHIFT <<
	MmModifiedPageMaximum@ "modified page maximum: %d pages (%d bytes)\n" "MmInitPhase1" HALLog

	auto ok
	0 // context1
	0 // context2
	pointerof MmModifiedPageWriter // startfunc
	0 // creationflags
	ACCESS_OWNER_ALL // permissions
	"ModifiedPageWriter" // name
	PsSystemProcess@ // processobject
	PsThreadCreateObject ok! MmModifiedPageWriterThread!

	if (ok@)
		ok@ "MmInitPhase1: couldn't create 'ModifiedPageWriter' thread (%i)\n" KeCrash
	end

	0 ObFullyNonpagedObjects!
end

fn MmReclaimMemory { -- }
	// lift the resource list and DLL list into our kernel heap and then
	// reclaim the reclaimable regions in the descriptor list.

	auto ok

	auto newlisthead

	// rebuild the resource list on our own heap

	auto prev
	auto new

	auto current
	HALLoaderInfo@ LdrInfo_ResourceListHead + @ current!

	0 newlisthead!

	while (current@)
		LdrBootResource_SIZEOF // bytes
		'nRSR' // tag
		MUSTSUCCEED // flags
		MmAllocWithTag ok! new!

		if (ok@)
			ok@ "MmReclaimMemory: couldn't relocate resources list (%i)\n" KeCrash
		end

		if (newlisthead@ ~~)
			new@ newlisthead!
		end else
			new@ prev@ LdrBootResource_Next + !
		end

		new@ prev!

		new@ // dest
		current@ // src
		LdrBootResource_SIZEOF // size
		memcpy

		0 new@ LdrBootResource_Next + !

		current@ LdrBootResource_Next + @ current!
	end

	newlisthead@ HALResourceListHead!

	// rebuild the DLL list on our own heap

	auto start
	auto sz
	auto rs

	0 newlisthead!

	HALLoaderInfo@ LdrInfo_DLLListHead + @ current!

	while (current@)
		LdrModule_SIZEOF // bytes
		'nDLL' // tag
		MUSTSUCCEED // flags
		MmAllocWithTag ok! new!

		if (ok@)
			ok@ "MmReclaimMemory: couldn't relocate DLL list (%i)\n" KeCrash
		end

		if (newlisthead@ ~~)
			new@ newlisthead!
		end else
			new@ prev@ LdrModule_Next + !
		end

		new@ prev!

		new@ // dest
		current@ // src
		LdrModule_SIZEOF // size
		memcpy

		0 new@ LdrModule_Next + !

		current@ LdrModule_Next + @ current!
	end

	newlisthead@ HALLoaderInfo@ LdrInfo_DLLListHead + !

	auto desc
	HALLoaderInfo@ LdrInfo_MemoryDescriptorListHead + @ desc!

	while (desc@)
		auto reclaim
		0 reclaim!

		if (desc@ LdrMemoryDescriptor_Type + @ OSLOADERMEM_RECLAIMABLE ==)
			1 reclaim!
		end elseif (desc@ LdrMemoryDescriptor_Type + @ OSLOADERMEM_DEBUG ==)
			if (HALResourceKeepHeaders@ ~~)
				1 reclaim!
			end
		end

		if (reclaim@)
			auto pages
			desc@ LdrMemoryDescriptor_Pages + @ pages!

			auto pfn
			desc@ LdrMemoryDescriptor_StartPFN + @ pfn!

			auto pfdbe
			pfn@ MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!

			while (pages@)
				1 MmPageFreeCount +=
				1 MmPageTotal +=

				MmPageFreeListHead@ pfdbe@ MmPageFrameEntryFree_Next + !
				pfdbe@ MmPageFreeListHead!

				MmPageFrameEntry_SIZEOF pfdbe +=
				1 pages -=
				1 pfn +=
			end
		end

		desc@ LdrMemoryDescriptor_Next + @ desc!
	end
end

fn MmResourceJettison { -- }
	auto rsrc
	HALResourceListHead@ rsrc!

	auto sz
	auto start
	auto ok

	auto rs

	while (rsrc@)
		if (rsrc@ LdrBootResource_Jettison + @)
			rsrc@ LdrBootResource_Size + @ PAGEOFFSETMASK + PAGESHIFT >> sz!

			// XXX assumes boot resources are allocated contiguously in
			// physical memory, which is OSLoader's current behavior.

			auto pteaddr
			rsrc@ LdrBootResource_Data + @ // vaddr
			HALPlatformKernelPageDirectory@ // pagemap
			MmVirtualtoPTEAddress pteaddr!

			pteaddr@ MmPTEInterpret ok! drop start!

			if (ok@)
				"MmResourceJettison: invalid PTE\n" KeCrash
			end

			PAGESHIFT start >>=

			while (sz@)
				start@ MmPageFree

				HALCPUInterruptDisable rs!
				// the page is now un-wired
				1 MmPageTotal +=
				rs@ HALCPUInterruptRestore

				1 start +=
				1 sz -=
			end

			0 rsrc@ LdrBootResource_Data + ! // mark jettisoned
		end

		auto nrsrc
		rsrc@ LdrBootResource_Next + @ nrsrc!

		rsrc@ MmFree

		nrsrc@ rsrc!
	end

	0 HALResourceListHead!
end

fn MmInitNonpageableCommit { -- }
	auto limit
	MmPageFreeCount@ PAGESHIFT << limit!

	// subtract the memory promised for FCB cache

	if (IOFileControlBlockCacheSize@ limit@ <)
		IOFileControlBlockCacheSize@ limit -=
	end

	// subtract an arbitrary 128KB for system usage

	if (MMSYSTEMPROMISE limit@ <)
		MMSYSTEMPROMISE limit -=
	end

	// guarantee a minimum

	if (limit@ MMNONPAGECOMMITMINIMUM <)
		MMNONPAGECOMMITMINIMUM limit!
	end

	// limit to total number of pages

	if (limit@ MmPageTotal@ PAGESHIFT << >)
		MmPageTotal@ PAGESHIFT << limit!
	end

	limit@ MmNonpageableCommitLimit!
end

fn MmQuery { query -- ok }
	0 ok!

	query@ // ptr
	OSMemoryInformation_SIZEOF // size
	0 // word
	memset

	MmPageTotalTotal@ query@ OSMemoryInformation_PageTotalCount + !
	MmPageTotal@ query@ OSMemoryInformation_PageTotalNonWiredCount + !
	
	MmPageFreeCount@ query@ OSMemoryInformation_PageFreeCount + !

	IOCachePagesUsed@ query@ OSMemoryInformation_FileCachePageCount + !
	MmAnonymousPageCount@ query@ OSMemoryInformation_AnonymousPageCount + !

	IOSwapPagesCount@ query@ OSMemoryInformation_SwapPageTotalCount + !
	IOSwapPagesUsed@ query@ OSMemoryInformation_SwapPageUsedCount + !
	IOSwapPagesUsedPeak@ query@ OSMemoryInformation_SwapPagePeakCount + !

	IOSwapPagesRead@ query@ OSMemoryInformation_SwapPagesRead + !
	IOSwapPagesWritten@ query@ OSMemoryInformation_SwapPagesWritten + !

	MmNonpagedHeapBytesUsedInternally@ MmNonpagedPoolBytesUsed@ +
	query@ OSMemoryInformation_NonpagedPoolBytesUsedInternally + !

	MmNonpagedHeapBytesUsedExternally@ MmNonpagedPoolBytesUsed@ +
	query@ OSMemoryInformation_NonpagedPoolBytesUsedExternally + !

	MmNonpagedHeapBytesUsedPeak@ MmNonpagedPoolBytesPeak@ +
	query@ OSMemoryInformation_NonpagedPoolBytesUsedPeak + !

	MmPagedHeapBytesUsedInternally@ MmPagedPoolBytesUsed@ +
	query@ OSMemoryInformation_PagedPoolBytesUsedInternally + !

	MmPagedHeapBytesUsedExternally@ MmPagedPoolBytesUsed@ +
	query@ OSMemoryInformation_PagedPoolBytesUsedExternally + !

	MmPagedHeapBytesUsedPeak@ MmPagedPoolBytesPeak@ +
	query@ OSMemoryInformation_PagedPoolBytesUsedPeak + !

	MmEvictablePageCount@ query@ OSMemoryInformation_EvictablePageCount + !

	MmModifiedPageCount@ query@ OSMemoryInformation_ModifiedPageCount + !
	MmModifiedPageMaximum@ query@ OSMemoryInformation_ModifiedPageMaximum + !

	MmTotalWorkingSetSize@ query@ OSMemoryInformation_WorkingSetPageTotalCount + !
	MmPeakWorkingSetSize@ query@ OSMemoryInformation_WorkingSetPagePeakCount + !

	MmPageFaultCount@ query@ OSMemoryInformation_PageFaultCount + !

	MmTheoreticalCommitLimit@ query@ OSMemoryInformation_TheoreticalCommitLimit + !
	MmCommitLimit@ query@ OSMemoryInformation_CommitLimit + !
	MmCommitUsage@ query@ OSMemoryInformation_CommitUsage + !

	MmNonpageableCommitLimit@ query@ OSMemoryInformation_NonpageableCommitLimit + !
	MmNonpageableCommitUsage@ query@ OSMemoryInformation_NonpageableCommitUsage + !

	MmModifiedFilePageCount@ query@ OSMemoryInformation_ModifiedFilePageCount + !
end