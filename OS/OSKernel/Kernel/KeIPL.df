//
// Implements Interrupt Priority Level (IPL) management.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"

#include "<inc>/Kernel.h"

#include "<inc>/HALInterrupt.h"

#include "<ll>/OSDLL/OSStatistics.h"

#include "KeInternal.h"

externptr KeIPLCurrent

fn KeIPLRaise { newipl -- oldipl }
	KeIPLCurrent@ oldipl!

	if (newipl@ oldipl@ ==)
		return
	end

	if (newipl@ oldipl@ <)
		[newipl@]HALIPLNames@ [oldipl@]HALIPLNames@ "IPL not greater or equal (old=%s new=%s)\n" KeCrash
	end

	// only call into the HAL if hardware interrupt level changed.
	// no need to call in for software interrupt levels IPLLOW-IPLDPC.

	newipl@ KeIPLCurrent!

	if (newipl@ IPLDPC >)
		newipl@ HALPlatformIPLSet
	end
end

fn KeIPLLower { newipl -- }
	auto oldipl
	KeIPLCurrent@ oldipl!

	if (newipl@ oldipl@ ==)
		return
	end

	if (newipl@ oldipl@ >)
		[newipl@]HALIPLNames@ [oldipl@]HALIPLNames@ "IPL not less or equal (old=%s new=%s)\n" KeCrash
	end

	if (oldipl@ IPLDPC >)
		newipl@ KeIPLCurrent!
		newipl@ HALPlatformIPLSet
	end

	if (newipl@ IPLDPC >=)
		newipl@ KeIPLCurrent!
		return
	end

	auto rs
	HALCPUInterruptDisable rs!

	if (KiPendingSoftwareInterrupts@)
		auto p
		KiPendingSoftwareInterrupts@ [newipl@]KiPendingSoftwareInterruptMask@ & p!

		while (p@)
			[p@]KiPendingSoftwareInterruptFirst@ KiSoftwareInterruptHandlerF

			KiPendingSoftwareInterrupts@ [newipl@]KiPendingSoftwareInterruptMask@ & p!
		end
	end

	newipl@ KeIPLCurrent!

	rs@ HALCPUInterruptRestore
end

var KiPendingSoftwareInterrupts 0
public KiPendingSoftwareInterrupts

table KiPendingSoftwareInterruptMask
	6 // IPLLOW (none masked)
	4 // IPLAPC (APCs masked)
endtable
public KiPendingSoftwareInterruptMask

table KiPendingSoftwareInterruptFirst // which to service first based on the mask
	0                                // 0
	0                                // 1
	pointerof KiSoftwareInterruptAPC // 2
	pointerof KiSoftwareInterruptAPC // 3
	pointerof KiSoftwareInterruptDPC // 4
	pointerof KiSoftwareInterruptDPC // 5
	pointerof KiSoftwareInterruptDPC // 6
	pointerof KiSoftwareInterruptDPC // 7
endtable
public KiPendingSoftwareInterruptFirst

fn (KiSoftwareInterruptHandlerF) KiSoftwareInterruptAPC { -- }
	1 IPLAPC << ~ KiPendingSoftwareInterrupts &=

	IPLAPC KeIPLCurrent!

	if (KeThreadCurrent@ KeThread_APCListHead + @)
		HALCPUInterruptEnable

		KiAPCDispatchQueue

		HALCPUInterruptDisable drop
	end
end

fn (KiSoftwareInterruptHandlerF) KiSoftwareInterruptDPC { -- }
	1 IPLDPC << ~ KiPendingSoftwareInterrupts &=

	IPLDPC KeIPLCurrent!

	if (KiDPCListHead@)
		KiDPCDispatchQueue
	end

	if (KiThreadNext@)
		HALCPUInterruptEnable

		KiThreadNextSwitch

		HALCPUInterruptDisable drop
	end
end

fn KiSoftwareInterruptRequest { ipl -- }
	1 ipl@ << KiPendingSoftwareInterrupts |=
end