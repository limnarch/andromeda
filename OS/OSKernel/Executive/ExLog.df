//
// Message logging for the MINTIA executive.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALConsole.h"
#include "<inc>/HALCrash.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/IO.h"

#include "<inc>/Console.h"

#include "<ll>/OSDLL/OS.h"

// run-time message logging for executive.

var ExSystemConsoleName 0
public ExSystemConsoleName

var ExSystemConsoleObject 0
public ExSystemConsoleObject

var ExSystemConsole 0
public ExSystemConsole

fn ExSystemConsoleSet { consoleobject -- }
	if (KeIPLCurrentGet IPLDPC >=)
		"ExSystemConsoleSet: IPL >= IPLDPC\n" KeCrash
	end

	consoleobject@ ObObjectReferenceByPointer drop

	auto console
	consoleobject@ IODeviceGetExtension console!

	auto name
	consoleobject@ ObObjectName name!

	auto rs
	HALCPUInterruptDisable rs!

	auto oldconsoleobject
	ExSystemConsoleObject@ oldconsoleobject!

	name@ ExSystemConsoleName!
	consoleobject@ ExSystemConsoleObject!
	console@ ExSystemConsole!

	HALLogReset

	rs@ HALCPUInterruptRestore

	if (oldconsoleobject@)
		oldconsoleobject@ ObObjectDereferenceByPointer drop
	end
end

fn FPuts { fd s -- }
	while (s@ gb)
		s@ gb Putc
		1 s +=
	end
end

fn FPutc { fd c -- }
	if (ExSystemConsole@)
		if (KeIPLCurrentGet IPLLOW ==)
			if (HALCrashed@ ~~)
				auto ok
				c@ // c
				ExSystemConsole@ // duplex
				CoDuplexWriteCharacter ok!

				if (ok@ ~~)
					return
				end
			end
		end
	end

	c@ HALPutc
end

fn Putc { c -- }
	0 c@ FPutc
end