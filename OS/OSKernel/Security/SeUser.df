#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Object.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

fn SeUserCreate { gid uid username -- user ok }
	SeUserListLock ok!

	if (ok@)
		return
	end

	uid@ 1 SeUserGetByUID ok! user!

	if (ok@ STATUS_SUCCESS ==)
		// uid already used

		SeUserListUnlock

		STATUS_INVALID_ARGUMENT ok!

		return
	end

	username@ 1 SeUserGetByUsername ok! user!

	if (ok@ STATUS_SUCCESS ==)
		// username already used

		SeUserListUnlock

		STATUS_INVALID_ARGUMENT ok!

		return
	end

	SeUser_SIZEOF 'User' MmAllocWithTag ok! user!

	if (ok@)
		SeUserListUnlock

		return
	end

	user@ SeUser_Username + username@ SEUSERNAMEMAX 1 - strncpy

	uid@ user@ SeUser_UID + !
	gid@ user@ SeUser_GID + !

	user@ SeUser_Permissions + SePermissionsInitialize ok!

	if (ok@)
		user@ MmFree

		SeUserListUnlock

		return
	end

	SeUserListHead@ user@ SeUser_Next + !
	user@ SeUserListHead!

	SeUserListUnlock
end

fn SeUserCurrentGet { -- user }
	KeProcessCurrent PsProcess_OwningUser + @ user!
end

fn SeUserListLock { -- ok }
	1 // alertable
	0 // nowait
	SeUserListRwLock // rwlock
	ExRwLockAcquireExclusive ok!
end

fn SeUserListLockShared { -- ok }
	1 // alertable
	0 // nowait
	0 // canstarve
	SeUserListRwLock // rwlock
	ExRwLockAcquireShared ok!
end

fn SeUserListUnlock { -- }
	SeUserListRwLock ExRwLockRelease
end

fn SeUserGetByUsername { username userlistlocked -- user ok }
	if (userlistlocked@ ~~)
		SeUserListLockShared ok!

		if (ok@)
			return
		end
	end

	STATUS_NO_SUCH_USER ok!

	SeUserListHead@ user!

	while (user@)
		if (user@ SeUser_Username + username@ strcmp)
			if (userlistlocked@ ~~)
				SeUserListUnlock
			end

			0 ok!

			return
		end

		user@ SeUser_Next + @ user!
	end

	if (userlistlocked@ ~~)
		SeUserListUnlock
	end
end

fn SeUserGetByUID { uid userlistlocked -- user ok }
	if (userlistlocked@ ~~)
		SeUserListLockShared ok!

		if (ok@)
			return
		end
	end

	STATUS_NO_SUCH_USER ok!

	SeUserListHead@ user!

	while (user@)
		if (user@ SeUser_UID + @ uid@ ==)
			if (userlistlocked@ ~~)
				SeUserListUnlock
			end

			0 ok!

			return
		end

		user@ SeUser_Next + @ user!
	end

	if (userlistlocked@ ~~)
		SeUserListUnlock
	end
end