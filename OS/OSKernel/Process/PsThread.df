//
// Implements management routines for the executive thread object.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/HALCPU.h"

#include "<inc>/HALDebug.h"

fn PsThreadGetFilePermissions { threadobject -- filepermissions }
	threadobject@ PsThread_FileCreationPermissions + @ filepermissions!
end

fn PsThreadSuspend { threadhandle -- ok }
	auto obj
	auto access

	PsThreadObjectType@ // type
	threadhandle@ // handle
	ObObjectReferenceByHandle ok! obj! access!

	if (ok@)
		return
	end

	access@ ACCESS_EXEC SeCheckAccess ok!

	if (ok@)
		obj@ ObObjectDereferenceByPointer drop

		return
	end

	obj@ KeThreadSuspend

	obj@ ObObjectDereferenceByPointer drop
end

fn PsThreadResume { threadhandle -- ok }
	auto obj
	auto access

	PsThreadObjectType@ // type
	threadhandle@ // handle
	ObObjectReferenceByHandle ok! obj! access!

	if (ok@)
		return
	end

	access@ ACCESS_EXEC SeCheckAccess ok!

	if (ok@)
		obj@ ObObjectDereferenceByPointer drop

		return
	end

	obj@ KeThreadResume

	obj@ ObObjectDereferenceByPointer drop
end

fn PsThreadReadStatusObject { threadobject -- status ok }
	if (threadobject@ KeThread_Killed + @ ~~)
		STATUS_ALIVE ok!
		return
	end

	0 ok!
	threadobject@ KeThread_KilledStatus + @ status!
end

fn PsThreadReadStatus { threadhandle -- status ok }
	auto obj

	PsThreadObjectType@ // type
	threadhandle@ // handle
	ObObjectReferenceByHandle ok! obj! drop

	if (ok@)
		return
	end

	obj@ PsThreadReadStatusObject ok! status!

	obj@ ObObjectDereferenceByPointer drop
end

fn PsThreadInformationQueryObject { threadobject query -- ok }
	0 ok!

	query@ // ptr
	OSThreadInformation_SIZEOF // size
	0 // word
	memset

	threadobject@ KeThread_Status + @ query@ OSThreadInformation_Status + !
	threadobject@ KeThread_Killed + @ query@ OSThreadInformation_Terminated + !
	threadobject@ KeThread_KilledStatus + @ query@ OSThreadInformation_ExitStatus + !

	threadobject@ KeThread_UserTime + KeTime_SecPart + @ query@ OSThreadInformation_UserTimeSecPart + !
	threadobject@ KeThread_UserTime + KeTime_MsPart + @ query@ OSThreadInformation_UserTimeMsPart + !

	threadobject@ KeThread_SysTime + KeTime_SecPart + @ query@ OSThreadInformation_SystemTimeSecPart + !
	threadobject@ KeThread_SysTime + KeTime_MsPart + @ query@ OSThreadInformation_SystemTimeMsPart + !
end

fn PsThreadInformationQuery { threadhandle query -- ok }
	auto threadobject

	PsThreadObjectType@ // type
	threadhandle@ // handle
	ObObjectReferenceByHandle ok! threadobject! drop

	if (ok@)
		return
	end

	threadobject@ // threadobject
	query@ // query
	PsThreadInformationQueryObject ok!

	threadobject@ ObObjectDereferenceByPointer drop
end