//
// Implements termination and deletion of executive process and thread
// objects.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<inc>/Console.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/IPC.h"

#include "PsInternal.h"

fn (ObTypeDeleteFunction) PsiProcessObjectDelete { object -- }
	fnsection "PAGE$text"

	// the last reference to this process object was removed.
	// finally free all resources associated with it.

	auto pagedarea
	object@ PsProcess_PagedArea + @ pagedarea!

	// remove from the global process list

	0 // locked
	object@ // process
	PsProcessRemove

	0 // locked
	object@ // process
	PsProcessRemoveForTrim

	// destroy PID

	0 PsiIdTableLock drop

	pagedarea@ PsProcessPaged_PID + @ PsiIdDelete

	PsiIdTableUnlock

	object@ PsiProcessCleanup

	object@ MmProcessDelete

	// deref quota block

	pagedarea@ PsProcessPaged_QuotaBlock + @ MmQuotaBlockDereference

	if (DEBUGCHECKS)
		if (pagedarea@ PsProcessPaged_CommittedPageTablesCount + @)
			"PsiProcessObjectDelete: failed to decommit page tables\n" KeCrash
		end
	end

	pagedarea@ MmFree
end

fn (ObTypeDeleteFunction) PsiThreadObjectDelete { object -- }
	fnsection "PAGE$text"

	// the last reference to this thread object was removed.
	// finally free all resources associated with it.

	if (DEBUGCHECKS)
		if (object@ KeThread_StatusB + gb THREADSTATUS_TERMINATED ~=)
			object@ KeThread_StatusB + gb
			"PsiThreadObjectDelete: attempt to delete non-terminated thread (%d)\n" KeCrash
		end
	end

	// destroy TID

	0 PsiIdTableLock drop

	object@ PsThread_TID + @ PsiIdDelete

	PsiIdTableUnlock

	object@ KeThread_KernelStackTop + @ KETHREADSTACKSIZE - MmKernelStackFree

	auto quotablock
	object@ KeThread_Process + @ PsProcess_PagedArea + @ PsProcessPaged_QuotaBlock + @ quotablock!

	KETHREADSTACKSIZE PAGESHIFT >> // charge
	quotablock@ // quotablock
	MmQuotaBlockUnchargeWS

	if (object@ PsThread_IPCBlock + @)
		object@ IPCThreadBlockFree
	end

	object@ KeThread_Process + @ ObObjectDereferenceByPointer drop
end

var PsReaperThreadListHead 0
public PsReaperThreadListHead

fn PsiReaperWorker { context1 context2 -- }
	// this thread reaps dead threads and dereferences them.
	// it also is a worker thread for reaping dead objects.

	auto object

	while (1)
		// Wait as USERMODE to allow our kernel stack to be swapped out.

		auto ok
		USERMODE // waitmode
		0 // alertable
		OSWAIT_TIMEOUTINFINITE // timeout
		PsReaperEvent // object
		KeThreadWaitForObject ok!

		if (DEBUGCHECKS)
			if (ok@)
				ok@ "PsiReaperWorker: failed to wait on reap event (%i)\n" KeCrash
			end
		end

		auto ipl
		IPLDPC KeIPLRaise ipl!

		while (PsReaperThreadListHead@)
			PsReaperThreadListHead@ object!
			object@ KeThread_QueueNext + @ PsReaperThreadListHead!

			0 object@ KeThread_QueueNext + !

			ipl@ KeIPLLower

			object@ ObObjectDereferenceByPointer drop

			IPLDPC KeIPLRaise ipl!
		end

		while (ObReaperListHead@)
			ObReaperPop object!

			ipl@ KeIPLLower

			object@ ObObjectFree ok!

			if (DEBUGCHECKS)
				if (ok@)
					"PsiReaperWorker: failed to free object\n" KeCrash
				end
			end

			IPLDPC KeIPLRaise ipl!
		end

		ipl@ KeIPLLower
	end
end

fn PsiProcessCleanup { process -- }
	fnsection "PAGE$text"

	auto pagedarea
	process@ PsProcess_PagedArea + @ pagedarea!

	if (pagedarea@ PsProcessPaged_CleanedUp + @)
		// The process had a last thread that did all this already.
		// This is synchronized via the createdelete lock, which is held
		// across both cases.

		return
	end

	1 pagedarea@ PsProcessPaged_CleanedUp + !

	// indicate termination to job

	if (pagedarea@ PsProcessPaged_Job + @)
		process@ PsiJobIndicateProcessExit
	end

	// destroy handle table

	pointerof ObHandleDestroyFunction // enum func
	pagedarea@ PsProcessPaged_HandleTable + @ // handle table
	ExHandleTableDelete

	// uninitialize virtual memory structures

	process@ MmProcessUninitialize

	// remove from console group

	process@ CoConsoleRemoveProcess drop
end

fn PsiProcessExit { status process -- }
	fnsection "PAGE$text"

	// this process has reached the end of its life.

	if (DEBUGCHECKS)
		if (process@ PsSystemProcess@ ==)
			status@ "PsiProcessExit: system process terminated (%i)\n" KeCrash
		end
	end

	if (process@ KeProcess_KilledStatus + @ ~~)
		status@ process@ KeProcess_KilledStatus + !
	end

	1 process@ KeProcess_TerminatedB + sb

	process@ PsiProcessCleanup

	0 // priboost
	process@ PsProcess_TerminationEvent + // event
	KeEventSignal

	status@ PsProcessSignalActivation drop
end

fn PsiThreadExit { -- }
	fnsection "PAGE$text"

	// This thread has reached the end of its life. Usermode will never regain
	// control.

	auto current
	KeThreadCurrent@ current!

	auto process
	current@ KeThread_Process + @ process!

	// Make our priority the highest non-realtime priority to expedite the
	// releasing of this thread's resources.

	PRIORITY_HIGHREALTIME 1 - // priority
	KeThreadCurrent@ // thread
	KeThreadPrioritySet

	// Ignore kill so nothing bad happens while we're cleaning up.

	current@ KeThreadIgnoreKill drop

	// If there are any asynchronous I/O operations in-flight, try to cancel
	// them and wait for completion.

	if (current@ PsThread_IOListHead + @)
		// It's safe to check this list in this manner because the current
		// thread is the only one who will ever enqueue items to this list,
		// and we ain't doing that right now. Although, I/O completion APCs
		// can asynchronously remove items from it in our context, but that
		// doesn't matter when we're just checking for emptiness.

		IOThreadRundown
	end

	0 current@ KeThread_KernelStackCanBeSwappedB + sb

	current@ KeThreadRundown

	current@ MmThreadUninitialize

	0 // alertable
	process@ // processobject
	PsProcessCreateDeleteLock drop

	current@ KeThreadUninitialize

	if (process@ KeProcess_ThreadCountI + gi ~~)
		// We were the last thread, so the process is dead now.
		
		current@ KeThread_KilledStatus + @ // status
		process@ // process
		PsiProcessExit
	end

	process@ PsProcessCreateDeleteUnlock

	// Signal our termination event.

	0 // priboost
	current@ PsThread_TerminationEvent + // event
	KeEventSignal

	// Call the nonpaged helper function that enqueues us to the reaper list
	// and yields execution for the last time.

	current@ PsiThreadGoByeBye
end

fn PsiThreadGoByeBye { current -- }
	// Add ourselves to the reaper thread list and signal the reaper event.
	// IPL has to be >=IPLDPC the entire time (i.e. the dispatcher lock must
	// be held), otherwise the reaper thread may try to delete us while we're
	// still executing. This scales to SMP because the current plan is to hold
	// the dispatcher lock across context switching anyway, with the release
	// being done by the guy who we switch to; this is a sensical thing to do
	// because a spinlock is held by a processor and not by a thread. Of
	// course, this means that we must protect the reaper list with the
	// dispatcher lock later.

	IPLDPC KeIPLRaise drop

	if (DEBUGCHECKS)
		if (current@ KeThread_APCListHead + @)
			current@ "PsiThreadExit: thread %x had pending APCs\n" KeCrash
		end
	end

	PsReaperThreadListHead@ current@ KeThread_QueueNext + !
	current@ PsReaperThreadListHead!

	0 // priboost
	PsReaperEvent // event
	KeEventSignal

	THREADSTATUS_TERMINATED KeThreadBlock
end

fn PsThreadTerminateObject { status threadobject -- ok }
	fnsection "PAGE$text"

	0 ok!

	status@ // status
	threadobject@ // thread
	KeThreadTerminate

	threadobject@ KeThreadResume
end

fn PsThreadTerminate { status threadhandle -- ok }
	fnsection "PAGE$text"

	auto obj
	auto access

	PsThreadObjectType@ // type
	threadhandle@ // handle
	ObObjectReferenceByHandle ok! obj! access!

	if (ok@)
		return
	end

	access@ ACCESS_WRITE SeCheckAccess ok!

	if (ok@)
		obj@ ObObjectDereferenceByPointer drop

		return
	end

	status@ // status
	obj@ // threadobject
	PsThreadTerminateObject ok!

	obj@ ObObjectDereferenceByPointer drop
end