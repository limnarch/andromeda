//
// Implements directory management for the AisixFS driver.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALInterrupt.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/IO.h"

#include "<inc>/FSRTL.h"

#include "AisixFS.h"

fn AFSDirectoryAllocateEntry { fcb -- dirent pfdbe ok }
	// FCB is locked by caller

	if (DEBUGCHECKS)
		if (fcb@ IOFileControlBlockGetType OSFILETYPE_DIRECTORY ~=)
			// should have been caught by the IO system code
			"AFSDirectoryAllocateEntry: not a directory\n" KeCrash
		end
	end

	auto cacheblock
	fcb@ IOFileControlBlockGetCacheInfoBlock cacheblock!

	if (DEBUGCHECKS)
		if (cacheblock@ ~~)
			"AFSDirectoryAllocateEntry: no cacheblock\n" KeCrash
		end
	end

	auto mount
	fcb@ IOFileControlBlockGetMount mount!

	auto byteoff
	0 byteoff!

	auto bytesize
	fcb@ IOFileControlBlockGetSize bytesize!

	auto pageno
	byteoff@ PAGESHIFT >> pageno!

	auto curpageno
	-1 curpageno!

	auto found

	0 pfdbe!

	while (byteoff@ bytesize@ <)
		if (pageno@ curpageno@ ~=)
			// must read in a new page

			if (pfdbe@)
				pfdbe@ MmEvictablePageDereference drop
			end

			0 // flags
			0 // kflags
			byteoff@ PAGENUMBERMASK & // offset
			fcb@ // fcb
			IOCachePageRead ok! pfdbe! dirent!

			if (ok@)
				return
			end

			byteoff@ PAGEOFFSETMASK & dirent +=

			pageno@ curpageno!
		end

		if (dirent@ AFSDirEnt_INum + @ ~~)
			// found a free dirent
			0 ok!
			return
		end

		AFSDirEnt_SIZEOF dirent +=
		AFSDirEnt_SIZEOF byteoff +=
		byteoff@ PAGESHIFT >> pageno!
	end

	if (pfdbe@)
		pfdbe@ MmEvictablePageDereference drop
	end

	// no existing free entries -- try to allocate a new one.

	bytesize@ AFSDirEnt_SIZEOF + // newsize
	1 // zero
	0 // flags
	fcb@ // fcb
	AFSTruncate ok!

	if (ok@)
		return
	end

	0 // flags
	0 // kflags
	bytesize@ PAGENUMBERMASK & // offset
	fcb@ // fcb
	IOCachePageRead ok! pfdbe! dirent!

	if (ok@)
		return
	end

	bytesize@ PAGEOFFSETMASK & dirent +=

	if (DEBUGCHECKS)
		if (dirent@ AFSDirEnt_INum + @)
			"AFSDirectoryAllocateEntry: fresh dirent had inum\n" KeCrash
		end
	end
end

fn AFSDirectoryGetEntry { seek afsdirent fcb -- nextseek ok }
	// FCB is locked by caller

	if (DEBUGCHECKS)
		if (fcb@ IOFileControlBlockGetType OSFILETYPE_DIRECTORY ~=)
			// should have been caught by the IO system code
			"AFSDirectoryGetEntry: not a directory\n" KeCrash
		end
	end

	auto cacheblock
	fcb@ IOFileControlBlockGetCacheInfoBlock cacheblock!

	if (DEBUGCHECKS)
		if (cacheblock@ ~~)
			"AFSDirectoryGetEntry: no cacheblock\n" KeCrash
		end
	end

	auto mount
	fcb@ IOFileControlBlockGetMount mount!

	auto byteoff
	seek@ AFSDirEnt_SIZEOF * byteoff!

	auto bytesize
	fcb@ IOFileControlBlockGetSize bytesize!

	auto pageno
	byteoff@ PAGESHIFT >> pageno!

	auto curpageno
	-1 curpageno!

	auto pfdbe
	0 pfdbe!

	auto data

	while (byteoff@ bytesize@ <)
		if (pageno@ curpageno@ ~=)
			// must read in a new page

			if (pfdbe@)
				pfdbe@ MmEvictablePageDereference drop
			end

			0 // flags
			0 // kflags
			byteoff@ PAGENUMBERMASK & // offset
			fcb@ // fcb
			IOCachePageRead ok! pfdbe! data!

			if (ok@)
				return
			end

			byteoff@ PAGEOFFSETMASK & data +=

			pageno@ curpageno!
		end

		if (data@ AFSDirEnt_INum + @)
			// found a dirent

			afsdirent@ AFSDirEnt_name + // dest
			data@ AFSDirEnt_name + // src
			59 // max
			strncpy

			data@ AFSDirEnt_INum + @ afsdirent@ AFSDirEnt_INum + !

			pfdbe@ MmEvictablePageDereference drop

			0 ok!

			seek@ 1 + nextseek!

			return
		end

		AFSDirEnt_SIZEOF data +=
		AFSDirEnt_SIZEOF byteoff +=
		byteoff@ PAGESHIFT >> pageno!
		1 seek +=
	end

	if (pfdbe@)
		pfdbe@ MmEvictablePageDereference drop
	end

	seek@ nextseek!
	STATUS_END_OF_FILE ok!
end

fn (IODispatchReadDirectoryFunction) AFSReadDirectory { seek dirent fcb -- nextseek ok }
	fcb@ IOFileControlBlockLockShared ok!

	if (ok@)
		return
	end

	auto afsdirent
	AFSDirEnt_SIZEOF alloc afsdirent!

	seek@ // seek
	afsdirent@ // afsdirent
	fcb@ // fcb
	AFSDirectoryGetEntry ok! nextseek!

	fcb@ IOFileControlBlockUnlock

	if (ok@)
		return
	end

	dirent@ OSDirectoryEntry_Name + // dest
	afsdirent@ AFSDirEnt_name + // src
	63 // max
	strncpy
end

fn AFSDirectoryFindEntry { name dirfcb -- dirent pfdbe ok }
	if (DEBUGCHECKS)
		if (dirfcb@ IOFileControlBlockGetType OSFILETYPE_DIRECTORY ~=)
			// should have been caught by the IO system code
			"AFSDirectoryFindEntry: not a directory\n" KeCrash
		end
	end

	auto cacheblock
	dirfcb@ IOFileControlBlockGetCacheInfoBlock cacheblock!

	if (DEBUGCHECKS)
		if (cacheblock@ ~~)
			"AFSDirectoryFindEntry: no cacheblock\n" KeCrash
		end
	end

	auto mount
	dirfcb@ IOFileControlBlockGetMount mount!

	auto byteoff
	0 byteoff!

	auto bytesize
	dirfcb@ IOFileControlBlockGetSize bytesize!

	auto pageno
	byteoff@ PAGESHIFT >> pageno!

	auto curpageno
	-1 curpageno!

	0 pfdbe!

	while (byteoff@ bytesize@ <)
		if (pageno@ curpageno@ ~=)
			// must read in a new page

			if (pfdbe@)
				pfdbe@ MmEvictablePageDereference drop
			end

			0 // flags
			0 // kflags
			byteoff@ PAGENUMBERMASK & // offset
			dirfcb@ // fcb
			IOCachePageRead ok! pfdbe! dirent!

			if (ok@)
				return
			end

			byteoff@ PAGEOFFSETMASK & dirent +=

			pageno@ curpageno!
		end

		if (dirent@ AFSDirEnt_INum + @)
			if (name@ -1 ==)
				// just checking for any valid entry

				pfdbe@ MmEvictablePageDereference drop

				0 ok!

				return
			end

			if (dirent@ AFSDirEnt_name + name@ strcmp)
				// found it!

				0 ok!

				return
			end
		end

		AFSDirEnt_SIZEOF dirent +=
		AFSDirEnt_SIZEOF byteoff +=
		byteoff@ PAGESHIFT >> pageno!
	end

	if (pfdbe@)
		pfdbe@ MmEvictablePageDereference drop
	end

	STATUS_NOT_FOUND ok!
end

fn AFSRenameLock { mount -- ok }
	KERNELMODE // waitmode
	1 // alertable
	OSWAIT_TIMEOUTINFINITE // timeout
	mount@ IOMountGetContext AFSData_RenameMutex + // object
	KeThreadWaitForObject ok!
end

fn AFSRenameUnlock { mount -- }
	0 // abandon
	mount@ IOMountGetContext AFSData_RenameMutex + // mutex
	KeMutexRelease drop
end

fn AFSDirectoryCheckForCycle { testfcb searchfcb mount -- ok }
	auto iparent

	auto fcbdata
	searchfcb@ IOFileControlBlockGetContext fcbdata!

	0 iparent!

	searchfcb@ IOFileControlBlockReference drop

	0 ok!

	while (searchfcb@ testfcb@ ~=)
		if (fcbdata@ AFSFCBData_IParent + @ iparent@ ==)
			// found the root directory. no cycles.
			searchfcb@ IOFileControlBlockDereference drop

			return
		end

		fcbdata@ AFSFCBData_IParent + @ iparent!

		auto nextfcb
		iparent@  // inum
		mount@ // mount
		AFSFCBRead ok! nextfcb!

		searchfcb@ IOFileControlBlockDereference drop

		if (ok@)
			return
		end

		nextfcb@ IOFileControlBlockGetContext fcbdata!
		nextfcb@ searchfcb!
	end

	searchfcb@ IOFileControlBlockDereference drop

	STATUS_FORBIDDEN_OPERATION ok!
end

fn (IODispatchUnlinkFunction) AFSUnlink { name dirfcb -- ok }
	name@ AFSVerifyName ok!

	if (ok@)
		if (ok@ STATUS_ALREADY_EXISTS ==)
			STATUS_FORBIDDEN_OPERATION ok!
		end

		return
	end

	auto pfdbe
	auto dirent

	name@ // name
	dirfcb@ // dirfcb
	AFSDirectoryFindEntry ok! pfdbe! dirent!

	if (ok@)
		return
	end

	auto mount
	dirfcb@ IOFileControlBlockGetMount mount!

	auto fcb
	dirent@ AFSDirEnt_INum + @  // inum
	mount@ // mount
	AFSFCBRead ok! fcb!

	if (ok@)
		pfdbe@ MmEvictablePageDereference drop

		return
	end

	if (fcb@ IOFileControlBlock_Flags + @ IOFCBFLAG_SYSTEMFILE &)
		pfdbe@ MmEvictablePageDereference drop
		fcb@ IOFileControlBlockDereference drop

		STATUS_FORBIDDEN_OPERATION ok!

		return
	end

	if (fcb@ IOFileControlBlockGetType OSFILETYPE_DIRECTORY ==)
		if (fcb@ IOFileControlBlockGetReferences 1 >)
			// unlinking directories is forbidden if anyone could possibly
			// add new entries to it afterwards.

			pfdbe@ MmEvictablePageDereference drop
			fcb@ IOFileControlBlockDereference drop

			STATUS_TRY_AGAIN_LATER ok!

			return
		end

		// we're unlinking a directory, ensure that it is empty first

		-1 // name
		fcb@ // dirfcb
		AFSDirectoryFindEntry ok! drop drop

		if (ok@ STATUS_NOT_FOUND ~=)
			pfdbe@ MmEvictablePageDereference drop
			fcb@ IOFileControlBlockDereference drop

			if (ok@ ~~)
				STATUS_IS_A_DIRECTORY ok!
			end

			return
		end
	end

	// free the directory entry
	0 dirent@ AFSDirEnt_INum + !

	pfdbe@ // pfdbe
	KeProcessCurrent // process
	MmEvictablePageModify

	pfdbe@ MmEvictablePageDereference drop
	fcb@ IOFileControlBlockDeleteLastReference
	fcb@ IOFileControlBlockDereference drop

	0 ok!
end

fn (IODispatchRenameFunction) AFSRename { srcname srcfcb destname destfcb -- ok }
	auto mount
	srcfcb@ IOFileControlBlockGetMount mount!

	if (destfcb@ IOFileControlBlockGetMount mount@ ~=)
		STATUS_CROSS_VOLUME ok!
		return
	end

	srcname@ AFSVerifyName ok!

	if (ok@)
		if (ok@ STATUS_ALREADY_EXISTS ==)
			STATUS_FORBIDDEN_OPERATION ok!
		end

		return
	end

	destname@ AFSVerifyName ok!

	if (ok@)
		return
	end

	auto srcpfdbe
	auto srcdirent

	srcname@ // name
	srcfcb@ // dirfcb
	AFSDirectoryFindEntry ok! srcpfdbe! srcdirent!

	if (ok@)
		return
	end

	if (srcfcb@ destfcb@ ==)
		if (srcname@ destname@ strcmp)
			srcpfdbe@ MmEvictablePageDereference drop

			0 ok!

			return
		end
	end

	auto destpfdbe
	auto destdirent

	// check to make sure the desired name isn't present in the destination
	// directory.

	destname@ // destname
	destfcb@ // dirfcb
	AFSDirectoryFindEntry ok! destpfdbe! destdirent!

	if (ok@ STATUS_NOT_FOUND ~=)
		srcpfdbe@ MmEvictablePageDereference drop

		if (ok@ ~~)
			destpfdbe@ MmEvictablePageDereference drop

			STATUS_ALREADY_EXISTS ok!
		end

		return
	end

	auto filefcb
	srcdirent@ AFSDirEnt_INum + @  // inum
	mount@ // mount
	AFSFCBRead ok! filefcb!

	if (ok@)
		srcpfdbe@ MmEvictablePageDereference drop

		return
	end

	auto renamelocked
	0 renamelocked!

	if (srcfcb@ destfcb@ ~=)
		auto filetype
		filefcb@ IOFileControlBlockGetType filetype!

		if (filetype@ OSFILETYPE_DIRECTORY ==)
			// make sure it won't create a cycle by walking from the
			// destination directory up to the root directory, and seeing if
			// the source appears along the way. if it does, this operation
			// would create a cycle.

			mount@ AFSRenameLock ok!

			if (ok@)
				srcpfdbe@ MmEvictablePageDereference drop
				filefcb@ IOFileControlBlockDereference drop

				return
			end

			filefcb@ // testfcb
			destfcb@ // searchfcb
			mount@ // mount
			AFSDirectoryCheckForCycle ok!

			if (ok@)
				mount@ AFSRenameUnlock

				srcpfdbe@ MmEvictablePageDereference drop
				filefcb@ IOFileControlBlockDereference drop

				return
			end

			1 renamelocked!
		end

		destfcb@ AFSDirectoryAllocateEntry ok! destpfdbe! destdirent!

		if (ok@)
			if (renamelocked@)
				mount@ AFSRenameUnlock
			end

			srcpfdbe@ MmEvictablePageDereference drop
			filefcb@ IOFileControlBlockDereference drop

			return
		end

		// move the directory entry over by invalidating the old one and updating
		// the new one.

		srcdirent@ AFSDirEnt_INum + @ destdirent@ AFSDirEnt_INum + !
		0 srcdirent@ AFSDirEnt_INum + !
	end else
		srcdirent@ destdirent!
	end

	destdirent@ AFSDirEnt_name + // dest
	destname@ // src
	strcpy

	srcpfdbe@ // pfdbe
	KeProcessCurrent // process
	MmEvictablePageModify

	if (srcfcb@ destfcb@ ~=)
		destpfdbe@ // pfdbe
		KeProcessCurrent // process
		MmEvictablePageModify

		destpfdbe@ MmEvictablePageDereference drop
	end

	if (renamelocked@)
		auto fcbdata
		filefcb@ IOFileControlBlockGetContext fcbdata!

		destfcb@ IOFileControlBlockGetContext AFSFCBData_INum + @ fcbdata@ AFSFCBData_IParent + !

		filefcb@ AFSINodeUpdate

		mount@ AFSRenameUnlock
	end

	srcpfdbe@ MmEvictablePageDereference drop
	filefcb@ IOFileControlBlockDereference drop
end