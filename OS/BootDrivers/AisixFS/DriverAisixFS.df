//
// Implements the AisixFS driver.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALInterrupt.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/IO.h"

#include "<inc>/FSRTL.h"

#include "<inc>/ViewCache.h"

#include "AisixFS.h"

datasection "text"

table DriverAFS
	IOVERSION_MAJOR                      // ioversion major
	IOVERSION_MINOR                      // ioversion minor

	"AisixFS"                            // name
	pointerof DriverAFSDispatch          // dispatch table
	0                                    // flags

	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
	0                                    // reserved
endtable

table DriverAFSFilesystem
	0                              // next
	0                              // prev

	pointerof DriverAFS            // driver

	pointerof DriverAFSMount       // mountfunction
	pointerof DriverAFSUnmount     // unmountfunction
	pointerof DriverAFSFlush       // flushfunction
	pointerof DriverAFSUpdateFlags // updateflags
	0                              // flags
	pointerof DriverAFSVolumeQuery // volumequery
	pointerof AFSINodeReclaim      // reclaimfunction

	0                              // reserved
	0                              // reserved
	0                              // reserved
	0                              // reserved
	0                              // reserved
	0                              // reserved
	0                              // reserved
endtable

table DriverAFSDispatch
	pointerof AFSOpen                    // open
	pointerof AFSClose                   // close
	0                                    // iocontrol
	0                                    // reserved
	0                                    // reserved
	pointerof AFSUnlink                  // unlink
	pointerof AFSParse                   // parse
	0                                    // reserved
	0                                    // flush
	pointerof AFSDeleteObject            // delete object
	pointerof AFSPoke                    // poke
	pointerof AFSSetSecurity             // setsecurity
	pointerof AFSRename                  // rename
	0                                    // readblock
	0                                    // writeblock
	pointerof AFSTruncate                // truncate
	pointerof AFSReadDirectory           // readdirectory
	0                                    // getpageaddr
	0                                    // delete device object
	0                                    // reserved
	0                                    // cancel
	pointerof AFSReadWriteFile           // read
	pointerof AFSReadWriteFile           // write
endtable
public DriverAFSDispatch

datasection "data"

fn (FDriverInit) DriverInit { stage -- ok }
	fnsection "PAGE$text"

	if (stage@ STAGE_THREAD ==)
		DriverAFSFilesystem // filesystem
		IOFilesystemRegister ok!

		if (ok@)
			return
		end
	end

	0 ok!
end

fn (IOFilesystemMountFunction) DriverAFSMount { flags deviceobject -- fsdeviceobject ok }
	fnsection "PAGE$text"

	deviceobject@ IODevice_FileControlBlock + @ IOFileControlBlockCacheCheck ok! drop

	if (ok@)
		return
	end

	auto pageframe
	auto bcb

	0 // offset
	deviceobject@ IODevice_FileControlBlock + @ // fcb
	VcFileControlBlockMap ok! bcb! pageframe!

	if (ok@)
		return
	end

	if (pageframe@ AFSSuperblock_Magic + @ AFSSUPERBLOCKMAGIC ~=)
		bcb@ VcBufferDecrementMapCount drop

		STATUS_BAD_FILESYSTEM ok!

		return
	end

	if (pageframe@ AFSSuperblock_Version + @ AFSSUPERBLOCKVERSION ~=)
		bcb@ VcBufferDecrementMapCount drop

		STATUS_BAD_FILESYSTEM ok!

		return
	end

	auto mount
	flags@ // mountflags
	AFSData_SIZEOF // extensionsize
	pageframe@ AFSSuperblock_DataStart + @ AFSBLOCKSHIFT << // sizeinbytes
	DriverAFSFilesystem // filesystem
	deviceobject@ // deviceobject
	IOVolumeDeviceCreate ok! fsdeviceobject! mount!

	if (ok@)
		bcb@ VcBufferDecrementMapCount drop

		return
	end

	auto afsdata
	mount@ IOMount_Extension + @ afsdata!

	0 afsdata@ AFSData_UsedBlocks + !

	pageframe@ AFSSuperblock_FATStart + @ afsdata@ AFSData_FATStart + !
	pageframe@ AFSSuperblock_FATSize + @ afsdata@ AFSData_FATSize + !
	pageframe@ AFSSuperblock_IStart + @ afsdata@ AFSData_IStart + !
	pageframe@ AFSSuperblock_ICount + @ afsdata@ AFSData_ICount + !
	pageframe@ AFSSuperblock_VolSize + @ pageframe@ AFSSuperblock_DataStart + @ - afsdata@ AFSData_VolSize + !
	pageframe@ AFSSuperblock_DataStart + @ afsdata@ AFSData_DataStart + !
	0 afsdata@ AFSData_FCBRefTotal + !

	0 afsdata@ AFSData_ReclaimableListHead + !
	0 afsdata@ AFSData_ReclaimableListTail + !

	0 afsdata@ AFSData_WritableFiles + !

	"AFSINodeAllocationMutex" // name
	KERNELMODE // mode
	afsdata@ AFSData_INodeAllocationMutex + // mutex
	KeMutexInitialize

	"AFSRenameMutex" // name
	KERNELMODE // mode
	afsdata@ AFSData_RenameMutex + // mutex
	KeMutexInitialize

	"AFSFCBCacheMutex" // name
	KERNELMODE // mode
	afsdata@ AFSData_FCBCacheMutex + // mutex
	KeMutexInitialize

	afsdata@ AFSData_FCBBucketListHeads + // ptr
	AFSFCBBUCKETS 8 * // size
	0 // word
	memset

	bcb@ VcBufferDecrementMapCount drop

	mount@ AFSBlockBitmapInitialize ok!

	if (ok@)
		fsdeviceobject@ IOVolumeDeviceUninitialize

		return
	end

	auto rootfcb
	0 // flags
	1 // inum
	mount@ // mount
	AFSFCBRead ok! rootfcb!

	if (ok@)
		mount@ AFSBlockBitmapUninitialize

		fsdeviceobject@ IOVolumeDeviceUninitialize

		return
	end

	rootfcb@ mount@ IOMount_RootFCB + !

	0 ok!
end

fn (IOFilesystemUnmountFunction) DriverAFSUnmount { fsdeviceobject -- ok }
	fnsection "PAGE$text"

	auto mount
	fsdeviceobject@ IODevice_RelevantMount + @ mount!

	auto afsdata
	mount@ IOMount_Extension + @ afsdata!

	if (afsdata@ AFSData_FCBRefTotal + @)
		STATUS_DEVICE_BUSY ok!

		return
	end

	1 // destroy
	mount@ // mount
	AFSFCBCacheFlush ok!

	if (ok@)
		return
	end

	mount@ AFSBlockBitmapUninitialize

	mount@ FSVolumeFlush drop
end

fn (IOFilesystemFlushFunction) DriverAFSFlush { shutdown fsdeviceobject -- ok }
	fnsection "PAGE$text"

	// "DriverAFSFlush: TODO\n" KeCrash

	// I mean, we access the volume through the page cache,
	// so we don't have anything to flush I don't think...

	// but we WILL when we have a dirty bit to track.

	0 ok!
end

fn (IOFilesystemUpdateFlagsFunction) DriverAFSUpdateFlags { newflags oldflags fsdeviceobject -- ok }
	fnsection "PAGE$text"

	0 ok!

	auto mount
	fsdeviceobject@ IODevice_RelevantMount + @ mount!

	if (newflags@ OSMOUNT_READONLY &)
		if (oldflags@ OSMOUNT_READONLY & ~~)
			// volume is being downgraded to read-only.
			// only allow if there are no writable files opened.

			if (mount@ IOMount_Extension + @ AFSData_WritableFiles + @)
				STATUS_DEVICE_BUSY ok!

				return
			end

			// flush all dirty pages to ensure the volume
			// won't be written while read-only.

			0 // destroy
			mount@ // mount
			AFSFCBCacheFlush ok!

			if (ok@)
				return
			end

			mount@ FSVolumeFlush ok!

			if (ok@)
				return
			end
		end
	end
end

fn (IOFilesystemVolumeQueryFunction) DriverAFSVolumeQuery { query fsdeviceobject -- ok }
	fnsection "PAGE$text"

	auto mount
	fsdeviceobject@ IODevice_RelevantMount + @ mount!

	auto afsdata
	mount@ IOMount_Extension + @ afsdata!

	afsdata@ AFSData_VolSize + @ AFSBLOCKSHIFT << query@ OSMountInformation_VolumeCapacity + !
	afsdata@ AFSData_UsedBlocks + @ AFSBLOCKSHIFT << query@ OSMountInformation_VolumeUsed + !

	auto label
	mount@ IOMount_UnderlyingDeviceObject + @ IODeviceGetLabel label!

	if (label@)
		query@ OSMountInformation_VolumeLabel + // dest
		label@ // src
		OBNAMEMAX 1 - // max
		strncpy
	end

	0 ok!
end