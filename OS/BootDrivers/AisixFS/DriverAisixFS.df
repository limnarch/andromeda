//
// Implements the AisixFS driver.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALInterrupt.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/IO.h"

#include "<inc>/FSRTL.h"

#include "<inc>/ViewCache.h"

#include "AisixFS.h"

table DriverAFSFilesystem
	0                              // next
	0                              // prev

	"AisixFS"                      // name
	pointerof DriverAFSDispatch    // dispatchtable

	pointerof DriverAFSMount       // mountfunction
	pointerof DriverAFSUnmount     // unmountfunction
	pointerof DriverAFSFlush       // flushfunction
	pointerof DriverAFSUpdateFlags // updateflags
	0                              // flags
	pointerof DriverAFSVolumeQuery // volumequery
	pointerof AFSINodeReclaim      // reclaimfunction

	0                              // reserved
	0                              // reserved
	0                              // reserved
	0                              // reserved
	0                              // reserved
	0                              // reserved
	0                              // reserved
endtable

table DriverAFSDispatch
	pointerof AFSOpen                    // open
	pointerof AFSClose                   // close
	0                                    // iocontrol
	pointerof AFSReadFile                // read
	pointerof AFSWriteFile               // write
	pointerof AFSUnlink                  // unlink
	pointerof AFSParse                   // parse
	0                                    // reserved
	0                                    // flush
	pointerof AFSDeleteObject            // delete object
	pointerof AFSPoke                    // poke
	pointerof AFSSetSecurity             // setsecurity
	pointerof AFSRename                  // rename
	0                                    // readblock
	0                                    // writeblock
	pointerof AFSTruncate                // truncate
	pointerof AFSReadDirectory           // readdirectory
	0                                    // getpageaddr
	0                                    // delete device object
	0                                    // reserved
	0                                    // cancel
	0                                    // reserved
	0                                    // reserved
endtable
public DriverAFSDispatch

fn (FDriverInit) DriverInit { stage -- ok }
	if (stage@ STAGE_THREAD ==)
		DriverAFSFilesystem // filesystem
		IOFilesystemRegister ok!

		if (ok@)
			return
		end
	end

	0 ok!
end

fn (IOFilesystemMountFunction) DriverAFSMount { mount -- ok }
	AFSBLOCKSIZE // blocksize
	mount@ // mount
	FSVolumeInitialize ok! drop

	if (ok@)
		return
	end

	auto pageframe

	auto bcb

	0 // offset
	0 // length
	mount@ FSVolumeGetFile // fileobject
	VcFileMap ok! bcb! pageframe!

	if (ok@)
		mount@ FSVolumeUninitialize drop
		return
	end

	if (pageframe@ AFSSuperblock_Magic + @ AFSSUPERBLOCKMAGIC ~=)
		bcb@ VcBufferDecrementMapCount drop

		mount@ FSVolumeUninitialize drop

		STATUS_BAD_FILESYSTEM ok!

		return
	end

	if (pageframe@ AFSSuperblock_Version + @ AFSSUPERBLOCKVERSION ~=)
		bcb@ VcBufferDecrementMapCount drop

		mount@ FSVolumeUninitialize drop

		STATUS_BAD_FILESYSTEM ok!

		return
	end

	pageframe@ AFSSuperblock_DataStart + @ AFSBLOCKSHIFT << // size
	mount@ // mount
	FSVolumeFileCreate ok!

	if (ok@)
		bcb@ VcBufferDecrementMapCount drop

		mount@ FSVolumeUninitialize drop

		return
	end

	auto afsdata
	AFSData_SIZEOF // bytes
	'AFSD' // tag
	0 // flags
	MmAllocWithTag ok! afsdata!

	if (ok@)
		bcb@ VcBufferDecrementMapCount drop

		mount@ FSVolumeUninitialize drop

		return
	end

	0 afsdata@ AFSData_UsedBlocks + !

	pageframe@ AFSSuperblock_FATStart + @ afsdata@ AFSData_FATStart + !
	pageframe@ AFSSuperblock_FATSize + @ afsdata@ AFSData_FATSize + !
	pageframe@ AFSSuperblock_IStart + @ afsdata@ AFSData_IStart + !
	pageframe@ AFSSuperblock_ICount + @ afsdata@ AFSData_ICount + !
	pageframe@ AFSSuperblock_VolSize + @ afsdata@ AFSData_VolSize + !
	0 afsdata@ AFSData_FCBRefTotal + !

	0 afsdata@ AFSData_ReclaimableListHead + !
	0 afsdata@ AFSData_ReclaimableListTail + !

	0 afsdata@ AFSData_WritableFiles + !

	"AFSINodeAllocationMutex" // name
	KERNELMODE // mode
	afsdata@ AFSData_INodeAllocationMutex + // mutex
	KeMutexInitialize

	"AFSRenameMutex" // name
	KERNELMODE // mode
	afsdata@ AFSData_RenameMutex + // mutex
	KeMutexInitialize

	"AFSFCBCacheMutex" // name
	KERNELMODE // mode
	afsdata@ AFSData_FCBCacheMutex + // mutex
	KeMutexInitialize

	afsdata@ AFSData_FCBBucketListHeads + // ptr
	AFSFCBBUCKETS 8 * // size
	0 // word
	memset

	afsdata@ mount@ IOMountSetContext

	bcb@ VcBufferDecrementMapCount drop

	mount@ AFSBlockBitmapInitialize ok!

	if (ok@)
		afsdata@ MmFree

		mount@ FSVolumeUninitialize drop

		return
	end

	auto rootfcb

	0 // flags
	1 // inum
	mount@ // mount
	AFSFCBRead ok! rootfcb!

	if (ok@)
		mount@ AFSBlockBitmapUninitialize

		afsdata@ MmFree

		mount@ FSVolumeUninitialize drop

		return
	end

	rootfcb@ mount@ IOMountSetRootFCB

	0 ok!
end

fn (IOFilesystemUnmountFunction) DriverAFSUnmount { mount -- ok }
	auto afsdata
	mount@ IOMountGetContext afsdata!

	if (afsdata@ AFSData_FCBRefTotal + @)
		STATUS_DEVICE_BUSY ok!

		return
	end

	1 // destroy
	mount@ // mount
	AFSFCBCacheFlush ok!

	if (ok@)
		return
	end

	mount@ FSVolumeUninitialize ok!

	if (ok@)
		return
	end

	mount@ AFSBlockBitmapUninitialize

	afsdata@ MmFree

	0 mount@ IOMountSetContext

	mount@ FSVolumeFlush drop
end

fn (IOFilesystemFlushFunction) DriverAFSFlush { shutdown mount -- ok }
	// "DriverAFSFlush: TODO\n" KeCrash

	// I mean, we access the volume through the page cache,
	// so we don't have anything to flush I don't think...

	// but we WILL when we have a dirty bit to track.

	0 ok!
end

fn (IOFilesystemUpdateFlagsFunction) DriverAFSUpdateFlags { newflags oldflags mount -- ok }
	0 ok!

	if (newflags@ OSMOUNT_READONLY &)
		if (oldflags@ OSMOUNT_READONLY & ~~)
			// volume is being downgraded to read-only.
			// only allow if there are no writable files opened.

			if (mount@ IOMountGetContext AFSData_WritableFiles + @)
				STATUS_DEVICE_BUSY ok!

				return
			end

			// flush all dirty pages to ensure the volume
			// won't be written while read-only.

			0 // destroy
			mount@ // mount
			AFSFCBCacheFlush ok!

			if (ok@)
				return
			end

			mount@ FSVolumeFlush ok!

			if (ok@)
				return
			end
		end
	end
end

fn (IOFilesystemVolumeQueryFunction) DriverAFSVolumeQuery { query mount -- ok }
	auto afsdata
	mount@ IOMountGetContext afsdata!

	afsdata@ AFSData_VolSize + @ AFSBLOCKSHIFT << query@ OSMountInformation_VolumeCapacity + !
	afsdata@ AFSData_UsedBlocks + @ AFSBLOCKSHIFT << query@ OSMountInformation_VolumeUsed + !

	0 ok!
end