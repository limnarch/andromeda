//
// Implements the Truncate dispatch routine for the AisixFS driver.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALInterrupt.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<inc>/FSRTL.h"

#include "AisixFS.h"

fn (IODispatchTruncateFunction) AFSTruncate { newsize zero fcb -- ok }
	// add or remove blocks from file specified by FCB according to newsize.

	auto oldsize
	fcb@ IOFileControlBlockGetSize oldsize!

	auto oldblocks
	oldsize@ AFSBLOCKOFFMASK + AFSBLOCKSHIFT >> oldblocks!

	auto newblocks
	newsize@ AFSBLOCKOFFMASK + AFSBLOCKSHIFT >> newblocks!

	0 ok!

	if (newblocks@ oldblocks@ ==)
		// no blocks to allocate or release, just set size and return

		if (newsize@ oldsize@ ~=)
			newsize@ // size
			fcb@ // fcb
			IOFileControlBlockSetSize

			fcb@ AFSINodeUpdate
		end

		return
	end

	if (zero@ ~~)
		if (newsize@ oldsize@ >)
			if (newsize@ AFSBLOCKOFFMASK &)
				1 zero!
			end
		end
	end

	newsize@ // size
	fcb@ // fcb
	IOFileControlBlockSetSize

	newblocks@ // newblocks
	oldblocks@ // oldblocks
	zero@ // zero
	fcb@ // fcb
	AFSBlockTruncate ok!

	if (ok@)
		// failed to truncate blocks, reset size
		oldsize@ // size
		fcb@ // fcb
		IOFileControlBlockSetSize
	end else
		fcb@ AFSINodeUpdate
	end
end