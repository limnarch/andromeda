//
// Implements disk block management for the AisixFS driver.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALInterrupt.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/IO.h"

#include "<inc>/FSRTL.h"

#include "AisixFS.h"

fn AFSWalkFAT { startcount startblkno mode mount kflags -- left blkno ok }
	startblkno@ blkno!
	startcount@ left!

	auto fatblkno
	0 fatblkno!

	auto curfatblkno
	-1 curfatblkno!

	auto fatpfdbe
	0 fatpfdbe!

	0 ok!

	auto off

	auto fatdata

	auto fatbase
	mount@ IOMountGetContext AFSData_FATStart + @ fatbase!

	while (left@)
		if (blkno@ 0xFFFFFFFF ==)
			if (mode@ AFSFATWALKMODE_GROW ~=)
				"AFSWalkFAT: blkno == 0xFFFFFFFF\n" KeCrash
			end
		end else
			blkno@ 2 << AFSBLOCKSIZE / fatblkno!
			blkno@ 2 << AFSBLOCKOFFMASK & off!

			if (fatblkno@ curfatblkno@ ~=)
				// must read in a new FAT block

				if (fatpfdbe@)
					fatpfdbe@ MmEvictablePageDereference drop
				end

				0 // flags
				kflags@ // kflags
				fatbase@ fatblkno@ + AFSBLOCKSHIFT << // offset
				mount@ // mount
				FSVolumeBlockRead ok! fatpfdbe! fatdata!

				if (ok@)
					return
				end

				fatblkno@ curfatblkno!
			end

			fatdata@ off@ + @ blkno!
		end

		1 left -=
	end

	if (fatpfdbe@)
		fatpfdbe@ MmEvictablePageDereference drop
	end
end

fn AFSBlockMap { blkoff fcb kflags -- blkno ok }
	// convert a block offset within an FCB to an absolute disk block number.

	// we use two FAT lookup reference points: the file's first block number,
	// and that of the last successful lookup. if blkoff is >= the last
	// successful lookup, we can use it as a jumping off point to speed
	// things up. if thats not the case, we have to start at the first block
	// number.

	0 ok!

	auto fcbdata
	fcb@ IOFileControlBlockGetContext fcbdata!

	auto fcbblocks
	fcb@ IOFileControlBlockGetSize AFSBLOCKOFFMASK + AFSBLOCKSHIFT >> fcbblocks!

	if (blkoff@ fcbblocks@ >=)
		"AFSBlockMap: blkoff > size in blocks\n" KeCrash
	end

	if (blkoff@ 0 ==)
		// bit of a fast path

		fcbdata@ AFSFCBData_FirstFATLink + @ blkno!

		return
	end

	auto lastlookup
	fcbdata@ AFSFCBData_LastFATLinkBlkOff + @ lastlookup!

	auto walkcount
	auto walkstart

	if (blkoff@ lastlookup@ >=)
		blkoff@ lastlookup@ - walkcount!
		fcbdata@ AFSFCBData_LastFATLinkValue + @ walkstart!
	end else
		blkoff@ walkcount!
		fcbdata@ AFSFCBData_FirstFATLink + @ walkstart!
	end

	walkcount@ // startcount
	walkstart@ // startblkno
	AFSFATWALKMODE_SEEK // mode
	fcb@ IOFileControlBlockGetMount // mount
	kflags@ // kflags
	AFSWalkFAT ok! blkno! walkcount!

	if (ok@)
		return
	end

	if (walkcount@)
		walkcount@ "AFSBlockMap: walkcount = %d\n" KeCrash
	end

	if (blkoff@ lastlookup@ >= blkoff@ 8 >= ||)
		blkoff@ fcbdata@ AFSFCBData_LastFATLinkBlkOff + !
		blkno@ fcbdata@ AFSFCBData_LastFATLinkValue + !
	end
end

fn AFSBlockTruncate { newblocks oldblocks fcb -- ok }
	auto fcbdata
	fcb@ IOFileControlBlockGetContext fcbdata!

	auto lastlookup
	fcbdata@ AFSFCBData_LastFATLinkBlkOff + @ lastlookup!

	auto walkcount
	auto walkstart

	auto mode
	if (newblocks@ oldblocks@ <)
		AFSFATWALKMODE_TRUNC mode!
	end else
		AFSFATWALKMODE_GROW mode!
	end

	if (mode@ AFSFATWALKMODE_GROW == oldblocks@ lastlookup@ >= &&)
		oldblocks@ lastlookup@ - walkcount!
		fcbdata@ AFSFCBData_LastFATLinkValue + @ walkstart!
	end else
		oldblocks@ walkcount!
		fcbdata@ AFSFCBData_FirstFATLink + @ walkstart!
	end

	auto blkno
	walkcount@ // startcount
	walkstart@ // startblkno
	mode@ // mode
	fcb@ IOFileControlBlockGetMount // mount
	0 // kflags
	AFSWalkFAT ok! blkno! walkcount!
end