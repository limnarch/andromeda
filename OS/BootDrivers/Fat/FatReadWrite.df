//
// Implements the Read and Write dispatch routines for the FAT driver.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALInterrupt.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/IO.h"

#include "<inc>/FSRTL.h"

#include "Fat.h"

fn (IODispatchEnqueueIOPFunction) FatReadWriteFile { iopl -- done ok }
	0 ok!
	0 done!

	auto fcb
	iopl@ IOPacketLocation_FileControlBlock + @ fcb!

	auto bytesize
	fcb@ IOFileControlBlock_SizeInBytes + @ bytesize!

	auto offset
	iopl@ IOPacketLocation_Offset + @ offset!

	auto iop
	iopl@ IOPacketFromLocation iop!

	auto mount
	fcb@ FSFileControlBlockGetMount mount!

	auto fatdata
	mount@ IOMount_Extension + @ fatdata!

	auto bytes
	iopl@ IOPacketLocation_Length + @ bytes!

	auto sectorsize
	fatdata@ FatData_SectorSizeBytes + @ sectorsize!

	auto rundownlisthead
	4 alloc rundownlisthead!

	0 rundownlisthead@!

	if (offset@ bytesize@ >=)
		1 done!

		if (iopl@ IOPacketLocation_FunctionCodeB + gb IODISPATCH_READ ==)
			0 iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
		end else
			STATUS_END_OF_FILE ok!
		end

		ok@ // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end

	// truncate length to file size.

	bytesize@ offset@ -
	bytes@
	min bytes!

	if (bytes@ ~~)
		1 done!

		0 iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

		0 // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end

	// round bytes up to next sector

	bytes@
	sectorsize@ 1 - +
	sectorsize@ 1 - ~ & bytes!

	if (fcb@ IOFileControlBlock_Paged + @ IOFileControlBlockPaged_FileType + @ OSFILETYPE_BLOCKDEVICE ==)
		// this is actually the volume device.
		// Make sector zero and the primary FAT directly mapped on reads.
		// Make the secondary FATs completely ignored on reads and writes.
		// If this is a write to the primary FAT, truncate the request to the
		// final sector in the primary FAT and then mirror it to the other
		// FATs in sequence.

		IODONE_SAMELOCATION done!

		mount@ IOMount_UnderlyingDeviceObject + @ IODevice_FileControlBlock + @ iopl@ IOPacketLocation_FileControlBlock + !

		if (iopl@ IOPacketLocation_FunctionCodeB + gb IODISPATCH_READ ==)
			bytes@ iopl@ IOPacketLocation_Length + !

			return
		end

		rundownlisthead@ // rundownlisthead
		offset@ // offset
		bytes@ // bytes
		mount@ // mount
		iopl@ // iopl
		iop@ // iop
		FatMirrorWrite ok!

		if (ok@)
			rundownlisthead@ FatCleanupAssociatedIOPs

			1 done!

			ok@ // status
			0 // priboost
			iop@ // iop
			IOPacketCompleteLow
		end

		return
	end

	if (fcb@ mount@ IOMount_RootFCB + @ ==
		fatdata@ FatData_FatType + @ 32 ~= &&)

		// root directory I/O

		offset@ // offset
		bytes@ // bytes
		mount@ // mount
		iopl@ // iopl
		iop@ // iop
		FatReadWriteRootDirectory ok!

		if (ok@)
			1 done!

			ok@ // status
			0 // priboost
			iop@ // iop
			IOPacketCompleteLow
		end else
			IODONE_SAMELOCATION done!
		end

		return
	end

	// we are now pretty sure this is a normal file/directory, so lock the
	// paging rwlock if applicable.

	iop@ fcb@ IOFileControlBlockLockSharedForPaging

	// get the file size again since it may have changed due to truncation.

	fcb@ IOFileControlBlock_SizeInBytes + @ bytesize!
	iopl@ IOPacketLocation_Length + @ bytes!

	// truncate length to file size.

	bytesize@ offset@ -
	bytes@
	min bytes!

	if (bytes@ ~~)
		iop@ fcb@ IOFileControlBlockUnlockForPaging

		1 done!

		0 iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

		0 // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end

	bytes@ iopl@ IOPacketLocation_Context + !

	pointerof FatReadWriteCallback iopl@ IOPacketLocation_CallbackRoutine + !

	rundownlisthead@ // rundownlisthead
	fcb@ // fcb
	offset@ // offset
	bytes@ // bytes
	mount@ // mount
	iopl@ // iopl
	iop@ // iop
	FatReadWrite ok!

	iop@ fcb@ IOFileControlBlockUnlockForPaging

	if (ok@)
		rundownlisthead@ FatCleanupAssociatedIOPs

		1 done!

		ok@ // status
		0 // priboost
		iop@ // iop
		IOPacketCompleteLow

		return
	end
end

fn FatCleanupAssociatedIOPs { rundownlisthead -- }
	// clean up any associated IOPs that were allocated before the error
	// condition occurred.

	auto associop
	rundownlisthead@@ associop!

	while (associop@)
		auto niop
		associop@ IOPacketHeader_DeviceQueueNext + @ niop!

		associop@ IOPacketFree

		niop@ associop!
	end
end

fn FatReadWrite { rundownlisthead fcb offset bytes mount iopl iop -- ok }
	// we need to allocate all of the associated IOPs that will be needed to
	// complete this operation. this has to be done first because there's no
	// good way to clean up associated IOPs in a failure scenario.

	// we link the associated IOPs into a rundown list, so that we can
	// deallocate all of them if there is a failure. we run the list through
	// the DeviceQueue links, since these are unused while the IOP is in our
	// custody. they are actually used for the per-thread associated IOP list,
	// but we don't associate them until after we no longer need the rundown
	// list, so that is fine.

	0 ok!

	auto rundownlisttail
	0 rundownlisttail!

	auto kflags
	iopl@ IOPacketHeader_KFlags + @ kflags!

	auto off
	0 off!

	auto fragiopl
	iopl@ IOPacketLocationNext fragiopl!

	auto fragiop
	iop@ fragiop!

	mount@ IOMount_UnderlyingDeviceObject + @ IODevice_FileControlBlock + @
	fragiopl@ IOPacketLocation_FileControlBlock + !

	iopl@ IOPacketLocation_FunctionCodeB + gb
	fragiopl@ IOPacketLocation_FunctionCodeB + sb

	iopl@ IOPacketLocation_Flags + @
	fragiopl@ IOPacketLocation_Flags + !

	auto lastvoloffset
	-1 lastvoloffset!

	auto fraglength
	0 fraglength!

	auto clustersize
	mount@ IOMount_Extension + @ FatData_ClusterSizeBytes + @ clustersize!

	while (off@ bytes@ <)
		auto voloffset
		offset@ off@ + // fileoffset
		fcb@ // fcb
		kflags@ // kflags
		FatBlockMap ok! voloffset!

		if (ok@)
			return
		end

		if (lastvoloffset@ -1 ~=)
			if (voloffset@
				lastvoloffset@ clustersize@ + ~=)

				// not contiguous -- new fragment.

				fraglength@ fragiopl@ IOPacketLocation_Length + !
				0 fraglength!

				iop@ IOPacketHeader_KFlags + @ // kflags
				IOPFLAG_FREEIOP IOPFLAG_QUOTA | // iopflags
				iopl@ // iopl
				IOPacketAllocateAssocStack ok! fragiop! fragiopl!

				if (ok@)
					return
				end

				mount@ IOMount_UnderlyingDeviceObject + @ IODevice_FileControlBlock + @
				fragiopl@ IOPacketLocation_FileControlBlock + !

				voloffset@ fragiopl@ IOPacketLocation_Offset + !

				iopl@ IOPacketLocation_FunctionCodeB + gb
				fragiopl@ IOPacketLocation_FunctionCodeB + sb

				iopl@ IOPacketLocation_Flags + @
				fragiopl@ IOPacketLocation_Flags + !

				iop@ IOPacketHeader_MDL + @
				fragiop@ IOPacketHeader_MDL + !

				iopl@ IOPacketLocation_OffsetInMDL + @ off@ +
				fragiopl@ IOPacketLocation_OffsetInMDL + !

				if (rundownlisttail@ ~~)
					fragiop@ rundownlisthead@!
				end else
					fragiop@ rundownlisttail@ IOPacketHeader_DeviceQueueNext + !
				end

				fragiop@ rundownlisttail!
			end
		end else
			voloffset@ fragiopl@ IOPacketLocation_Offset + !
		end

		voloffset@ lastvoloffset!

		// deal with partials

		if (bytes@ off@ - clustersize@ >=)
			clustersize@ fraglength +=
		end else
			bytes@ off@ - fraglength +=
		end

		clustersize@ off +=
	end

	fraglength@ fragiopl@ IOPacketLocation_Length + !

	// allocating the packets succeeded, so associate them now.
	// this will add them to the deferred-enqueue list of the current thread,
	// and when this IOP has been processed and enqueued, the others will be
	// processed iteratively. this song and dance avoids the unbounded
	// recursion of traditional layered driver models.

	rundownlisthead@@ fragiop!

	while (fragiop@)
		auto niop
		fragiop@ IOPacketHeader_DeviceQueueNext + @ niop!

		fragiop@ // associop
		iop@ // iop
		IOPacketAssociate

		niop@ fragiop!
	end
end

fn (IOPacketCompletionDPCRoutine) FatReadWriteCallback { iopl -- ok }
	// save the actual length if successful

	auto iop
	iopl@ IOPacketFromLocation iop!

	if (iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Status + @ ~~)
		iopl@ IOPacketLocation_Context + @ iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !
	end

	0 ok!
end

fn FatReadWriteRootDirectory { offset bytes mount iopl iop -- ok }
	fnsection "PAGE$text"

	0 ok!

	// access the FAT12/16 root directory.

	auto fatdata
	mount@ IOMount_Extension + @ fatdata!

	mount@ IOMount_UnderlyingDeviceObject + @ IODevice_FileControlBlock + @ iopl@ IOPacketLocation_FileControlBlock + !

	fatdata@ FatData_RootFirstCluster + @ // actually the first sector
	fatdata@ FatData_SectorSizeShift + @ <<
	iopl@ IOPacketLocation_Offset + +=
end

fn FatMirrorWrite { rundownlisthead offset bytes mount iopl iop -- ok }
	fnsection "PAGE$text"

	0 ok!

	auto fatdata
	mount@ IOMount_Extension + @ fatdata!

	auto fatsizebytes
	fatdata@ FatData_FatSize + @ fatdata@ FatData_SectorSizeShift + @ << fatsizebytes!

	auto fatstart
	fatdata@ FatData_ReservedSectorCount + @ fatdata@ FatData_SectorSizeShift + @ << fatstart!

	if (offset@ fatstart@ <)
		// skip over the reserved sectors

		auto diff
		fatstart@ offset@ - diff!

		if (diff@ bytes@ >=)
			// ignore this write entirely

			0 iop@ IOPacketHeader_StatusBlock + OSStatusBlock_Length + !

			0 // status
			0 // priboost
			iop@ // iop
			IOPacketCompleteLow

			return
		end

		diff@ offset +=
		diff@ bytes -=
		diff@ iopl@ IOPacketLocation_OffsetInMDL + +=
	end

	offset@ iopl@ IOPacketLocation_Offset + !
	bytes@ iopl@ IOPacketLocation_Length + !

	auto rundownlisttail
	0 rundownlisttail!

	auto fragiop
	auto fragiopl

	// mirror the write

	auto fats
	fatdata@ FatData_FatCount + @ 1 - fats!

	while (fats@)
		fatsizebytes@ offset +=

		iop@ IOPacketHeader_KFlags + @ // kflags
		IOPFLAG_FREEIOP IOPFLAG_QUOTA | // iopflags
		iopl@ // iopl
		IOPacketAllocateAssocStack ok! fragiop! fragiopl!

		if (ok@)
			return
		end

		mount@ IOMount_UnderlyingDeviceObject + @ IODevice_FileControlBlock + @
		fragiopl@ IOPacketLocation_FileControlBlock + !

		offset@ fragiopl@ IOPacketLocation_Offset + !

		IODISPATCH_WRITE fragiopl@ IOPacketLocation_FunctionCodeB + sb

		iopl@ IOPacketLocation_Flags + @
		fragiopl@ IOPacketLocation_Flags + !

		iop@ IOPacketHeader_MDL + @
		fragiop@ IOPacketHeader_MDL + !

		iopl@ IOPacketLocation_OffsetInMDL + @ fragiopl@ IOPacketLocation_OffsetInMDL + !

		bytes@ fragiopl@ IOPacketLocation_Length + !

		if (rundownlisttail@ ~~)
			fragiop@ rundownlisthead@!
		end else
			fragiop@ rundownlisttail@ IOPacketHeader_DeviceQueueNext + !
		end

		fragiop@ rundownlisttail!

		1 fats -=
	end

	rundownlisthead@@ fragiop!

	while (fragiop@)
		auto niop
		fragiop@ IOPacketHeader_DeviceQueueNext + @ niop!

		fragiop@ // associop
		iop@ // iop
		IOPacketAssociate

		niop@ fragiop!
	end
end