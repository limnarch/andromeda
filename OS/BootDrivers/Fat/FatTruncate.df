//
// Implements the Truncate dispatch routine for the FAT driver.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALInterrupt.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<inc>/FSRTL.h"

#include "Fat.h"

fn (IODispatchTruncateFunction) FatTruncate { newsize zero flags fcb -- ok }
	fnsection "PAGE$text"

	// add or remove clusters from file specified by FCB according to newsize.

	auto oldsize
	fcb@ IOFileControlBlock_SizeInBytes + @ oldsize!

	auto fcbdata
	fcb@ IOFileControlBlock_Paged + @ IOFileControlBlockPaged_FSContext + @ fcbdata!

	auto fatdata
	fcb@ FSFileControlBlockGetMount IOMount_Extension + @ fatdata!

	auto oldclusters
	oldsize@ fatdata@ FatData_ClusterSizeBytes + @ 1 - +
	fatdata@ FatData_ClusterSizeShift + @ >> oldclusters!

	auto newclusters
	newsize@ fatdata@ FatData_ClusterSizeBytes + @ 1 - +
	fatdata@ FatData_ClusterSizeShift + @ >> newclusters!

	0 ok!

	if (newclusters@ oldclusters@ ==)
		// no blocks to allocate or release, just set size and return

		if (newsize@ oldsize@ ~=)
			newsize@ fcb@ IOFileControlBlock_SizeInBytes + !

			fcb@ FatFileUpdate
		end

		return
	end

	if (zero@ ~~)
		if (newsize@ oldsize@ >)
			if (newsize@ fatdata@ FatData_ClusterSizeBytes + @ 1 - &)
				2 zero!
			end
		end
	end

	newsize@ fcb@ IOFileControlBlock_SizeInBytes + !

	newclusters@ // newclusters
	oldclusters@ // oldclusters
	zero@ // zero
	flags@ // flags
	fcb@ // fcb
	FatClusterTruncate ok!

	if (ok@)
		// failed to truncate blocks, reset size

		oldsize@ fcb@ IOFileControlBlock_SizeInBytes + !
	end else
		fcb@ FatFileUpdate
	end
end