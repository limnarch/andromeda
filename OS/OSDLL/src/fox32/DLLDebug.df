//
// Implements userspace debug tracing for the fox32 architecture.
//

#include "<df>/dragonfruit.h"

#include "<inc>/OSCalls.h"
#include "<inc>/OSMap.h"
#include "<inc>/OSFile.h"
#include "<inc>/OSAbort.h"
#include "<inc>/OSAccess.h"
#include "<inc>/OSObject.h"
#include "<inc>/OSStatus.h"
#include "<inc>/OSModule.h"
#include "<inc>/OSAlloc.h"
#include "<inc>/OSWait.h"
#include "<inc>/OSContext.h"

#include "../OSDLL.h"

table DLLContextNames
	"t0"    "t1"
	"t2"    "t3"
	"t4"    "t5"
	"t6"
	"a0"    "a1"
	"a2"    "a3"
	"s0"    "s1"
	"s2"    "s3"
	"s4"    "s5"
	"s6"    "s7"
	"s8"    "s9"
	"s10"   "s11"
	"s12"   "s13"
	"s14"   "s15"
	"s16"   "s17"

	"at"

	"tp"

	"r31"
	"fp"

	"addr"
	"ef"
	"epc"
	"sp"
endtable

fn DLLDebugGetSymbol { address -- dll symbase symname symbol ok }
	ComDLLListHead@ dll!

	STATUS_NO_SYMBOL ok!

	while (dll@)
		address@ // address
		dll@ // dll
		ComDLLGetSymbolByAddress ok! symbol! symname! symbase!

		if (ok@ ~~)
			return
		end

		dll@ ComDLL_Next + @ dll!
	end
end

fn DLLDebugTrace { context -- }
	auto ctxrows
	OSCONTEXTCOUNT 4 + 5 / ctxrows!

	auto ictx
	0 ictx!

	while (ctxrows@)
		auto ctxcols
		5 ctxcols!

		while (ctxcols@)
			if (ictx@ OSCONTEXTCOUNT <)
				auto off

				if (ictx@ 34 >)
					ictx@ 2 << 3 - off!
				end else
					ictx@ 2 << off!
				end

				auto name
				DLLContextNames ictx@ 2 << + @ name!

				if (name@)
					name@ "%-5s " OSGetStdErr FPrintf

					if (ictx@ 34 ==)
						context@ off@ + gb "%08x " OSGetStdErr FPrintf
					end else
						context@ off@ + @ "%08x " OSGetStdErr FPrintf
					end
				end
			end

			1 ictx +=
			1 ctxcols -=
		end

		"\n" OSGetStdErr FPrintf

		1 ctxrows -=
	end

	"Stack trace:\n" OSGetStdErr FPrintf

	auto link
	context@ OSContext_fp + @ link!

	auto pc
	context@ OSContext_epc + @ pc!

	auto tracenamebuf
	256 alloc tracenamebuf!

	while (1)
		auto ok
		auto symbol
		auto dll
		auto symname
		auto symbase

		pc@ // address
		DLLDebugGetSymbol ok! symbol! symname! symbase! dll!

		if (ok@)
			pc@
			"%08x UNKNOWN\n" OSGetStdErr FPrintf

			break
		end else
			if (symname@ ~~)
				"NAMELESS" symname!
			end

			dll@ ComDLL_Name + @ OSLastComponent ictx!

			symname@
			ictx@
			pc@
			"%08x %s!%s\n" OSGetStdErr FPrintf
		end

		if (link@ ~~)
			break
		end

		link@ 4 + @ pc!
		link@@ link!
	end
end