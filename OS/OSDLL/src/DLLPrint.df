//
// Implements standard IO support.
//

#include "<df>/dragonfruit.h"

#include "<inc>/OSMap.h"
#include "<inc>/OSCalls.h"
#include "<inc>/OSFile.h"
#include "<inc>/OSObject.h"
#include "<inc>/OSWait.h"
#include "<inc>/OSStatus.h"
#include "<inc>/OSSignal.h"
#include "<inc>/OSProcess.h"
#include "<inc>/OSFastMutex.h"

#include "OSDLL.h"

fn OSGetStdIn { -- fd }
	OSPEB OSPEBs_StdIn + @ fd!
end

fn OSGetStdOut { -- fd }
	OSPEB OSPEBs_StdOut + @ fd!
end

fn OSGetStdErr { -- fd }
	OSPEB OSPEBs_StdErr + @ fd!
end

fn OSSetStdIn { fd -- }
	fd@ OSPEB OSPEBs_StdIn + !
end

fn OSSetStdOut { fd -- }
	if (OSPEB OSPEBs_StdOut + @ fd@ ~=)
		OSFlushLine
		fd@ OSPEB OSPEBs_StdOut + !
	end
end

fn OSSetStdErr { fd -- }
	fd@ OSPEB OSPEBs_StdErr + !
end

fn OSAbort { ... fmt -- }
	argv
	argc@
	fmt@
	OSPEB OSPEBs_StdErr + @
	VFPrintf

	OSSIGNALACTION_DEFAULT // action
	OSSIGNAL_ABRT // signum
	OSSignalSetAction drop

	// send OSSIGNAL_ABRT to ourselves

	OSSIGNAL_ABRT // signal
	OSCURRENTPROCESS // processhandle
	OSProcessSignal drop

	-1 OSExit
end

fn OSError { ... fmt -- }
	argv
	argc@
	fmt@
	OSPEB OSPEBs_StdErr + @
	VFPrintf

	-1 OSExit
end

fn FPuts { fd s -- }
	while (s@ gb)
		fd@ s@ gb FPutc
		1 s +=
	end
end

fn VPrintf { argvt argcn fmt -- }
	argvt@
	argcn@
	fmt@
	OSPEB OSPEBs_StdOut + @
	VFPrintf
end

const LINEBUFFERMAX 80
buffer FPutcLineBuffer (LINEBUFFERMAX 16 +)
var FPutcLineBufferLength 0

fn OSFlushLine { -- }
	if (OSPEB OSPEBs_StdOut + @ OSNONE ==)
		return
	end

	auto len
	FPutcLineBufferLength@ len!

	if (len@ ~~)
		return
	end

	0 FPutcLineBufferLength!

	0 // flags
	len@ // length
	FPutcLineBuffer // buffer
	OSPEB OSPEBs_StdOut + @ // filehandle
	OSFileWrite drop drop
end

fn OSPutc { c filehandle -- ok }
	auto small
	4 alloc small!

	c@ small@ sb

	0 // flags
	1 // length
	small@ // buffer
	filehandle@ // filehandle
	OSFileWrite ok! drop
end

fn FPutc { fd c -- }
	if (fd@ OSNONE ==)
		c@ OSConsolePutCharacter drop
	end elseif (fd@ OSPEB OSPEBs_StdOut + @ ==)
		c@ FPutcLineBuffer FPutcLineBufferLength@ + sb
		
		1 FPutcLineBufferLength +=

		if (FPutcLineBufferLength@ LINEBUFFERMAX ==)
			OSFlushLine
		end elseif (c@ '\n' ==)
			OSFlushLine
		end
	end else
		c@ fd@ OSPutc drop
	end
end

fn Putc { c -- }
	OSPEB OSPEBs_StdOut + @
	c@
	FPutc
end

fn Puts { s -- }
	while (s@ gb)
		s@ gb Putc
		1 s +=
	end
end

fn OSReadline { s max -- eof }
	OSFlushLine

	auto bytes
	0 bytes!

	0 eof!

	auto ok
	OSWAIT_TIMEOUTINFINITE // timeout
	0 // flags
	max@ // length
	s@ // buffer
	OSPEB OSPEBs_StdIn + @ // filehandle
	OSFileRead ok! bytes!

	if (ok@)
		ok@ OSStatusGetName "OSReadline: %s\n" OSAbort
	end

	if (bytes@ 0 ==)
		1 eof!
		return
	end

	if (s@ bytes@ 1 - + gb '\n' ~=)
		0 s@ sb
		return
	end

	0 s@ bytes@ 1 - + sb
end

fn Gets { s max -- }
	auto eof
	1 eof!

	while (eof@)
		s@ max@ OSReadline eof!
	end
end