//
// Implements support for users.cfg parsing.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OSCalls.h"
#include "<ll>/OSDLL/OSMap.h"
#include "<ll>/OSDLL/OSAccess.h"
#include "<ll>/OSDLL/OSFile.h"
#include "<ll>/OSDLL/OSObject.h"
#include "<ll>/OSDLL/OSEnvironment.h"
#include "<ll>/OSDLL/OSStatus.h"
#include "<ll>/OSDLL/OSAlloc.h"
#include "<ll>/OSDLL/OSWait.h"
#include "<ll>/OSDLL/OSConfiguration.h"
#include "<ll>/OSDLL/OSUserInfo.h"

#include "OSDLL.h"

fn OSUserInfoOpen { -- userinfo ok }
	"/mintia/users.cfg" // path
	0 // create
	0 // write
	OSConfigFileLoad ok! userinfo!
end

fn OSUserInfoClose { userinfo -- }
	userinfo@ OSConfigFileFree
end

fn OSUserInfoUserGetByName { username userinfo -- userptr ok }
	auto iterator
	userinfo@ OSConfigGetSectionIterator ok! iterator!

	while (iterator@)
		iterator@ // iterator
		userinfo@ // config
		OSConfigNextSection ok! iterator! userptr!

		if (ok@)
			return
		end

		if (userptr@ OSConfigGetSectionName username@ strcmp)
			return
		end
	end

	STATUS_NO_SUCH_USER ok!
end

fn OSUserInfoUserGetByUID { uid userinfo -- userptr ok }
	auto iterator
	userinfo@ OSConfigGetSectionIterator ok! iterator!

	while (iterator@)
		iterator@ // iterator
		userinfo@ // config
		OSConfigNextSection ok! iterator! userptr!

		if (ok@)
			return
		end

		auto key
		"uid" // keyname
		userptr@ // section
		OSConfigGetKey ok! key!

		if (ok@ ~~)
			auto chkuid
			key@ OSConfigGetKeyValue chkuid!

			if (chkuid@ ~~)
				continue
			end

			chkuid@ atoi chkuid!

			if (chkuid@ uid@ ==)
				return
			end
		end
	end

	STATUS_NO_SUCH_USER ok!
end

fn OSUserInfoUserGetName { userptr -- username }
	userptr@ OSConfigGetSectionName username!
end

fn OSUserInfoUserGetProperty { property userptr -- value ok }
	auto key
	property@ // keyname
	userptr@ // section
	OSConfigGetKey ok! key!

	if (ok@)
		return
	end

	key@ OSConfigGetKeyValue value!
end

// groups

fn OSGroupInfoOpen { -- groupinfo ok }
	"/mintia/groups.cfg" // path
	0 // create
	0 // write
	OSConfigFileLoad ok! groupinfo!
end

fn OSGroupInfoClose { groupinfo -- }
	groupinfo@ OSConfigFileFree
end

fn OSGroupInfoGroupGetByName { groupname groupinfo -- groupptr ok }
	auto iterator
	groupinfo@ OSConfigGetSectionIterator ok! iterator!

	while (iterator@)
		iterator@ // iterator
		groupinfo@ // config
		OSConfigNextSection ok! iterator! groupptr!

		if (ok@)
			return
		end

		if (groupptr@ OSConfigGetSectionName groupname@ strcmp)
			return
		end
	end

	STATUS_NO_SUCH_GROUP ok!
end

fn OSGroupInfoGroupGetByGID { gid groupinfo -- groupptr ok }
	auto iterator
	groupinfo@ OSConfigGetSectionIterator ok! iterator!

	while (iterator@)
		iterator@ // iterator
		groupinfo@ // config
		OSConfigNextSection ok! iterator! groupptr!

		if (ok@)
			return
		end

		auto key
		"gid" // keyname
		groupptr@ // section
		OSConfigGetKey ok! key!

		if (ok@ ~~)
			auto chkgid
			key@ OSConfigGetKeyValue chkgid!

			if (chkgid@ ~~)
				continue
			end

			chkgid@ atoi chkgid!

			if (chkgid@ gid@ ==)
				return
			end
		end
	end

	STATUS_NO_SUCH_GROUP ok!
end

fn OSGroupInfoGroupGetName { groupptr -- groupname }
	groupptr@ OSConfigGetSectionName groupname!
end

fn OSGroupInfoGroupGetProperty { property groupptr -- value ok }
	auto key
	property@ // keyname
	groupptr@ // section
	OSConfigGetKey ok! key!

	if (ok@)
		return
	end

	key@ OSConfigGetKeyValue value!
end