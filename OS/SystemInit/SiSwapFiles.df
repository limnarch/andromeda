//
// Implements the creation of the initial system swapfiles.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "SystemInit.h"

fn SiSetSwapFile { section -- }
	auto ok
	auto key
	auto path
	auto maxsize
	auto minsize
	auto priority

	"name" section@ OSConfigGetKey ok! key!

	if (ok@)
		"* Bad swapfile specification: no path\n" Printf
		return
	end

	key@ OSConfigGetKeyValue path!

	"minsize" section@ OSConfigGetKey ok! key!

	if (ok@)
		"* Bad swapfile specification: no minsize\n" Printf
		return
	end

	key@ OSConfigGetKeyValue atoi minsize!

	"maxsize" section@ OSConfigGetKey ok! key!

	if (ok@)
		"* Bad swapfile specification: no maxsize\n" Printf
		return
	end

	key@ OSConfigGetKeyValue atoi maxsize!

	"priority" section@ OSConfigGetKey ok! key!

	if (ok@)
		"* Bad swapfile specification: no priority\n" Printf
		return
	end

	key@ OSConfigGetKeyValue atoi priority!

	minsize@ OSPAGESHIFT >> // minpages
	maxsize@ OSPAGESHIFT >> // maxpages
	priority@ // pri
	path@ // path
	OSSwapFileCreate ok!

	if (ok@)
		ok@ OSStatusGetName path@ "* Failed to create %s: %s\n" Printf
		return
	end

	1 SiSwapFileCount +=
end

fn SiSetSwapFiles { -- }
	auto config
	SiConfigFile@ config!

	auto ok
	auto iterator

	config@ OSConfigGetSectionIterator ok! iterator!

	while (1)
		auto section
		iterator@ config@ OSConfigNextSection ok! iterator! section!

		if (ok@)
			break
		end

		if (section@ OSConfigGetSectionName "SwapFile" strcmp)
			section@ SiSetSwapFile
		end
	end

	OSFlushModifiedPages drop

	0 ok!
end