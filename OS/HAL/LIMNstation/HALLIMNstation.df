//
// Initialization of the LIMNstation HAL.
//

#include "<ll>/rta3x/a3x.h"
#include "<df>/dragonfruit.h"
#include "../../Loader/LoaderGlobal.h"

#include "<inc>/HALDriver.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALCrash.h"

#include "<ll>/OSDLL/OS.h"

extern HALMain { ldrinfo -- ret }

externptr HALLoaderInfo

// gluing with rta3x
fn Main { ldrinfo args -- ret }
	ldrinfo@ HALMain ret!
end

var InfoTotalRAM 0

var HALPlatformKernelPageDirectory 0
public HALPlatformKernelPageDirectory

var HALLIMNstationDKSBuffer 0
public HALLIMNstationDKSBuffer

buffer HALLIMNstationLocalInfo LoaderInfoPlatform_SIZEOF

fn HALPlatformInfo { -- }
	// we're supposed to print platform info in this function

	InfoTotalRAM@ 1048576 / HALLoaderInfo@ LoaderInfo_PlatformInfo + @ LoaderInfoPlatform_PlatformModel + @ "%s (%dMB) " Printf
	HALLoaderInfo@ LoaderInfo_PlatformInfo + @ LoaderInfoPlatform_BusModel + @ "%s " Printf
	HALLoaderInfo@ LoaderInfo_PlatformInfo + @ LoaderInfoPlatform_CPUModel + @ "%s " Printf

	HALCPUModel // dest
	HALLoaderInfo@ LoaderInfo_PlatformInfo + @ LoaderInfoPlatform_CPUModel + @ // src
	strcpy

	HALPlatformModel // dest
	HALLoaderInfo@ LoaderInfo_PlatformInfo + @ LoaderInfoPlatform_PlatformModel + @ // src
	strcpy
end

extern HALLIMNstationCitronInit { ldrinfo -- }

extern HALLIMNstationLSICInit { ldrinfo -- }

extern HALLIMNstationAmtsuInit { ldrinfo -- }

fn HALPlatformInit { ldrinfo -- ret }
	0 ret!

	HALLIMNstationLocalInfo // dest
	ldrinfo@ LoaderInfo_PlatformInfo + @ // src
	LoaderInfoPlatform_SIZEOF // size
	memcpy

	HALLIMNstationLocalInfo ldrinfo@ LoaderInfo_PlatformInfo + !

	ldrinfo@ LoaderInfo_TotalRAM + @ InfoTotalRAM!

	HALPlatformInfo

	ldrinfo@ HALLIMNstationCitronInit

	ldrinfo@ HALLIMNstationLSICInit

	ldrinfo@ HALLIMNstationAmtsuInit

	ldrinfo@ LoaderInfo_PlatformInfo + @ LoaderInfoPlatform_DKSBuffer + @ HALLIMNstationDKSBuffer!
end

fn HALPlatformCrash { -- }
	HALPlatformMapKernelSwitch drop drop
end

extern HALLimn2500Exit { code sp -- }

extern HALLimn2500Reset { -- }

extern HALCPUExit { -- }

fn HALPlatformExit { ret mode -- }
	// MUST BE CALLED WITH INTERRUPTS DISABLED

	// HALPlatformMapKernelSwitch drop drop

	// HALCPUExit

	if (mode@ OSSHUTDOWN_HALT ==)
		// ret@ HALLoaderInfo@ LoaderInfo_ReturnSP + @ HALLimn2500Exit
	end elseif (mode@ OSSHUTDOWN_REBOOT ==)
		HALLimn2500Reset
	end else
		"?" Printf

		HALLimn2500Reset
	end
end