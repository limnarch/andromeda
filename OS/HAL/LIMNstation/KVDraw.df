#include "<ll>/rta3x/a3x.h"
#include "<df>/dragonfruit.h"
#include "../../Loader/LoaderGlobal.h"

#include "<inc>/HALConsole.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALResource.h"

externptr HALLoaderInfo

var FBAddr 0
var FBSize 0
var FBModulo 0

fn KVInit { -- }
	if (HALLoaderInfo@ LoaderInfo_HALConsoleFBFormat + @ BOOTFBFORMAT_AB5G5R5 ~=)
		0 FBAddr!
		return
	end

	HALLoaderInfo@ LoaderInfo_HALConsoleFBAddr + @ FBAddr!
	HALConsoleWidthPix@ HALConsoleHeightPix@ * 1 << FBSize!
	HALConsoleWidthPix@ HALConsoleFontWidth@ - 1 << FBModulo!
end

fn KVQuery { -- w h ok }
	if (FBAddr@ ~~)
		-1 ok!
		return
	end

	0 ok!

	HALConsoleWidthPix@ w!
	HALConsoleHeightPix@ h!
end

fn KVFontGet { name -- font ok }
	auto rsrc
	name@ HALResourceByName rsrc!

	if (rsrc@ ~~)
		-1 ok!
		return
	end

	rsrc@ BootResource_Data + @ font!

	if (font@ HCFHeader_Magic + @ HCFMAGIC ~=)
		-1 ok!
		return
	end

	0 ok!

	rsrc@ HALResourceWire
end

fn KVFontQuery { font -- width height }
	font@ HCFHeader_Width + @ width!
	font@ HCFHeader_Height + @ height!
end

fn KVRGB24ToNative { rgb24a -- nativecolor }
	// convert to 15 bit BGR

	rgb24a@ 16 >> 0xFF & 3 >> nativecolor!
	rgb24a@ 8 >> 0xFF & 3 >> 5 << nativecolor |=
	rgb24a@ 0xFF & 3 >> 10 << nativecolor |=
end

fn KVClearScreen { nativecolor -- }
	auto c32
	nativecolor@ 16 << nativecolor@ | c32!

	FBAddr@ FBSize@ c32@ memset
end

fn KVDrawFilledRectangle { nativecolor x y w h -- }
	auto row
	0 row!

	auto v
	nativecolor@ 16 << nativecolor@ | v!

	auto gw
	HALConsoleWidthPix@ gw!

	auto fb
	FBAddr@ fb!

	auto ptr
	y@ gw@ * 1 << x@ 1 << + fb@ + ptr!

	auto mod
	gw@ 1 << mod!

	auto rowsz
	w@ 1 << rowsz!

	while (row@ h@ <)
		ptr@ rowsz@ v@ memset

		1 row +=
		mod@ ptr +=
	end
end

fn KVDrawEmptyRectangle { nativecolor x y w h -- }
	auto gw
	HALConsoleWidthPix@ gw!

	auto mod
	gw@ 1 << mod!

	auto v
	nativecolor@ 16 << nativecolor@ | v!

	auto kptr
	y@ gw@ * 1 << x@ 1 << + FBAddr@ + kptr!

	auto rowsz
	w@ 1 << rowsz!

	// top edge
	kptr@ rowsz@ v@ memset

	auto bptr
	h@ gw@ * 1 << kptr@ + bptr!

	// bottom edge
	bptr@ rowsz@ 2 + v@ memset

	auto ptr
	kptr@ ptr!

	auto hk
	h@ hk!

	// left edge
	while (hk@)
		nativecolor@ ptr@ si

		mod@ ptr +=
		1 hk -=
	end

	kptr@ w@ 1 << + ptr!

	h@ hk!

	// right edge
	while (hk@)
		nativecolor@ ptr@ si

		mod@ ptr +=
		1 hk -=
	end
end

fn KVDrawCharacter { char nativefg nativebg x y font -- }
	auto row
	0 row!

	auto gw
	HALConsoleWidthPix@ gw!

	auto fb
	FBAddr@ fb!

	auto w
	font@ HCFHeader_Width + @ w!

	auto h
	font@ HCFHeader_Height + @ h!

	auto dest
	y@ gw@ * 1 << x@ 1 << + fb@ + dest!

	auto mod
	gw@ w@ - 1 << mod!

	auto bmp
	w@ 7 + 3 >> h@ * char@ * font@ HCFHeader_SIZEOF + + bmp!

	auto bitd
	font@ HCFHeader_Flags + @ HCFFLAG_REVERSE & ~~ bitd!

	if (nativefg@ -1 ==)
		if (nativebg@ -1 ==)
			return
		end

		if (bitd@)
			nativebg@ bmp@ dest@ mod@ w@ h@ KVBlitBitsFastBackwardsBG
		end else
			nativebg@ bmp@ dest@ mod@ w@ h@ KVBlitBitsFastBG
		end
	end elseif (nativebg@ -1 ==)
		if (bitd@)
			nativefg@ bmp@ dest@ mod@ w@ h@ KVBlitBitsFastBackwardsFG
		end else
			nativefg@ bmp@ dest@ mod@ w@ h@ KVBlitBitsFastFG
		end
	end else
		if (bitd@)
			nativefg@ nativebg@ bmp@ dest@ mod@ w@ h@ KVBlitBitsFastBackwardsFGBG
		end else
			nativefg@ nativebg@ bmp@ dest@ mod@ w@ h@ KVBlitBitsFastFGBG
		end
	end
end

// mountains of redundant functions for various bit blit ops

fn KVBlitBitsFastFG { fg ptr dest mod w h -- }
	auto j
	auto byte

	auto left

	while (h@)
		w@ 3 >> left!
		while (left@)
			ptr@ gb byte!

			if (byte@)
				if (byte@ 128 &)
					fg@ dest@ 14 + si
				end

				if (byte@ 64 &)
					fg@ dest@ 12 + si
				end

				if (byte@ 32 &)
					fg@ dest@ 10 + si
				end

				if (byte@ 16 &)
					fg@ dest@ 8 + si
				end

				if (byte@ 8 &)
					fg@ dest@ 6 + si
				end

				if (byte@ 4 &)
					fg@ dest@ 4 + si
				end

				if (byte@ 2 &)
					fg@ dest@ 2 + si
				end

				if (byte@ 1 &)
					fg@ dest@ si
				end
			end

			16 dest +=
			1 left -=
			1 ptr +=
		end

		w@ 7 & left!
		if (left@)
			ptr@ gb byte!
			1 ptr +=
			1 j!

			while (left@)
				if (byte@ j@ &)
					fg@ dest@ si
				end

				2 dest +=
				1 j <<=
				1 left -=
			end
		end

		mod@ dest +=
		1 h -=
	end
end

fn KVBlitBitsFastBG { bg ptr dest mod w h -- }
	auto j
	auto byte

	auto left

	while (h@)
		w@ 3 >> left!
		while (left@)
			ptr@ gb byte!

			if (byte@ ~~)
				bg@ dest@ si
				bg@ dest@ 2 + si
				bg@ dest@ 4 + si
				bg@ dest@ 6 + si
				bg@ dest@ 8 + si
				bg@ dest@ 10 + si
				bg@ dest@ 12 + si
				bg@ dest@ 14 + si
			end else
				if (byte@ 128 & ~~)
					bg@ dest@ 14 + si
				end

				if (byte@ 64 & ~~)
					bg@ dest@ 12 + si
				end

				if (byte@ 32 & ~~)
					bg@ dest@ 10 + si
				end

				if (byte@ 16 & ~~)
					bg@ dest@ 8 + si
				end

				if (byte@ 8 & ~~)
					bg@ dest@ 6 + si
				end

				if (byte@ 4 & ~~)
					bg@ dest@ 4 + si
				end

				if (byte@ 2 & ~~)
					bg@ dest@ 2 + si
				end

				if (byte@ 1 & ~~)
					bg@ dest@ si
				end
			end

			16 dest +=
			1 left -=
			1 ptr +=
		end

		w@ 7 & left!
		if (left@)
			ptr@ gb byte!
			1 ptr +=
			1 j!

			while (left@)
				if (byte@ j@ & ~~)
					bg@ dest@ si
				end

				2 dest +=
				1 j <<=
				1 left -=
			end
		end

		mod@ dest +=
		1 h -=
	end
end

fn KVBlitBitsFastFGBG { fg bg ptr dest mod w h -- }
	auto j
	auto byte

	auto left

	while (h@)
		w@ 3 >> left!
		while (left@)
			ptr@ gb byte!

			if (byte@ ~~)
				bg@ dest@ si
				bg@ dest@ 2 + si
				bg@ dest@ 4 + si
				bg@ dest@ 6 + si
				bg@ dest@ 8 + si
				bg@ dest@ 10 + si
				bg@ dest@ 12 + si
				bg@ dest@ 14 + si
			end else
				if (byte@ 128 &)
					fg@ dest@ 14 + si
				end else
					bg@ dest@ 14 + si
				end

				if (byte@ 64 &)
					fg@ dest@ 12 + si
				end else
					bg@ dest@ 12 + si
				end

				if (byte@ 32 &)
					fg@ dest@ 10 + si
				end else
					bg@ dest@ 10 + si
				end

				if (byte@ 16 &)
					fg@ dest@ 8 + si
				end else
					bg@ dest@ 8 + si
				end

				if (byte@ 8 &)
					fg@ dest@ 6 + si
				end else
					bg@ dest@ 6 + si
				end

				if (byte@ 4 &)
					fg@ dest@ 4 + si
				end else
					bg@ dest@ 4 + si
				end

				if (byte@ 2 &)
					fg@ dest@ 2 + si
				end else
					bg@ dest@ 2 + si
				end

				if (byte@ 1 &)
					fg@ dest@ si
				end else
					bg@ dest@ si
				end
			end

			16 dest +=
			1 left -=
			1 ptr +=
		end

		w@ 7 & left!
		if (left@)
			ptr@ gb byte!
			1 ptr +=
			1 j!

			while (left@)
				if (byte@ j@ &)
					fg@ dest@ si
				end else
					bg@ dest@ si
				end

				2 dest +=
				1 j <<=
				1 left -=
			end
		end

		mod@ dest +=
		1 h -=
	end
end

// reversed ones

fn KVBlitBitsFastBackwardsFG { fg ptr dest mod w h -- }
	auto j
	auto byte

	auto left

	while (h@)
		w@ 3 >> left!
		while (left@)
			ptr@ gb byte!

			if (byte@)
				if (byte@ 128 &)
					fg@ dest@ si
				end

				if (byte@ 64 &)
					fg@ dest@ 2 + si
				end

				if (byte@ 32 &)
					fg@ dest@ 4 + si
				end

				if (byte@ 16 &)
					fg@ dest@ 6 + si
				end

				if (byte@ 8 &)
					fg@ dest@ 8 + si
				end

				if (byte@ 4 &)
					fg@ dest@ 10 + si
				end

				if (byte@ 2 &)
					fg@ dest@ 12 + si
				end

				if (byte@ 1 &)
					fg@ dest@ 14 + si
				end
			end

			16 dest +=
			1 left -=
			1 ptr +=
		end

		w@ 7 & left!
		if (left@)
			ptr@ gb byte!
			1 ptr +=

			if (w@ 3 >>)
				128 j!
			end else
				1 left@ << j!
			end

			while (left@)
				if (byte@ j@ &)
					fg@ dest@ si
				end

				2 dest +=
				1 j >>=
				1 left -=
			end
		end

		mod@ dest +=
		1 h -=
	end
end

fn KVBlitBitsFastBackwardsBG { bg ptr dest mod w h -- }
	auto j
	auto byte

	auto left

	while (h@)
		w@ 3 >> left!
		while (left@)
			ptr@ gb byte!

			if (byte@ ~~)
				bg@ dest@ si
				bg@ dest@ 2 + si
				bg@ dest@ 4 + si
				bg@ dest@ 6 + si
				bg@ dest@ 8 + si
				bg@ dest@ 10 + si
				bg@ dest@ 12 + si
				bg@ dest@ 14 + si
			end else
				if (byte@ 128 & ~~)
					bg@ dest@ si
				end

				if (byte@ 64 & ~~)
					bg@ dest@ 2 + si
				end

				if (byte@ 32 & ~~)
					bg@ dest@ 4 + si
				end

				if (byte@ 16 & ~~)
					bg@ dest@ 6 + si
				end

				if (byte@ 8 & ~~)
					bg@ dest@ 8 + si
				end

				if (byte@ 4 & ~~)
					bg@ dest@ 10 + si
				end

				if (byte@ 2 & ~~)
					bg@ dest@ 12 + si
				end

				if (byte@ 1 & ~~)
					bg@ dest@ 14 + si
				end
			end

			16 dest +=
			1 left -=
			1 ptr +=
		end

		w@ 7 & left!
		if (left@)
			ptr@ gb byte!
			1 ptr +=

			if (w@ 3 >>)
				128 j!
			end else
				1 left@ << j!
			end

			while (left@)
				if (byte@ j@ & ~~)
					bg@ dest@ si
				end

				2 dest +=
				1 j >>=
				1 left -=
			end
		end

		mod@ dest +=
		1 h -=
	end
end

fn KVBlitBitsFastBackwardsFGBG { fg bg ptr dest mod w h -- }
	auto j
	auto byte

	auto left

	while (h@)
		w@ 3 >> left!
		while (left@)
			ptr@ gb byte!

			if (byte@ ~~)
				bg@ dest@ si
				bg@ dest@ 2 + si
				bg@ dest@ 4 + si
				bg@ dest@ 6 + si
				bg@ dest@ 8 + si
				bg@ dest@ 10 + si
				bg@ dest@ 12 + si
				bg@ dest@ 14 + si
			end else
				if (byte@ 128 &)
					fg@ dest@ si
				end else
					bg@ dest@ si
				end

				if (byte@ 64 &)
					fg@ dest@ 2 + si
				end else
					bg@ dest@ 2 + si
				end

				if (byte@ 32 &)
					fg@ dest@ 4 + si
				end else
					bg@ dest@ 4 + si
				end

				if (byte@ 16 &)
					fg@ dest@ 6 + si
				end else
					bg@ dest@ 6 + si
				end

				if (byte@ 8 &)
					fg@ dest@ 8 + si
				end else
					bg@ dest@ 8 + si
				end

				if (byte@ 4 &)
					fg@ dest@ 10 + si
				end else
					bg@ dest@ 10 + si
				end

				if (byte@ 2 &)
					fg@ dest@ 12 + si
				end else
					bg@ dest@ 12 + si
				end

				if (byte@ 1 &)
					fg@ dest@ 14 + si
				end else
					bg@ dest@ 14 + si
				end
			end

			16 dest +=
			1 left -=
			1 ptr +=
		end

		w@ 7 & left!
		if (left@)
			ptr@ gb byte!
			1 ptr +=

			if (w@ 3 >>)
				128 j!
			end else
				1 left@ << j!
			end

			while (left@)
				if (byte@ j@ &)
					fg@ dest@ si
				end else
					bg@ dest@ si
				end

				2 dest +=
				1 j >>=
				1 left -=
			end
		end

		mod@ dest +=
		1 h -=
	end
end