//
// Implements KDebug stack tracing.
//

#include "<df>/dragonfruit.h"

#include "../../OSLoader/OSLoaderGlobal.h"

#include "<inc>/HALConsole.h"
#include "<inc>/HALMap.h"
#include "<inc>/DebugTrace.h"
#include "<inc>/DebugSymbols.h"

#include "<inc>/Kernel.h"
#include "<inc>/Memory.h"

externptr HALLoaderInfo

var DebugLink 0

asm "

GetLink:
	mov  [DebugLink], fp
	ret

"

extern GetLink { -- }

fn DebugDump { rows cols -- }
	GetLink

	auto link
	DebugLink@ link!

	pointerof DebugDump link@ rows@ cols@ DebugTrace
end

buffer TraceNameBuffer 128

fn DebugTrace { pc link rows cols -- }
	"\n" Printf

	1 rows -=

	auto infostr
	"InstrPtr Name                                     " infostr!

	auto infocols
	cols@ infostr@ strlen / infocols!

	auto i
	infocols@ i!

	while (i@)
		infostr@ Printf
		1 i -=
	end

	"\n" Printf

	1 rows -=

	auto links
	0 links!

	if (link@ 0 ==)
		"zero link!\n" Printf
		return
	end

	auto kdir
	HALPlatformKernelPageDirectory@ kdir!

	while (rows@)
		infocols@ i!

		while (i@)
			if (links@ 128 >=)
				" maxtrace!\n" Printf
				return
			end

			auto symname
			auto symbase
			auto dll

			auto ok
			pc@ DebugGetSymDLL ok! drop symname! symbase! dll!

			if (ok@)
				TraceNameBuffer "UNKNOWN" strcpy
			end else
				if (symname@ ~~)
					"NAMELESS" symname!
				end

				TraceNameBuffer	dll@ ComDLL_Name + @ strcpy
				TraceNameBuffer TraceNameBuffer strlen + "!" strcpy
				TraceNameBuffer TraceNameBuffer strlen + symname@ strcpy
			end

			TraceNameBuffer
			pc@
			"%08x %41s" Printf

			if (link@ ~~)
				break
			end

			if (link@ kdir@ MmVirtualAddressIsValid ~~)
				" #\n" Printf
				0 link!
				break
			end

			if (link@ 4 + kdir@ MmVirtualAddressIsValid ~~)
				" #\n" Printf
				0 link!
				break
			end

			link@ 4 + @ pc!
			link@@ link!

			1 i -=
			1 links +=
		end

		"\n" Printf

		if (link@ ~~)
			break
		end

		1 rows -=
	end
end