//
// Implements the API entrypoints for the MINTIA Command Language library.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"
#include "<ll>/MCL/Mcl.h"

#include "Mcl.h"

fn (OSModuleMainFunction) DLLMain { -- ok }
	MclpLexInit

	0 ok!
end

fn MclMachineCreate { -- machine ok }
	MclpMachine_SIZEOF OSHeapAllocate ok! machine!

	if (ok@)
		return
	end

	"MclpMachineMutex" // name
	machine@ MclpMachine_Mutex + // fastmutex
	OSFastMutexInitialize

	machine@ MclpMachine_SymbolTable + // symboltable
	MclpSymbolTableInitialize

	0 machine@ MclpMachine_HistoryCount + !
	0 machine@ MclpMachine_HistoryListHead + !
	0 machine@ MclpMachine_HistoryListTail + !
end

fn MclMachineFree { machine -- }
	machine@ MclpMachine_Mutex + OSFastMutexDelete

	machine@ OSHeapFree
end

fn MclMachineParseFile { filename interactive streamhandle machine -- rootblock ok }
	filename@ // filename
	interactive@ // interactive
	streamhandle@ // streamhandle
	machine@ // machine
	MclpParseFile ok! rootblock!
end

fn MclParseSubtreeFree { node -- }
	node@ MclpParseSubtreeFree
end

fn MclpMachineLock { machine -- }
	machine@ MclpMachine_Mutex + OSFastMutexAcquire
end

fn MclpMachineUnlock { machine -- }
	machine@ MclpMachine_Mutex + OSFastMutexRelease
end