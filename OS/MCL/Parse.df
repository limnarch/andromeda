//
// Implements the parser for the MINTIA Command Language.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"
#include "<ll>/MCL/Mcl.h"

#include "Mcl.h"

fn MclpParseDiagnostic { ... fmt node ctx -- }
	auto linenum
	auto fname

	if (node@)
		node@ MclpParseNode_LineNumber + @ linenum!
		node@ MclpParseNode_FileName + @ fname!
	end else
		ctx@ MclpParseContext_LexLineNumber + @ linenum!
		ctx@ MclpParseContext_FileName + @ fname!
	end

	linenum@
	fname@
	"%s:%d: " OSGetStdErr FPrintf

	argv // argvt
	argc@ // argcn
	fmt@ // fmt
	OSGetStdErr // fd
	VFPrintf
end

fn MclpParseContextInitialize { ctx -- }
	ctx@ // ptr
	MclpParseContext_SIZEOF // sz
	0 // word
	memset

	LEX_NEWSTATEMENT ctx@ MclpParseContext_LexFlags + !
end

fn MclpParseNodeCreate { type size ctx -- node ok }
	size@ OSHeapAllocate ok! node!

	if (ok@)
		return
	end

	ctx@ MclpParseContext_LexLineNumber + @ node@ MclpParseNode_LineNumber + !
	ctx@ MclpParseContext_FileName + @ node@ MclpParseNode_FileName + !

	type@ node@ MclpParseNode_Type + !
	0 node@ MclpParseNode_Next + !
end

fn MclpParseContextUninitialize { ctx -- }
	if (ctx@ MclpParseContext_LexBuffer + @)
		if (ctx@ MclpParseContext_LexFlags + @ LEX_INTERACTIVE &)
			// free buffer

			ctx@ MclpParseContext_LexBuffer + @ OSHeapFree
		end else
			// unmap buffer

			ctx@ MclpParseContext_LexInputLength + @ // length
			ctx@ MclpParseContext_LexBuffer + @ // vaddr
			OSCURRENTPROCESS // processhandle
			OSUnmapView drop
		end
	end
end

fn MclpQueryFileLength { ctx -- ok }
	auto query
	OSFileInformation_SIZEOF alloc query!

	ctx@ MclpParseContext_StreamHandle + @ OSStreamGetFileHandle // filehandle
	query@ // query
	OSFileQuery ok!

	if (ok@)
		return
	end

	query@ OSFileInformation_Size + @ ctx@ MclpParseContext_LexInputLength + !
end

fn MclpParseFile { filename interactive streamhandle machine -- rootblock ok }
	auto ctx
	MclpParseContext_SIZEOF alloc ctx!

	ctx@ MclpParseContextInitialize

	if (interactive@)
		LEX_INTERACTIVE ctx@ MclpParseContext_LexFlags + |=
	end

	if (filename@)
		filename@ ctx@ MclpParseContext_FileName + !
	end else
		"????" ctx@ MclpParseContext_FileName + !
	end

	streamhandle@ ctx@ MclpParseContext_StreamHandle + !

	0 ctx@ MclpParseContext_LexBuffer + !
	0 ctx@ MclpParseContext_LexLastOffset + !
	0 ctx@ MclpParseContext_LexOffset + !
	0 ctx@ MclpParseContext_LexValidLength + !
	1 ctx@ MclpParseContext_LexLineNumber + !

	machine@ ctx@ MclpParseContext_Machine + !

	if (interactive@)
		// endless input (at least until EOF)
		-1 ctx@ MclpParseContext_LexInputLength + !
	end else
		ctx@ MclpQueryFileLength ok!

		if (ok@)
			return
		end
	end

	0 // endtoken
	ctx@ // ctx
	MclpParseBlock ok! rootblock!

	ctx@ MclpParseContextUninitialize
end

fn MclpParseSubtreeFree { node -- }
	if (node@ ~~)
		return
	end

	auto subnode
	auto nsubnode

	if (node@ MclpParseNode_Type + @ PARSENODE_BLOCK ==)
		node@ MclpParseNodeBlock_NodeListHead + @ subnode!

		while (subnode@)
			subnode@ MclpParseNode_Next + @ nsubnode!

			subnode@ MclpParseSubtreeFree

			nsubnode@ subnode!
		end
	end elseif (node@ MclpParseNode_Type + @ PARSENODE_PIPELINE ==)
		node@ MclpParseNodePipeline_CmdListHead + @ subnode!

		while (subnode@)
			subnode@ MclpParseNode_Next + @ nsubnode!

			subnode@ MclpParseSubtreeFree

			nsubnode@ subnode!
		end

		node@ MclpParseNodePipeline_StdInRedirectionPath + @ MclpParseSubtreeFree
		node@ MclpParseNodePipeline_StdOutRedirectionPath + @ MclpParseSubtreeFree
		node@ MclpParseNodePipeline_StdErrRedirectionPath + @ MclpParseSubtreeFree
	end elseif (node@ MclpParseNode_Type + @ PARSENODE_COMMAND ==)
		node@ MclpParseNodeCommand_ArgListHead + @ subnode!

		while (subnode@)
			subnode@ MclpParseNode_Next + @ nsubnode!

			subnode@ MclpParseSubtreeFree

			nsubnode@ subnode!
		end

		node@ MclpParseNodeCommand_Name + @ MclpParseSubtreeFree
	end elseif (node@ MclpParseNode_Type + @ PARSENODE_FUNCCALL ==)
		node@ MclpParseNodeFunctionCall_ArgListHead + @ subnode!

		while (subnode@)
			subnode@ MclpParseNode_Next + @ nsubnode!

			subnode@ MclpParseSubtreeFree

			nsubnode@ subnode!
		end

		node@ MclpParseNodeFunctionCall_Name + @ MclpParseSubtreeFree
	end elseif (node@ MclpParseNode_Type + @ PARSENODE_COND ==
				node@ MclpParseNode_Type + @ PARSENODE_WHILE == ||)

		node@ MclpParseNodeConditionalBlock_Conditional + @ MclpParseSubtreeFree

		node@ MclpParseNodeConditionalBlock_Body + @ MclpParseSubtreeFree
	end elseif (node@ MclpParseNode_Type + @ PARSENODE_IF ==)
		node@ MclpParseNodeIf_CondListHead + @ subnode!

		while (subnode@)
			subnode@ MclpParseNode_Next + @ nsubnode!

			subnode@ MclpParseSubtreeFree

			nsubnode@ subnode!
		end

		node@ MclpParseNodeIf_ElseBody + @ MclpParseSubtreeFree
	end elseif (node@ MclpParseNode_Type + @ PARSENODE_FUNCDEF ==)
		node@ MclpParseNodeFuncDef_Body + @ MclpParseSubtreeFree
		node@ MclpParseNodeFuncDef_Name + @ MclpParseSubtreeFree

		node@ MclpParseNodeFuncDef_ArgNameListHead + @ subnode!

		while (subnode@)
			subnode@ MclpParseNode_Next + @ nsubnode!

			subnode@ MclpParseSubtreeFree

			nsubnode@ subnode!
		end
	end elseif (node@ MclpParseNode_Type + @ PARSENODE_VARSET ==)
		node@ MclpParseNodeVarSet_NewValue + @ MclpParseSubtreeFree
		node@ MclpParseNodeVarSet_Name + @ MclpParseSubtreeFree
	end elseif (node@ MclpParseNode_Type + @ PARSENODE_VARREF ==)
		node@ MclpParseNodeVarRef_Name + @ MclpParseSubtreeFree
	end

	node@ OSHeapFree
end

fn MclpParseBlock { endtoken ctx -- block ok }
	PARSENODE_BLOCK // type
	MclpParseNodeBlock_SIZEOF // size
	ctx@ // ctx
	MclpParseNodeCreate ok! block!

	if (ok@)
		return
	end

	endtoken@ ~~ block@ MclpParseNodeBlock_IsRootBlock + !

	auto nodes
	0 nodes!

	1 ctx@ MclpParseContext_Depth + +=

	auto hp
	auto tp

	block@ MclpParseNodeBlock_NodeListHead + hp!
	block@ MclpParseNodeBlock_NodeListTail + tp!

	0 hp@!
	0 tp@!

	auto tokbuf
	ctx@ MclpParseContext_TokBuffer + tokbuf!

	while (1)
		auto tokflag
		auto toklen
		0 // peek
		tokbuf@ // tokbuf
		ctx@ // ctx
		MclpLexNextToken ok! toklen! tokflag!

		if (ok@)
			if (endtoken@ ~~)
				if (ok@ STATUS_END_OF_FILE ==)
					0 ok!
				end
			end

			if (ok@)
				block@ MclpParseSubtreeFree
			end

			break
		end

		if (toklen@ ~~ tokflag@ LEXTOK_LITERAL & ~~ &&)
			if (endtoken@ ~~ ctx@ MclpParseContext_LexFlags + @ LEX_INTERACTIVE & &&)
				break
			end else
				continue
			end
		end

		if (endtoken@ tokflag@ LEXTOK_LITERAL & ~~ &&)
			if (tokbuf@ endtoken@ strcmp)
				break
			end
		end

		auto node
		0 node!

		0 ok!

		if (tokbuf@ "!" strcmp tokflag@ LEXTOK_LITERAL & ~~ &&)
			ctx@ MclpParseDirective ok! node!
		end elseif (tokbuf@ "{" strcmp tokflag@ LEXTOK_LITERAL & ~~ &&)
			"}" // endtoken
			ctx@ // ctx
			MclpParseBlock ok! node!
		end else
			// command statement

			ctx@ MclpLexLastToken

			endtoken@ // endtoken
			ctx@ // ctx
			MclpParsePipeline ok! node!
		end

		if (ok@)
			block@ MclpParseSubtreeFree

			break
		end

		if (node@)
			// add to block list

			if (hp@@ ~~)
				node@ hp@!
				node@ tp@!
			end else
				node@ tp@@ MclpParseNode_Next + !
				node@ tp@!
			end

			1 nodes +=
		end

		if (endtoken@ ~~ ctx@ MclpParseContext_LexFlags + @ LEX_INTERACTIVE & &&)
			break
		end
	end

	1 ctx@ MclpParseContext_Depth + -=

	if (ok@ ~~)
		if (nodes@ ~~)
			block@ MclpParseSubtreeFree
			0 block!
		end
	end
end

fn MclpParseDirective { ctx -- node ok }
	0 node!

	auto tokbuf
	ctx@ MclpParseContext_TokBuffer + tokbuf!

	1 ctx@ MclpParseContext_Depth + +=

	auto tokflag
	auto toklen

	0 // stopnl
	tokbuf@ // tokbuf
	ctx@ // ctx
	MclpLexNextNonemptyToken ok! toklen! tokflag!

	if (ok@)
		1 ctx@ MclpParseContext_Depth + -=

		return
	end

	if (tokbuf@ "IF" strcmp)
		ctx@ MclpParseIfStatement ok! node!
	end elseif (tokbuf@ "WHILE" strcmp)
		ctx@ MclpParseWhileStatement ok! node!
	end elseif (tokbuf@ "SET" strcmp)
		ctx@ MclpParseSetStatement ok! node!
	end elseif (tokbuf@ "FUNC" strcmp)
		ctx@ MclpParseFuncStatement ok! node!
	end elseif (tokbuf@ "RETURN" strcmp)
		ctx@ MclpParseReturnStatement ok! node!
	end elseif (tokbuf@ "BREAK" strcmp)
		ctx@ MclpParseBreakStatement ok! node!
	end elseif (tokbuf@ "CONTINUE" strcmp)
		ctx@ MclpParseContinueStatement ok! node!
	end elseif (tokbuf@ "EVAL" strcmp)
		ctx@ MclpParseEvalStatement ok! node!
	end else
		tokbuf@
		"unknown directive '%s'\n" // fmt
		0 // node
		ctx@ // ctx
		MclpParseDiagnostic

		STATUS_SYNTAX_ERROR ok!
	end

	1 ctx@ MclpParseContext_Depth + -=
end

fn MclpParseIfStatement { ctx -- node ok }
	// conditional and then a block
	// then if there is an elseif, another conditional and another block
	// then if there is an else, another block

	auto tokbuf
	ctx@ MclpParseContext_TokBuffer + tokbuf!

	PARSENODE_IF // type
	MclpParseNodeIf_SIZEOF // size
	ctx@ // ctx
	MclpParseNodeCreate ok! node!

	if (ok@)
		return
	end

	0 node@ MclpParseNodeIf_ElseBody + !

	auto hp
	auto tp

	node@ MclpParseNodeIf_CondListHead + hp!
	node@ MclpParseNodeIf_CondListTail + tp!

	0 hp@!
	0 tp@!

	while (1)
		auto condblock
		PARSENODE_COND // type
		ctx@ // ctx
		MclpParseConditionalBlock ok! condblock!

		if (ok@)
			node@ MclpParseSubtreeFree

			return
		end

		if (hp@@ ~~)
			condblock@ hp@!
			condblock@ tp@!
		end else
			condblock@ tp@@ MclpParseNode_Next + !
			condblock@ tp@!
		end

		auto tokflag
		auto toklen

		1 // stopnl
		tokbuf@ // tokbuf
		ctx@ // ctx
		MclpLexNextNonemptyToken ok! toklen! tokflag!

		if (ok@ STATUS_END_OF_FILE ==)
			0 ok!
		end else
			if (ok@)
				node@ MclpParseSubtreeFree

				return
			end

			if (toklen@)
				if (tokflag@ LEXTOK_LITERAL & ~~)
					if (tokbuf@ "ELSE" strcmp)
						auto body
						"END" // endtoken
						ctx@ // ctx
						MclpParseBlock ok! body!

						if (ok@)
							node@ MclpParseSubtreeFree

							return
						end

						body@ node@ MclpParseNodeIf_ElseBody + !
					end elseif (tokbuf@ "ELSEIF" strcmp)
						continue
					end
				end
			end

			ctx@ MclpLexLastToken
		end

		break
	end
end

fn MclpParseConditionalBlock { type ctx -- node ok }
	type@ // type
	MclpParseNodeConditionalBlock_SIZEOF // size
	ctx@ // ctx
	MclpParseNodeCreate ok! node!

	if (ok@)
		return
	end

	0 node@ MclpParseNodeConditionalBlock_Body + !
	0 node@ MclpParseNodeConditionalBlock_Conditional + !

	auto cond
	0 // endtoken
	ctx@ // ctx
	MclpParseExpression ok! cond! drop

	if (ok@)
		node@ MclpParseSubtreeFree

		return
	end

	cond@ node@ MclpParseNodeConditionalBlock_Conditional + !

	auto body
	"END" // endtoken
	ctx@ // ctx
	MclpParseBlock ok! body!

	if (ok@)
		node@ MclpParseSubtreeFree

		return
	end

	body@ node@ MclpParseNodeConditionalBlock_Body + !
end

fn MclpParseWhileStatement { ctx -- node ok }
	PARSENODE_WHILE // type
	ctx@ // ctx
	MclpParseConditionalBlock ok! node!
end

fn MclpParseSetStatement { ctx -- node ok }
	// parse set variable

	PARSENODE_VARSET // type
	MclpParseNodeVarSet_SIZEOF // size
	ctx@ // ctx
	MclpParseNodeCreate ok! node!

	if (ok@)
		return
	end

	0 node@ MclpParseNodeVarSet_Name + !
	0 node@ MclpParseNodeVarSet_NewValue + !

	auto name
	0 // endtoken
	ctx@ // ctx
	MclpParseExpression ok! name! drop

	if (ok@)
		node@ MclpParseSubtreeFree

		return
	end

	name@ node@ MclpParseNodeVarSet_Name + !

	auto newvalue
	0 // endtoken
	ctx@ // ctx
	MclpParseExpression ok! newvalue! drop

	if (ok@)
		node@ MclpParseSubtreeFree

		return
	end

	newvalue@ node@ MclpParseNodeVarSet_NewValue + !
end

fn MclpParseFuncStatement { ctx -- node ok }
	PARSENODE_FUNCDEF // type
	MclpParseNodeFuncDef_SIZEOF // size
	ctx@ // ctx
	MclpParseNodeCreate ok! node!

	if (ok@)
		return
	end

	0 node@ MclpParseNodeFuncDef_Name + !
	0 node@ MclpParseNodeFuncDef_Body + !

	auto hp
	auto tp

	node@ MclpParseNodeFuncDef_ArgNameListHead + hp!
	node@ MclpParseNodeFuncDef_ArgNameListTail + tp!

	0 hp@!
	0 tp@!

	0 node@ MclpParseNodeFuncDef_ArgCount + !

	auto tokflag
	auto subnode

	0 // endtoken
	ctx@ // ctx
	MclpParseExpression ok! subnode! tokflag!

	if (ok@)
		node@ MclpParseSubtreeFree

		return
	end

	subnode@ node@ MclpParseNodeFuncDef_Name + !

	if (tokflag@ LEXTOK_NEWLINE & ~~)
		while (1)
			auto argnode
			0 // endtoken
			ctx@ // ctx
			MclpParseExpression ok! argnode! tokflag!

			if (ok@)
				return
			end

			if (hp@@ ~~)
				argnode@ hp@!
				argnode@ tp@!
			end else
				argnode@ tp@@ MclpParseNode_Next + !
				argnode@ tp@!
			end

			1 node@ MclpParseNodeFuncDef_ArgCount + +=

			if (tokflag@ LEXTOK_NEWLINE &)
				break
			end
		end
	end

	auto body
	"END" // endtoken
	ctx@ // ctx
	MclpParseBlock ok! body!

	if (ok@)
		node@ MclpParseSubtreeFree

		return
	end

	body@ node@ MclpParseNodeFuncDef_Body + !
end

fn MclpParseReturnStatement { ctx -- node ok }
	PARSENODE_RETURN // type
	MclpParseNode_SIZEOF // size
	ctx@ // ctx
	MclpParseNodeCreate ok! node!
end

fn MclpParseBreakStatement { ctx -- node ok }
	PARSENODE_BREAK // type
	MclpParseNode_SIZEOF // size
	ctx@ // ctx
	MclpParseNodeCreate ok! node!
end

fn MclpParseContinueStatement { ctx -- node ok }
	PARSENODE_CONTINUE // type
	MclpParseNode_SIZEOF // size
	ctx@ // ctx
	MclpParseNodeCreate ok! node!
end

fn MclpParseEvalStatement { ctx -- node ok }
	0 // endtoken
	ctx@ // ctx
	MclpParseExpression ok! node! drop
end

fn MclpParseExpression { endtoken ctx -- tokflag node ok }
	auto tokbuf
	ctx@ MclpParseContext_TokBuffer + tokbuf!

	1 ctx@ MclpParseContext_Depth + +=

	auto toklen
	0 // stopnl
	tokbuf@ // tokbuf
	ctx@ // ctx
	MclpLexNextNonemptyToken ok! toklen! tokflag!

	if (ok@)
		1 ctx@ MclpParseContext_Depth + -=

		return
	end

	if (endtoken@ tokflag@ LEXTOK_LITERAL & ~~ &&)
		if (tokbuf@ endtoken@ strcmp)
			ctx@ MclpLexLastToken

			0 node!

			1 ctx@ MclpParseContext_Depth + -=

			return
		end
	end

	auto subnode

	if (tokbuf@ "$" strcmp tokflag@ LEXTOK_LITERAL & ~~ &&)
		// var ref

		PARSENODE_VARREF // type
		MclpParseNodeVarRef_SIZEOF // size
		ctx@ // ctx
		MclpParseNodeCreate ok! node!

		if (ok@)
			1 ctx@ MclpParseContext_Depth + -=

			return
		end

		0 // endtoken
		ctx@ // ctx
		MclpParseExpression ok! subnode! tokflag!

		if (ok@)
			node@ MclpParseSubtreeFree

			1 ctx@ MclpParseContext_Depth + -=

			return
		end

		subnode@ node@ MclpParseNodeVarRef_Name + !
	end elseif (tokbuf@ "(" strcmp tokflag@ LEXTOK_LITERAL & ~~ &&)
		// func call

		")" // endtoken
		ctx@ // ctx
		MclpParseExpression ok! subnode! tokflag!

		if (ok@)
			1 ctx@ MclpParseContext_Depth + -=

			return
		end

		if (subnode@ ~~)
			1 ctx@ MclpParseContext_Depth + -=

			"expected a function name\n" // fmt
			0 // node
			ctx@ // ctx
			MclpParseDiagnostic

			STATUS_SYNTAX_ERROR ok!

			return
		end

		PARSENODE_FUNCCALL // type
		MclpParseNodeFunctionCall_SIZEOF // size
		ctx@ // ctx
		MclpParseNodeCreate ok! node!

		if (ok@)
			1 ctx@ MclpParseContext_Depth + -=

			subnode@ MclpParseSubtreeFree

			return
		end

		subnode@ node@ MclpParseNodeFunctionCall_Name + !

		auto hp
		auto tp

		node@ MclpParseNodeFunctionCall_ArgListHead + hp!
		node@ MclpParseNodeFunctionCall_ArgListTail + tp!

		0 hp@!
		0 tp@!

		0 node@ MclpParseNodeFunctionCall_ArgCount + !

		while (1)
			")" // endtoken
			ctx@ // ctx
			MclpParseExpression ok! subnode! tokflag!

			if (ok@)
				1 ctx@ MclpParseContext_Depth + -=

				node@ MclpParseSubtreeFree

				return
			end

			if (subnode@ ~~)
				break
			end

			// add to arg list

			if (hp@@ ~~)
				subnode@ hp@!
				subnode@ tp@!
			end else
				subnode@ tp@@ MclpParseNode_Next + !
				subnode@ tp@!
			end

			1 node@ MclpParseNodeFunctionCall_ArgCount + +=
		end
	end else
		// ident

		PARSENODE_LITERAL // type
		MclpParseNodeLiteral_SIZEOF toklen@ + 1 + // size
		ctx@ // ctx
		MclpParseNodeCreate ok! node!

		if (ok@)
			1 ctx@ MclpParseContext_Depth + -=

			return
		end

		toklen@ node@ MclpParseNodeLiteral_Length + !

		node@ MclpParseNodeLiteral_Word + // dest
		tokbuf@ // src
		strcpy
	end

	1 ctx@ MclpParseContext_Depth + -=
end

const WHYRETURN_TERM     1
const WHYRETURN_REDIRECT 2
const WHYRETURN_PIPE     3

fn MclpParsePipeline { endtoken ctx -- node ok }
	PARSENODE_PIPELINE // type
	MclpParseNodePipeline_SIZEOF // size
	ctx@ // ctx
	MclpParseNodeCreate ok! node!

	if (ok@)
		return
	end

	auto hp
	auto tp

	node@ MclpParseNodePipeline_CmdListHead + hp!
	node@ MclpParseNodePipeline_CmdListTail + tp!

	0 hp@!
	0 tp@!

	0 node@ MclpParseNodePipeline_StdInRedirectionPath + !
	0 node@ MclpParseNodePipeline_StdOutRedirectionPath + !
	0 node@ MclpParseNodePipeline_StdErrRedirectionPath + !

	0 node@ MclpParseNodePipeline_Flags + !

	auto tokbuf
	ctx@ MclpParseContext_TokBuffer + tokbuf!

	while (1)
		// parse each fragment of the pipeline in sequence until we encounter
		// either newline, EOF, redirection, or the endtoken.

		auto whyreturn
		auto cmdnode
		endtoken@ // endtoken
		ctx@ // ctx
		MclpParseCommand ok! cmdnode! whyreturn!

		if (ok@)
			node@ MclpParseSubtreeFree

			return
		end

		if (cmdnode@)
			if (hp@@ ~~)
				cmdnode@ hp@!
				cmdnode@ tp@!
			end else
				cmdnode@ tp@@ MclpParseNode_Next + !
				cmdnode@ tp@!
			end
		end

		if (whyreturn@ WHYRETURN_PIPE ==)
			continue
		end

		if (whyreturn@ WHYRETURN_REDIRECT ==)
			node@ // node
			ctx@ // ctx
			MclpParseRedirection ok!

			if (ok@)
				node@ MclpParseSubtreeFree

				return
			end
		end

		break
	end
end

fn MclpParseRedirection { node ctx -- ok }
	auto tokbuf
	ctx@ MclpParseContext_TokBuffer + tokbuf!

	auto toklen
	auto tokflag
	0 tokflag!

	while (1)
		if (tokflag@ LEXTOK_NEWLINE &)
			break
		end

		0 // stopnl
		tokbuf@ // tokbuf
		ctx@ // ctx
		MclpLexNextNonemptyToken ok! toklen! tokflag!

		if (ok@)
			break
		end

		auto off

		if (tokbuf@ "<" strcmp)
			MclpParseNodePipeline_StdInRedirectionPath off!
		end elseif (tokbuf@ gb '>' ==)
			if (tokbuf@ ">" strcmp)
				MclpParseNodePipeline_StdOutRedirectionPath off!
				PIPELINEFLAG_OUTAPPEND ~ node@ MclpParseNodePipeline_Flags + &=
			end elseif (tokbuf@ ">>" strcmp)
				MclpParseNodePipeline_StdOutRedirectionPath off!
				PIPELINEFLAG_OUTAPPEND node@ MclpParseNodePipeline_Flags + |=
			end elseif (tokbuf@ ">>>" strcmp)
				MclpParseNodePipeline_StdErrRedirectionPath off!
				PIPELINEFLAG_ERRAPPEND ~ node@ MclpParseNodePipeline_Flags + &=
			end elseif (tokbuf@ ">>>>" strcmp)
				MclpParseNodePipeline_StdErrRedirectionPath off!
				PIPELINEFLAG_ERRAPPEND node@ MclpParseNodePipeline_Flags + |=
			end else
				"unknown redirection\n" // fmt
				0 // node
				ctx@ // ctx
				MclpParseDiagnostic

				STATUS_SYNTAX_ERROR ok!
				break
			end
		end elseif (tokbuf@ "&" strcmp)
			PIPELINEFLAG_ASYNC node@ MclpParseNodePipeline_Flags + |=
			continue
		end else
			"odd redirection\n" // fmt
			0 // node
			ctx@ // ctx
			MclpParseDiagnostic

			STATUS_SYNTAX_ERROR ok!
			break
		end

		auto redirnode
		0 // endtoken
		ctx@ // ctx
		MclpParseExpression ok! redirnode! tokflag!

		if (ok@)
			break
		end

		if (node@ off@ + @)
			node@ off@ + @ MclpParseSubtreeFree
		end

		redirnode@ node@ off@ + !
	end
end

fn MclpParseCommand { endtoken ctx -- whyreturn node ok }
	auto tokbuf
	ctx@ MclpParseContext_TokBuffer + tokbuf!

	0 node!
	0 ok!
	0 whyreturn!

	auto namenode
	auto tokflag

	endtoken@ // endtoken
	ctx@ // ctx
	MclpParseExpression ok! namenode! tokflag!

	if (ok@)
		return
	end

	if (namenode@ ~~)
		WHYRETURN_TERM whyreturn!
		return
	end

	PARSENODE_COMMAND // type
	MclpParseNodeCommand_SIZEOF // size
	ctx@ // ctx
	MclpParseNodeCreate ok! node!

	if (ok@)
		return
	end

	auto hp
	auto tp

	node@ MclpParseNodeCommand_ArgListHead + hp!
	node@ MclpParseNodeCommand_ArgListTail + tp!

	0 hp@!
	0 tp@!

	0 node@ MclpParseNodeCommand_ArgCount + !

	namenode@ node@ MclpParseNodeCommand_Name + !

	if (tokflag@ LEXTOK_NEWLINE &)
		WHYRETURN_TERM whyreturn!
		return
	end

	while (1)
		// parse all of the arguments

		auto toklen
		1 // stopnl
		tokbuf@ // tokbuf
		ctx@ // ctx
		MclpLexNextNonemptyToken ok! toklen! tokflag!

		if (ok@)
			return
		end

		if (toklen@ ~~)
			WHYRETURN_TERM whyreturn!
			break
		end

		if (tokflag@ LEXTOK_LITERAL & ~~)
			if (tokbuf@ "|" strcmp)
				WHYRETURN_PIPE whyreturn!
			end elseif (tokbuf@ "<" strcmp)
				WHYRETURN_REDIRECT whyreturn!
			end elseif (tokbuf@ "&" strcmp)
				WHYRETURN_REDIRECT whyreturn!
			end elseif (tokbuf@ gb '>' ==)
				WHYRETURN_REDIRECT whyreturn!
			end

			if (whyreturn@)
				if (whyreturn@ WHYRETURN_REDIRECT ==)
					ctx@ MclpLexLastToken
				end

				break
			end
		end

		ctx@ MclpLexLastToken

		auto argnode
		endtoken@ // endtoken
		ctx@ // ctx
		MclpParseExpression ok! argnode! tokflag!

		if (ok@)
			node@ MclpParseSubtreeFree

			return
		end

		if (argnode@ ~~)
			WHYRETURN_TERM whyreturn!
			break
		end

		if (hp@@ ~~)
			argnode@ hp@!
			argnode@ tp@!
		end else
			argnode@ tp@@ MclpParseNode_Next + !
			argnode@ tp@!
		end

		if (tokflag@ LEXTOK_NEWLINE &)
			WHYRETURN_TERM whyreturn!
			break
		end
	end
end