//
// Implements a generic resource file parser for OSLoader.
//

#include "<df>/dragonfruit.h"

#include "OSLoader.h"

buffer LdrResourceFileLineBuffer 256

fn LdrResourceFileParse { callback path device -- ok }
	auto file

	path@ // path
	device@ // device
	LdrFileOpen ok! file!

	if (ok@)
		return
	end

	// naively read the file byte-by-byte and call the callback for each
	// non-comment line.

	auto offset
	0 offset!

	auto buf
	4 alloc buf!

	auto lbp
	LdrResourceFileLineBuffer lbp!

	auto linelen
	0 linelen!

	auto incomment
	0 incomment!

	auto linestart
	1 linestart!

	auto bytesread

	while (1)
		1 // length
		buf@ // buf
		offset@ // offset
		file@ // file
		LdrFileRead ok! bytesread!

		if (ok@)
			return
		end

		if (bytesread@ ~~)
			if (linelen@)
				0 lbp@ sb

				// call the callback

				LdrResourceFileLineBuffer // linebuf
				callback@ LdrResourceFileCallbackFunction
			end

			return
		end

		auto c
		buf@ gb c!

		if (c@ '\n' ==)
			if (linelen@)
				0 lbp@ sb

				// call the callback

				LdrResourceFileLineBuffer // linebuf
				callback@ LdrResourceFileCallbackFunction
			end

			0 linelen!
			0 incomment!
			1 linestart!
			LdrResourceFileLineBuffer lbp!
		end elseif (incomment@ ~~)
			if (c@ '#' ==)
				while (linelen@ lbp@ 1 - gb ' ' == &&)
					1 lbp -=
					0 lbp@ sb
					1 linelen -=
				end

				1 incomment!
			end elseif (c@ ' ' == linestart@ &&)
				// nada
			end else
				0 linestart!

				c@ lbp@ sb
				1 lbp +=
				1 linelen +=
			end
		end

		1 offset +=
	end
end