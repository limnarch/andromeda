//
// Implements argument parsing for OSLoader.
//

#include "<df>/dragonfruit.h"

#include "OSLoader.h"

buffer LdrArgsNameBuffer 32

fn LdrArgsValue { arg -- out }
	auto p
	LdrArgsBuffer p!

	auto tp
	LdrArgsNameBuffer tp!

	auto tpm
	tp@ 32 + tpm!

	auto m
	p@ 255 + m!

	0 out!

	while (p@ m@ <)
		if (p@ gb ~~)
			LdrArgsNameBuffer tp!

			1 p +=

			if (p@ gb ~~)
				break
			end
		end elseif (p@ gb '=' ==)
			0 tp@ sb

			if (LdrArgsNameBuffer arg@ strcmp)
				p@ 1 + out!
				return
			end

			p@ strlen p +=

			continue
		end

		p@ gb tp@ sb

		1 tp +=
		1 p +=
	end
end

fn LdrArgsCheck { arg -- present }
	auto p
	LdrArgsBuffer p!

	auto m
	p@ 255 + m!

	0 present!

	while (p@ m@ <)
		if (p@ gb ~~)
			break
		end

		if (arg@ p@ strcmp)
			1 present!
			return
		end else
			p@ strlen 1 + p +=
		end
	end
end