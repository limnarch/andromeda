//
// Implements the LIMNstation-specific part of bootloader initialization.
//

#include "<df>/dragonfruit.h"
#include "<ll>/rta3x/a3x.h"

extern BlMain { args -- ret }

buffer ArgsBuffer 256
public ArgsBuffer

fn ArgsInit { argp -- }
	auto p
	ArgsBuffer p!

	auto l
	0 l!

	auto esc
	0 esc!

	if (argp@ ~~)
		return
	end

	while (argp@ gb)
		if (l@ 255 >=)
			1 p -=

			while (p@ gb)
				0 p@ sb
				1 p -=
			end

			return
		end

		if (esc@)
			0 esc!
			argp@ gb p@ sb
			1 p +=
			1 l +=
		end elseif (argp@ gb ' ' ==)
			0 p@ sb
			1 p +=
			1 l +=
		end elseif (argp@ gb '\\' ==)
			1 esc!
		end else
			argp@ gb p@ sb
			1 p +=
			1 l +=
		end

		1 argp +=
	end

	0 p@ sb
end

fn Main { args -- ret }
	// a3x entrypoint

	// convert arguments to HAL-preferred format

	args@ ArgsInit

	// call generic bootloader entrypoint

	ArgsBuffer BlMain ret!
end