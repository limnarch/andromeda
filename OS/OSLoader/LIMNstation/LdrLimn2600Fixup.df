//
// Implements LOFF fixup resolution for the limn2600 architecture.
//

#include "<df>/dragonfruit.h"

#include "../OSLoader.h"

const RELOC_LIMN2500_LONG 1
const RELOC_LIMN2500_ABSJ 2
const RELOC_LIMN2500_LA   3
const RELOC_LIMN2600_FAR_INT  4
const RELOC_LIMN2600_FAR_LONG 5

fn LdrDoFixup { ptr value type -- ok }
	// ptr@ value@ type@ "t=%d v=%08x p=%08x\n" Printf

	0 ok!

	if (type@ RELOC_LIMN2500_LONG ==)
		value@ ptr@!
	end elseif (type@ RELOC_LIMN2500_ABSJ ==)
		ptr@@ 0x7 & value@ 2 >> 3 << | ptr@!
	end elseif (type@ RELOC_LIMN2500_LA ==)
		value@ 0xFFFF0000 & ptr@@ 0xFFFF & | ptr@!
		value@ 0xFFFF & 16 << ptr@ 4 + @ 0xFFFF & | ptr@ 4 + !
	end elseif (type@ RELOC_LIMN2600_FAR_INT ==)
		value@ 0xFFFF0000 & ptr@@ 0xFFFF & | ptr@!
		value@ 0xFFFF & 1 >> 16 << ptr@ 4 + @ 0xFFFF & | ptr@ 4 + !
	end elseif (type@ RELOC_LIMN2600_FAR_LONG ==)
		value@ 0xFFFF0000 & ptr@@ 0xFFFF & | ptr@!
		value@ 0xFFFF & 2 >> 16 << ptr@ 4 + @ 0xFFFF & | ptr@ 4 + !
	end else
		LDRSTATUS_BAD_EXECUTABLE ok!
	end
end