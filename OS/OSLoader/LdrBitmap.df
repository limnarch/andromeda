//
// Faster bitmap management routines. Stolen from MINTIA executive for
// OSLoader.
//

#include "<df>/dragonfruit.h"

#include "OSLoader.h"

// XXX these tables each take up 1024 bytes because dragonfruit is ridiculous 
// so that is a memory consumption issue to keep in mind...

// table of largest # of contiguous zero bits in a byte
table LdrBitmapContiguousZeroes
	8 7 6 6 5 5 5 5 4 4 4 4 4 4 4 4
	4 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3
	5 4 3 3 2 2 2 2 3 2 2 2 2 2 2 2
	4 3 2 2 2 2 2 2 3 2 2 2 2 2 2 2
	6 5 4 4 3 3 3 3 3 2 2 2 2 2 2 2
	4 3 2 2 2 1 1 1 3 2 1 1 2 1 1 1
	5 4 3 3 2 2 2 2 3 2 1 1 2 1 1 1
	4 3 2 2 2 1 1 1 3 2 1 1 2 1 1 1
	7 6 5 5 4 4 4 4 3 3 3 3 3 3 3 3
	4 3 2 2 2 2 2 2 3 2 2 2 2 2 2 2
	5 4 3 3 2 2 2 2 3 2 1 1 2 1 1 1
	4 3 2 2 2 1 1 1 3 2 1 1 2 1 1 1
	6 5 4 4 3 3 3 3 3 2 2 2 2 2 2 2
	4 3 2 2 2 1 1 1 3 2 1 1 2 1 1 1
	5 4 3 3 2 2 2 2 3 2 1 1 2 1 1 1
	4 3 2 2 2 1 1 1 3 2 1 1 2 1 1 0
endtable

// table of contiguous low zero bits in a byte
table LdrBitmapLowZeroes
	8 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	4 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	5 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	4 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	6 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	4 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	5 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	4 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	7 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	4 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	5 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	4 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	6 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	4 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	5 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
	4 0 1 0 2 0 1 0 3 0 1 0 2 0 1 0
endtable

// table of contiguous high zero bits in a byte
table LdrBitmapHighZeroes
	8 7 6 6 5 5 5 5 4 4 4 4 4 4 4 4
	3 3 3 3 3 3 3 3 3 3 3 3 3 3 3 3
	2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
	2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2
	1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1
	1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1
	1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1
	1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1
	0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
	0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
	0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
	0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
	0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
	0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
	0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
	0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
endtable

// table of total number of clear bits in a byte
table LdrBitmapTotalZeroes
	8 7 7 6 7 6 6 5 7 6 6 5 6 5 5 4
	7 6 6 5 6 5 5 4 6 5 5 4 5 4 4 3
	7 6 6 5 6 5 5 4 6 5 5 4 5 4 4 3
	6 5 5 4 5 4 4 3 5 4 4 3 4 3 3 2
	7 6 6 5 6 5 5 4 6 5 5 4 5 4 4 3
	6 5 5 4 5 4 4 3 5 4 4 3 4 3 3 2
	6 5 5 4 5 4 4 3 5 4 4 3 4 3 3 2
	5 4 4 3 4 3 3 2 4 3 3 2 3 2 2 1
	7 6 6 5 6 5 5 4 6 5 5 4 5 4 4 3
	6 5 5 4 5 4 4 3 5 4 4 3 4 3 3 2
	6 5 5 4 5 4 4 3 5 4 4 3 4 3 3 2
	5 4 4 3 4 3 3 2 4 3 3 2 3 2 2 1
	6 5 5 4 5 4 4 3 5 4 4 3 4 3 3 2
	5 4 4 3 4 3 3 2 4 3 3 2 3 2 2 1
	5 4 4 3 4 3 3 2 4 3 3 2 3 2 2 1
	4 3 3 2 3 2 2 1 3 2 2 1 2 1 1 0
endtable

table LdrBitmapFillMasks
	0   1   3   7   15  31  63  127 255
endtable

table LdrBitmapZeroMasks
	255 254 252 248 240 224 192 128 0
endtable

fn LdrBitmapInitialize { sizeinbits data header -- }
	data@ header@ LdrBitmapHeader_Data + !
	sizeinbits@ header@ LdrBitmapHeader_SizeInBits + !

	auto sizeinbytes
	sizeinbits@ 7 + 3 >> sizeinbytes!

	if (sizeinbits@ 7 &)
		[sizeinbits@ 7 &]LdrBitmapZeroMasks@ data@ sizeinbytes@ 1 - + sb
	end
end

fn LdrBitmapFindRun { hint runlength header -- offset ok }
	0 ok!

	auto sizeinbits
	header@ LdrBitmapHeader_SizeInBits + @ sizeinbits!

	auto sizeinbytes
	sizeinbits@ 7 + 3 >> sizeinbytes!

	auto currentposition

	if (hint@ sizeinbits@ >=)
		0 hint!
	end

	3 hint >>=

	auto i
	0 i!

	while (i@ 2 <)
		auto startindex
		auto endindex

		if (i@ 0 ==)
			hint@ startindex!
			sizeinbytes@ endindex!
		end elseif (hint@)
			0 startindex!

			if (runlength@ 2 <)
				hint@ endindex!
			end else
				runlength@ 2 - 3 >> 1 + hint@ + endindex!

				if (endindex@ sizeinbytes@ >)
					sizeinbytes@ endindex!
				end
			end
		end else
			-1 ok!

			return
		end

		auto endbitindex
		auto prevbyte
		auto curbyte
		auto theotherbyte
		auto bitmaskorstartindex

		header@ LdrBitmapHeader_Data + @ startindex@ + currentposition!

		if (runlength@ 10 <)
			0xFF prevbyte!

			startindex@ 3 << offset!
			endindex@ 3 << endbitindex!

			while (offset@ endbitindex@ <)
				currentposition@ gb curbyte!
				1 currentposition +=

				if ([curbyte@]LdrBitmapContiguousZeroes@ runlength@ >=)
					// we can satisfy this with this single byte

					[runlength@]LdrBitmapFillMasks@ bitmaskorstartindex!

					while (curbyte@ bitmaskorstartindex@ &)
						1 bitmaskorstartindex <<=
						1 offset +=
					end

					return
				end elseif ([prevbyte@]LdrBitmapHighZeroes@
					[curbyte@]LdrBitmapLowZeroes@ + runlength@ >=)
					// this and the last byte work

					offset@ [prevbyte@]LdrBitmapHighZeroes@ - bitmaskorstartindex!

					if (bitmaskorstartindex@ runlength@ + sizeinbits@ <=)
						bitmaskorstartindex@ offset!

						return
					end
				end

				curbyte@ prevbyte!
				8 offset +=
			end
		end elseif (runlength@ 15 <)
			0xFF prevbyte!
			0xFF theotherbyte!

			startindex@ 3 << offset!
			endindex@ 3 << endbitindex!

			while (offset@ endbitindex@ <)
				currentposition@ gb curbyte!
				1 currentposition +=

				if ([prevbyte@]LdrBitmapHighZeroes@
					[curbyte@]LdrBitmapLowZeroes@ + runlength@ >=)
					// fits in this byte and the last byte

					offset@ [prevbyte@]LdrBitmapHighZeroes@ - bitmaskorstartindex!

					if (bitmaskorstartindex@ runlength@ + sizeinbits@ <=)
						bitmaskorstartindex@ offset!

						return
					end
				end

				if (prevbyte@ 0 ==)
					if ([theotherbyte@]LdrBitmapHighZeroes@
						[curbyte@]LdrBitmapLowZeroes@ 8 + + runlength@ >=)
						// fits in this byte, the last byte, and the other byte

						offset@ 8 - [theotherbyte@]LdrBitmapHighZeroes@ - bitmaskorstartindex!

						if (bitmaskorstartindex@ runlength@ + sizeinbits@ <=)
							bitmaskorstartindex@ offset!

							return
						end
					end
				end

				prevbyte@ theotherbyte!
				curbyte@ prevbyte!
				8 offset +=
			end
		end else
			// big runlength, can't try our fancy tricks above...
			// need new fancy tricks.

			auto zeroneeded
			runlength@ 7 - 3 >> zeroneeded!

			auto zerobytesrun
			0 zerobytesrun!

			// store start of run byte in hint
			0xFF hint!

			startindex@ 1 - bitmaskorstartindex!

			// store current index in theotherbyte
			startindex@ theotherbyte!

			while (theotherbyte@ endindex@ <)
				currentposition@ gb curbyte!
				1 currentposition +=

				if (zerobytesrun@ zeroneeded@ >=)
					if ([hint@]LdrBitmapHighZeroes@
						zerobytesrun@ 3 << +
						[curbyte@]LdrBitmapLowZeroes@ + runlength@ >=)

						bitmaskorstartindex@ 3 <<
						8 [hint@]LdrBitmapHighZeroes@ - + bitmaskorstartindex!

						if (bitmaskorstartindex@ runlength@ + sizeinbits@ <=)
							bitmaskorstartindex@ offset!

							return
						end
					end
				end

				if (curbyte@ ~~)
					1 zerobytesrun +=
				end else
					curbyte@ hint!
					0 zerobytesrun!
					theotherbyte@ bitmaskorstartindex!
				end

				1 theotherbyte +=
			end
		end

		1 i +=
	end

	-1 ok!
end

fn LdrBitmapSetBits { runlength index header -- }
	if (DEBUGCHECKS)
		if (runlength@ index@ + header@ LdrBitmapHeader_SizeInBits + @ >)
			"LdrBitmapSetBits: attempt to set beyond the end of the bitmap\n" LdrCrash
		end
	end

	if (runlength@ 0 ==)
		return
	end

	auto bitoffset
	index@ 31 & bitoffset!

	auto curlongptr
	header@ LdrBitmapHeader_Data + @ index@ 5 >> 2 << + curlongptr!

	if (bitoffset@ runlength@ + 32 <=)
		// you are expected to understand this

		curlongptr@@ 0xFFFFFFFF 32 runlength@ - >> bitoffset@ << | curlongptr@! 

		return
	end

	curlongptr@@ 0xFFFFFFFF bitoffset@ << | curlongptr@!

	4 curlongptr +=
	32 bitoffset@ - runlength -=

	while (runlength@ 32 >=)
		0xFFFFFFFF curlongptr@!

		4 curlongptr +=
		32 runlength -=
	end

	if (runlength@)
		curlongptr@@ 0xFFFFFFFF runlength@ << ~ | curlongptr@!
	end
end

fn LdrBitmapClearBits { runlength index header -- }
	if (DEBUGCHECKS)
		if (runlength@ index@ + header@ LdrBitmapHeader_SizeInBits + @ >)
			"LdrBitmapClearBits: attempt to clear beyond the end of the bitmap\n" LdrCrash
		end
	end

	if (runlength@ 0 ==)
		return
	end

	auto bitoffset
	index@ 31 & bitoffset!

	auto curlongptr
	header@ LdrBitmapHeader_Data + @ index@ 5 >> 2 << + curlongptr!

	if (bitoffset@ runlength@ + 32 <=)
		// you are expected to understand this

		curlongptr@@ 0xFFFFFFFF 32 runlength@ - >> bitoffset@ << ~ & curlongptr@! 

		return
	end

	curlongptr@@ 0xFFFFFFFF bitoffset@ << ~ & curlongptr@!

	4 curlongptr +=
	32 bitoffset@ - runlength -=

	while (runlength@ 32 >=)
		0 curlongptr@!

		4 curlongptr +=
		32 runlength -=
	end

	if (runlength@)
		curlongptr@@ 0xFFFFFFFF runlength@ << & curlongptr@!
	end
end

fn LdrBitmapCheckClearBits { runlength index header -- isclear }
	if (DEBUGCHECKS)
		if (runlength@ ~~)
			"LdrBitmapCheckClearBits: runlength=0\n" LdrCrash
		end
	end

	if (runlength@ index@ + header@ LdrBitmapHeader_SizeInBits + @ >)
		0 isclear!
		return
	end

	auto endindex
	index@ runlength@ + 1 - endindex!

	auto startbyteoffset
	index@ 3 >> startbyteoffset!

	auto endbyteoffset
	endindex@ 3 >> endbyteoffset!

	auto startbitoffset
	index@ 7 & startbitoffset!

	auto endbitoffset
	endindex@ 7 & endbitoffset!

	auto currentposition
	header@ LdrBitmapHeader_Data + @ startbyteoffset@ + currentposition!

	auto curbyte
	currentposition@ gb curbyte!

	if (startbyteoffset@ endbyteoffset@ ==)
		[startbitoffset@]LdrBitmapZeroMasks@ [endbitoffset@ 1 +]LdrBitmapFillMasks@ &
		curbyte@ & ~~ isclear!
	end else
		if ([startbitoffset@]LdrBitmapZeroMasks@ curbyte@ &)
			0 isclear!
			return
		end

		auto endposition
		header@ LdrBitmapHeader_Data + @ endbyteoffset@ + endposition!

		1 currentposition +=

		while (currentposition@ endposition@ <)
			if (currentposition@ gb)
				0 isclear!
				return
			end

			1 currentposition +=
		end

		currentposition@ gb curbyte!

		if ([endbitoffset@ 1 +]LdrBitmapFillMasks@ curbyte@ &)
			0 isclear!
		end else
			1 isclear!
		end
	end
end