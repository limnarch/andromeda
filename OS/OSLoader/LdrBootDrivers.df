//
// Implements boot driver loading.
//

#include "<df>/dragonfruit.h"

#include "OSLoader.h"

fn LdrBootDriversLoadAll { -- }
	// allow the platform to load drivers according to its device tree or
	// whatever.

	LdrPlatformBootDriversLoadAll

	// load whatever drivers are specified in BootDrivers.txt

	auto ok

	pointerof LdrBootDriverCallback // callback
	"/mintia/BootDrivers.txt" // path
	LdrBootDevice // device
	LdrResourceFileParse ok!

	if (ok@)
		ok@ "LdrBootDriversLoadAll: couldn't open /mintia/BootDrivers.txt (%i)\n" Printf
		return
	end
end

buffer LdrBootDriverPath 128

fn LdrBootDriverLoad { flags name -- dll ok }
	LdrBootDriverPath // dest
	"/mintia/BootDrivers/" // src
	strcpy

	LdrBootDriverPath 20 + // dest
	name@ // src
	strcpy

	0 // flags
	LdrBootDriverPath // name
	LdrModuleLoad ok! dll!

	if (ok@)
		return
	end

	auto di
	"DriverInit" // name
	dll@ // dll
	LdrGetSymbolAddress ok! di!

	if (ok@ ~~)
		di@ dll@ LdrModule_DriverInit + !
	end
end

fn (LdrResourceFileCallbackFunction) LdrBootDriverCallback { linebuf -- }
	0 // flags
	linebuf@ // name
	LdrBootDriverLoad drop drop
end