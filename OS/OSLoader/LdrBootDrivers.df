//
// Implements boot driver loading.
//

#include "<df>/dragonfruit.h"

#include "OSLoader.h"

fn LdrBootDriversLoadAll { -- }
	// allow the platform to load drivers according to its device tree or
	// whatever.

	LdrPlatformBootDriversLoadAll

	// load whatever drivers are specified in BootDrivers.txt

	auto ok

	pointerof LdrBootDriverCallback // callback
	"/mintia/BootDrivers.txt" // path
	LdrBootDevice // device
	LdrResourceFileParse ok!

	if (ok@)
		ok@ "LdrBootDriversLoadAll: couldn't open /mintia/BootDrivers.txt (%i)\n" Printf
		return
	end
end

buffer LdrBootDriverPath 128

fn LdrBootDriverLoad { flags name -- dll ok }
	LdrBootDriverPath // dest
	"/mintia/BootDrivers/" // src
	strcpy

	LdrBootDriverPath 20 + // dest
	name@ // src
	strcpy

	0 // flags
	LdrBootDriverPath // name
	ComDLLLoad ok! dll!

	if (ok@)
		return
	end

	auto di
	"DriverInit" // name
	dll@ // dll
	ComDLLGetSymbolAddress ok! di!

	if (ok@ ~~)
		di@ dll@ ComDLL_LoadFunction + !
	end
end

fn (LdrResourceFileCallbackFunction) LdrBootDriverCallback { linebuf -- }
	auto ok

	0 // flags
	linebuf@ // name
	LdrBootDriverLoad ok! drop

	if (ok@)
		if (ok@ LDRSTATUS_NOT_FOUND ~=)
			ok@ "LdrBootDrivers: failed to load (%i)\n" LdrCrash
		end
	end
end